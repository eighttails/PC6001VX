			  Z80 ASSEMBLER - ZASM VER 1.6
                      	;Compatible BASIC for PC-6001
                      	; by AKIKAWA, Hisashi  2013-2021
                      	
                      	;This software is redistributable under the LGPLv2.1 or any later version.
                      	
                      	
                      	;subroutine entry address
  0000                	BOOT	equ	0000h
  0008                	CHKPAR	equ	0008h
  0010                	ANADAT	equ	0010h
  0018                	JPHK1	equ	0018h
  0020                	CPHLDE	equ	0020h
  0028                	CHKSGN	equ	0028h
  0038                	JPHK2	equ	0038h
  0384                	ERRSTR	equ	0384h
  038B                	ERRIN	equ	038bh
  0390                	OK	equ	0390h
  0442                	EDIT	equ	0442h
  06EA                	CMDEND	equ	06eah
  0709                	INTPRT2	equ	0709h
  0741                	FTOI2	equ	0741h
  0BEE                	TOUPHL	equ	0beeh
  0D16                	ABTOF	equ	0d16h
  0DE3                	INT1INC	equ	0de3h
  0DE4                	INT1ARG	equ	0de4h
  0E78                	IN90H	equ	0e78h
  0E8F                	OUT90H	equ	0e8fh
  0EB0                	INTGRP	equ	0eb0h
  0EB5                	INTKEY	equ	0eb5h
  0EF6                	IKBF	equ	0ef6h
  0F2B                	IKPOP	equ	0f2bh
  0F74                	INTTIM	equ	0f74h
  0FBC                	GETCH	equ	0fbch
  0FC4                	GETC	equ	0fc4h
  103A                	GETKBF	equ	103ah
  1058                	CLRKBF	equ	1058h
  1061                	STICK	equ	1061h
  1075                	PRTC	equ	1075h
  10AA                	PRTCH	equ	10aah
  10EA                	PATCH1	equ	10eah
  116D                	SETCSR	equ	116dh
  1179                	CSRON	equ	1179h
  1181                	CSROFF	equ	1181h
  11B8                	Y2AD	equ	11b8h
  11CD                	XY2AD	equ	11cdh
  11DA                	DELLIN	equ	11dah
  1257                	PRTCHXY	equ	1257h
  1260                	SCRLUP	equ	1260h
  12A9                	SCRLDW	equ	12a9h
  1390                	CHGMOD	equ	1390h
  13ED                	CHGDSP	equ	13edh
  140C                	CHGACT	equ	140ch
  1478                	AD2GAD	equ	1478h
  14A0                	CGROM	equ	14a0h
  14AF                	PRTCHAD	equ	14afh
  15C0                	SETATT	equ	15c0h
  1A1C                	PUTPRT	equ	1a1ch
  1A4F                	CNVKANA	equ	1a4fh
  1A61                	ROPEN	equ	1a61h
  1A70                	GETCMT	equ	1a70h
  1AAA                	RCLOSE	equ	1aaah
  1AB8                	WOPEN	equ	1ab8h
  1ACC                	PUTCMT	equ	1acch
  1AED                	CHK90H	equ	1aedh
  1B06                	WCLOSE	equ	1b06h
  1B2A                	BLNKAST	equ	1b2ah
  1B49                	RLOFF	equ	1b49h
  1B4B                	RLON	equ	1b4bh
  1B54                	OUTB0H	equ	1b54h
  1B60                	PLSTPS	equ	1b60h
  1BB3                	PLSTOP	equ	1bb3h
  1BBE                	SETPSG2	equ	1bbeh
  1BC5                	SETPSG	equ	1bc5h
  1BCD                	BELL	equ	1bcdh
  1CA6                	JOYSTK	equ	1ca6h
  1CB4                	PATCH2	equ	1cb4h
  1D73                	SETCNSL	equ	1d73h
  1DBB                	SETC3	equ	1dbbh
  1DFB                	CLS	equ	1dfbh
  1EB3                	PLAY	equ	1eb3h
  2030                	SCALE2	equ	2030h		;for mkII
  204B                	SCALE	equ	204bh
  2539                	GETFN	equ	2539h
  254E                	CMTSKP	equ	254eh
  2576                	FOUND	equ	2576h
  2583                	PUTFN	equ	2583h
  259A                	INPOPN	equ	259ah
  25A8                	PRTOPN	equ	25a8h
  25E5                	WAIT	equ	25e5h
  26C7                	PUTDEV	equ	26c7h
  2701                	PUTPRNL	equ	2701h
  272D                	PUTNLX	equ	272dh
  2739                	PUTNL	equ	2739h
  274D                	STOPESC	equ	274dh
  28F9                	INPT1	equ	28f9h
  2D13                	SETGXY	equ	2d13h
  2D47                	PSET	equ	2d47h
  2DE4                	LINEBF	equ	2de4h
  2E1F                	LINE	equ	2e1fh
  2E35                	LINEB	equ	2e35h
  2EF1                	PAINT2	equ	2ef1h
  2EFA                	PAINT	equ	2efah
  30CE                	PUTSINC	equ	30ceh
  30CF                	PUTS	equ	30cfh
  310F                	GC	equ	310fh
  34F9                	RESSTK	equ	34f9h
  397A                	PUTI1	equ	397ah		;for mkII
  3A99                	INLNUM	equ	3a99h
  3AA1                	PUTI2	equ	3aa1h
  3AA5                	I2TOA	equ	3aa5h
  3AAC                	FTOA	equ	3aach
  3BAF                	RNDPLS	equ	3bafh
                      	
                      	;BASIC command
  0781                	C_RUN	equ	0781h		;RUN
  07E0                	C_DATA	equ	07e0h		;DATA
  07E2                	C_REM	equ	07e2h		;REM
  087A                	C_LPRT	equ	087ah		;LPRINT
  087E                	C_PRT	equ	087eh		;PRINT
  1CD2                	C_LOCA	equ	1cd2h		;LOCATE
  1CF6                	C_CNSL	equ	1cf6h		;CONSOLE
  1D9B                	C_COLR	equ	1d9bh		;COLOR
  1E04                	C_SCRN	equ	1e04h		;SCREEN
                      	
                      	;work area
  FA18                	STOPFLG	equ	0fa18h		;03h=STOP,1bh=ESC
  FA19                	CMTSTAT	equ	0fa19h		;bit1=data received, bit4=error
  FA1A                	INPDEV	equ	0fa1ah		;input device
  FA1D                	CMTBUF	equ	0fa1dh		;CMT data
  FA1E                	ASTSTAT	equ	0fa1eh		;cload asterisk status
  FA1F                	BAUD	equ	0fa1fh		;baud rate (00=600baud, ff=1200baud)
  FA20                	HEIGHT	equ	0fa20h		;Y max+1
  FA27                	PORTB0H	equ	0fa27h		;port b0h
  FA28                	TMCNT	equ	0fa28h		;time count, fa28-fa2b
  FA2D                	CONSOL4	equ	0fa2dh		;console 4th parameter
  FA2E                	CSRBLNK	equ	0fa2eh		;0=cursor blink off, 1=on
  FA2F                	CSRSTAT	equ	0fa2fh		;0=normal, ff=reversed
  FA30                	GRPWRK	equ	0fa30h		;input graphic character work
  FA31                	GRPFLG	equ	0fa31h		;print graphic character flag
  FA32                	FKEYCNT	equ	0fa32h		;function key counter
  FA51                	RSEED	equ	0fa51h		;random seed, fa51-fa55
  FA57                	PRTPOS	equ	0fa57h		;printer head position
  FA58                	DEVICE	equ	0fa58h		;output device
  FA5B                	STACK	equ	0fa5bh		;initial stack pointer
  FA5D                	LINENUM	equ	0fa5dh		;current line number
  FA5F                	STARTAD	equ	0fa5fh		;basic program start address
  FA61                	CMDTBL	equ	0fa61h		;command jump table
  FAE5                	FNCTBL	equ	0fae5h		;function jump table
  FB3D                	FKEYTBL	equ	0fb3dh		;function key data
  FB8D                	FKEYAD	equ	0fb8dh		;function key address
  FB8F                	KYBFIN	equ	0fb8fh		;key buffer pointer for input
  FB90                	KYBFOUT	equ	0fb90h		;key buffer pointer for output
  FB92                	KYBFSZ	equ	0fb92h		;key buffer size
  FB93                	KYBFAD	equ	0fb93h		;key buffer address
  FB95                	RSBFIN	equ	0fb95h		;RS-232C buffer in
  FB96                	RSBFOUT	equ	0fb96h		;RS-232C buffer out
  FB98                	RSBFSZ	equ	0fb98h		;RS-232C buffer size
  FB99                	RSBFAD	equ	0fb99h		;RS-232C buffer address
                      	
                      	;PLAY command
  FBA1                	BUFAIN	equ	0fba1h		;channelA buffer pointer for input
  FBA2                	BUFAOUT	equ	0fba2h		;channelA buffer pointer for output
  FBA4                	BUFASZ	equ	0fba4h		;channelA buffer size
  FBA5                	BUFAAD	equ	0fba5h		;channelA buffer address
  FBA7                	BUFBIN	equ	0fba7h		;channelB buffer in
  FBAD                	BUFCIN	equ	0fbadh		;channelC buffer in
                      	
  FBB9                	KEYBUF	equ	0fbb9h		;key buffer, fbb9-fbf8
  FBF9                	RSBUF	equ	0fbf9h		;RS-232C buffer, fbf9-fc38
                      	
                      	;PLAY command
  FC39                	BUFA	equ	0fc39h		;channelA buffer, fc39-fc78
  FC79                	BUFB	equ	0fc79h		;channelB buffer, fc79-fcb8
  FCB9                	BUFC	equ	0fcb9h		;channelC buffer, fcb9-fcf8
  FD14                	CHANNEL	equ	0fd14h		;channel (0-2)
  FD15                	SVBCK	equ	0fd15h		;backup of S/V-value
  FD17                	PLAYLEN	equ	0fd17h		;length
  FD18                	PLAYSTR	equ	0fd18h		;string address
  FD1B                	PLAYST	equ	0fd1bh		;PLAY status
  FD1D                	PLWKA	equ	0fd1dh		;PLAY work for channelA
  0000                	REMAIN	equ	0fd1dh-PLWKA	;remaining time
  0002                	PLLEN	equ	0fd1fh-PLWKA	;string length
  0003                	PLADR	equ	0fd20h-PLWKA	;string address
  000F                	OCTAVE	equ	0fd2ch-PLWKA	;O-value
  0010                	LENGTH	equ	0fd2dh-PLWKA	;L-value
  0011                	TEMPO	equ	0fd2eh-PLWKA	;T-value
  0012                	VOLUME	equ	0fd2fh-PLWKA	;V-value
  0013                	PERIOD	equ	0fd30h-PLWKA	;M-value
  FD42                	PLWKB	equ	0fd42h		;PLAY work for channelB
  FD67                	PLWKC	equ	0fd67h		;PLAY work for channelC
                      	
  FD8C                	PAGES	equ	0fd8ch		;How many pages?
  FD8D                	USREND	equ	0fd8dh		;user area end, fd8d-fd8e
  FD8F                	SCREEN2	equ	0fd8fh		;screen 2nd parameter-1
  FD90                	SCREEN3	equ	0fd90h		;screen 3rd parameter-1
                      	
  FD91                	VRAM	equ	0fd91h		;VRAM address (high)
  FD92                	SCREEN1	equ	0fd92h		;screen 1st parameter-1
  FD93                	COLOR1	equ	0fd93h		;color 1st parameter
  FD94                	COLOR2	equ	0fd94h		;color 2nd parameter
  FD95                	COLOR3	equ	0fd95h		;color 3rd parameter (1,2 -> 0,2)
  FD96                	M1COLOR	equ	0fd96h		;screen mode 1 color parameters, fd96-fd98
  FD99                	M2COLOR	equ	0fd99h		;screen mode 2 color parameters, fd99-fd9b
  FD9C                	M3COLOR	equ	0fd9ch		;screen mode 3 color parameters, fd9c-fd9e
  FD9F                	M4COLOR	equ	0fd9fh		;screen mode 4 color parameters, fd9f-fda1
  FDA2                	CONSOL1	equ	0fda2h		;console 1st line number+1
  FDA3                	CONSOL2	equ	0fda3h		;console last line number+1
  FDA5                	LASTLIN	equ	0fda5h		;sc12:(CONSOL2)-(CONSOL3) sc34:(CONSOLE2)
  FDA6                	CONSOL3	equ	0fda6h		;console 3rd parameter
  FDA7                	FKEYSFT	equ	0fda7h		;shift key status for function key display
  FDA8                	CSRY	equ	0fda8h		;cursor Y+1
  FDA9                	CSRX	equ	0fda9h		;cursor X+1
  FDAA                	CSRAD	equ	0fdaah		;cursor address
  FDAC                	WIDTH	equ	0fdach		;X max+1
  FDAE                	GRPX1	equ	0fdaeh		;graphic X, fdae-fdaf
  FDB0                	GRPY1	equ	0fdb0h		;graphic Y, fdb0-fdb1
  FDB7                	LINEST	equ	0fdb7h		;line connection status, (0=connect) fdb7-fdc6
  FDC8                	PAGE1	equ	0fdc8h		;page1 data, fdc8-fdfe
  FDFF                	PAGE2	equ	0fdffh		;page2 data, fdff-fe35
  FE36                	PAGE3	equ	0fe36h		;page3 data, fe36-fe6c
  FE6D                	PAGE4	equ	0fe6dh		;page4 data, fe6d-fea3
                      	
  FEA4                	INPTXY	equ	0fea4h		;INPUT command initial position
  FEA6                	INPTX	equ	0fea6h		;INPUT command end position
  FEA7                	INPTPAG	equ	0fea7h		;INPUT command page
  FEA8                	INSMODE	equ	0fea8h		;0=not insert mode, ff=insert mode
  FEAA                	STOPSC2	equ	0feaah		;screen 2nd parameter -1 in stop
  FEAB                	STOPSC3	equ	0feabh		;screen 3rd parameter -1 in stop
  FEAC                	ATTDAT	equ	0feach		;color attribute data
  FEAD                	GRPX2	equ	0feadh		;graphic X, fead-feae
  FEAF                	GRPY2	equ	0feafh		;graphic Y, feaf-feb0
  FEB1                	GRPX3	equ	0feb1h		;graphic work, feb1-feb2
  FEB3                	GRPY3	equ	0feb3h		;graphic work, feb3-feb4
  FEB5                	PAWRK	equ	0feb5h		;paint work, feb5-feb6
  FEB7                	PACNT	equ	0feb7h		;paint work
  FEC5                	BORDERA	equ	0fec5h		;paint border color attribute
  FEC6                	BORDERC	equ	0fec6h		;paint border color code
  FECA                	GMKYBUF	equ	0fecah		;reply to game key query
                      	;TARGET	equ	0fecbh		;target file name, fecb-fed0
  FED1                	FNAME	equ	0fed1h		;loading file name, fed1-fed6
  FED8                	VERIFY	equ	0fed8h		;load/verify flag 00h=load ffh=verify
  FEDA                	INPBUF	equ	0fedah		;input buffer, feda-ff21, (ff22)-(ff23)=00h
  FF25                	ARGTYP	equ	0ff25h		;0=value 1=string
  FF27                	STREND	equ	0ff27h		;strings area end
  FF29                	BASICAD	equ	0ff29h		;basic area start
  FF2D                	STRDSC1	equ	0ff2dh		;temporary string descriptor, ff2d-ff30
  FF31                	STRDSC2	equ	0ff31h		;ff31-ff34
  FF35                	STRDSC3	equ	0ff35h		;ff35-ff38
  FF39                	STRDSC4	equ	0ff39h		;ff39-ff3c
  FF3D                	STRAD	equ	0ff3dh		;string area start address - 1
  FF41                	GCWRK	equ	0ff41h		;garbage collection work
  FF45                	DATALN	equ	0ff45h		;data line number
  FF4E                	PROGAD	equ	0ff4eh		;program address in interpreting
  FF50                	TMP	equ	0ff50h		;temporary
  FF52                	STOPLN	equ	0ff52h		;line number at stop
  FF54                	STOPAD	equ	0ff54h		;stop address
  FF56                	VARAD	equ	0ff56h		;variable area start address
  FF58                	ARRAD	equ	0ff58h		;array area start address
  FF5A                	FREEAD	equ	0ff5ah		;free area start address
  FF5C                	DATAAD	equ	0ff5ch		;data command address
  FF5E                	FNPAR	equ	0ff5eh		;FN() parameter name
  FF60                	FNARG	equ	0ff60h		;FN() argument value, ff60-ff64
  FF65                	OPRTR	equ	0ff65h		;operator, FAC1-1
  FF66                	FAC1	equ	0ff66h		;floating accumlator 1, ff66-ff6a
  FF6D                	FAC2	equ	0ff6dh		;floating accumlator 2, ff6d-ff71
  FF72                	FAC3	equ	0ff72h		;floating accumlator 3 (string conversion)
  FF8A                	HOOK	equ	0ff8ah		;hook area, ff8a-ffe3
  FF93                	HOOKEDT	equ	0ff93h		;hook for screen editor
  FF99                	HOOKCLP	equ	0ff99h		;hook for changing link pointer
  FFD8                	HOOKSTP	equ	0ffd8h		;hook for stop key
  FFDB                	HOOK18H	equ	0ffdbh		;hook for 0018h
  FFE1                	HOOK38H	equ	0ffe1h		;hook for 0038h
                      	
  0020                	COLUMNS	equ	32
  0010                	ROWS	equ	16
                      	
  0081                	FOR_	equ	081h
  0083                	DATA_	equ	083h
  0088                	GOTO_	equ	088h
  0089                	RUN_	equ	089h
  008C                	GOSUB_	equ	08ch
  008E                	REM_	equ	08eh
  0093                	DEF_	equ	093h
  0095                	PRINT_	equ	095h
  009F                	SCREEN_	equ	09fh
  00AA                	CMDLAST	equ	0aah
  00C2                	TAB_	equ	0c2h
  00C3                	TO_	equ	0c3h
  00C4                	FN_	equ	0c4h
  00C5                	SPC_	equ	0c5h
  00C6                	INKEY_	equ	0c6h
  00C7                	THEN_	equ	0c7h
  00C8                	NOT_	equ	0c8h
  00C9                	STEP_	equ	0c9h
  00CA                	PLUS_	equ	0cah
  00CB                	MINUS_	equ	0cbh
  00CC                	ASTRSK_	equ	0cch
  00D1                	GT_	equ	0d1h
  00D2                	EQ_	equ	0d2h
  00D3                	LT_	equ	0d3h
  00D4                	FNC1ST	equ	0d4h
  00D7                	USR_	equ	0d7h
  00EA                	LEFT_	equ	0eah
  00EC                	MID_	equ	0ech
  00F1                	FNCLAST	equ	0f1h
                      	
                      	
                      	;;;;;;;;;;;;;;;;;;;;;;;;;
                      	
                      	;boot
  0000                		org	BOOT
  0000  F3            		di
  0001  C36316        		jp	COLD
                      	
                      	
                      	;check parameter
                      	; compare (hl) and data at (sp)
                      	;input: sp,hl
                      	;output: a=data, hl=data address, z-flag(a=00h or 3ah), c-flag(a=30h-39h)
  0004                	_CHKPAR:ds	CHKPAR-_CHKPAR
  0008                		org	CHKPAR
                      	
  0008  E3            		ex	(sp),hl
  0009  7E            		ld	a,(hl)
  000A  23            		inc	hl
  000B  E3            		ex	(sp),hl
  000C  BE            		cp	(hl)
  000D  C2DC03        		jp	nz,SNERR
                      	
                      	
                      	;skip space and analyze a byte
                      	;input: hl=address-1
                      	;output: a=data, hl=data address, z-flag(a=00h or 3ah), c-flag(a=30h-39h)
                      	;destroy: f
  0010                	_ANADAT:ds	ANADAT-_ANADAT
  0010                		org	ANADAT
                      	
  0010  CD573F        		call	SKIPSPINC
  0013  C3C00B        		jp	CHKFIG
                      	
                      	
                      	;jump hook
  0016                	_JPHK1:	ds	JPHK1-_JPHK1
  0018                		org	JPHK1
                      	
  0018  C3DBFF        		jp	HOOK18H
                      	
                      	
                      	;compare hl and de
                      	;input: hl,de
                      	;output: f
                      	;destroy: af
  001B                	_CPHLDE:ds	CPHLDE-_CPHLDE
  0020                		org	CPHLDE
                      	
  0020  7C            		ld	a,h
  0021  92            		sub	d
  0022  C0            		ret	nz
  0023  7D            		ld	a,l
  0024  93            		sub	e
  0025  C9            		ret
                      	
                      	
                      	;check sign of FAC1
                      	;input: FAC1
                      	;output: a(0:FAC1=0 1:FAC1>0 ff:FAC1<0), z(1:FAC1=0)
                      	;destroy: af
  0026                	_CHKSGN:ds	CHKSGN-_CHKSGN
  0028                		org	CHKSGN
  0028  3A6AFF        		ld	a,(FAC1+4)
  002B  B7            		or	a
  002C  C8            		ret	z
  002D  C30239        		jp	CHKSGN2
                      	
                      	
                      	;jump hook
  0030                	_JPHK2:	ds	JPHK2-_JPHK2
  0038                		org	JPHK2
                      	
  0038  C3E1FF        		jp	HOOK38H
                      	
                      	
                      	;hot start
  003B                	HOT:
                      	;initialize function key
  003B  217001        		ld	hl,FKEYLST
  003E  113DFB        		ld	de,FKEYTBL
  0041  0E0A          		ld	c,0ah
  0043                	SETFKLP1:
  0043  7E            		ld	a,(hl)
  0044  23            		inc	hl
  0045  E5            		push	hl		;
  0046  CDB525        		call	CNVASC
                      	
  0049  0608          		ld	b,08h
  004B                	SETFKLP2:
  004B  05            		dec	b
  004C  12            		ld	(de),a
  004D  13            		inc	de
  004E  23            		inc	hl
  004F  7E            		ld	a,(hl)
  0050  B7            		or	a
  0051  F24B00        		jp	p,SETFKLP2
                      	
  0054  E1            		pop	hl		;
  0055  7E            		ld	a,(hl)
  0056  23            		inc	hl
                      	
  0057                	SETFKLP3:
  0057  12            		ld	(de),a
  0058  13            		inc	de
  0059  AF            		xor	a
  005A  10FB          		djnz	SETFKLP3
                      	
  005C  0D            		dec	c
  005D  20E4          		jr	nz,SETFKLP1
  005F  CDAF12        		call	PRTFKEY
                      	
                      	;check external ROM
  0062  2A0040        		ld	hl,(4000h)
  0065  114142        		ld	de,'A'+'B'*100h
  0068  E7            		rst	CPHLDE
  0069  2A0240        		ld	hl,(4002h)
  006C  CCA507        		call	z,JPHL
                      	
                      	;How Many Pages?
  006F                	PAGELP:
  006F  211601        		ld	hl,HOWMANY
  0072  CDCF30        		call	PUTS
  0075  CDF928        		call	INPT1
  0078  D7            		rst	ANADAT
  0079  1600          		ld	d,00h
  007B  3017          		jr	nc,SETPAGEMAX
  007D  42            		ld	b,d		;=0
  007E  CDD627        		call	ATOI2LEN
  0081  7A            		ld	a,d
  0082  B7            		or	a
  0083  2008          		jr	nz,PAGEFC
  0085  7B            		ld	a,e
  0086  3D            		dec	a
  0087  218CFD        		ld	hl,PAGES
  008A  BE            		cp	(hl)
  008B  3814          		jr	c,SETPAGE
  008D                	PAGEFC:
  008D  1E08          		ld	e,08h		;?FC Error
  008F                	PAGEERR:
  008F  CDBF03        		call	PRTERR
  0092  18DB          		jr	PAGELP
                      	
  0094                	SETPAGEMAX:
  0094  1E02          		ld	e,02h		;?SN Error
  0096  20F7          		jr	nz,PAGEERR
  0098  B7            		or	a
  0099  1E26          		ld	e,26h		;?MO Error
  009B  20F2          		jr	nz,PAGEERR	;colon
                      	
  009D  218CFD        		ld	hl,PAGES
  00A0  5E            		ld	e,(hl)
  00A1                	SETPAGE:
  00A1  73            		ld	(hl),e
                      	
  00A2  CDDB34        		call	NEW
  00A5  211101        		ld	hl,ENDTBL-1
  00A8  19            		add	hl,de
  00A9  66            		ld	h,(hl)
  00AA  2EFF          		ld	l,0ffh
  00AC  228DFD        		ld	(USREND),hl
  00AF  013200        		ld	bc,50
  00B2  CDBC35        		call	CLRMAIN
                      	
                      	;print start message
  00B5  CDD43F        		call	CHK6001A
  00B8  212701        		ld	hl,SYSNAME
  00BB  2003          		jr	nz,PC6001
  00BD  212C01        		ld	hl,SYSNAME2
  00C0                	PC6001:
  00C0  CDCF30        		call	PUTS
  00C3  213801        		ld	hl,BASICVER
  00C6  CDCF30        		call	PUTS
  00C9  CDE934        		call	CHKFRE
  00CC  212F00        		ld	hl,002fh	;for compatibility with N60-BASIC
  00CF  19            		add	hl,de
  00D0  CDA13A        		call	PUTI2
  00D3  210601        		ld	hl,BFREE
  00D6                	PUTSEDIT:
  00D6  CDCF30        		call	PUTS
  00D9  C34204        		jp	EDIT
                      	
                      	
                      	;initialize table
  00DC                	IOTBL93:
  00DC  C0            		db	0c0h		;8255 portA=mode2 portB=mode0
  00DD  0D            		db	0dh		;enable 8255 INT for writing
  00DE  09            		db	09h		;enable 8255 INT for reading
  00DF  05            		db	05h		;CGROM off
  00E0  03            		db	03h		;CRTC
  00E1  0F            		db	0fh		;nobf=1 (for emulator?)
                      	
  00E2                	IOTBL81:
  00E2  12            		db	12h		;reset error, DTR=1, RxE/TxEN=0
  00E3  12            		db	12h		; set twice for synchronous mode
  00E4  52            		db	52h		;reset, reset error, DTR=1, RxE/TxEN=0
  00E5  4F            		db	4fh		;stop bits=1, parity disable,
                      					; character length=8bits, baud rate factor=64
  00E6  37            		db	37h		;reset error, RTS/DTR/RxE/TxEN=1
                      	
                      	;for mkII
  00E7                	IOTBLF0:
                      	;F0:	db	0ddh		;0000-7fff:internal RAM for reading (test)
  00E7  71            	F0:	db	71h		;0000-3fff:internal ROM for reading
                      					;4000-7fff:external ROM for reading
  00E8  DD            	F1:	db	0ddh		;8000-ffff:internal RAM for reading
  00E9  55            	F2:	db	55h		;0000-ffff:internal RAM for writing
  00EA  C2            	F3:	db	0c2h		;wait & int control
  00EB  00            	F4:	db	00h		;INT1 address
  00EC  00            	F5:	db	00h		;INT2 address
  00ED  03            	F6:	db	03h		;timer count up
  00EE  06            	F7:	db	06h		;timer interrupt address
  00EF  C3            	F8:	db	0c3h		;CG rom access control
                      					; bit7: wait (0=off, 1=on)
                      					; bit6: read enable (0=disable, 1=enable)
                      					; bit5: bit2 mask (1=mask)
                      					; bit4: bit1 mask (1=mask)
                      					; bit3: bit0 mask (1=mask)
                      					; bit2-0: CGROM address (=A15-A13)
                      	
                      	;interrupt
  00F0                	INTTBL:
  00F0  B50E          		dw	INTKEY		;fa02 normal key
  00F2  590F          		dw	INT232		;fa04 RS-232C
  00F4  740F          		dw	INTTIM		;fa06 2ms timer
  00F6  9A0F          		dw	INTCMT		;fa08 CMT read
  00F8  A70F          		dw	EIRET		;fa0a ?
  00FA  A70F          		dw	EIRET		;fa0c ?
  00FC  A90F          		dw	INTWSTP		;fa0e CMT write stop
  00FE  A90F          		dw	INTRSTP		;fa10 CMT read stop
  0100  B10F          		dw	INTERR		;fa12 CMT error
  0102  B00E          		dw	INTGRP		;fa14 GRAPH key etc.
  0104  100F          		dw	INTGAM		;fa16 reply to game key query
                      	
                      	
  0106                	BFREE:
  0106  20427974657320		db	" Bytes free", 00h
  0112                	ENDTBL:
  0112  F9DFBF9F      		db	0f9h, 0dfh, 0bfh, 9fh
  0116                	HOWMANY:
  0116  486F77204D616E		db	"How Many Pages"
  0124                	QMARK:
  0124  3F2000        		db	"? ", 00h
  0127                	SYSNAME:
  0127  9ADE96FD00    		db	9ah, 0deh, 96h, 0fdh, 00h
  012C                	SYSNAME2:
  012C  436F6D70617469		db	"Compatible ", 00h
  0138                	BASICVER:
  0138  42415349432056		db	"BASIC Ver.0.7.3", 0dh, 0ah, 00h
                      	
  014A                	PAGEDATA:
  014A  C0            		db	0c0h		;fd91	VRAM address
  014B  00            		db	00h		;fd92	screen 1st parameter-1
  014C  01            		db	01h		;fd93	color 1st parameter
  014D  00            		db	00h		;fd94	color 2nd parameter
  014E  00            		db	00h		;fd95	color 3rd parameter (1,2 -> 0,2)
  014F  010000        		db	01h, 00h, 00h	;fd96	screen mode 1 color parameters
  0152  010000        		db	01h, 00h, 00h	;fd99	screen mode 2 color parameters
  0155  020000        		db	02h, 00h, 00h	;fd9c	screen mode 3 color parameters
  0158  030000        		db	03h, 00h, 00h	;fd9f	screen mode 4 color parameters
  015B  01            		db	01h		;fda2	console 1st line number+1
  015C  10            		db	10h		;fda3	console last line number+1
  015D  00            		db	00h		;fda4
  015E  0F            		db	0fh		;fda5	sc12:(fda3)-(fda6) sc34:(fda3)
  015F  01            		db	01h		;fda6	console 3rd parameter
  0160  00            		db	00h		;fda7	shift key status for function key disp
  0161  01            		db	01h		;fda8	cursor Y+1
  0162  01            		db	01h		;fda9	cursor X+1
  0163  00C2          		dw	0c200h		;fdaa	cursor address
  0165  20            		db	COLUMNS		;fdac	X max+1
  0166  00            		db	00h		;fdad
  0167  0000          		dw	0000h		;fdae	graphic X
  0169  0000          		dw	0000h		;fdb0	graphic Y
  016B  00            		db	00h		;fdb2
  016C  00            		db	00h		;fdb3
  016D  00            		db	00h		;fdb4
  016E  00            		db	00h		;fdb5
  016F  10            		db	10h		;fdb6	LINEST-1
                      	
                      	
  0170                	FKEYLST:
  0170  9A20          		db	9ah, ' '	;COLOR
  0172  A322          		db	0a3h, 22h	;CLOAD"
  0174  8820          		db	88h, ' '	;GOTO
  0176  9720          		db	97h, ' '	;LIST
  0178  890D          		db	89h, 0dh	;RUN
  017A  9F20          		db	9fh, ' '	;SCREEN
  017C  A422          		db	0a4h, 22h	;CSAVE"
  017E  9520          		db	95h, ' '	;PRINT
  0180  A720          		db	0a7h, ' '	;PLAY
  0182  960D          		db	96h, 0dh	;CONT
                      	
                      	
  0184                	OPRTBL:
                      	;c4-c7
                      	;	dw					F_FN,	F_SPC,	F_INKY,	C_THEN
                      	;c8-cf
  0184  F80BDC036F3685		dw	O_NOT,	C_STEP,	O_PLS,	O_MNS,	O_MUL,	O_DIV,	O_POW,	O_AND
                      	;d0-d3
  0194  C90B0A0C      		dw	O_OR,	O_GTEQLT
                      	
  0198                	CMDLST:
                      	;80-8f
  0198  65356306DC35E0		dw	C_END,	C_FOR,	C_NEXT,	C_DATA,	C_INPT,	C_DIM,	C_READ,	C_LET
  01A8  A6078107320816		dw	C_GOTO,	C_RUN,	C_IF,	C_RSTR,	C_GSUB,	C_RET,	C_REM,	C_STOP
                      	;90-9f
  01B8  B60D0B087A0857		dw	C_OUT,	C_ON,	C_LPRT,	C_DEF,	C_POKE,	C_PRT,	C_CONT,	C_LIST
  01C8  BE0593359B1D38		dw	C_LLST,	C_CLR,	C_COLR,	C_PSET,	C_PRST,	C_LINE,	C_PAIN,	C_SCRN
                      	;a0-aa
  01D8  FB1DD21CF61C5A		dw	C_CLS,	C_LOCA,	C_CNSL,	C_CLD,	C_CSV,	C_EXEC,	C_SOUN,	C_PLAY
  01E8  E6224E22CD34  		dw	C_KEY,	C_LCPY,	C_NEW
                      	
  01EE                	FNCLST:
                      	;d4-df
  01EE  0A3980391639E5		dw					F_SGN,	F_INT,	F_ABS,	F_USR
  01F6  1332A80D330D41		dw	F_FRE,	F_INP,	F_LPOS,	F_POS,	F_SQR,	F_RND,	F_LOG,	F_EXP
                      	;e0-ef
  0206  753D883D533EEE		dw	F_COS,	F_SIN,	F_TAN,	F_PEEK,	F_LEN,	F_HEX,	F_STR,	F_VAL
  0216  AC31B631C031C7		dw	F_ASC,	F_CHR,	F_LEFT,	F_RGT,	F_MID,	F_POIN,	F_CSRL,	F_STCK
                      	;f0-f1
  0226  2E226F1E      		dw	F_STRG,	F_TIME
                      	
                      	
  022A                	CMDNAME:
  022A  C54E44C64F52CE		db	'E'+80h,"ND",	'F'+80h,"OR",	'N'+80h,"EXT",	'D'+80h,"ATA"
  0238  C94E505554C449		db	'I'+80h,"NPUT",	'D'+80h,"IM",	'R'+80h,"EAD",	'L'+80h,"ET"
  0247  C74F544FD2554E		db	'G'+80h,"OTO",	'R'+80h,"UN",	'I'+80h,"F",	'R'+80h,"ESTORE"
  0257  C74F535542D245		db	'G'+80h,"OSUB",	'R'+80h,"ETURN",'R'+80h,"EM",	'S'+80h,"TOP"
                      	
  0269  CF5554CF4ECC50		db	'O'+80h,"UT",	'O'+80h,"N",	'L'+80h,"PRINT",'D'+80h,"EF"
  0277  D04F4B45D05249		db	'P'+80h,"OKE",	'P'+80h,"RINT",	'C'+80h,"ONT",	'L'+80h,"IST"
  0288  CC4C495354C34C		db	'L'+80h,"LIST",	'C'+80h,"LEAR",	'C'+80h,"OLOR",	'P'+80h,"SET"
  029B  D05245534554CC		db	'P'+80h,"RESET",'L'+80h,"INE",	'P'+80h,"AINT",	'S'+80h,"CREEN"
                      	
  02B0  C34C53CC4F4341		db	'C'+80h,"LS",	'L'+80h,"OCATE",'C'+80h,"ONSOLE",'C'+80h,"LOAD"
  02C5  C353415645C558		db	'C'+80h,"SAVE",	'E'+80h,"XEC",	'S'+80h,"OUND",	'P'+80h,"LAY"
  02D7  CB4559CC434F50		db	'K'+80h,"EY",	'L'+80h,"COPY",	'N'+80h,"EW"
                      	
  02E2                	FNCNAME:
  02E2  D4414228D44F  		db					'T'+80h,"AB(",	'T'+80h,"O"
  02E8  C64ED3504328C9		db	'F'+80h,"N",	'S'+80h,"PC(",	'I'+80h,"NKEY$",'T'+80h,"HEN"
  02F8  CE4F54D3544550		db	'N'+80h,"OT",	'S'+80h,"TEP",	'+'+80h,	'-'+80h
  0301  AAAFDEC14E44  		db	'*'+80h,	'/'+80h,	'^'+80h,	'A'+80h,"ND"
                      	
  0307  CF52BEBDBC    		db	'O'+80h,"R",	'>'+80h,	'='+80h,	'<'+80h
  030C  D3474EC94E54C1		db	'S'+80h,"GN",	'I'+80h,"NT",	'A'+80h,"BS",	'U'+80h,"SR"
  0318  C65245C94E50CC		db	'F'+80h,"RE",	'I'+80h,"NP",	'L'+80h,"POS",	'P'+80h,"OS"
  0325  D35152D24E44CC		db	'S'+80h,"QR",	'R'+80h,"ND",	'L'+80h,"OG",	'E'+80h,"XP"
                      	
  0331  C34F53D3494ED4		db	'C'+80h,"OS",	'S'+80h,"IN",	'T'+80h,"AN",	'P'+80h,"EEK"
  033E  CC454EC8455824		db	'L'+80h,"EN",	'H'+80h,"EX$",	'S'+80h,"TR$",	'V'+80h,"AL"
  034C  C15343C3485224		db	'A'+80h,"SC",	'C'+80h,"HR$",	'L'+80h,"EFT$",	'R'+80h,"IGHT$"
  035E  CD494424D04F49		db	'M'+80h,"ID$",	'P'+80h,"OINT",	'C'+80h,"SRLIN",'S'+80h,"TICK"
                      	
  0372  D354524947D449		db	'S'+80h,"TRIG",	'T'+80h,"IME",	80h
                      	
                      	
  037C                	_ERRBELL:ds	ERRSTR-1-_ERRBELL
  0383                		org	ERRSTR-1
  0383                	ERRBELL:
  0383  07            		db	07h
                      	
  0384                	_ERRSTR:ds	ERRSTR-_ERRSTR
  0384                		org	ERRSTR
  0384  204572726F7200		db	" Error", 00h
                      	
  038B                	_ERRIN:	ds	ERRIN-_ERRIN
  038B                		org	ERRIN
  038B  20696E2000    		db	" in ", 00h
                      	
  0390                	_OK:
  0390                		ds	OK-_OK
  0390                		org	OK
  0390  4F6B0D0A00    		db	"Ok", 0dh, 0ah, 00h
                      	
  0395                	ERRID:
  0395  4E46534E52474F		db	"NF", "SN", "RG", "OD", "FC", "OV", "OM", "UL"
  03A5  425344442F3049		db	"BS", "DD", "/0", "ID", "TM", "OS", "LS", "ST"
  03B5  434E554654524D		db	"CN", "UF", "TR", "MO", "FD"
                      	
                      	
                      	;print error message
                      	;input: e=error number
                      	;destroy: af,bc,de,hl
  03BF                	PRTERR:
  03BF  3E3F          		ld	a,'?'
  03C1  CD7510        		call	PRTC
  03C4  219503        		ld	hl,ERRID
  03C7  1600          		ld	d,00h
  03C9  19            		add	hl,de
  03CA  7E            		ld	a,(hl)
  03CB  CD7510        		call	PRTC
  03CE  23            		inc	hl
  03CF  7E            		ld	a,(hl)
  03D0  CD7510        		call	PRTC
  03D3  218303        		ld	hl,ERRBELL
  03D6  C3CF30        		jp	PUTS
                      	
                      	
  03D9                	NFERR:
  03D9  1E00          		ld	e,00h
  03DB  01            		db	01h		;ld bc,****
  03DC                	F_HEX:
  03DC                	C_TO:
  03DC                	C_TAB:
  03DC                	F_SPC:
  03DC                	C_THEN:
  03DC                	C_STEP:
  03DC                	SNERR:
  03DC  1E02          		ld	e,02h
  03DE  01            		db	01h		;ld bc,****
  03DF                	RGERR:
  03DF  1E04          		ld	e,04h
  03E1  01            		db	01h		;ld bc,****
  03E2                	ODERR:
  03E2  1E06          		ld	e,06h
  03E4  01            		db	01h		;ld bc,****
  03E5                	FCERR:
  03E5                	F_USR:
  03E5  1E08          		ld	e,08h
  03E7  01            		db	01h		;ld bc,****
  03E8                	OVERR:
  03E8  1E0A          		ld	e,0ah
  03EA  01            		db	01h		;ld bc,****
  03EB                	OMERR:
  03EB  1E0C          		ld	e,0ch
  03ED  01            		db	01h		;ld bc,****
  03EE                	ULERR:
  03EE  1E0E          		ld	e,0eh
  03F0  01            		db	01h		;ld bc,****
  03F1                	BSERR:
  03F1  1E10          		ld	e,10h
  03F3  01            		db	01h		;ld bc,****
  03F4                	DDERR:
  03F4  1E12          		ld	e,12h
  03F6  01            		db	01h		;ld bc,****
  03F7                	DIV0ERR:
  03F7  1E14          		ld	e,14h
  03F9  01            		db	01h		;ld bc,****
  03FA                	IDERR:
  03FA  1E16          		ld	e,16h
  03FC  01            		db	01h		;ld bc,****
  03FD                	TMERR:
  03FD  1E18          		ld	e,18h
  03FF  01            		db	01h		;ld bc,****
  0400                	OSERR:
  0400  1E1A          		ld	e,1ah
  0402  01            		db	01h		;ld bc,****
  0403                	LSERR:
  0403  1E1C          		ld	e,1ch
  0405  01            		db	01h		;ld bc,****
  0406                	STERR:
  0406  1E1E          		ld	e,1eh
  0408  01            		db	01h		;ld bc,****
  0409                	CNERR:
  0409  1E20          		ld	e,20h
  040B  01            		db	01h		;ld bc,****
  040C                	UFERR:
  040C  1E22          		ld	e,22h
  040E  01            		db	01h		;ld bc,****
  040F                	TRERR:
  040F  1E24          		ld	e,24h
  0411  01            		db	01h		;ld bc,****
  0412                	MOERR:
  0412  1E26          		ld	e,26h
  0414  01            		db	01h		;ld bc,****
  0415                	FDERR:
  0415  1E28          		ld	e,28h
                      	
  0417  ED7B5BFA      		ld	sp,(STACK)
  041B  AF            		xor	a
  041C  3231FF        		ld	(STRDSC2),a
  041F  3235FF        		ld	(STRDSC3),a
  0422  3239FF        		ld	(STRDSC4),a
  0425  3D            		dec	a
  0426  3255FF        		ld	(STOPAD+1),a	;(STOPAD)>=fa00h
  0429  CD5709        		call	PRTNLXTXT
  042C  CDBF03        		call	PRTERR
                      	
  042F                	PRTLNUM:
  042F  CDB31B        		call	PLSTOP
                      	
  0432  2A5DFA        		ld	hl,(LINENUM)
  0435  7C            		ld	a,h
  0436  A5            		and	l
  0437  3C            		inc	a
  0438  C4993A        		call	nz,INLNUM	;if (LINENUM)<>0ffffh
  043B  C34204        		jp	EDIT
                      	
                      	
                      	;program edit and direct command mode
  043E                	_EDIT:	ds	EDIT-_EDIT
  0442                		org	EDIT
                      	
  0442  CD93FF        		call	HOOKEDT
  0445  CD5810        		call	CLRKBF
  0448  CD5409        		call	PRTNLXALL
  044B  219003        		ld	hl,OK
  044E  CDCF30        		call	PUTS
  0451                	EDIT2:
  0451  CD0127        		call	PUTPRNL
  0454  21FFFF        		ld	hl,0ffffh
  0457  225DFA        		ld	(LINENUM),hl
                      	
  045A                	EDITLP:
  045A  CDF928        		call	INPT1		;hl=INPBUF-1
  045D  DCB31B        		call	c,PLSTOP
  0460  CD2405        		call	CNVIL
  0463  21D9FE        		ld	hl,INPBUF-1
  0466  D7            		rst	ANADAT		;
  0467  3C            		inc	a
  0468  3D            		dec	a
  0469  28EF          		jr	z,EDITLP
  046B  D20A07        		jp	nc,INTPRT	;
                      	
                      	
                      	;change program
  046E                	CHGPRG:
  046E  CD6B07        		call	GETLN
  0471  E5            		push	hl		;buffer address after line number
  0472  D5            		push	de		;line number
  0473  E5            		push	hl		;buffer address after line number
  0474  CDF004        		call	SRCHLN
  0477  DCDA04        		call	c,DELPRG	;c-flag=1
  047A  E1            		pop	hl		;buffer address after line number
  047B  34            		inc	(hl)
  047C  35            		dec	(hl)
  047D  2005          		jr	nz,INSPRG
  047F  3836          		jr	c,CHGLKP	;
  0481  C3EE03        		jp	ULERR
                      	
                      	;insert program
  0484                	INSPRG:
  0484  AF            		xor	a
  0485  110500        		ld	de,0005h
  0488                	PRGLP:
  0488  1C            		inc	e
  0489  3C            		inc	a
  048A  23            		inc	hl
  048B  34            		inc	(hl)
  048C  35            		dec	(hl)
  048D  20F9          		jr	nz,PRGLP
                      	
  048F  2A56FF        		ld	hl,(VARAD)
  0492  E5            		push	hl
                      	;	or	a
  0493  ED42          		sbc	hl,bc
  0495  E5            		push	hl		;slide size
  0496  09            		add	hl,bc		;old (VARAD)
  0497  19            		add	hl,de		;old (VARAD)+size
  0498  CDEC34        		call	CHKSTK
  049B  2256FF        		ld	(VARAD),hl
  049E  EB            		ex	de,hl
  049F  C1            		pop	bc		;slide size
  04A0  E1            		pop	hl		;old (VARAD)
  04A1  2B            		dec	hl
  04A2  1B            		dec	de
  04A3  EDB8          		lddr
                      	
  04A5                	COPYPRG:
  04A5  23            		inc	hl		;address to be inserted in
  04A6  23            		inc	hl
  04A7  13            		inc	de
  04A8  13            		inc	de
  04A9  73            		ld	(hl),e
  04AA  23            		inc	hl
  04AB  72            		ld	(hl),d
  04AC  23            		inc	hl
  04AD  D1            		pop	de		;line number
  04AE  73            		ld	(hl),e
  04AF  23            		inc	hl
  04B0  72            		ld	(hl),d
  04B1  23            		inc	hl
  04B2  EB            		ex	de,hl
  04B3  E1            		pop	hl		;buffer address after line number
                      	
  04B4  4F            		ld	c,a		;b=0
  04B5  EDB0          		ldir
                      	;	jr	CHGLKP
                      	
                      	
                      	;change link pointer
  04B7                	CHGLKP:
  04B7  CD99FF        		call	HOOKCLP
  04BA  CDF934        		call	RESSTK
  04BD  CDC204        		call	CHGLKPMAIN
  04C0  188F          		jr	EDIT2
                      	
                      	
                      	;change link pointer (main)
                      	;output: hl=last address
                      	;destroy: af,bc,de
  04C2                	CHGLKPMAIN:
  04C2  ED5B5FFA      		ld	de,(STARTAD)
                      	;skip address and line number
  04C6                	CHGLKPLP:
  04C6  62            		ld	h,d
  04C7  6B            		ld	l,e
  04C8  7E            		ld	a,(hl)
  04C9  23            		inc	hl
  04CA  B6            		or	(hl)
  04CB  C8            		ret	z
                      	
  04CC  010300        		ld	bc,0003h
  04CF  09            		add	hl,bc
                      	
                      	;search for next line
  04D0  AF            		xor	a
  04D1  4F            		ld	c,a		;b=0
  04D2  EDB1          		cpir
                      	
                      	;next line address
  04D4  EB            		ex	de,hl
  04D5  73            		ld	(hl),e
  04D6  23            		inc	hl
  04D7  72            		ld	(hl),d
  04D8  18EC          		jr	CHGLKPLP
                      	
                      	
                      	;input: bc=address to be deleted, hl=next line address
                      	;output: c-flag=1
                      	;destroy: af,de,hl
  04DA                	DELPRG:
  04DA  C5            		push	bc		;address to be deleted
  04DB  EB            		ex	de,hl		;de <- next line address
  04DC  2A56FF        		ld	hl,(VARAD)
  04DF  B7            		or	a
  04E0  ED52          		sbc	hl,de
  04E2  44            		ld	b,h
  04E3  4D            		ld	c,l
  04E4  EB            		ex	de,hl		;hl <- next line address
  04E5  D1            		pop	de		;address to be deleted
  04E6  D5            		push	de
  04E7  EDB0          		ldir
  04E9  ED5356FF      		ld	(VARAD),de
  04ED  C1            		pop	bc
  04EE  37            		scf
  04EF  C9            		ret
                      	
                      	
                      	;search for line number
                      	;input: de=line number
                      	;output: z=1,c=1) bc=address, hl=next line address
                      	;	 z=0,c=0) bc=next line address, hl=next next line address
                      	;	 z=1,c=0) bc=hl=end address
                      	;destroy: af
  04F0                	SRCHLN:
  04F0  2A5DFA        		ld	hl,(LINENUM)
  04F3  E7            		rst	CPHLDE
  04F4  300C          		jr	nc,SRCHSTART	;less than or equal to current line number
  04F6  2A4EFF        		ld	hl,(PROGAD)
  04F9                	SRCHLP1:
  04F9  7E            		ld	a,(hl)
  04FA  23            		inc	hl
  04FB  B7            		or	a
  04FC  20FB          		jr	nz,SRCHLP1
                      	
                      	;for PORTOPIA (mk2)
                      	;	jr	SRCHLP2
  04FE  CD0505        		call	SRCHLP2
  0501  D8            		ret	c
                      	
  0502                	SRCHSTART:
  0502  2A5FFA        		ld	hl,(STARTAD)	;search from program start address
                      	
  0505                	SRCHLP2:
  0505  44            		ld	b,h
  0506  4D            		ld	c,l
  0507  7E            		ld	a,(hl)
  0508  23            		inc	hl
  0509  B6            		or	(hl)
  050A  2815          		jr	z,SRCHEND2	;program end
  050C  23            		inc	hl
  050D  23            		inc	hl
  050E  7E            		ld	a,(hl)
  050F  2B            		dec	hl
  0510  BA            		cp	d
  0511  2002          		jr	nz,SRCHNZ
  0513  7E            		ld	a,(hl)
  0514  BB            		cp	e
  0515                	SRCHNZ:
  0515  0A            		ld	a,(bc)
  0516  2B            		dec	hl
  0517  66            		ld	h,(hl)
  0518  6F            		ld	l,a
  0519  38EA          		jr	c,SRCHLP2	;(hl,hl-1)-de
  051B  2001          		jr	nz,SRCHEND	;(hl,hl-1)-de
  051D  37            		scf
  051E                	SRCHEND:
  051E  0B            		dec	bc
  051F  2B            		dec	hl
  0520  C9            		ret
                      	
  0521                	SRCHEND2:
  0521  2B            		dec	hl
  0522  18FA          		jr	SRCHEND
                      	
                      	
                      	;convert to intermediate language
                      	;input: hl=INPBUF-1
  0524                	CNVIL:
  0524  54            		ld	d,h
  0525  5D            		ld	e,l
                      	
  0526                	ILLP1:
  0526  13            		inc	de
  0527  1A            		ld	a,(de)
                      	
  0528  B7            		or	a
  0529  FA2605        		jp	m,ILLP1		;skip kana
                      	
  052C  23            		inc	hl
  052D  2827          		jr	z,ILEND1	;00=line end
                      	
  052F  CDC00B        		call	CHKFIG
  0532  3818          		jr	c,ILPUT
                      	
  0534  FE22          		cp	22h		;double quotation mark
  0536  2817          		jr	z,ILDQ
  0538  FE14          		cp	14h
  053A  280C          		jr	z,ILGRP
  053C  FE2A          		cp	'*'
  053E  380C          		jr	c,ILPUT		;00h-29h
  0540  FE3F          		cp	'?'
  0542  203F          		jr	nz,ILCMP
                      	
                      	;question mark
  0544  3695          		ld	(hl),PRINT_	;95h
  0546  18DE          		jr	ILLP1
                      	
                      	;graphic character
  0548                	ILGRP:
  0548  77            		ld	(hl),a
  0549  23            		inc	hl
  054A  13            		inc	de
  054B  1A            		ld	a,(de)
  054C                	ILPUT:
  054C  77            		ld	(hl),a
  054D  18D7          		jr	ILLP1
                      	
                      	
                      	;double quotation mark
  054F                	ILDQ:
  054F  77            		ld	(hl),a
                      	
                      	;REM
  0550                	ILREM:
  0550  4F            		ld	c,a		;double quotation mark or 0(REM)
  0551  0601          		ld	b,01h		;double quotation counter
                      	
                      	;REM,DATA,""
                      	;loop until (de)=0 or (de)=c
  0553                	ILLP2:
  0553  13            		inc	de
  0554  23            		inc	hl
  0555  1A            		ld	a,(de)
  0556                	ILEND1:
  0556  77            		ld	(hl),a
  0557  B7            		or	a
  0558  280E          		jr	z,ILEND2
  055A  FE22          		cp	22h
  055C  2001          		jr	nz,ILNZ
  055E  04            		inc	b
  055F                	ILNZ:
  055F  CB40          		bit	0,b
  0561  20F0          		jr	nz,ILLP2
  0563  B9            		cp	c
  0564  20ED          		jr	nz,ILLP2
  0566  18BE          		jr	ILLP1
                      	
  0568                	ILEND2:
  0568  23            		inc	hl
  0569  77            		ld	(hl),a		;a=0
  056A  23            		inc	hl
  056B  77            		ld	(hl),a		;a=0
  056C  C9            		ret
                      	
                      	
                      	;found
  056D                	ILFOUND:
  056D  F1            		pop	af		;cancel
  056E  E1            		pop	hl
  056F  70            		ld	(hl),b
  0570  78            		ld	a,b
  0571  D68E          		sub	REM_
  0573  28DB          		jr	z,ILREM
  0575  FEF5          		cp	DATA_-REM_
  0577  20AD          		jr	nz,ILLP1
                      	
                      	;DATA
  0579                	ILDAT:
  0579  013A00        		ld	bc,003ah	;b=0, c=':'
  057C  18D5          		jr	ILLP2
                      	
                      	
                      	;not found
  057E                	ILNC:
  057E  E1            		pop	hl
  057F  1A            		ld	a,(de)
  0580  77            		ld	(hl),a
  0581  18A3          		jr	ILLP1
                      	
                      	;compare with command string
  0583                	ILCMP:
  0583  CDEF0B        		call	TOUPPER
  0586  12            		ld	(de),a
                      	
  0587  E5            		push	hl
  0588  212A02        		ld	hl,CMDNAME
  058B  0680          		ld	b,80h
                      	
  058D                	ILLP3:
  058D  1A            		ld	a,(de)
  058E  F680          		or	80h
  0590  BE            		cp	(hl)
  0591  2018          		jr	nz,ILLP6
                      	
                      	;1st character matched
  0593  D5            		push	de
  0594                	ILLP4:
  0594  23            		inc	hl
  0595  7E            		ld	a,(hl)
  0596  07            		rlca
  0597  38D4          		jr	c,ILFOUND
                      	
  0599  78            		ld	a,b
  059A  FE88          		cp	GOTO_
  059C                	ILLP5:
  059C  13            		inc	de
  059D  1A            		ld	a,(de)
  059E  2004          		jr	nz,NOGOTO
  05A0  FE20          		cp	' '
  05A2  28F8          		jr	z,ILLP5
  05A4                	NOGOTO:
  05A4  CDEF0B        		call	TOUPPER
  05A7  BE            		cp	(hl)
  05A8  28EA          		jr	z,ILLP4
  05AA  D1            		pop	de
                      	
  05AB                	ILLP6:
  05AB  23            		inc	hl
  05AC  7E            		ld	a,(hl)
  05AD  07            		rlca
  05AE  30FB          		jr	nc,ILLP6
  05B0  04            		inc	b
                      	
  05B1  78            		ld	a,b
  05B2  FEF2          		cp	FNCLAST+1
  05B4  30C8          		jr	nc,ILNC
  05B6  FEAB          		cp	CMDLAST+1
  05B8  20D3          		jr	nz,ILLP3
  05BA  06C2          		ld	b,TAB_
  05BC  18CF          		jr	ILLP3
                      	
                      	
                      	;LLIST command
  05BE                	C_LLST:
  05BE  3E01          		ld	a,01h
  05C0  3258FA        		ld	(DEVICE),a
                      	;	jp	C_LIST
                      	
                      	
                      	;LIST command
  05C3                	C_LIST:
  05C3  F1            		pop	af		;cancel return address
  05C4  110000        		ld	de,0000h
  05C7  CD5F3F        		call	CHKCLN
  05CA  2814          		jr	z,LISTTOEND
                      	
  05CC  CD6B07        		call	GETLN
  05CF  D5            		push	de		;start line number
  05D0  CD5F3F        		call	CHKCLN
  05D3  281D          		jr	z,LISTSTRT
                      	
  05D5  FECB          		cp	MINUS_
  05D7  C2DC03        		jp	nz,SNERR
  05DA  CD5E3F        		call	CHKCLNINC
  05DD  2007          		jr	nz,LISTNZ
  05DF  D1            		pop	de
  05E0                	LISTTOEND:
  05E0  01FAFF        		ld	bc,65530
  05E3  C5            		push	bc
  05E4  180C          		jr	LISTSTRT
                      	
  05E6                	LISTNZ:
  05E6  CD6B07        		call	GETLN
  05E9  CD5F3F        		call	CHKCLN
  05EC  C2DC03        		jp	nz,SNERR
                      	
  05EF  E1            		pop	hl
  05F0  D5            		push	de		;push end line number
  05F1  EB            		ex	de,hl		;de <- start line number
                      	
                      	
                      	;de=start line number, (sp)=end line number
  05F2                	LISTSTRT:
  05F2  CDF004        		call	SRCHLN
  05F5  60            		ld	h,b
  05F6  69            		ld	l,c
                      	
  05F7                	LISTLP1:
  05F7  CD4D27        		call	STOPESC
                      	
  05FA  23            		inc	hl
  05FB  7E            		ld	a,(hl)
  05FC  23            		inc	hl
  05FD  B6            		or	(hl)
  05FE  2843          		jr	z,LISTEND2
                      	
  0600  CD3927        		call	PUTNL
                      	
  0603  D1            		pop	de		;end line number
  0604  D5            		push	de
                      	
  0605  23            		inc	hl
  0606  7E            		ld	a,(hl)
  0607  23            		inc	hl
  0608  E5            		push	hl
  0609  66            		ld	h,(hl)
  060A  6F            		ld	l,a
                      	
  060B  13            		inc	de
  060C  E7            		rst	CPHLDE
  060D  3033          		jr	nc,LISTEND
                      	
  060F  CDA13A        		call	PUTI2
  0612  3E20          		ld	a,' '
  0614  CDC726        		call	PUTDEV
                      	
  0617  E1            		pop	hl
                      	
  0618  010000        		ld	bc,0000h	;double quotation mark and DATA counter
  061B  50            		ld	d,b		;REM counter
  061C                	LISTLP3:
  061C  23            		inc	hl
  061D  7E            		ld	a,(hl)
  061E  B7            		or	a
  061F  28D6          		jr	z,LISTLP1
                      	
  0621  FE22          		cp	22h
  0623  2822          		jr	z,LISTDQ
                      	
  0625  CB40          		bit	0,b
  0627  2014          		jr	nz,LISTPUT
                      	
  0629  0C            		inc	c
  062A  0D            		dec	c
  062B  201D          		jr	nz,LISTCHKCLN
                      	
  062D  14            		inc	d
  062E  15            		dec	d
  062F  200C          		jr	nz,LISTPUT
                      	
  0631  FE8E          		cp	REM_
  0633  281C          		jr	z,LISTREM
  0635  FE83          		cp	DATA_
  0637  2819          		jr	z,LISTDATA
  0639  B7            		or	a
  063A  FA5306        		jp	m,LISTCMD
  063D                	LISTPUT:
  063D  CDC726        		call	PUTDEV
  0640  18DA          		jr	LISTLP3
                      	
  0642                	LISTEND:
  0642  F1            		pop	af		;cancel stack
  0643                	LISTEND2:
  0643  F1            		pop	af		;cancel stack
  0644  C34204        		jp	EDIT
                      	
  0647                	LISTDQ:
  0647  04            		inc	b
  0648  18F3          		jr	LISTPUT
                      	
  064A                	LISTCHKCLN:
  064A  FE3A          		cp	':'
  064C  20EF          		jr	nz,LISTPUT
  064E  0D            		dec	c
  064F  18EC          		jr	LISTPUT
                      	
  0651                	LISTREM:
  0651  14            		inc	d
  0652                	LISTDATA:
  0652  0C            		inc	c
  0653                	LISTCMD:
  0653  E5            		push	hl
  0654  CDB525        		call	CNVASC
                      	
  0657                	LISTLP4:
  0657  CDC726        		call	PUTDEV
  065A  23            		inc	hl
  065B  7E            		ld	a,(hl)
  065C  B7            		or	a
  065D  F25706        		jp	p,LISTLP4
  0660  E1            		pop	hl
  0661  18B9          		jr	LISTLP3
                      	
                      	
                      	;FOR command
  0663                	C_FOR:
  0663  F1            		pop	af		;cancel return address
  0664  CD7A33        		call	CHKVAR
  0667  7E            		ld	a,(hl)
  0668  FE28          		cp	'('
  066A  CADC03        		jp	z,SNERR
                      	
  066D  CD9432        		call	GETVAR
  0670  2A4EFF        		ld	hl,(PROGAD)
                      	
  0673  D5            		push	de		;variable address
                      	
  0674  CD9B34        		call	VARIN
  0677  CD7B0B        		call	CHKNUM
  067A  7E            		ld	a,(hl)
  067B  FEC3          		cp	TO_
  067D  C2DC03        		jp	nz,SNERR
  0680  23            		inc	hl
  0681  CD720B        		call	NUMARGMO
  0684  EB            		ex	de,hl
  0685  CD1F39        		call	PUSHF1		;TO value
  0688  EB            		ex	de,hl
  0689  CD5F3F        		call	CHKCLN		;a=(hl)
  068C  224EFF        		ld	(PROGAD),hl
  068F  CC4122        		call	z,SETPLS1	;no change in z-flag
  0692  2809          		jr	z,STEP1
  0694  FEC9          		cp	STEP_
  0696  C2DC03        		jp	nz,SNERR
  0699  23            		inc	hl
  069A  CD720B        		call	NUMARGMO
  069D                	STEP1:
  069D  CD4C39        		call	POPF2		;TO value
  06A0  C1            		pop	bc		;variable address
                      	
  06A1  2A4EFF        		ld	hl,(PROGAD)
  06A4  E5            		push	hl		;program address
  06A5  210200        		ld	hl,0002h
  06A8  39            		add	hl,sp
  06A9                	FORLP:
  06A9  CDD507        		call	SRCHSTK2
  06AC  280F          		jr	z,FOROK		;not found or gosub identifier found
                      	
                      	;check program address
  06AE  111100        		ld	de,0011h
  06B1  19            		add	hl,de
  06B2  5E            		ld	e,(hl)
  06B3  23            		inc	hl
  06B4  56            		ld	d,(hl)
  06B5  23            		inc	hl
  06B6  E3            		ex	(sp),hl
  06B7  E7            		rst	CPHLDE
  06B8  E3            		ex	(sp),hl
  06B9  20EE          		jr	nz,FORLP
                      	
  06BB  F9            		ld	sp,hl
  06BC  D5            		push	de		;program address
                      	
  06BD                	FOROK:
  06BD  2A5DFA        		ld	hl,(LINENUM)
  06C0  E5            		push	hl		;line number
  06C1  CD2E39        		call	PUSHF2		;TO value
  06C4  CD1F39        		call	PUSHF1		;STEP value
  06C7  C5            		push	bc		;variable address
  06C8  3E81          		ld	a,FOR_		;identifier
  06CA  F5            		push	af
  06CB  33            		inc	sp
  06CC  1819          		jr	INTPEND
                      	
                      	
                      	;
                      	;
                      	;
                      	;
  06CE                	_COMMAND:ds	CMDEND-14-_COMMAND
  06DC                		org	CMDEND-14
                      	
                      	;execute command
  06DC                	COMMAND:
  06DC  FEAB          		cp	CMDLAST+1	;abh
  06DE  D2DC03        		jp	nc,SNERR
  06E1  1161FA        		ld	de,CMDTBL
  06E4  CD493F        		call	JPTBL
                      	
                      	;intepretation end
  06E7                	INTPEND:
  06E7  2A4EFF        		ld	hl,(PROGAD)
                      	
                      	;command end
  06EA                	_CMDEND:ds	CMDEND-_CMDEND
  06EA                		org	CMDEND
                      	
  06EA  2B            		dec	hl
  06EB  D7            		rst	ANADAT
                      	
                      	;	call	CHKCLN
                      	
  06EC  CA0A07        		jp	z,INTPRT
  06EF  C3DC03        		jp	SNERR
                      	
                      	
                      	;continued from C_RUN
                      	;input: hl(=(STARTAD)), z(1=colon or 00h)
  06F2                	RUN2:
  06F2  2807          		jr	z,RUNLP
  06F4  EB            		ex	de,hl
  06F5  CDA607        		call	C_GOTO
  06F8  60            		ld	h,b
  06F9  69            		ld	l,c
  06FA  23            		inc	hl
                      	
  06FB                	RUNLP:
  06FB  7E            		ld	a,(hl)
  06FC  23            		inc	hl
  06FD  B6            		or	(hl)
  06FE  CA4204        		jp	z,EDIT
  0701  23            		inc	hl
  0702  5E            		ld	e,(hl)
  0703  23            		inc	hl
  0704  56            		ld	d,(hl)
  0705  ED535DFA      		ld	(LINENUM),de
                      	
                      	
                      	;0709h: used by PORTOPIA
  0709                	_INTPRT2:ds	INTPRT2-_INTPRT2
  0709                		org	INTPRT2
                      	
  0709  23            		inc	hl
                      	
  070A                	INTPRT:
  070A  CD5B27        		call	CHKSTOP
  070D  CA3635        		jp	z,STOP1
  0710  7E            		ld	a,(hl)
  0711  23            		inc	hl
  0712  224EFF        		ld	(PROGAD),hl
  0715  B7            		or	a
  0716  FADC06        		jp	m,COMMAND
  0719  28E0          		jr	z,RUNLP
  071B  FE3A          		cp	':'
  071D  28EB          		jr	z,INTPRT
  071F  FE20          		cp	' '
  0721  28E7          		jr	z,INTPRT
  0723  2B            		dec	hl
  0724  C3FC07        		jp	VAR
                      	
                      	
                      	;convert float to 2-byte integer for USR() (-32768~32767)
                      	;input: FAC1
                      	;output: de
                      	;destroy: af,bc,hl
  0727                	_FTOI2:	ds	FTOI2-_FTOI2
  0741                		org	FTOI2
                      	
  0741  CDBD0C        		call	FTOI
  0744  3A69FF        		ld	a,(FAC1+3)
  0747  AA            		xor	d
  0748  F0            		ret	p
  0749  C3E503        		jp	FCERR
                      	
                      	
                      	;get a 1-byte integer for function
                      	;input: hl=program address
                      	;output: FAC1,e, hl=next address, a(0=ok)
                      	;destroy: FAC2,f,bc
  074C                	FNCI1:
  074C  CD670B        		call	FNCNUM
                      	;	jp	FTOI1
                      	
                      	
                      	;convert float to 1-byte integer
                      	;input: FAC1
                      	;output: e, a(0=ok)
                      	;destroy: f,bc,de,hl
  074F                	FTOI1:
  074F  CD4107        		call	FTOI2
  0752  7A            		ld	a,d
  0753  B7            		or	a
  0754  C8            		ret	z
  0755  C3E503        		jp	FCERR
                      	
                      	
                      	;convert FAC1 and FAC2 to 2-byte integer
                      	;input: FAC1, FAC2
                      	;output: hl, de
                      	;destroy: af,bc,FAC1,FAC2
  0758                	F12TOI2:
  0758  3A25FF        		ld	a,(ARGTYP)
  075B  B7            		or	a
  075C  C2FD03        		jp	nz,TMERR
  075F  CD4107        		call	FTOI2
  0762  D5            		push	de		;
  0763  CD6E39        		call	EXFAC
  0766  CD4107        		call	FTOI2
  0769  E1            		pop	hl		;
  076A  C9            		ret
                      	
                      	
                      	;get line number from string (0-65529)
                      	;input: hl=program address
                      	;output: de=line number, hl=next address
                      	;destroy: af,bc
  076B                	GETLN:
  076B  CDD427        		call	ATOI2
  076E  E5            		push	hl
  076F  21F9FF        		ld	hl,65529
  0772  E7            		rst	CPHLDE
  0773  E1            		pop	hl
  0774  D0            		ret	nc
  0775  119919        		ld	de,6553
  0778                	GETLNLP:
  0778  2B            		dec	hl
  0779  7E            		ld	a,(hl)
  077A  FE20          		cp	' '
  077C  C0            		ret	nz
  077D  18F9          		jr	GETLNLP
                      	
                      	
                      	;run command
  077F                	_C_RUN:	ds	C_RUN-_C_RUN
  0781                		org	C_RUN
                      	
  0781  CDF934        		call	RESSTK
  0784  CD5F3F        		call	CHKCLN
  0787  EB            		ex	de,hl
  0788  2A5FFA        		ld	hl,(STARTAD)
  078B  C3F206        		jp	RUN2
                      	
                      	
                      	;GOSUB command
  078E                	C_GSUB:
  078E  CDA607        		call	C_GOTO
  0791  2B            		dec	hl
  0792                	GSUBLP:
  0792  CD5E3F        		call	CHKCLNINC
  0795  20FB          		jr	nz,GSUBLP
                      	
  0797  44            		ld	b,h
  0798  4D            		ld	c,l
                      	
  0799  CDE934        		call	CHKFRE
  079C  2A5DFA        		ld	hl,(LINENUM)
  079F  E3            		ex	(sp),hl
  07A0  C5            		push	bc		;program address
  07A1  3E8C          		ld	a,GOSUB_
  07A3  F5            		push	af		;gosub identifier
  07A4  33            		inc	sp
  07A5                	JPHL:
  07A5  E9            		jp	(hl)
                      	
                      	
                      	;GOTO command
                      	;output: bc=new address, hl=old address (after line number)
  07A6                	C_GOTO:
  07A6  CD6B07        		call	GETLN
  07A9  E5            		push	hl
  07AA  CDF004        		call	SRCHLN
  07AD  E1            		pop	hl
  07AE  D2EE03        		jp	nc,ULERR
  07B1  ED434EFF      		ld	(PROGAD),bc
  07B5  C9            		ret
                      	
                      	
                      	;RETURN command
  07B6                	C_RET:
  07B6  C1            		pop	bc		;return address
  07B7                	RETLP:
  07B7  CDD107        		call	SRCHSTK
  07BA  D2DF03        		jp	nc,RGERR	;not found
  07BD  2807          		jr	z,RETOK		;gosub identifier found
  07BF  211300        		ld	hl,0013h
  07C2  39            		add	hl,sp
  07C3  F9            		ld	sp,hl
  07C4  18F1          		jr	RETLP
                      	
  07C6                	RETOK:
  07C6  33            		inc	sp
  07C7  E1            		pop	hl
  07C8  224EFF        		ld	(PROGAD),hl
  07CB  E1            		pop	hl
  07CC  225DFA        		ld	(LINENUM),hl
  07CF  C5            		push	bc		;return address
  07D0  C9            		ret
                      	
                      	
                      	;search stack for for/gosub identifier
                      	;output: c-flag(1=found), z-flag(0=for/1=gosub if found)
                      	;destroy: af,de,hl
  07D1                	SRCHSTK:
  07D1  210200        		ld	hl,0002h
  07D4  39            		add	hl,sp
                      	
                      	;search (hl)
                      	;destroy: af,de
  07D5                	SRCHSTK2:
  07D5  ED5B5BFA      		ld	de,(STACK)
  07D9  E7            		rst	CPHLDE
  07DA  D0            		ret	nc		;(z-flag=1)
  07DB  7E            		ld	a,(hl)
  07DC  FE8C          		cp	GOSUB_
  07DE  37            		scf
  07DF  C9            		ret
                      	
                      	
                      	;DATA command
  07E0                	_C_DATA:ds	C_DATA-_C_DATA
  07E0                		org	C_DATA
                      	
  07E0  013A          		db	01h, ':'	;ld bc,0e3ah/nop
                      	
                      	
                      	;REM command
  07E2                	_C_REM:	ds	C_REM-_C_REM
  07E2                		org	C_REM
                      	
  07E2  0E00          		ld	c,00h
                      	
                      	;input: c
  07E4                	DATREM:
  07E4  0600          		ld	b,00h
  07E6                	DATALP:
  07E6  7E            		ld	a,(hl)
  07E7  B7            		or	a
  07E8  CAAB0B        		jp	z,SETPRGAD
                      	
  07EB  CB40          		bit	0,b
  07ED  2004          		jr	nz,DATANZ
  07EF  B9            		cp	c
  07F0  CAAB0B        		jp	z,SETPRGAD
  07F3                	DATANZ:
  07F3  23            		inc	hl
  07F4  FE22          		cp	22h		;double quotation mark
  07F6  20EE          		jr	nz,DATALP
  07F8  04            		inc	b
  07F9  18EB          		jr	DATALP
                      	
                      	
                      	;LET command
  07FB                	C_LET:
  07FB  F1            		pop	af		;cancel return address
  07FC                	VAR:
  07FC  CD7A33        		call	CHKVAR
  07FF  CD9432        		call	GETVAR
  0802  2A4EFF        		ld	hl,(PROGAD)
  0805  CD9B34        		call	VARIN
  0808  C3E706        		jp	INTPEND
                      	
                      	
                      	;ON command
  080B                	C_ON:
  080B  CDE40D        		call	INT1ARG
  080E  7E            		ld	a,(hl)
  080F  57            		ld	d,a		;
  0810  F604          		or	04h
  0812  FE8C          		cp	GOSUB_		;88h(GOTO) or 8ch(GOSUB)
  0814  C2DC03        		jp	nz,SNERR
                      	
  0817                	ONLP:
  0817  23            		inc	hl
  0818  1D            		dec	e
  0819  280E          		jr	z,ONZ
  081B  D5            		push	de
  081C  CD6B07        		call	GETLN
  081F  224EFF        		ld	(PROGAD),hl
  0822  D1            		pop	de
  0823  7E            		ld	a,(hl)
  0824  FE2C          		cp	','
  0826  28EF          		jr	z,ONLP
  0828  C9            		ret
                      	
  0829                	ONZ:
  0829  7A            		ld	a,d		;
  082A  FE88          		cp	GOTO_
  082C  CAA607        		jp	z,C_GOTO
  082F  C38E07        		jp	C_GSUB
                      	
                      	
                      	;IF command
  0832                	C_IF:
  0832  CD720B        		call	NUMARGMO
  0835  7E            		ld	a,(hl)
  0836  FEC7          		cp	THEN_
  0838  2805          		jr	z,IFOK
  083A  FE88          		cp	GOTO_
  083C  C2DC03        		jp	nz,SNERR
  083F                	IFOK:
  083F  3A6AFF        		ld	a,(FAC1+4)
  0842  B7            		or	a
  0843  289D          		jr	z,C_REM
                      	;	call	SKIPSPINC
                      	;	sub	'0'
                      	;	cp	'9'-'0'+1
                      	
  0845  D7            		rst	ANADAT
                      	
  0846  DAA607        		jp	c,C_GOTO
  0849                	IFEND:
  0849  224EFF        		ld	(PROGAD),hl
  084C  F1            		pop	af		;cancel return address
  084D  C30A07        		jp	INTPRT		;skip colon check
                      	
                      	
                      	;LPRINT command
  0850                	_C_LPRT:ds	C_LPRT-_C_LPRT
  087A                		org	C_LPRT
                      	
  087A  3E01          		ld	a,01h
  087C  1803          		jr	PRTDEV
                      	
                      	
                      	;PRINT command
  087E                	_C_PRT:	ds	C_PRT-_C_PRT
  087E                		org	C_PRT
                      	
  087E  CD7327        		call	DEVNUM
  0881                	PRTDEV:
  0881  3258FA        		ld	(DEVICE),a
  0884  07            		rlca
  0885  DCA825        		call	c,PRTOPN
                      	
  0888                	PRTLP1:
  0888  CD5F3F        		call	CHKCLN		;a=(hl)
  088B  284B          		jr	z,PRTEOL
  088D                	PRTLP2:
  088D  FE3B          		cp	';'
  088F  284F          		jr	z,PRTNEXT
  0891  FE2C          		cp	','
  0893  2848          		jr	z,PRTCMM
  0895  FEC5          		cp	SPC_
  0897  CA2209        		jp	z,PRTSPC
  089A  FEC2          		cp	TAB_
  089C  CA3209        		jp	z,PRTTAB
                      	
  089F  CD4317        		call	ARG
  08A2  3A25FF        		ld	a,(ARGTYP)
  08A5  B7            		or	a
  08A6  2813          		jr	z,PRTNUM
                      	
                      	;string
  08A8  CD9326        		call	STRARG2
  08AB  2A4EFF        		ld	hl,(PROGAD)
  08AE  B7            		or	a
  08AF  28D7          		jr	z,PRTLP1
  08B1  47            		ld	b,a
  08B2                	PRTSLP:
  08B2  1A            		ld	a,(de)
  08B3  13            		inc	de
  08B4  CDC726        		call	PUTDEV
  08B7  10F9          		djnz	PRTSLP
  08B9  18CD          		jr	PRTLP1
                      	
  08BB                	PRTNUM:
  08BB  E5            		push	hl
  08BC  CDAC3A        		call	FTOA
                      	
  08BF  3AACFD        		ld	a,(WIDTH)
  08C2  C603          		add	a,03h
  08C4  4F            		ld	c,a
  08C5  AF            		xor	a
  08C6  EDB1          		cpir
  08C8  3AA9FD        		ld	a,(CSRX)
  08CB  B9            		cp	c
  08CC  D43927        		call	nc,PUTNL
  08CF  2172FF        		ld	hl,FAC3
  08D2  CDCF30        		call	PUTS
  08D5  E1            		pop	hl
  08D6  18B0          		jr	PRTLP1
                      	
  08D8                	PRTEOL:
                      	;	xor	a
                      	;	ld	(GRPFLG),a
  08D8  CD3927        		call	PUTNL
  08DB  1809          		jr	PRTEND
                      	
  08DD                	PRTCMM:
  08DD  CDF408        		call	CMMMAIN
  08E0                	PRTNEXT:
  08E0  23            		inc	hl
  08E1                	PRTNEXT2:
  08E1  CD5F3F        		call	CHKCLN
  08E4  20A7          		jr	nz,PRTLP2
  08E6                	PRTEND:
  08E6  224EFF        		ld	(PROGAD),hl
  08E9  2158FA        		ld	hl,DEVICE
  08EC  7E            		ld	a,(hl)
  08ED  3600          		ld	(hl),00h
  08EF  07            		rlca
  08F0  D0            		ret	nc
  08F1  C3061B        		jp	WCLOSE
                      	
  08F4                	CMMMAIN:
  08F4  3A58FA        		ld	a,(DEVICE)
  08F7  B7            		or	a
  08F8  2807          		jr	z,CMM0	;CRT
  08FA  3D            		dec	a
  08FB  2817          		jr	z,CMM1	;printer
                      	
                      	;RS-232C,CMT
  08FD  060E          		ld	b,0eh
  08FF  180B          		jr	PUTSPLP
                      	
                      	;CRT
  0901                	CMM0:
  0901  3AA9FD        		ld	a,(CSRX)
  0904  D60F          		sub	0fh
  0906  D23927        		jp	nc,PUTNL
  0909                	CMMSP:
  0909  ED44          		neg
                      	;	call	PUTSP
                      	;	ret
                      	
                      	;a>0
  090B                	PUTSP:
  090B  47            		ld	b,a
  090C                	PUTSPLP:
  090C  3E20          		ld	a,' '
  090E  CDC726        		call	PUTDEV
  0911  10F9          		djnz	PUTSPLP
  0913  C9            		ret
                      	
                      	;printer
  0914                	CMM1:
  0914  3A57FA        		ld	a,(PRTPOS)
  0917  0608          		ld	b,08h
  0919                	CMM1LP:
  0919  D60E          		sub	0eh
  091B  38EC          		jr	c,CMMSP
  091D  10FA          		djnz	CMM1LP
  091F  C33927        		jp	PUTNL
                      	
  0922                	PRTSPC:
  0922  23            		inc	hl
  0923  CD6A0B        		call	FNCNUMR
  0926  E5            		push	hl
  0927  CD4F07        		call	FTOI1
  092A  E1            		pop	hl
  092B                	PRTSPC2:
  092B  7B            		ld	a,e
  092C                	PRTSPC3:
  092C  B7            		or	a
  092D  C40B09        		call	nz,PUTSP
  0930  18AF          		jr	PRTNEXT2
                      	
  0932                	PRTTAB:
  0932  23            		inc	hl
  0933  CD6A0B        		call	FNCNUMR
  0936  E5            		push	hl
  0937  CD4F07        		call	FTOI1
  093A  E1            		pop	hl
                      	
  093B  3A58FA        		ld	a,(DEVICE)
  093E  B7            		or	a
  093F  2808          		jr	z,PRTTAB0	;0=CRT
  0941  3D            		dec	a
  0942  20E7          		jr	nz,PRTSPC2	;2,80-ff=RS-232C,CMT
                      	
                      	;printer
  0944                	PRTTAB1:
  0944  3A57FA        		ld	a,(PRTPOS)
  0947  1804          		jr	TABCOMMON
                      	
                      	;CRT
  0949                	PRTTAB0:
  0949  3AA9FD        		ld	a,(CSRX)
  094C  3D            		dec	a
  094D                	TABCOMMON:
  094D  93            		sub	e
  094E  3091          		jr	nc,PRTNEXT2
  0950  ED44          		neg
  0952  18D8          		jr	PRTSPC3
                      	
                      	
                      	;print new line in CRT (graphic and text screen)
  0954                	PRTNLXALL:
  0954  CD5A09        		call	PRTNLX
                      	
                      	;change to text screen and print new line in CRT
                      	;if (CSRX)>1 then print new line
  0957                	PRTNLXTXT:
  0957  CD7B13        		call	CHGTXT
  095A                	PRTNLX:
  095A  AF            		xor	a
  095B  3258FA        		ld	(DEVICE),a
  095E  3AA9FD        		ld	a,(CSRX)
  0961  3D            		dec	a
  0962  C8            		ret	z
  0963  C33927        		jp	PUTNL
                      	
                      	
                      	;INPUT command
  0966                	C_INPT:
  0966  7C            		ld	a,h
  0967  FEFA          		cp	0fah
  0969  D2FA03        		jp	nc,IDERR
  096C  2B            		dec	hl
  096D  2254FF        		ld	(STOPAD),hl
  0970  23            		inc	hl
                      	
  0971  CD7327        		call	DEVNUM
  0974  321AFA        		ld	(INPDEV),a
  0977  47            		ld	b,a		;
  0978  B7            		or	a
  0979  CC7B13        		call	z,CHGTXT
  097C  E5            		push	hl		;program address
  097D  7E            		ld	a,(hl)
  097E  FE22          		cp	22h		;double quotation mark
  0980  2022          		jr	nz,INPTMAIN
                      	
  0982  78            		ld	a,b		;
  0983  B7            		or	a
  0984  C2DC03        		jp	nz,SNERR
  0987  F1            		pop	af		;cancel program address
  0988                	PROMPT:
  0988  23            		inc	hl
  0989  7E            		ld	a,(hl)
  098A  B7            		or	a
  098B  CADC03        		jp	z,SNERR
  098E  FE22          		cp	22h		;double quotation mark
  0990  2805          		jr	z,PROMPTEND
  0992  CD7510        		call	PRTC
  0995  18F1          		jr	PROMPT
  0997                	PROMPTEND:
  0997  CD573F        		call	SKIPSPINC
  099A  FE3B          		cp	';'
  099C  C2DC03        		jp	nz,SNERR
  099F  23            		inc	hl
  09A0  224EFF        		ld	(PROGAD),hl
  09A3  E5            		push	hl		;program address
                      	
  09A4                	INPTMAIN:
  09A4  3A1AFA        		ld	a,(INPDEV)
  09A7  B7            		or	a
  09A8  C2FE23        		jp	nz,INPTEXT
                      	
  09AB  CD6215        		call	CUTLINE
                      	
  09AE  212401        		ld	hl,QMARK
  09B1  CDCF30        		call	PUTS
  09B4  CDF928        		call	INPT1
  09B7  D1            		pop	de		;program address
  09B8  C1            		pop	bc		;return address
  09B9  DA3E35        		jp	c,STOP3
  09BC  C5            		push	bc		;return address
  09BD  D5            		push	de		;program address
                      	
  09BE  3EFF          		ld	a,0ffh
  09C0  3255FF        		ld	(STOPAD+1),a
                      	
  09C3  23            		inc	hl		;INPBUF
  09C4  3AA6FE        		ld	a,(INPTX)
  09C7  47            		ld	b,a
  09C8  B7            		or	a
  09C9  2809          		jr	z,INPTLP2
  09CB  3E14          		ld	a,14h
  09CD                	INPTLP1:
  09CD  BE            		cp	(hl)
  09CE  2001          		jr	nz,INPTNZ
  09D0  23            		inc	hl
  09D1                	INPTNZ:
  09D1  23            		inc	hl
  09D2  10F9          		djnz	INPTLP1
                      	
  09D4                	INPTLP2:
  09D4  70            		ld	(hl),b		;b=0
  09D5  2B            		dec	hl
  09D6  7E            		ld	a,(hl)
  09D7  FE20          		cp	' '
  09D9  28F9          		jr	z,INPTLP2
                      	
                      	;analyze input buffer
  09DB                	INPTANA:
  09DB  11DAFE        		ld	de,INPBUF
  09DE                	INPTLP3:
  09DE  2A4EFF        		ld	hl,(PROGAD)
  09E1  CD7A33        		call	CHKVAR
  09E4  D5            		push	de		;input buffer
  09E5  CD9432        		call	GETVAR
  09E8  E1            		pop	hl		;input buffer
                      	
  09E9  CB79          		bit	7,c
  09EB  2066          		jr	nz,INPTSTR
                      	
  09ED                	INPTNUM:
  09ED  D5            		push	de		;variable address
  09EE  3EFF          		ld	a,0ffh
  09F0  CD9839        		call	ATOF
  09F3  D1            		pop	de		;variable address
  09F4  3ADAFE        		ld	a,(INPBUF)
  09F7  B7            		or	a
  09F8  2824          		jr	z,INPTNEXT
  09FA  E5            		push	hl		;input buffer
  09FB  2166FF        		ld	hl,FAC1
  09FE  CD300C        		call	SETF
  0A01  E1            		pop	hl		;input buffer
  0A02  7E            		ld	a,(hl)
  0A03  B7            		or	a
  0A04  2818          		jr	z,INPTNEXT
  0A06  FE2C          		cp	','
  0A08  2814          		jr	z,INPTNEXT
  0A0A  3A1AFA        		ld	a,(INPDEV)
  0A0D  B7            		or	a
  0A0E  C21504        		jp	nz,FDERR
  0A11  214924        		ld	hl,REDO
  0A14  CDCF30        		call	PUTS
  0A17  E1            		pop	hl		;program address
  0A18  224EFF        		ld	(PROGAD),hl
  0A1B  E5            		push	hl		;program address
  0A1C  1886          		jr	INPTMAIN
                      	
  0A1E                	INPTNEXT:
  0A1E  EB            		ex	de,hl
  0A1F  2A4EFF        		ld	hl,(PROGAD)
  0A22  2B            		dec	hl
  0A23  D7            		rst	ANADAT
  0A24  FE2C          		cp	','
  0A26  201F          		jr	nz,INPTEND
  0A28  23            		inc	hl
  0A29  224EFF        		ld	(PROGAD),hl
                      	
  0A2C  3ADAFE        		ld	a,(INPBUF)
  0A2F  B7            		or	a
  0A30  28AC          		jr	z,INPTLP3
  0A32  1A            		ld	a,(de)
  0A33  13            		inc	de
  0A34  FE2C          		cp	','
  0A36  28A6          		jr	z,INPTLP3
  0A38  3A1AFA        		ld	a,(INPDEV)
  0A3B  B7            		or	a
  0A3C  C2E203        		jp	nz,ODERR
  0A3F  3E3F          		ld	a,'?'
  0A41  CD7510        		call	PRTC
  0A44  C3A409        		jp	INPTMAIN
                      	
  0A47                	INPTEND:
  0A47  F1            		pop	af		;cancel program address
  0A48  1A            		ld	a,(de)
  0A49  FE2C          		cp	','
  0A4B  C0            		ret	nz
  0A4C  213824        		ld	hl,EXTRA
  0A4F  C3CF30        		jp	PUTS
                      	
                      	
  0A52                	INPTSLP1:
  0A52  23            		inc	hl
  0A53                	INPTSTR:
  0A53  7E            		ld	a,(hl)
  0A54  FE20          		cp	' '
  0A56  28FA          		jr	z,INPTSLP1
  0A58  0E00          		ld	c,00h		;double quotation mark count
  0A5A  FE22          		cp	22h		;double quotation mark
  0A5C  2002          		jr	nz,INPTSNZ
  0A5E  23            		inc	hl
  0A5F  0C            		inc	c
  0A60                	INPTSNZ:
  0A60  E5            		push	hl
  0A61  0600          		ld	b,00h
                      	
  0A63                	INPTSLP2:
  0A63  79            		ld	a,c
  0A64  B7            		or	a
  0A65  7E            		ld	a,(hl)
  0A66  200B          		jr	nz,INPTCHKDQ
  0A68  FE2C          		cp	','
  0A6A  280F          		jr	z,INPTSZ
  0A6C                	INPTCHKZ:
  0A6C  B7            		or	a
  0A6D  280C          		jr	z,INPTSZ
  0A6F  23            		inc	hl
  0A70  04            		inc	b
  0A71  18F0          		jr	INPTSLP2
                      	
  0A73                	INPTCHKDQ:
  0A73  FE22          		cp	22h		;double quotation mark
  0A75  20F5          		jr	nz,INPTCHKZ
  0A77  0D            		dec	c
  0A78  23            		inc	hl
  0A79  18E8          		jr	INPTSLP2
                      	
  0A7B                	INPTSZ:
  0A7B  E3            		ex	(sp),hl		;push input buffer
  0A7C  78            		ld	a,b
  0A7D  D5            		push	de		;variable address
  0A7E  CD2326        		call	MAKESTR
  0A81  D1            		pop	de		;variable address
  0A82  212DFF        		ld	hl,STRDSC1
  0A85  010400        		ld	bc,0004h
  0A88  EDB0          		ldir
  0A8A  E1            		pop	hl		;input buffer
  0A8B  1891          		jr	INPTNEXT
                      	
                      	
                      	;READ command
  0A8D                	C_READ:
  0A8D  CD7A33        		call	CHKVAR
  0A90  CD9432        		call	GETVAR
  0A93  C5            		push	bc		;variable name
  0A94  D5            		push	de		;variable address
                      	
  0A95  2A5CFF        		ld	hl,(DATAAD)
  0A98  ED5B45FF      		ld	de,(DATALN)
  0A9C  7E            		ld	a,(hl)
  0A9D  FE2C          		cp	','
  0A9F  2847          		jr	z,READDATA
  0AA1  FE3A          		cp	':'
  0AA3  2810          		jr	z,READLP3
  0AA5  B7            		or	a
  0AA6  C2E203        		jp	nz,ODERR
                      	
  0AA9                	READLP1:
  0AA9  23            		inc	hl
  0AAA                	READLP2:
  0AAA  4F            		ld	c,a		;always a=0, double quation mark counter
  0AAB  7E            		ld	a,(hl)
  0AAC  23            		inc	hl
  0AAD  B6            		or	(hl)
  0AAE  CAE203        		jp	z,ODERR
  0AB1  23            		inc	hl
  0AB2  5E            		ld	e,(hl)
  0AB3  23            		inc	hl
  0AB4  56            		ld	d,(hl)
                      	
  0AB5                	READLP3:
  0AB5  23            		inc	hl
  0AB6  7E            		ld	a,(hl)
  0AB7  B7            		or	a
  0AB8  28EF          		jr	z,READLP1
  0ABA  FE83          		cp	DATA_
  0ABC  282A          		jr	z,READDATA
  0ABE  FE22          		cp	22h		;double quotation mark
  0AC0  281D          		jr	z,READDQ
  0AC2  FE3A          		cp	':'
  0AC4  28EF          		jr	z,READLP3
  0AC6  FE20          		cp	' '
  0AC8  28EB          		jr	z,READLP3
  0ACA  D68E          		sub	REM_
  0ACC  2814          		jr	z,READREM
                      	
  0ACE                	READLP4:
  0ACE  23            		inc	hl
  0ACF  7E            		ld	a,(hl)
  0AD0  B7            		or	a
  0AD1  28D6          		jr	z,READLP1
  0AD3  CB41          		bit	0,c
  0AD5  2004          		jr	nz,SKIPCLN
  0AD7  FE3A          		cp	':'
  0AD9  28DA          		jr	z,READLP3
  0ADB                	SKIPCLN:
  0ADB  FE22          		cp	22h		;double quotation mark
  0ADD  20EF          		jr	nz,READLP4
  0ADF                	READDQ:
  0ADF  0C            		inc	c
  0AE0  18EC          		jr	READLP4
                      	
                      	;a=0
  0AE2                	READREM:
                      	;	xor	a
  0AE2  47            		ld	b,a
  0AE3  4F            		ld	c,a
  0AE4  EDB1          		cpir
  0AE6  18C2          		jr	READLP2
                      	
  0AE8                	READDATA:
  0AE8  ED5345FF      		ld	(DATALN),de
  0AEC  CD573F        		call	SKIPSPINC
                      	
  0AEF  D1            		pop	de		;variable address
  0AF0  C1            		pop	bc		;variable name
  0AF1  CB79          		bit	7,c
  0AF3  2022          		jr	nz,READSTR
                      	
  0AF5  B7            		or	a
  0AF6  280E          		jr	z,READNUM
  0AF8  FE26          		cp	'&'
  0AFA  280A          		jr	z,READNUM
  0AFC  FE2F          		cp	'/'
  0AFE  283F          		jr	z,READERR
  0B00  D62B          		sub	'+'
  0B02  FE0F          		cp	'9'-'+'+1	;+,-.0123456789
  0B04  3039          		jr	nc,READERR
  0B06                	READNUM:
  0B06  D5            		push	de		;variable address
  0B07  3EFF          		ld	a,0ffh
  0B09  CD9839        		call	ATOF
  0B0C  E3            		ex	(sp),hl		;push data address, pop variable address
  0B0D  EB            		ex	de,hl
  0B0E  2166FF        		ld	hl,FAC1
  0B11  CD300C        		call	SETF
  0B14  E1            		pop	hl		;data address
  0B15  1842          		jr	READEND
                      	
  0B17                	READSTR:
  0B17  010000        		ld	bc,0000h	;b=double quotation mark?, c=length
  0B1A  FE22          		cp	22h		;double quotation mark
  0B1C  2002          		jr	nz,RDSNDQ
  0B1E  04            		inc	b
  0B1F  23            		inc	hl
  0B20                	RDSNDQ:
  0B20  13            		inc	de
  0B21  13            		inc	de
  0B22  7D            		ld	a,l
  0B23  12            		ld	(de),a
  0B24  13            		inc	de
  0B25  7C            		ld	a,h
  0B26  12            		ld	(de),a
  0B27  1B            		dec	de
  0B28  1B            		dec	de
  0B29  1B            		dec	de
                      	
  0B2A                	RDSLP2:
  0B2A  7E            		ld	a,(hl)
  0B2B  B7            		or	a
  0B2C  2829          		jr	z,RDSEND
  0B2E  CB40          		bit	0,b
  0B30  2816          		jr	z,RDSCHK
  0B32  FE22          		cp	22h		;double quotation mark
  0B34  201A          		jr	nz,RDSNEXT
  0B36  CD5E3F        		call	CHKCLNINC
  0B39  281C          		jr	z,RDSEND
  0B3B  FE2C          		cp	','
  0B3D  2818          		jr	z,RDSEND
                      	
  0B3F                	READERR:
  0B3F  2A45FF        		ld	hl,(DATALN)
  0B42  225DFA        		ld	(LINENUM),hl
  0B45  C3DC03        		jp	SNERR
                      	
  0B48                	RDSCHK:
  0B48  FE2C          		cp	','
  0B4A  280B          		jr	z,RDSEND
  0B4C  FE3A          		cp	':'
  0B4E  2807          		jr	z,RDSEND
                      	
  0B50                	RDSNEXT:
  0B50  23            		inc	hl
  0B51  0C            		inc	c
  0B52  20D6          		jr	nz,RDSLP2
  0B54  C30304        		jp	LSERR
                      	
  0B57                	RDSEND:
  0B57  79            		ld	a,c
  0B58  12            		ld	(de),a
                      	
  0B59                	READEND:
  0B59  225CFF        		ld	(DATAAD),hl
  0B5C  2A4EFF        		ld	hl,(PROGAD)
  0B5F  7E            		ld	a,(hl)
  0B60  FE2C          		cp	','
  0B62  C0            		ret	nz
  0B63  23            		inc	hl
  0B64  C38D0A        		jp	C_READ
                      	
                      	
                      	;get a numerical argument for function
                      	;input: hl=program address
                      	;output: FAC1
                      	;destroy: FAC2,af,bc,de,hl
  0B67                	FNCNUM:
  0B67  CD950B        		call	CHKLPAR
  0B6A                	FNCNUMR:
  0B6A  CD4317        		call	ARG
  0B6D  CDA40B        		call	CHKRPAR
  0B70  1809          		jr	CHKNUM
                      	
                      	
                      	;get a numerical argument and check result (?MO Error)
                      	;input: hl=program address
                      	;output: FAC1, hl=next address
                      	;destroy: af,bc,de,FAC2
  0B72                	NUMARGMO:
  0B72  CD5F3F        		call	CHKCLN
  0B75  CA1204        		jp	z,MOERR
                      	;	call	NUMARG
                      	;	ret
                      	
                      	
                      	;get a numerical argument and check result
                      	;input: hl=program address
                      	;output: FAC1, hl=next address
                      	;destroy: af,bc,de,FAC2
  0B78                	NUMARG:
  0B78  CD4317        		call	ARG
                      	;	call	CHKNUM
                      	;	ret
                      	
                      	
                      	;check numeric or not
                      	;output: z-flag (1=ok)
                      	;destroy: af
  0B7B                	CHKNUM:
  0B7B  3A25FF        		ld	a,(ARGTYP)
  0B7E  B7            		or	a
  0B7F  C8            		ret	z
  0B80  3D            		dec	a
  0B81                	CHKNERR:
  0B81  CAFD03        		jp	z,TMERR
  0B84  C3DC03        		jp	SNERR
                      	
                      	
                      	;check string argument or not
                      	;output: a=length, hl=string descriptor address, z-flag(1=ok)
                      	;destroy: f
  0B87                	CHKSTR:
  0B87  3A25FF        		ld	a,(ARGTYP)
  0B8A  3D            		dec	a
  0B8B  2005          		jr	nz,CHKSERR
  0B8D  2A67FF        		ld	hl,(FAC1+1)
  0B90  7E            		ld	a,(hl)
  0B91  C9            		ret
                      	
  0B92                	CHKSERR:
  0B92  3C            		inc	a
  0B93  18EC          		jr	CHKNERR
                      	
                      	
                      	;check (
                      	;input: hl=program address
                      	;output: hl=next address
                      	;destroy: af
  0B95                	CHKLPAR:
  0B95  CD583F        		call	SKIPSP
  0B98  FE28          		cp	'('
  0B9A  C2DC03        		jp	nz,SNERR
  0B9D  CD5E3F        		call	CHKCLNINC
  0BA0  C0            		ret	nz
  0BA1  C31204        		jp	MOERR
                      	
                      	
                      	;check )
                      	;input: hl=program address
                      	;output: hl=next address
                      	;destroy: af
  0BA4                	CHKRPAR:
  0BA4  7E            		ld	a,(hl)
  0BA5  FE29          		cp	')'
  0BA7  C2DC03        		jp	nz,SNERR
  0BAA  23            		inc	hl
  0BAB                	SETPRGAD:
  0BAB  224EFF        		ld	(PROGAD),hl
  0BAE  C9            		ret
                      	
                      	
                      	;alphabet, numeral, or katakana? (case ignored)
  0BAF                	ALPNUM2:
  0BAF  FEE0          		cp	0e0h
  0BB1  3F            		ccf
  0BB2  D8            		ret	c
  0BB3  FEA1          		cp	0a1h
  0BB5  D0            		ret	nc
  0BB6  CDEF0B        		call	TOUPPER
                      	;	call	ALPNUM
                      	;	ret
                      	
                      	
                      	;alphabet (capital) or numeral?
                      	;input: a
                      	;output: c-flag(1=yes,0=no)
                      	;destroy: f
  0BB9                	ALPNUM:
  0BB9  FE5B          		cp	'Z'+1
  0BBB  D0            		ret	nc
  0BBC  FE41          		cp	'A'
  0BBE  3F            		ccf
  0BBF  D8            		ret	c
                      	;	jp	CHKFIG
                      	
                      	
                      	;analyze for 0010h
                      	;input: a
                      	;output: z-flag(1=00h or 3ah), c-flag(1=30h-39h)
                      	;destroy: f
  0BC0                	CHKFIG:
  0BC0  B7            		or	a
  0BC1  C8            		ret	z
  0BC2  FE30          		cp	'0'
  0BC4  3F            		ccf
  0BC5  D0            		ret	nc
  0BC6  FE3A          		cp	':'
                      	;	ret	z
                      	;	cp	'9'+1
  0BC8  C9            		ret
                      	
                      	
                      	;OR operator
  0BC9                	O_OR:
  0BC9  CD5807        		call	F12TOI2
  0BCC  7C            		ld	a,h
  0BCD  B2            		or	d
  0BCE  67            		ld	h,a
  0BCF  7D            		ld	a,l
  0BD0  B3            		or	e
  0BD1  6F            		ld	l,a
  0BD2  C3180D        		jp	S2TOF
                      	
                      	
                      	;AND operator
  0BD5                	O_AND:
  0BD5  CD5807        		call	F12TOI2
  0BD8  7C            		ld	a,h
  0BD9  A2            		and	d
  0BDA  67            		ld	h,a
  0BDB  7D            		ld	a,l
  0BDC  A3            		and	e
  0BDD  6F            		ld	l,a
  0BDE  C3180D        		jp	S2TOF
                      	
                      	
                      	;convert lowercase letter to uppercase letter
                      	;input: hl=data address
                      	;output: a, c-flag(1=not converted)
                      	;destroy: f
  0BE1                	_TOUPHL:ds	TOUPHL-_TOUPHL
  0BEE  7E            		ld	a,(hl)
                      	
                      	;input: a=data
  0BEF                	TOUPPER:
  0BEF  FE7B          		cp	'z'+1
  0BF1  D0            		ret	nc
  0BF2                	TOUPPER2:
  0BF2  FE61          		cp	'a'
  0BF4  D8            		ret	c
  0BF5  D620          		sub	'a'-'A'
  0BF7  C9            		ret
                      	
                      	
                      	;NOT operator
  0BF8                	O_NOT:
  0BF8  CD7B0B        		call	CHKNUM
  0BFB  CD6E39        		call	EXFAC
  0BFE  CD4107        		call	FTOI2
  0C01  7A            		ld	a,d
  0C02  2F            		cpl
  0C03  67            		ld	h,a
  0C04  7B            		ld	a,e
  0C05  2F            		cpl
  0C06  6F            		ld	l,a
  0C07  C3180D        		jp	S2TOF
                      	
                      	
                      	;>,=,< operator
                      	;bit0: >
                      	;bit1: =
                      	;bit2: <
  0C0A                	O_GTEQLT:
  0C0A  3A25FF        		ld	a,(ARGTYP)
  0C0D  B7            		or	a
  0C0E  280D          		jr	z,GTEQLTZ
  0C10  D603          		sub	03h		;string and string
  0C12  C2FD03        		jp	nz,TMERR
  0C15  3225FF        		ld	(ARGTYP),a	;a=0
  0C18  CD400C        		call	CMPSTR
  0C1B  1803          		jr	GTEQLTEND
  0C1D                	GTEQLTZ:
  0C1D  CD1B3F        		call	CMPF
  0C20                	GTEQLTEND:
  0C20  79            		ld	a,c		;operator
  0C21  2803          		jr	z,CASE_EQ
  0C23  3002          		jr	nc,CASE_GT
  0C25                	CASE_LT:
  0C25  0F            		rrca
  0C26                	CASE_EQ:
  0C26  0F            		rrca
  0C27                	CASE_GT:
  0C27  0F            		rrca
  0C28  3811          		jr	c,SETTRUE
                      	;	jr	SETFALSE
                      	
  0C2A                	SETFALSE:
  0C2A                	SETZERO:
  0C2A  21AB0E        		ld	hl,ZERO
  0C2D                	SETF1:
  0C2D  1166FF        		ld	de,FAC1
  0C30                	SETF:
                      	;	ldi
                      	;	ldi
                      	;	ldi
                      	;	ldi
                      	;	ldi
  0C30  010500        		ld	bc,0005h
  0C33  EDB0          		ldir
  0C35  C9            		ret
                      	
  0C36                	SETF2:
  0C36  116DFF        		ld	de,FAC2
  0C39  18F5          		jr	SETF
                      	
  0C3B                	SETTRUE:
  0C3B                	SETMNS1:
  0C3B  21A60E        		ld	hl,MNSONE
  0C3E  18ED          		jr	SETF1
                      	
                      	
  0C40                	CMPSTR:
  0C40  2139FF        		ld	hl,STRDSC4
  0C43  46            		ld	b,(hl)
  0C44  3600          		ld	(hl),00h
  0C46  3A2DFF        		ld	a,(STRDSC1)
  0C49  B8            		cp	b
  0C4A  F5            		push	af		;flag
  0C4B  3801          		jr	c,CMPSNC
  0C4D  78            		ld	a,b
  0C4E                	CMPSNC:
  0C4E  B7            		or	a
  0C4F  2810          		jr	z,CMPSZ
                      	
  0C51  ED5B2FFF      		ld	de,(STRDSC1+2)
  0C55  2A3BFF        		ld	hl,(STRDSC4+2)
  0C58  47            		ld	b,a
  0C59                	CMPSLP:
  0C59  1A            		ld	a,(de)
  0C5A  BE            		cp	(hl)
  0C5B  2006          		jr	nz,CMPSNZ
  0C5D  13            		inc	de
  0C5E  23            		inc	hl
  0C5F  10F8          		djnz	CMPSLP
  0C61                	CMPSZ:
  0C61  F1            		pop	af		;flag
  0C62  C9            		ret
                      	
  0C63                	CMPSNZ:
  0C63  E1            		pop	hl		;cancel stack
  0C64  C9            		ret
                      	
                      	
                      	;input: a=integer (unsigned)
  0C65                	I1TOFA:
  0C65  6F            		ld	l,a
                      	
                      	;convert 1- or 2-byte integer to float
                      	;input: hl=integer (unsigned)
                      	;output: FAC1
                      	;destroy: af,b,hl
  0C66                	I1TOF:
  0C66  2600          		ld	h,00h
  0C68                	I2TOF:
  0C68  D5            		push	de
  0C69  110000        		ld	de,0000h
  0C6C  7C            		ld	a,h
  0C6D  B5            		or	l
  0C6E  280F          		jr	z,I2TOFZERO
  0C70  0610          		ld	b,10h
  0C72                	I2TOFLP:
  0C72  29            		add	hl,hl
  0C73  3802          		jr	c,I2TOFEND
  0C75  10FB          		djnz	I2TOFLP
                      	
  0C77                	I2TOFEND:
  0C77  CB3C          		srl	h
  0C79  CB1D          		rr	l
  0C7B  EB            		ex	de,hl
  0C7C                	I2TOFEND2:
  0C7C  78            		ld	a,b
  0C7D  C680          		add	a,80h
  0C7F                	I2TOFZERO:
                      	;	ld	(FAC1),hl
                      	;	ld	(FAC1+2),de
  0C7F  326AFF        		ld	(FAC1+4),a
  0C82  CD8837        		call	SETI4
  0C85  D1            		pop	de
  0C86  C9            		ret
                      	
                      	
                      	;convert float to 4-byte integer (>=0)
                      	;(round toward zero)
                      	;input: FAC1
                      	;output: FAC1 (integer)
                      	;destroy: af,b,de,hl
  0C87                	FTOI4:
  0C87  3EA0          		ld	a,81h+1fh
  0C89  216AFF        		ld	hl,FAC1+4
  0C8C  96            		sub	(hl)		;
  0C8D  47            		ld	b,a
  0C8E  2A66FF        		ld	hl,(FAC1)
  0C91  ED5B68FF      		ld	de,(FAC1+2)
  0C95  CBFA          		set	7,d
  0C97  280A          		jr	z,FTOI4END	;
  0C99                	FTOI4LP:
  0C99  CB3A          		srl	d
  0C9B  CB1B          		rr	e
  0C9D  CB1C          		rr	h
  0C9F  CB1D          		rr	l
  0CA1  10F6          		djnz	FTOI4LP
  0CA3                	FTOI4END:
                      	;	ld	(FAC1),hl
                      	;	ld	(FAC1+2),de
                      	;	ret
  0CA3  C38837        		jp	SETI4
                      	
                      	
                      	;convert 4-byte integer to float (unsigned)
                      	;input: de-hl
                      	;output: FAC1
                      	;destroy: af,b,hl
  0CA6                	I4TOF:
  0CA6  D5            		push	de
  0CA7  7A            		ld	a,d
  0CA8  B3            		or	e
  0CA9  B4            		or	h
  0CAA  B5            		or	l
  0CAB  28D2          		jr	z,I2TOFZERO
                      	
  0CAD  7A            		ld	a,d
  0CAE  0620          		ld	b,20h
  0CB0                	I4TOFLP:
  0CB0  C680          		add	a,80h
  0CB2  3806          		jr	c,I4TOFEND
  0CB4  29            		add	hl,hl
  0CB5  CB13          		rl	e
  0CB7  17            		rla
  0CB8  10F6          		djnz	I4TOFLP
                      	
  0CBA                	I4TOFEND:
  0CBA  57            		ld	d,a
  0CBB  18BF          		jr	I2TOFEND2
                      	
                      	
                      	;convert float to 2-byte integer (-65535~65535)
                      	;(round toward minus infinity)
                      	;input: FAC1
                      	;output: de
                      	;destroy: af,bc,hl
  0CBD                	FTOI:
  0CBD  110000        		ld	de,0000h
  0CC0  3A6AFF        		ld	a,(FAC1+4)
  0CC3  B7            		or	a
  0CC4  C8            		ret	z
  0CC5  D691          		sub	91h
  0CC7  D2E503        		jp	nc,FCERR
  0CCA  ED44          		neg
  0CCC  47            		ld	b,a
                      	
  0CCD  2A66FF        		ld	hl,(FAC1)
  0CD0  7C            		ld	a,h
  0CD1  B5            		or	l		;fraction
                      	
  0CD2  2A68FF        		ld	hl,(FAC1+2)
  0CD5  4C            		ld	c,h		;sign
  0CD6  CBFC          		set	7,h
  0CD8  05            		dec	b
  0CD9  2809          		jr	z,FTOIZ
  0CDB                	FTOILP:
  0CDB  CB3C          		srl	h
  0CDD  CB1D          		rr	l
  0CDF  3001          		jr	nc,FTOINC
  0CE1  07            		rlca			;a>0
  0CE2                	FTOINC:
  0CE2  10F7          		djnz	FTOILP
                      	
  0CE4                	FTOIZ:
  0CE4  EB            		ex	de,hl
  0CE5  CB79          		bit	7,c		;sign
  0CE7  C8            		ret	z
                      	
  0CE8  ED44          		neg			;set c-flag if a>0
  0CEA  ED52          		sbc	hl,de
  0CEC  EB            		ex	de,hl
  0CED  C9            		ret
                      	
                      	
                      	;get integer part from float
                      	;(round toward zero)
                      	;input: FAC1
                      	;output: FAC1
                      	;destroy: af,b,hl
  0CEE                	GETINT:
  0CEE  3A6AFF        		ld	a,(FAC1+4)
  0CF1  FE81          		cp	81h
  0CF3  DA2A0C        		jp	c,SETZERO
  0CF6  D6A0          		sub	0a0h
  0CF8  D0            		ret	nc
  0CF9  2166FF        		ld	hl,FAC1
  0CFC                	GETILP1:
  0CFC  C608          		add	a,08h		;
  0CFE  3805          		jr	c,GETIC
  0D00  3600          		ld	(hl),0
  0D02  23            		inc	hl
  0D03  18F7          		jr	GETILP1
  0D05                	GETIC:
  0D05  47            		ld	b,a
  0D06  04            		inc	b
  0D07  AF            		xor	a
  0D08                	GETILP2:
  0D08  1F            		rra
  0D09  37            		scf
  0D0A  10FC          		djnz	GETILP2
  0D0C  A6            		and	(hl)
  0D0D  77            		ld	(hl),a
  0D0E  C9            		ret
                      	
                      	
                      	;convert 1-byte signed integer to float
                      	;input: b=integer (signed)
                      	;output: FAC1
                      	;destroy: af,bc,hl
  0D0F                	_S1TOF:	ds	ABTOF-5-_S1TOF
  0D11                		org	ABTOF-5
  0D11                	S1TOF:
  0D11  78            		ld	a,b
  0D12  07            		rlca
  0D13  9F            		sbc	a,a		;a=0(b<80h) or ffh(b>=80h)
  0D14  1800          		jr	ABTOF
                      	
                      	
                      	;convert 2-byte integer to float for USR()
                      	;input: ab
                      	;output: FAC1
                      	;destroy: af,bc,hl
  0D16                	_ABTOF:	ds	ABTOF-_ABTOF
  0D16                		org	ABTOF
                      	
  0D16  67            		ld	h,a
  0D17  68            		ld	l,b
                      	;	jr	S2TOF
                      	
                      	
                      	;convert 2-byte signed integer to float
                      	;input: hl=integer (signed)
                      	;output: FAC1
                      	;destroy: af,b,hl
  0D18                	S2TOF:
  0D18  CB7C          		bit	7,h
  0D1A  CA680C        		jp	z,I2TOF
  0D1D  AF            		xor	a
  0D1E  95            		sub	l
  0D1F  6F            		ld	l,a
  0D20  9C            		sbc	a,h
  0D21  95            		sub	l
  0D22  67            		ld	h,a
  0D23  CD680C        		call	I2TOF
  0D26  1805          		jr	NEGABSNZ
                      	
                      	
                      	;FAC1=-abs(FAC1)
                      	;destroy: af,hl
  0D28                	NEGABS:
  0D28  3A6AFF        		ld	a,(FAC1+4)
  0D2B  B7            		or	a
  0D2C  C8            		ret	z
                      	
                      	;FAC1=-abs(FAC1) without checking zero
                      	;destroy: f,hl
  0D2D                	NEGABSNZ:
  0D2D  2169FF        		ld	hl,FAC1+3
  0D30  CBFE          		set	7,(hl)
  0D32  C9            		ret
                      	
                      	
                      	;LPOS() function
  0D33                	F_LPOS:
  0D33  3A25FF        		ld	a,(ARGTYP)
  0D36  FE02          		cp	02h
  0D38  D2DC03        		jp	nc,SNERR
  0D3B  2A57FA        		ld	hl,(PRTPOS)
  0D3E  C3A431        		jp	SETI1
                      	
                      	
                      	;POS() function
  0D41                	F_POS:
  0D41  3A25FF        		ld	a,(ARGTYP)
  0D44  FE02          		cp	02h
  0D46  D2DC03        		jp	nc,SNERR
  0D49  2AA9FD        		ld	hl,(CSRX)
  0D4C  2D            		dec	l
  0D4D  C3A431        		jp	SETI1
                      	
                      	
                      	;CSRLINE function
  0D50                	F_CSRL:
  0D50  2AA8FD        		ld	hl,(CSRY)
  0D53  2D            		dec	l
  0D54  C3660C        		jp	I1TOF
                      	
                      	
                      	;DEF command
  0D57                	C_DEF:
  0D57  ED5B5DFA      		ld	de,(LINENUM)
  0D5B  7A            		ld	a,d
  0D5C  A3            		and	e
  0D5D  3C            		inc	a
  0D5E  CAFA03        		jp	z,IDERR
  0D61  CD583F        		call	SKIPSP
  0D64  FEC4          		cp	FN_
  0D66  C2DC03        		jp	nz,SNERR
  0D69  23            		inc	hl
  0D6A  CD7A33        		call	CHKVAR
  0D6D  CB79          		bit	7,c
  0D6F  C2FD03        		jp	nz,TMERR
  0D72  CBF8          		set	7,b
  0D74  E5            		push	hl		;program address
  0D75  CDAC33        		call	SRCHVAR
  0D78  DCD633        		call	c,MAKEVAR
  0D7B  E1            		pop	hl		;program address
  0D7C  CD950B        		call	CHKLPAR
  0D7F  CD7A33        		call	CHKVAR
  0D82  CB79          		bit	7,c
  0D84  C2FD03        		jp	nz,TMERR
  0D87  CDA40B        		call	CHKRPAR
  0D8A  CD583F        		call	SKIPSP
  0D8D  FED2          		cp	EQ_
  0D8F  C2DC03        		jp	nz,SNERR
  0D92  CD573F        		call	SKIPSPINC
                      	
  0D95  EB            		ex	de,hl
  0D96  73            		ld	(hl),e
  0D97  23            		inc	hl
  0D98  72            		ld	(hl),d
  0D99  23            		inc	hl
  0D9A  70            		ld	(hl),b
  0D9B  23            		inc	hl
  0D9C  71            		ld	(hl),c
                      	;	inc	hl
                      	;	ld	(hl),00h
  0D9D  EB            		ex	de,hl
                      	
  0D9E  2B            		dec	hl
  0D9F                	DEFLP:
  0D9F  CD5E3F        		call	CHKCLNINC
  0DA2  20FB          		jr	nz,DEFLP
                      	
  0DA4  224EFF        		ld	(PROGAD),hl
  0DA7  C9            		ret
                      	
                      	
                      	;INP() function
  0DA8                	F_INP:
  0DA8  CD7B0B        		call	CHKNUM
  0DAB  CD4F07        		call	FTOI1
  0DAE  42            		ld	b,d		;d=0
  0DAF  4B            		ld	c,e
  0DB0  ED68          		in	l,(c)
  0DB2  62            		ld	h,d		;d=0
  0DB3  C3680C        		jp	I2TOF
                      	
                      	
                      	;OUT command
  0DB6                	C_OUT:
  0DB6  CDE40D        		call	INT1ARG
  0DB9  7E            		ld	a,(hl)
  0DBA  FE2C          		cp	','
  0DBC  C2DC03        		jp	nz,SNERR
  0DBF  D5            		push	de		;
  0DC0  CDE30D        		call	INT1INC
  0DC3  C1            		pop	bc		;
  0DC4  ED59          		out	(c),e
  0DC6  C9            		ret
                      	
                      	
                      	;increment hl and get a 1-byte integer argument (0~255)
  0DC7                	_INT1INC:ds	INT1INC-_INT1INC
  0DE3                		org	INT1INC
                      	
  0DE3  23            		inc	hl
                      	
                      	;get a 1-byte integer argument (0~255)
                      	;input: hl=program address
                      	;output: FAC1,a,de=integer, hl=next address
                      	;destroy: FAC2, f, bc
  0DE4                	_INT1ARG:ds	INT1ARG-_INT1ARG
  0DE4                		org	INT1ARG
                      	
  0DE4  CD1A0E        		call	INT2ARG
  0DE7  7A            		ld	a,d
  0DE8  B7            		or	a
  0DE9  7B            		ld	a,e
  0DEA  C8            		ret	z
  0DEB  C3E503        		jp	FCERR
                      	
                      	
                      	;PEEK() function
  0DEE                	F_PEEK:
  0DEE  CD7B0B        		call	CHKNUM
  0DF1  CDBD0C        		call	FTOI
  0DF4  1A            		ld	a,(de)
  0DF5  C3650C        		jp	I1TOFA
                      	
                      	
                      	;POKE command
  0DF8                	C_POKE:
  0DF8  CD720B        		call	NUMARGMO
  0DFB  7E            		ld	a,(hl)
  0DFC  FE2C          		cp	','
  0DFE  C2DC03        		jp	nz,SNERR
  0E01  E5            		push	hl
  0E02  CDBD0C        		call	FTOI
  0E05  E1            		pop	hl
  0E06  D5            		push	de		;
  0E07  23            		inc	hl
  0E08  CD720B        		call	NUMARGMO
  0E0B  CD4F07        		call	FTOI1
  0E0E  E1            		pop	hl		;
  0E0F  73            		ld	(hl),e
  0E10  C9            		ret
                      	
                      	
                      	;get a 2-byte integer argument (-65535~65535)
                      	;input: hl=program address
                      	;output: FAC1,de=integer, hl=next address
                      	;destroy: FAC2,af,bc
  0E11                	INTARG:
  0E11  CD720B        		call	NUMARGMO
  0E14  E5            		push	hl
  0E15  CDBD0C        		call	FTOI
  0E18  E1            		pop	hl
  0E19  C9            		ret
                      	
                      	
                      	;get a 2-byte integer argument (-32768~32767)
                      	;input: hl=program address
                      	;output: FAC1,de=integer, hl=next address
                      	;destroy: FAC2,af,bc
  0E1A                	INT2ARG:
  0E1A  CD720B        		call	NUMARGMO
  0E1D  E5            		push	hl
  0E1E  CD4107        		call	FTOI2
  0E21  E1            		pop	hl
  0E22  C9            		ret
                      	
                      	
                      	;get two 2-byte integer arguments (-65535~65535)
                      	;input: hl=program address
                      	;output: FAC1, bc=1st, de=2nd, hl=next address
                      	;destroy: af
  0E23                	INTARG2:
  0E23  CD110E        		call	INTARG
  0E26  7E            		ld	a,(hl)
  0E27  FE2C          		cp	','
  0E29  C2DC03        		jp	nz,SNERR
  0E2C  23            		inc	hl
  0E2D  D5            		push	de		;
  0E2E  CD110E        		call	INTARG
  0E31  C1            		pop	bc		;
  0E32  C9            		ret
                      	
                      	
                      	;get data from port 90h
                      	;output: a
                      	;destroy: f
  0E33                	_IN90H:	ds	IN90H-_IN90H
  0E78                		org	IN90H
                      	
  0E78  3E0C          		ld	a,0ch
  0E7A  D393          		out	(93h),a		;disable 8255 INT for writing
  0E7C                	IN90HLP:
  0E7C  DB92          		in	a,(92h)
  0E7E  2F            		cpl
  0E7F  E628          		and	28h
  0E81  20F9          		jr	nz,IN90HLP	;wait for ibf=1,intr=1
  0E83  3E0D          		ld	a,0dh
  0E85  D393          		out	(93h),a		;enable 8255 INT for writing
  0E87  DB90          		in	a,(90h)
  0E89  C9            		ret
                      	
                      	
                      	;output to port 90h
                      	;input: a=data
                      	;destroy: none
  0E8A                	_OUT90H:ds	OUT90H-_OUT90H
  0E8F                		org	OUT90H
                      	
  0E8F  F5            		push	af
  0E90  3E08          		ld	a,08h
  0E92  D393          		out	(93h),a		;disable 8255 INT for reading
  0E94                	OUT90HLP:
  0E94  DB92          		in	a,(92h)
  0E96  07            		rlca
  0E97  30FB          		jr	nc,OUT90HLP	;wait for nobf=1
  0E99  3E09          		ld	a,09h
  0E9B  D393          		out	(93h),a		;enable 8255 INT for reading
  0E9D  F1            		pop	af
  0E9E  D390          		out	(90h),a
  0EA0  C9            		ret
                      	
                      	
  0EA1                	PLSONE:
  0EA1  0000000081    		db	00h, 00h, 00h, 00h, 81h
                      	
  0EA6                	MNSONE:
  0EA6  0000008081    		db	00h, 00h, 00h, 80h, 81h
                      	
  0EAB                	ZERO:
  0EAB  0000000000    		db	00h, 00h, 00h, 00h, 00h
                      	
                      	
                      	;interrupt for graphic key
  0EB0                	_INTGRP:ds	INTGRP-_INTGRP
  0EB0                		org	INTGRP
                      	
  0EB0  C5            		push	bc
  0EB1  0601          		ld	b,01h
  0EB3  1803          		jr	KEYCOMMON
                      	
                      	
                      	;interrupt for normal key
  0EB5                	_INTKEY:ds	INTKEY-_INTKEY
  0EB5                		org	INTKEY
                      	
  0EB5  C5            		push	bc
  0EB6  0600          		ld	b,00h
                      	
  0EB8                	KEYCOMMON:
  0EB8  F5            		push	af
  0EB9  CD780E        		call	IN90H
                      	
                      	;0ebch: used by SPACE HARRIER
  0EBC  D5            		push	de
  0EBD  E5            		push	hl
  0EBE  B7            		or	a
  0EBF  286A          		jr	z,IKPOP
                      	;	dec	b
                      	;	jr	nz,NORMAL
  0EC1  1020          		djnz	NORMAL
                      	
  0EC3                	GRAPH:
  0EC3  FEFC          		cp	0fch		;SHIFT+PAGE, only for emulator
  0EC5  285D          		jr	z,RESSTOP
  0EC7  FEFE          		cp	0feh		;KANA, only for emulator
  0EC9  2859          		jr	z,RESSTOP
                      	
  0ECB  CD6F15        		call	CLICK
  0ECE  FEFA          		cp	0fah		;STOP
  0ED0  2848          		jr	z,STOPKEY
  0ED2  3057          		jr	nc,IKPOP	;CAPS/MODE, for mk2
  0ED4  47            		ld	b,a		;;
  0ED5  218FFB        		ld	hl,KYBFIN
  0ED8  4E            		ld	c,(hl)		;
  0ED9  3E14          		ld	a,14h
  0EDB  CD360F        		call	PUTKBF
  0EDE  3844          		jr	c,RESSTOP
  0EE0  78            		ld	a,b		;;
  0EE1  1822          		jr	IKBF2
                      	
                      	
                      	;normal key
  0EE3                	NORMAL:
  0EE3  CD6F15        		call	CLICK
  0EE6  FE03          		cp	03h		;Ctrl-C
  0EE8  2835          		jr	z,STOPKEY2
  0EEA  FE1B          		cp	1bh
  0EEC  2831          		jr	z,STOPKEY2
  0EEE  FE7F          		cp	7fh
  0EF0  2004          		jr	nz,IKBF
  0EF2  3E08          		ld	a,08h		;DEL->ctrl-H, not necessary for emulator?
                      	
  0EF4  1800          		jr	IKBF
                      	
                      	
                      	;0ef6h: used by PORTOPIA
  0EF6                	_IKBF:ds	IKBF-_IKBF
  0EF6                		org	IKBF
                      	
  0EF6  218FFB        		ld	hl,KYBFIN
  0EF9  4E            		ld	c,(hl)		;
  0EFA  CD360F        		call	PUTKBF
  0EFD  3825          		jr	c,RESSTOP
  0EFF  FE14          		cp	14h
  0F01  2021          		jr	nz,RESSTOP
  0F03  3EEF          		ld	a,0efh		;Ctrl-T->14h+0efh
                      	
  0F05                	IKBF2:
  0F05  CD360F        		call	PUTKBF
  0F08  301A          		jr	nc,RESSTOP
                      	
  0F0A  218FFB        		ld	hl,KYBFIN
  0F0D  71            		ld	(hl),c		;
  0F0E  1814          		jr	RESSTOP
                      	
                      	
                      	;interrupt for reply to key query
  0F10                	INTGAM:
  0F10  F5            		push	af
  0F11  CD780E        		call	IN90H
  0F14  32CAFE        		ld	(GMKYBUF),a
  0F17  F1            		pop	af
  0F18  FB            		ei
  0F19  C9            		ret
                      	
                      	
  0F1A                	STOPKEY:
  0F1A  CDD8FF        		call	HOOKSTP
  0F1D  3E03          		ld	a,03h
  0F1F                	STOPKEY2:
  0F1F  CD360F        		call	PUTKBF
  0F22  1801          		jr	SETSTOP
                      	
  0F24                	RESSTOP:
  0F24  AF            		xor	a
  0F25                	SETSTOP:
  0F25  3218FA        		ld	(STOPFLG),a
                      	;	jr	IKPOP
                      	
                      	
                      	;pop register for key interrupt
  0F28                	_IKPOP:	ds	IKPOP-_IKPOP
  0F2B                		org	IKPOP
                      	
  0F2B  E1            		pop	hl
  0F2C  D1            		pop	de
                      	
                      	;0f2dh: used by SPACE HARRIER
  0F2D  F1            		pop	af
  0F2E  C1            		pop	bc
  0F2F  FB            		ei
  0F30  C9            		ret
                      	
                      	
                      	;put into PLAY command buffer
                      	;input: a=data
                      	;destroy: f,hl
  0F31                	PUTPLBF:
  0F31  CD941C        		call	GETPLAD
  0F34  1803          		jr	PUTBF
                      	
                      	
                      	;put into key buffer
                      	;input: a
                      	;output: c-flag (1=buffer full)
                      	;destroy: f,hl
  0F36                	PUTKBF:
  0F36  218FFB        		ld	hl,KYBFIN
                      	;	call	PUTBF
                      	;	ret
                      	
                      	
                      	;put characer into buffer
                      	;input: a=character, hl=buffer control address
                      	;output: c-flag (1=buffer full)
                      	;destroy: f,hl
  0F39                	PUTBF:
  0F39  F5            		push	af
  0F3A  7E            		ld	a,(hl)		;in
  0F3B  3C            		inc	a
  0F3C  2C            		inc	l		;inc hl
  0F3D  2C            		inc	l		;inc hl
  0F3E  2C            		inc	l		;inc hl
  0F3F  A6            		and	(hl)		;size
  0F40  2D            		dec	l		;dec hl
  0F41  2D            		dec	l		;dec hl
  0F42  BE            		cp	(hl)		;out
  0F43  2811          		jr	z,BFFULL
                      	
  0F45  2D            		dec	l		;dec hl
  0F46  77            		ld	(hl),a		;in
  0F47  2C            		inc	l		;inc hl
  0F48  2C            		inc	l		;inc hl
  0F49  2C            		inc	l		;inc hl
  0F4A  2C            		inc	l		;inc hl
  0F4B  86            		add	a,(hl)		;address
  0F4C  2C            		inc	l		;inc hl
  0F4D  66            		ld	h,(hl)
  0F4E  6F            		ld	l,a
  0F4F  3001          		jr	nc,PTBNC
  0F51  24            		inc	h
  0F52                	PTBNC:
  0F52  F1            		pop	af
  0F53  77            		ld	(hl),a
  0F54  B7            		or	a		;reset c-flag
  0F55  C9            		ret
                      	
  0F56                	BFFULL:
  0F56  F1            		pop	af
  0F57  37            		scf
  0F58  C9            		ret
                      	
                      	
                      	;interrupt for RS-232C
  0F59                	INT232:
  0F59  F5            		push	af
  0F5A  E5            		push	hl
  0F5B  3E17          		ld	a,17h		;RTS=0
  0F5D  D381          		out	(81h),a
  0F5F                	INT232LP:
  0F5F  DB81          		in	a,(81h)
  0F61  E602          		and	02h
  0F63  28FA          		jr	z,INT232LP	;wait for RxRDY=1
  0F65  DB80          		in	a,(80h)
                      	
  0F67  2195FB        		ld	hl,RSBFIN
  0F6A  CD390F        		call	PUTBF
                      	
  0F6D  3E37          		ld	a,37h		;RTS=1
  0F6F  D381          		out	(81h),a
  0F71  E1            		pop	hl
  0F72  1832          		jr	POPAF
                      	
                      	
                      	;interrupt for timer
  0F74                	_INTTIM:ds	INTTIM-_INTTIM
  0F74                		org	INTTIM
                      	
  0F74  C5            		push	bc
  0F75  F5            		push	af
  0F76  D5            		push	de
  0F77  E5            		push	hl
                      	
  0F78  CDEC1B        		call	PLAYINT
                      	
  0F7B  2128FA        		ld	hl,TMCNT
  0F7E  34            		inc	(hl)
  0F7F  34            		inc	(hl)
  0F80  20A9          		jr	nz,IKPOP
  0F82  2C            		inc	l		;inc hl
  0F83  34            		inc	(hl)
  0F84  2006          		jr	nz,TIMCSR
  0F86  2C            		inc	l
  0F87  34            		inc	(hl)
  0F88  2002          		jr	nz,TIMCSR
  0F8A  2C            		inc	l
  0F8B  34            		inc	(hl)
                      	
  0F8C                	TIMCSR:
                      	;cursor blink
  0F8C  3A92FD        		ld	a,(SCREEN1)
  0F8F  212EFA        		ld	hl,CSRBLNK
  0F92  2F            		cpl
  0F93  0F            		rrca
  0F94  A6            		and	(hl)
  0F95  C48F11        		call	nz,CSRREV	;(SCREEN1)=0or1, (CSRBLNK)=1
  0F98  1891          		jr	IKPOP
                      	
                      	
                      	;interrupt for CMT load
  0F9A                	INTCMT:
  0F9A  F5            		push	af
  0F9B  CD780E        		call	IN90H
  0F9E  321DFA        		ld	(CMTBUF),a
  0FA1  3E02          		ld	a,02h
  0FA3                	SETCMTST:
  0FA3  3219FA        		ld	(CMTSTAT),a
  0FA6                	POPAF:
  0FA6  F1            		pop	af
  0FA7                	EIRET:
  0FA7  FB            		ei
  0FA8  C9            		ret
                      	
                      	
                      	;interrupt for CMT write stop
  0FA9                	INTWSTP:
                      	;interrupt for CMT read stop
  0FA9                	INTRSTP:
  0FA9  F5            		push	af
  0FAA  3E03          		ld	a,03h
  0FAC  3218FA        		ld	(STOPFLG),a
  0FAF  18F5          		jr	POPAF
                      	
                      	
                      	;interrupt for CMT error
  0FB1                	INTERR:
  0FB1  F5            		push	af
  0FB2  3E10          		ld	a,10h
  0FB4  18ED          		jr	SETCMTST
                      	
                      	
                      	;get input character
                      	;output: a, z-flag(1=no input)
                      	;destroy: f
  0FB6                	_GETCH:	ds	GETCH-_GETCH
  0FBC                		org	GETCH
                      	
  0FBC  E5            		push	hl
  0FBD  CDF10F        		call	GETCH2
  0FC0  E1            		pop	hl
  0FC1  C9            		ret
                      	
                      	
                      	;wait for input and get a character
                      	;output: a
                      	;destroy: f
  0FC2                	_GETC:	ds	GETC-_GETC
  0FC4                		org	GETC
                      	
  0FC4  E5            		push	hl
  0FC5                	GETCLP1:
  0FC5  CD7911        		call	CSRON
  0FC8  2600          		ld	h,00h
  0FCA                	GETCLP2:
  0FCA  24            		inc	h
  0FCB  CCB312        		call	z,PRTFKEY2
  0FCE  CDBC0F        		call	GETCH
  0FD1  28F7          		jr	z,GETCLP2
                      	
  0FD3  CD8111        		call	CSROFF
  0FD6  FEFE          		cp	0feh		;page switching key
  0FD8  2807          		jr	z,GETCPAGE
  0FDA  67            		ld	h,a
  0FDB  CD7B13        		call	CHGTXT
  0FDE  7C            		ld	a,h
  0FDF  E1            		pop	hl
  0FE0  C9            		ret
                      	
  0FE1                	GETCPAGE:
  0FE1  3A8FFD        		ld	a,(SCREEN2)
  0FE4  3C            		inc	a
  0FE5  218CFD        		ld	hl,PAGES
  0FE8  BE            		cp	(hl)
  0FE9  3801          		jr	c,GETCC
  0FEB  AF            		xor	a
  0FEC                	GETCC:
  0FEC  CD8813        		call	CHGSCR
  0FEF  18D4          		jr	GETCLP1
                      	
                      	
  0FF1                	GETCH2:
  0FF1  3A32FA        		ld	a,(FKEYCNT)
  0FF4  B7            		or	a
  0FF5  202E          		jr	nz,GETFKEY
  0FF7  2130FA        		ld	hl,GRPWRK
  0FFA  B6            		or	(hl)		;a=0
  0FFB  2808          		jr	z,GETCHZ	;(hl)=0?
  0FFD  3600          		ld	(hl),00h
  0FFF  FEEF          		cp	0efh		;Ctrl-T=14h+0efh
  1001  C8            		ret	z
  1002  C630          		add	a,30h
  1004  C9            		ret			;z-flag=0
                      	
  1005                	GETCHZ:
                      	;(GRPWRK)=0
  1005  CD3A10        		call	GETKBF
  1008  C8            		ret	z
  1009  FE14          		cp	14h
  100B  C0            		ret	nz
                      	
                      	;14h+**
  100C  CD3A10        		call	GETKBF
  100F  FEF0          		cp	0f0h
  1011  3006          		jr	nc,CNVFKEY
  1013  3230FA        		ld	(GRPWRK),a
  1016  3E14          		ld	a,14h
  1018  C9            		ret			;z-flag=0
                      	
                      	;a=f0-f9
  1019                	CNVFKEY:
  1019  21BDFB        		ld	hl,FKEYTBL+80h
                      	;	and	0fh
  101C  87            		add	a,a
  101D  87            		add	a,a
  101E  87            		add	a,a
  101F  85            		add	a,l		;no carry
  1020  6F            		ld	l,a
                      	;	jr	nc,CNVFKNC
                      	;	inc	h
                      	;CNVFKNC:
  1021  3E07          		ld	a,07h
  1023  1804          		jr	GETFKEY2
                      	
                      	;function key
  1025                	GETFKEY:
  1025  2A8DFB        		ld	hl,(FKEYAD)
  1028  3D            		dec	a
  1029                	GETFKEY2:
  1029  3232FA        		ld	(FKEYCNT),a
  102C  7E            		ld	a,(hl)
  102D  23            		inc	hl
  102E  228DFB        		ld	(FKEYAD),hl
  1031  B7            		or	a
  1032  C0            		ret	nz
  1033  3232FA        		ld	(FKEYCNT),a	;=0
  1036  C9            		ret			;z-flag=1: no input
                      	
                      	
                      	;get a character from key buffer
                      	;output: a,z-flag(1=no input)
                      	;destroy: f,hl
  1037                	_GETKBF:ds	GETKBF-_GETKBF
  103A                		org	GETKBF
                      	
  103A  218FFB        		ld	hl,KYBFIN
  103D                	CALLGETBF:
  103D  F3            		di
  103E  CD0B26        		call	GETBF
  1041  FB            		ei
  1042  C9            		ret
                      	
                      	
                      	;get a character from RS-232C buffer
                      	;output: a,z-flag(1=no input)
                      	;destroy: f,hl
  1043                	GETRSBF:
  1043  2195FB        		ld	hl,RSBFIN
  1046  18F5          		jr	CALLGETBF
                      	
                      	
                      	;get from PLAY command buffer
                      	;input: b=channel+1 (1,2,3)
                      	;output: a,z
                      	;destroy: f,hl
  1048                	GETPLBF:
  1048  219BFB        		ld	hl,BUFAIN-6
  104B  78            		ld	a,b
  104C  87            		add	a,a		;*2
  104D  80            		add	a,b		;*3
  104E  87            		add	a,a		;*6
  104F  85            		add	a,l		;no carry
  1050  6F            		ld	l,a
  1051  C30B26        		jp	GETBF
                      	
                      	
                      	;clear key buffer pointer
                      	;destroy: a,(f,bc,de,hl)
  1054                	_CLRKBF:ds	CLRKBF-_CLRKBF
  1058                		org	CLRKBF
                      	
  1058  3A8FFB        		ld	a,(KYBFIN)
  105B  3290FB        		ld	(KYBFOUT),a
  105E  C9            		ret
                      	
                      	
                      	;send key-query to sub CPU and get reply
                      	;output: a (space-0-left-right-down-up-stop-shift)
                      	;destroy: f
  105F                	_STICK:	ds	STICK-_STICK
  1061                		org	STICK
                      	
  1061  3EFF          		ld	a,0ffh
  1063  32CAFE        		ld	(GMKYBUF),a
  1066  3E06          		ld	a,06h
  1068  F3            		di
  1069  CD8F0E        		call	OUT90H
  106C  FB            		ei
  106D                	STICKLP:
  106D  3ACAFE        		ld	a,(GMKYBUF)
  1070  FEFF          		cp	0ffh		;bit6=0?
  1072  28F9          		jr	z,STICKLP
  1074  C9            		ret
                      	
                      	
                      	;print a character in CRT
                      	;input: a=character code
                      	;destroy: none
  1075                	_PRTC:	ds	PRTC-_PRTC
  1075                		org	PRTC
                      	
  1075  F5            		push	af
  1076  E5            		push	hl
                      	
  1077  2A31FA        		ld	hl,(GRPFLG)
  107A  2C            		inc	l
  107B  2D            		dec	l
  107C  200D          		jr	nz,PRTGRP
  107E  FE20          		cp	20h
  1080  3010          		jr	nc,PRTCNC
  1082  D5            		push	de
  1083  C5            		push	bc
  1084  CDFA10        		call	PRTCTL
  1087  C1            		pop	bc
  1088  D1            		pop	de
  1089  180A          		jr	PRTCEND
                      	
  108B                	PRTGRP:
  108B  D630          		sub	30h
  108D  2131FA        		ld	hl,GRPFLG
  1090  3600          		ld	(hl),00h
  1092                	PRTCNC:
  1092  CDAA10        		call	PRTCH
                      	
  1095                	PRTCEND:
  1095  E1            		pop	hl
  1096  F1            		pop	af
  1097  C9            		ret
                      	
                      	
                      	;print a character in graphic mode (screen mode 3,4)
                      	;input: a=character
                      	;destroy: af,hl
  1098                	PRT34:
  1098  2AA8FD        		ld	hl,(CSRY)
  109B  CDD514        		call	PRT34XY
  109E                	CTLRGT:
  109E  2AAAFD        		ld	hl,(CSRAD)
  10A1  23            		inc	hl
  10A2  1813          		jr	CHKNL
                      	
                      	;print a character in CRT (no control code)
                      	;input: a=character code
                      	;destroy: af,hl
  10A4                	_PRTCH:ds	PRTCH-_PRTCH
  10AA                		org	PRTCH
                      	
  10AA  2A92FD        		ld	hl,(SCREEN1)
  10AD  CB4D          		bit	1,l
  10AF  20E7          		jr	nz,PRT34	;screen mode 3,4
                      	
  10B1  2AAAFD        		ld	hl,(CSRAD)
  10B4  CDCA14        		call	PRT12
                      	
                      	;next line?
  10B7                	CHKNL:
  10B7  22AAFD        		ld	(CSRAD),hl
  10BA  21A9FD        		ld	hl,CSRX
  10BD  34            		inc	(hl)
  10BE  3AACFD        		ld	a,(WIDTH)
  10C1  BE            		cp	(hl)
  10C2  D0            		ret	nc
  10C3                	CHKNL2:
  10C3  3601          		ld	(hl),1
                      	
  10C5  3AA5FD        		ld	a,(LASTLIN)
  10C8  2AA2FD        		ld	hl,(CONSOL1)	;l=(CONSOL1)
  10CB  95            		sub	l
  10CC  2802          		jr	z,CTLLF
  10CE  3EFF          		ld	a,0ffh
                      	
  10D0                	CTLLF:
  10D0  3C            		inc	a		;0 or 1
  10D1  2AA8FD        		ld	hl,(CSRY)	;l=y+1,h=x+1
                      	
  10D4  E5            		push	hl
  10D5  D5            		push	de
  10D6  CD6715        		call	SETLINE
  10D9  D1            		pop	de
  10DA  E1            		pop	hl
                      	
  10DB  2C            		inc	l
  10DC  3AA5FD        		ld	a,(LASTLIN)
  10DF  BD            		cp	l
  10E0  D26D11        		jp	nc,SETCSR
                      	
                      	;scroll
  10E3  D5            		push	de
  10E4  54            		ld	d,h		;x+1
  10E5  2AA1FD        		ld	hl,(CONSOL1-1)	;h=(CONSOL1)
  10E8  1801          		jr	SKPPATCH1
                      	
                      	
                      	;10eah: to be patched by iP6/iP6win
  10EA                	_PATCH1:ds	PATCH1-_PATCH1
  10EA                		org	PATCH1
  10EA  00            		db	00h
                      	
  10EB                	SKPPATCH1:
  10EB  6F            		ld	l,a
  10EC  9C            		sbc	a,h		;c-flag=1, (LASTLIN)-(CONSOL1)-1
  10ED  D46012        		call	nc,SCRLUP
                      	
  10F0  62            		ld	h,d		;x+1
  10F1  CDB811        		call	Y2AD
  10F4  CDDA11        		call	DELLIN
  10F7  D1            		pop	de
  10F8  1873          		jr	SETCSR
                      	
                      	
  10FA                	PRTCTL:
  10FA  FE14          		cp	14h
  10FC  2847          		jr	z,CTLGRP
  10FE  FE07          		cp	07h
  1100  CACD1B        		jp	z,BELL
  1103  D60A          		sub	0ah
  1105  28C9          		jr	z,CTLLF
  1107  3D            		dec	a
  1108  283F          		jr	z,CTLHOM	;0bh
  110A  3D            		dec	a
  110B  CAFB1D        		jp	z,CLS		;0ch
  110E  2AA8FD        		ld	hl,(CSRY)	;l=y+1,h=x+1
  1111  3D            		dec	a
  1112  2838          		jr	z,CTLCR		;0dh
  1114  D60F          		sub	1ch-0dh
  1116  2886          		jr	z,CTLRGT	;1ch
  1118  3D            		dec	a
  1119  2808          		jr	z,CTLLFT	;1dh
  111B  3D            		dec	a
  111C  2814          		jr	z,CTLUP		;1eh
  111E  3D            		dec	a
  111F  C0            		ret	nz
                      	;	jr	z,CTLDWN	;1fh
                      	
  1120                	CTLDWN:
  1120  2C            		inc	l
  1121  1819          		jr	CTLUPNZ
                      	
  1123                	CTLLFT:
  1123  25            		dec	h
  1124  2047          		jr	nz,SETCSR
  1126  3AA2FD        		ld	a,(CONSOL1)
  1129  BD            		cp	l
  112A  D0            		ret	nc
  112B  2D            		dec	l
  112C  3AACFD        		ld	a,(WIDTH)
  112F  67            		ld	h,a
  1130  183B          		jr	SETCSR
                      	
  1132                	CTLUP:
  1132  3AA2FD        		ld	a,(CONSOL1)
  1135  BD            		cp	l
  1136  C8            		ret	z
  1137  2D            		dec	l
  1138  2002          		jr	nz,CTLUPNZ
  113A  2E01          		ld	l,01h
  113C                	CTLUPNZ:
  113C  3AA5FD        		ld	a,(LASTLIN)
  113F  BD            		cp	l
  1140  302B          		jr	nc,SETCSR
  1142  6F            		ld	l,a
  1143  1828          		jr	SETCSR
                      	
  1145                	CTLGRP:
  1145  3231FA        		ld	(GRPFLG),a	;a=14h
  1148  C9            		ret
                      	
  1149                	CTLHOM:
  1149  2AA2FD        		ld	hl,(CONSOL1)	;l=(CONSOL1)
                      	
  114C                	CTLCR:
  114C  2601          		ld	h,1
  114E  181D          		jr	SETCSR
                      	
                      	
                      	;set cursor position
                      	;input: h=x+1, l=y+1
                      	;output: (fda8h)=y+1,(fda9h)=x+1,(fdaa-fdab)=VRAM address
                      	;destroy: none
  1150                	_SETCSR:ds	SETCSR-_SETCSR
  116D                		org	SETCSR
  116D  E5            		push	hl
  116E  22A8FD        		ld	(CSRY),hl
  1171  CDCD11        		call	XY2AD
  1174  22AAFD        		ld	(CSRAD),hl
  1177  E1            		pop	hl
  1178  C9            		ret
                      	
                      	
                      	;cursor blink on
                      	;destroy: none
  1179                	_CSRON:
  1179                		ds	CSRON-_CSRON
  1179                		org	CSRON
                      	
  1179  F5            		push	af
  117A  3E01          		ld	a,01h
  117C  322EFA        		ld	(CSRBLNK),a
  117F  F1            		pop	af
  1180  C9            		ret
                      	
                      	
                      	;cursor blink off
                      	;destroy: hl
  1181                	_CSROFF:
  1181                		ds	CSROFF-_CSROFF
  1181                		org	CSROFF
                      	
  1181  F5            		push	af
  1182  AF            		xor	a
  1183  322EFA        		ld	(CSRBLNK),a
  1186  3A2FFA        		ld	a,(CSRSTAT)
  1189  B7            		or	a
  118A  C48F11        		call	nz,CSRREV
  118D  F1            		pop	af
  118E  C9            		ret
                      	
                      	
                      	;reverse cursor
                      	;destroy: af,hl
  118F                	CSRREV:
  118F  212FFA        		ld	hl,CSRSTAT
  1192  7E            		ld	a,(hl)
  1193  2F            		cpl
  1194  77            		ld	(hl),a
  1195  2AAAFD        		ld	hl,(CSRAD)
  1198  25            		dec	h
  1199  25            		dec	h
  119A  7E            		ld	a,(hl)
  119B  CB77          		bit	6,a		;semi-graphic mode?
  119D  2004          		jr	nz,CSRSEMI
  119F  EE01          		xor	01h
  11A1  77            		ld	(hl),a
  11A2  C9            		ret
                      	
  11A3                	CSRSEMI:
  11A3  24            		inc	h
  11A4  24            		inc	h
  11A5  7E            		ld	a,(hl)
  11A6  2F            		cpl
  11A7  77            		ld	(hl),a
  11A8  C9            		ret
                      	
                      	
                      	;get left edge address
                      	;input: l=y+1
                      	;output: de=VRAM address
                      	;destroy: none
  11A9                	_Y2AD:ds	Y2AD-_Y2AD
  11B8                		org	Y2AD
                      	
  11B8  F5            		push	af
  11B9  7D            		ld	a,l
  11BA  3D            		dec	a
  11BB  87            		add	a,a		;*2
  11BC  87            		add	a,a		;*4
  11BD  87            		add	a,a		;*8
  11BE  87            		add	a,a		;*16
  11BF  87            		add	a,a		;*32
  11C0  5F            		ld	e,a
  11C1  3A91FD        		ld	a,(VRAM)
  11C4  CE02          		adc	a,02h
  11C6  57            		ld	d,a
  11C7  F1            		pop	af
  11C8  C9            		ret
                      	
                      	
                      	;get VRAM character address (screen mode 1,2)
                      	;input: h=x+1, l=y+1
                      	;output: hl=VRAM address
                      	;destroy: none
  11C9                	_XY2AD:	ds	XY2AD-_XY2AD
  11CD                		org	XY2AD
                      	
  11CD  F5            		push	af
  11CE  D5            		push	de
  11CF  CDB811        		call	Y2AD
  11D2  25            		dec	h
  11D3  6C            		ld	l,h
  11D4  2600          		ld	h,00h
  11D6  19            		add	hl,de
  11D7  D1            		pop	de
  11D8  F1            		pop	af
  11D9  C9            		ret
                      	
                      	
                      	;delete line
                      	;input: de=left edge address (for screen mode 1,2)
                      	;destroy: none
  11DA                	_DELLIN:ds	DELLIN-_DELLIN
  11DA                		org	DELLIN
                      	
  11DA  F5            		push	af
  11DB  E5            		push	hl
  11DC  D5            		push	de
  11DD  C5            		push	bc
                      	
  11DE  3A92FD        		ld	a,(SCREEN1)
  11E1  FE02          		cp	02h
  11E3  3020          		jr	nc,DELG
                      	
  11E5  D5            		push	de		;
  11E6  15            		dec	d
  11E7  15            		dec	d
  11E8  62            		ld	h,d
  11E9  6B            		ld	l,e
  11EA  13            		inc	de
  11EB  CD2215        		call	GETSPA
  11EE  77            		ld	(hl),a
  11EF  011F00        		ld	bc,COLUMNS-1
  11F2  EDB0          		ldir
                      	
  11F4  E1            		pop	hl		;
  11F5  0E1F          		ld	c,COLUMNS-1
                      	
  11F7                	DELLEND:
  11F7  54            		ld	d,h
  11F8  5D            		ld	e,l
  11F9  13            		inc	de
  11FA  CD0D16        		call	GETSP
  11FD  77            		ld	(hl),a
  11FE  EDB0          		ldir
                      	
  1200  C1            		pop	bc
  1201  D1            		pop	de
  1202  E1            		pop	hl
  1203  F1            		pop	af
  1204  C9            		ret
                      	
                      	
                      	;for graphic mode (screen3,4)
  1205                	DELG:
  1205  EB            		ex	de,hl
  1206  CD7814        		call	AD2GAD
  1209  017F01        		ld	bc,0180h-1
  120C  18E9          		jr	DELLEND
                      	
                      	
                      	;for graphic mode (screen mode 3,4)
  120E                	SCRLG:
                      	;bc=a*32*12=a*(256+128)
  120E  48            		ld	c,b		;b=0
  120F  47            		ld	b,a
  1210  1F            		rra			;c-flag=0
  1211  CB19          		rr	c
  1213  80            		add	a,b
  1214  47            		ld	b,a
                      	
  1215  2601          		ld	h,1
  1217  CD7D14        		call	XY2GAD
  121A  EB            		ex	de,hl
  121B  218001        		ld	hl,COLUMNS*12
  121E  19            		add	hl,de
  121F  F1            		pop	af		;up/down
  1220  2003          		jr	nz,SCRLATT
  1222  EB            		ex	de,hl
  1223  1B            		dec	de
  1224  2B            		dec	hl
                      	
  1225                	SCRLATT:
  1225  CDAA3F        		call	LDIDR
  1228  C1            		pop	bc
  1229  D1            		pop	de
  122A  E1            		pop	hl
                      	
                      	;scroll INPUT prompt position
                      	;z=0: up,h=first,l=last
                      	;z=1: down,h=last,l=first
  122B  E5            		push	hl
  122C  D5            		push	de
                      	
  122D  1EFF          		ld	e,0-01h
  122F  2005          		jr	nz,SCRLIUP
  1231  1E01          		ld	e,01h
  1233  7C            		ld	a,h
  1234  65            		ld	h,l
  1235  6F            		ld	l,a
  1236                	SCRLIUP:
  1236  3AABFD        		ld	a,(CSRAD+1)
  1239  57            		ld	d,a
  123A  3AA7FE        		ld	a,(INPTPAG)
  123D  AA            		xor	d
  123E  E6F0          		and	0f0h
  1240  200E          		jr	nz,SCRLIEND
                      	
  1242  3AA4FE        		ld	a,(INPTXY)
  1245  BC            		cp	h
  1246  3808          		jr	c,SCRLIEND
  1248  2C            		inc	l
  1249  BD            		cp	l
  124A  3004          		jr	nc,SCRLIEND
  124C  83            		add	a,e
  124D  32A4FE        		ld	(INPTXY),a
  1250                	SCRLIEND:
  1250  D1            		pop	de
  1251  E1            		pop	hl
  1252  C9            		ret
                      	
                      	
                      	;print a character in CRT (no control code, no move in cursor)
                      	;input: a=character code, h=x+1, l=y+1
                      	;destroy: none
  1253                	_PRTCHXY:ds	PRTCHXY-_PRTCHXY
  1257                		org	PRTCHXY
                      	
  1257  E5            		push	hl
  1258  CDCD11        		call	XY2AD
  125B  CDAF14        		call	PRTCHAD
  125E  E1            		pop	hl
  125F  C9            		ret
                      	
                      	
                      	;scroll up
                      	;input: h=first line+1, l=last line+1
                      	;destroy: af
  1260                	_SCRLUP:ds	SCRLUP-_SCRLUP
  1260                		org	SCRLUP
                      	
  1260  7D            		ld	a,l
  1261  94            		sub	h		;a=(LASTLIN)-(CONSOL1) < 16
                      	;	ret	z
                      	;	ret	c
  1262  E5            		push	hl
                      	
                      	;z=0:scroll up, z=1:scroll down
  1263                	SCRLUD:
  1263  D5            		push	de
  1264  C5            		push	bc
  1265  F5            		push	af		;up/down
                      	
                      	;line connection status
  1266  6C            		ld	l,h
  1267  E5            		push	hl
  1268  CD6715        		call	SETLINE		;get work area address, no change in z-flag
  126B  54            		ld	d,h
  126C  5D            		ld	e,l
  126D  0600          		ld	b,00h
  126F  4F            		ld	c,a
                      	
  1270  2008          		jr	nz,SCRLNZ1
  1272  12            		ld	(de),a		;>0
  1273  1D            		dec	e
  1274  2D            		dec	l		;dec hl
  1275  2D            		dec	l
  1276  EDB8          		lddr
                      	
  1278  1803          		jr	SCRLSKP1
  127A                	SCRLNZ1:
  127A  2C            		inc	l		;inc hl
  127B  EDB0          		ldir
  127D                	SCRLSKP1:
                      	
  127D  12            		ld	(de),a		;>0
  127E  2A92FD        		ld	hl,(SCREEN1)
  1281  2D            		dec	l
  1282  2D            		dec	l
  1283  E1            		pop	hl
  1284  F20E12        		jp	p,SCRLG		;screen mode 3,4
                      	
  1287  CD9F3F        		call	MUL32
  128A  CDB811        		call	Y2AD
  128D  212000        		ld	hl,COLUMNS
  1290  19            		add	hl,de
  1291  F1            		pop	af		;up/down
  1292  F5            		push	af		;up/down
  1293  2003          		jr	nz,SCRLNZ2
  1295  EB            		ex	de,hl
  1296  1B            		dec	de
  1297  2B            		dec	hl
                      	
  1298                	SCRLNZ2:
  1298  E5            		push	hl
  1299  D5            		push	de
  129A  C5            		push	bc
  129B  CDAA3F        		call	LDIDR
  129E  C1            		pop	bc
  129F  D1            		pop	de
  12A0  E1            		pop	hl
                      	
                      	;attribute
  12A1  25            		dec	h
  12A2  25            		dec	h
  12A3  15            		dec	d
  12A4  15            		dec	d
  12A5  F1            		pop	af		;up/down
  12A6  C32512        		jp	SCRLATT
                      	
                      	
                      	;scroll down
                      	;input: h=last line+1, l=first line+1
                      	;destroy: af
  12A9                	_SCRLDW:ds	SCRLDW-_SCRLDW
  12A9                		org	SCRLDW
                      	
  12A9  7C            		ld	a,h
  12AA  95            		sub	l
                      	;	ret	z
                      	;	ret	c
  12AB  E5            		push	hl
  12AC  BF            		cp	a		;set z-flag
  12AD  18B4          		jr	SCRLUD
                      	
                      	
                      	;print function key
                      	;input: none
                      	;output: none
                      	;destroy: af
  12AF                	PRTFKEY:
  12AF  AF            		xor	a
  12B0  32A7FD        		ld	(FKEYSFT),a
                      	
  12B3                	PRTFKEY2:
  12B3  3A92FD        		ld	a,(SCREEN1)
  12B6  FE02          		cp	02h
  12B8  D0            		ret	nc		;screen mode 3 or 4
                      	
  12B9  E5            		push	hl
  12BA  D5            		push	de
  12BB  C5            		push	bc
                      	
  12BC  21A7FD        		ld	hl,FKEYSFT
  12BF  3AA6FD        		ld	a,(CONSOL3)
  12C2  B7            		or	a
  12C3  285F          		jr	z,DELFK
                      	
  12C5  113DFB        		ld	de,FKEYTBL
  12C8  CD6110        		call	STICK
  12CB  E601          		and	01h
  12CD  2802          		jr	z,FKEYZ
  12CF  1E65          		ld	e,FKEYTBL+8*5-(FKEYTBL+8*5)/256*256	;de=FKEYTBL+8*5
  12D1                	FKEYZ:
  12D1  87            		add	a,a
  12D2  3C            		inc	a
  12D3  BE            		cp	(hl)
  12D4  284A          		jr	z,PFKEND
  12D6  77            		ld	(hl),a
                      	
  12D7  D5            		push	de
  12D8  2A20FA        		ld	hl,(HEIGHT)
  12DB  CDB811        		call	Y2AD
  12DE  EB            		ex	de,hl
  12DF  D1            		pop	de
                      	
  12E0  CD6A13        		call	CHRREV
  12E3  3A8FFD        		ld	a,(SCREEN2)
  12E6  C631          		add	a,'1'
  12E8  CDCA14        		call	PRT12
                      	
  12EB  CD6A13        		call	CHRREV
  12EE  3E20          		ld	a,' '
  12F0  CDCA14        		call	PRT12
                      	
  12F3  0E05          		ld	c,05h
  12F5                	FKEYLP1:
  12F5  CD6A13        		call	CHRREV
  12F8  0605          		ld	b,05h
  12FA                	FKEYLP2:
  12FA  1A            		ld	a,(de)
  12FB  B7            		or	a
  12FC  280C          		jr	z,FKEYNEXT
  12FE  FE20          		cp	20h
  1300  3002          		jr	nc,FKEYNC
  1302  3E20          		ld	a,' '
  1304                	FKEYNC:
  1304  CDCA14        		call	PRT12
  1307  1C            		inc	e		;inc de
  1308  10F0          		djnz	FKEYLP2
                      	
  130A                	FKEYNEXT:
  130A  04            		inc	b
  130B                	FKEYLP3:
  130B  78            		ld	a,b
  130C  3D            		dec	a
  130D  CC6A13        		call	z,CHRREV
  1310  3E20          		ld	a,' '
  1312  CDCA14        		call	PRT12
  1315  1C            		inc	e		;inc de
  1316  10F3          		djnz	FKEYLP3
                      	
  1318  1C            		inc	e		;inc de
  1319  1C            		inc	e		;inc de
  131A  0D            		dec	c
  131B  20D8          		jr	nz,FKEYLP1
                      	
  131D                	PFKEND2:
  131D  CD771D        		call	SETCNSL2
  1320                	PFKEND:
  1320  C1            		pop	bc
  1321  D1            		pop	de
  1322  E1            		pop	hl
  1323  C9            		ret
                      	
  1324                	DELFK:
  1324  BE            		cp	(hl)		;hl=FKEYSFT
  1325  28F9          		jr	z,PFKEND
  1327  77            		ld	(hl),a
  1328  2A20FA        		ld	hl,(HEIGHT)
  132B  CDB811        		call	Y2AD
  132E  CDDA11        		call	DELLIN
  1331  18EA          		jr	PFKEND2
                      	
                      	
                      	;convert VRAM address to text screen (x,y)
                      	;input: hl=address
                      	;output: h=x+1, l=y+1(0=error)
                      	;destroy: af
  1333                	AD2XY:
  1333  CB4C          		bit	1,h
  1335  280F          		jr	z,AD2XYZ
                      	
  1337  7C            		ld	a,h
  1338  E601          		and	01h
  133A  67            		ld	h,a
  133B  7D            		ld	a,l
  133C  E61F          		and	1fh
  133E  29            		add	hl,hl
  133F  29            		add	hl,hl
  1340  29            		add	hl,hl
  1341  6C            		ld	l,h
  1342  67            		ld	h,a
  1343  24            		inc	h
  1344  2C            		inc	l
  1345  C9            		ret
                      	
  1346                	AD2XYZ:
  1346  2E00          		ld	l,00h
  1348  C9            		ret
                      	
                      	
                      	;get corrected color parameter
                      	;output: a
                      	;destroy: f
  1349                	CORRC:
  1349  3A92FD        		ld	a,(SCREEN1)
  134C  FE03          		cp	03h		;
  134E  0F            		rrca			;;
  134F  3A93FD        		ld	a,(COLOR1)
  1352  3008          		jr	nc,CORRC13	;;
  1354  280F          		jr	z,CORRC4	;
                      	
                      	;screen mode 2: 0~8
  1356                	CORRC2:
  1356  FE08          		cp	08h
  1358  D8            		ret	c
  1359  3E08          		ld	a,08h
  135B  C9            		ret
                      	
                      	;screen mode 1,3: 1~4
  135C                	CORRC13:
  135C  B7            		or	a
  135D  2808          		jr	z,CORRCZ
  135F  FE04          		cp	04h
  1361  D8            		ret	c
  1362  3E04          		ld	a,04h
  1364  C9            		ret
                      	
                      	;screen mode 4: 0~1
  1365                	CORRC4:
  1365  B7            		or	a
  1366  C8            		ret	z
  1367                	CORRCZ:
  1367  3E01          		ld	a,01h
  1369  C9            		ret
                      	
                      	
                      	;reverse character attribute
                      	;destroy: af
  136A                	CHRREV:
  136A  CD4913        		call	CORRC
  136D  EE07          		xor	07h
  136F  FE05          		cp	05h
  1371  3802          		jr	c,REVEND
  1373  E603          		and	03h
  1375                	REVEND:
  1375  3293FD        		ld	(COLOR1),a
  1378  C3C015        		jp	SETATT
                      	
                      	
                      	;change to text screen
                      	;input: none
                      	;output: none
                      	;destroy: af
  137B                	CHGTXT:
  137B  3A90FD        		ld	a,(SCREEN3)
  137E  CD0C14        		call	CHGACT
  1381  3A92FD        		ld	a,(SCREEN1)
  1384  FE02          		cp	02h
  1386  D8            		ret	c
  1387  AF            		xor	a
  1388                	CHGSCR:
  1388  CD0C14        		call	CHGACT
  138B  C3ED13        		jp	CHGDSP
                      	
                      	
                      	;change screen mode
                      	;input: a=mode-1
                      	;destroy: af,de
  138E                	_CHGMOD:ds	CHGMOD-_CHGMOD
  1390                		org	CHGMOD
                      	
  1390  FE04          		cp	04h
  1392  D2E503        		jp	nc,FCERR
  1395  E5            		push	hl
  1396  2192FD        		ld	hl,SCREEN1
  1399  5E            		ld	e,(hl)		;
  139A  BB            		cp	e
  139B  2847          		jr	z,CMODEND
  139D  77            		ld	(hl),a
                      	
  139E  C5            		push	bc
  139F  F5            		push	af		;;
                      	
  13A0  FE02          		cp	02h
  13A2  3E20          		ld	a,COLUMNS	;screen mode 1,2,4:width=32
  13A4  2001          		jr	nz,SETWID
  13A6  1F            		rra			;screen mode 3:width=16
  13A7                	SETWID:
  13A7  32ACFD        		ld	(WIDTH),a
                      	
  13AA  2196FD        		ld	hl,M1COLOR
  13AD  010300        		ld	bc,0003h
  13B0  50            		ld	d,b		;b=0
  13B1  19            		add	hl,de		;
  13B2  19            		add	hl,de
  13B3  19            		add	hl,de
  13B4  EB            		ex	de,hl
  13B5  2193FD        		ld	hl,COLOR1
  13B8  EDB0          		ldir
                      	
  13BA  F1            		pop	af		;;
  13BB  2196FD        		ld	hl,M1COLOR
  13BE  50            		ld	d,b		;b=0
  13BF  5F            		ld	e,a
  13C0  19            		add	hl,de
  13C1  19            		add	hl,de
  13C2  19            		add	hl,de
  13C3  1193FD        		ld	de,COLOR1
  13C6  0E03          		ld	c,03h		;b=0
  13C8  EDB0          		ldir
                      	
  13CA  CD2215        		call	GETSPA
  13CD  2A90FD        		ld	hl,(VRAM-1)	;h=(VRAM)
  13D0  68            		ld	l,b		;b=0
  13D1  77            		ld	(hl),a
  13D2  04            		inc	b
  13D3  54            		ld	d,h
  13D4  58            		ld	e,b		;de=hl+1
  13D5  0D            		dec	c		;bc=1ffh
  13D6  EDB0          		ldir
                      	
  13D8  CD771D        		call	SETCNSL2
  13DB  2E01          		ld	l,01h
  13DD  3A20FA        		ld	a,(HEIGHT)
  13E0  CD3916        		call	CLSMAIN
                      	
  13E3  C1            		pop	bc
  13E4                	CMODEND:
  13E4  E1            		pop	hl
  13E5  C9            		ret
                      	
                      	
                      	;change display page
                      	;page1=8000h or c000h	out (0b0h),4 or 0
                      	;page2=e000h		out (0b0h),2
                      	;page3=c000h		out (0b0h),0
                      	;page4=a000h		out (0b0h),6
                      	;input: a=page-1
                      	;destroy: af
  13E6                	_CHGDSP:ds	CHGDSP-_CHGDSP
  13ED                		org	CHGDSP
                      	
  13ED  3290FD        		ld	(SCREEN3),a
  13F0  B7            		or	a
  13F1  2006          		jr	nz,DSPOK
  13F3  3A2AFF        		ld	a,(BASICAD+1)	;84h or c4h
  13F6  07            		rlca
  13F7  07            		rlca
  13F8  87            		add	a,a		;32KB:page1=8000h
                      					;16KB:page1=c000h
  13F9                	DSPOK:
                      	;0,1,2,3->4,2,0,6
  13F9  3C            		inc	a
  13FA  2F            		cpl
  13FB  07            		rlca
                      	;	and	06h
                      	
  13FC  C5            		push	bc
  13FD  0606          		ld	b,00000110b
  13FF  CD541B        		call	OUTB0H
  1402  C1            		pop	bc
                      	
  1403  C3B312        		jp	PRTFKEY2
                      	
                      	
                      	;change active page
                      	;input: a=page-1
                      	;destroy: none
  1406                	_CHGACT:ds	CHGACT-_CHGACT
  140C                		org	CHGACT
                      	
  140C  F5            		push	af
  140D  E5            		push	hl
                      	
  140E  218FFD        		ld	hl,SCREEN2
  1411  BE            		cp	(hl)
  1412  281C          		jr	z,ACTEND
                      	
  1414  D5            		push	de
  1415  C5            		push	bc
                      	
  1416  57            		ld	d,a		;
  1417  7E            		ld	a,(hl)
  1418  72            		ld	(hl),d		;
  1419  CD3314        		call	GETPGAD
  141C  7A            		ld	a,d		;
  141D  EB            		ex	de,hl
  141E  2191FD        		ld	hl,VRAM		;top of active page data
  1421  EDB0          		ldir
                      	
  1423  CD3314        		call	GETPGAD
  1426  1191FD        		ld	de,VRAM
  1429  EDB0          		ldir
                      	
  142B  CDAF12        		call	PRTFKEY
                      	
  142E  C1            		pop	bc
  142F  D1            		pop	de
                      	
  1430                	ACTEND:
  1430  E1            		pop	hl
  1431  F1            		pop	af
  1432  C9            		ret
                      	
                      	
                      	;get page data address
                      	;input: a=page-1
                      	;output: hl=address, bc=size
                      	;destroy: af
  1433                	GETPGAD:
  1433  21C8FD        		ld	hl,PAGE1
  1436  013700        		ld	bc,PAGE2-PAGE1
  1439  B7            		or	a
  143A                	PGADLP:
  143A  C8            		ret	z
  143B  09            		add	hl,bc
  143C  3D            		dec	a
  143D  18FB          		jr	PGADLP
                      	
                      	
                      	;get VRAM dot pattern address (screen mode 3,4)
                      	;hl=(VRAM+0200h)+(X/8)+Y*32
                      	;input: bc=graphic X, de=graphic Y
                      	;output: hl=VRAM address, d=bit
                      	;destroy: af,bc,e
  143F                	GXY2GAD:
  143F  CDD82B        		call	CHKGXY
  1442  EB            		ex	de,hl
  1443  29            		add	hl,hl		;*2
  1444  29            		add	hl,hl		;*4
  1445  29            		add	hl,hl		;*8
  1446  29            		add	hl,hl		;*16
  1447  29            		add	hl,hl		;*32
  1448  3A91FD        		ld	a,(VRAM)
  144B  C602          		add	a,02h
  144D  47            		ld	b,a
  144E  79            		ld	a,c
  144F  CB39          		srl	c
  1451  CB39          		srl	c
  1453  CB39          		srl	c
  1455  09            		add	hl,bc
                      	
  1456  E607          		and	07h
  1458  3C            		inc	a
  1459  47            		ld	b,a
                      	
  145A  3A92FD        		ld	a,(SCREEN1)
  145D  0F            		rrca
  145E  3E01          		ld	a,01h
  1460  3806          		jr	c,GXY2GADLP	;screen mode 4
                      	
  1462  05            		dec	b
  1463  CB80          		res	0,b
  1465  04            		inc	b
  1466  3E81          		ld	a,81h
                      	
  1468                	GXY2GADLP:
  1468  0F            		rrca
  1469  10FD          		djnz	GXY2GADLP
  146B  57            		ld	d,a
  146C  C9            		ret
                      	
                      	
                      	;convert character VRAM address to graphic VRAM address
                      	;(screen mode 1,2 -> screen mode 3,4)
                      	;input: hl=character VRAM address
                      	;output: hl=graphic VRAM address
                      	;destroy: none
  146D                	_AD2GAD:ds	AD2GAD-_AD2GAD
  1478                		org	AD2GAD
                      	
  1478  F5            		push	af
  1479  CD3313        		call	AD2XY
  147C  F1            		pop	af
                      	;	jr	XY2GAD
                      	
                      	
                      	;get VRAM address (screen mode 3,4)
                      	;input: h=x+1, l=y+1
                      	;output: hl=graphic VRAM address =(VRAM+200h)+y*0180h+x
                      	;destroy: none
  147D                	XY2GAD:
  147D  F5            		push	af
  147E  25            		dec	h
  147F  2D            		dec	l
  1480  3A92FD        		ld	a,(SCREEN1)
  1483  FE02          		cp	02h
  1485  2002          		jr	nz,XY2GADC
  1487  CB24          		sla	h		;screen mode 3
  1489                	XY2GADC:
  1489  CB24          		sla	h
  148B  3A91FD        		ld	a,(VRAM)
  148E  C602          		add	a,02h
  1490  85            		add	a,l
  1491  CB3D          		srl	l
  1493  CB1C          		rr	h
  1495  85            		add	a,l
  1496  6C            		ld	l,h
  1497  67            		ld	h,a
  1498  F1            		pop	af
  1499  C9            		ret
                      	
                      	
                      	;get CGROM address (DE=6000h+A*10h)
                      	;input: a=character code
                      	;output: de=address
                      	;destroy: none
  149A                	_CGROM:	ds	CGROM-_CGROM
  14A0                		org	CGROM
                      	
  14A0  F5            		push	af
  14A1  07            		rlca
  14A2  07            		rlca
  14A3  07            		rlca
  14A4  07            		rlca
  14A5  57            		ld	d,a
  14A6  E6F0          		and	0f0h
  14A8  5F            		ld	e,a
  14A9  AA            		xor	d
  14AA  C660          		add	a,60h
  14AC  57            		ld	d,a
  14AD  F1            		pop	af
  14AE  C9            		ret
                      	
                      	;	push	af		;push flag
                      	;	ex	de,hl
                      	;	ld	h,06h
                      	;	ld	l,a
                      	;	add	hl,hl
                      	;	add	hl,hl
                      	;	add	hl,hl
                      	;	add	hl,hl
                      	;	ex	de,hl
                      	;	pop	af		;pop flag
                      	;	ret
                      	
                      	
                      	;print a character in CRT (no control code, no move in cursor)
                      	;input: a=character code, hl=VRAM address
                      	;destroy: none
  14AF                	_PRTCHAD:ds	PRTCHAD-_PRTCHAD
  14AF                		org	PRTCHAD
                      	
  14AF  E5            		push	hl
  14B0  F5            		push	af
  14B1  3A92FD        		ld	a,(SCREEN1)
  14B4  FE02          		cp	02h
  14B6  380A          		jr	c,PRTCHXY12
                      	
                      	;screen mode 3,4
  14B8  CD3313        		call	AD2XY
  14BB  F1            		pop	af
  14BC  F5            		push	af
  14BD  CDD514        		call	PRT34XY
  14C0  1805          		jr	PRTCHADEND
                      	
  14C2                	PRTCHXY12:
  14C2  F1            		pop	af
  14C3  F5            		push	af
  14C4  CDCA14        		call	PRT12
                      	
  14C7                	PRTCHADEND:
  14C7  F1            		pop	af
  14C8  E1            		pop	hl
  14C9  C9            		ret
                      	
                      	
                      	;print a character in text mode (screen mode 1,2)
                      	;input: a=character code, hl=VRAM address
                      	;output: hl=hl+1
                      	;destroy: af
  14CA                	PRT12:
  14CA  77            		ld	(hl),a
  14CB  CDF515        		call	CNVATT1
  14CE  25            		dec	h
  14CF  25            		dec	h
  14D0  77            		ld	(hl),a
  14D1  24            		inc	h
  14D2  24            		inc	h
  14D3  23            		inc	hl
  14D4  C9            		ret
                      	
                      	
                      	;print a character in graphic mode (screen mode 3,4)
                      	;input: a=character code, h=x+1, l=y+1
                      	;destroy af,hl
  14D5                	PRT34XY:
  14D5  D5            		push	de
  14D6  C5            		push	bc
                      	
  14D7  CDA014        		call	CGROM
  14DA  CD7D14        		call	XY2GAD
                      	
  14DD  3E04          		ld	a,04h
  14DF  D393          		out	(93h),a		;CGROM ON
  14E1  3A93FD        		ld	a,(COLOR1)
  14E4  CDC015        		call	SETATT
                      	
  14E7  060C          		ld	b,0ch
  14E9                	PRT34XYLP:
  14E9  D5            		push	de
  14EA  3A92FD        		ld	a,(SCREEN1)
  14ED  0F            		rrca			;
  14EE  1A            		ld	a,(de)
  14EF  3005          		jr	nc,CALL3	;
  14F1  CD1915        		call	PRT4
  14F4  1804          		jr	CALL34END
  14F6                	CALL3:
  14F6  CD0915        		call	PRT3
  14F9  2B            		dec	hl
                      	
  14FA                	CALL34END:
  14FA  112000        		ld	de,COLUMNS
  14FD  19            		add	hl,de
  14FE  D1            		pop	de
  14FF  1C            		inc	e		;inc de
  1500  10E7          		djnz	PRT34XYLP
                      	
  1502  3E05          		ld	a,05h
  1504  D393          		out	(93h),a		;CGROM OFF
                      	
  1506  C1            		pop	bc
  1507  D1            		pop	de
  1508  C9            		ret
                      	
  1509                	PRT3:
  1509  5F            		ld	e,a
  150A  CD0E15        		call	PRT3HALF
  150D  23            		inc	hl
  150E                	PRT3HALF:
  150E  1604          		ld	d,04h
  1510                	PRT3LP:
  1510  CB03          		rlc	e
  1512  17            		rla
  1513  0F            		rrca
  1514  07            		rlca
  1515  17            		rla
  1516  15            		dec	d
  1517  20F7          		jr	nz,PRT3LP
                      	
  1519                	PRT4:
  1519  4F            		ld	c,a
  151A  3AACFE        		ld	a,(ATTDAT)
  151D  AE            		xor	(hl)
  151E  A1            		and	c
  151F  AE            		xor	(hl)
  1520  77            		ld	(hl),a
  1521  C9            		ret
                      	
                      	
                      	;get space attribute data for scroll
                      	;output: a
                      	;destroy: f
  1522                	GETSPA:
  1522  3A92FD        		ld	a,(SCREEN1)
  1525  3D            		dec	a
  1526  FAF515        		jp	m,CNVATT1
  1529  280B          		jr	z,GETSPA2
                      	
  152B                	GETSPAG:
  152B  3D            		dec	a
  152C  3A95FD        		ld	a,(COLOR3)
  152F  2802          		jr	z,GETSPA3
  1531  C650          		add	a,50h
  1533                	GETSPA3:
  1533  C68C          		add	a,8ch		;screen mode 3=8ch, screen mode 4=dch
  1535  C9            		ret
                      	
  1536                	GETSPA2:
  1536  3A94FD        		ld	a,(COLOR2)
  1539  B7            		or	a
  153A  2003          		jr	nz,GETSPA2NZ
  153C  3A93FD        		ld	a,(COLOR1)
                      	
  153F                	GETSPA2NZ:
  153F  FE05          		cp	05h
  1541  3A95FD        		ld	a,(COLOR3)
  1544  3802          		jr	c,GETSPAC
  1546  EE02          		xor	02h
  1548                	GETSPAC:
  1548  C660          		add	a,60h
  154A  C9            		ret
                      	
                      	
                      	;check connection to previous character
                      	;input: hl=VRAM address
                      	;output: z-flag(1:connect)
                      	;destroy: af
  154B                	CHKLINE:
  154B  7D            		ld	a,l
  154C  E61F          		and	1fh
  154E  2010          		jr	nz,CHKLINENZ
  1550  E5            		push	hl
  1551  29            		add	hl,hl
  1552  29            		add	hl,hl
  1553  29            		add	hl,hl
  1554  7C            		ld	a,h
  1555  E60F          		and	0fh
  1557  21B6FD        		ld	hl,LINEST-1
  155A  85            		add	a,l		;no carry
  155B  6F            		ld	l,a
  155C  7E            		ld	a,(hl)
  155D  E1            		pop	hl
  155E  B7            		or	a
  155F  C9            		ret
                      	
  1560                	CHKLINENZ:
  1560  AF            		xor	a
  1561  C9            		ret
                      	
                      	
                      	;cut connection before cursor line
                      	;output: hl=line status address
                      	;destroy: af,de
  1562                	CUTLINE:
  1562  3AA8FD        		ld	a,(CSRY)	;a>0
                      	
                      	;input: a=y+1
                      	;output: hl=line status address
                      	;destroy: f,de
  1565                	CUTLINE2:
  1565  6F            		ld	l,a
  1566  2D            		dec	l
                      	;	jp	SETLINE
                      	
                      	;set line connection status (connection to next line)
                      	;input: l=y+1, a=data (0=connect or not)
                      	;output: hl=line status address
                      	;destroy: f,de
  1567                	SETLINE:
  1567  2600          		ld	h,00h
  1569  11B6FD        		ld	de,LINEST-1
  156C  19            		add	hl,de
  156D  77            		ld	(hl),a
  156E  C9            		ret
                      	
                      	
                      	;click sound
                      	;destroy: de
  156F                	CLICK:
  156F  F5            		push	af
  1570  3A2DFA        		ld	a,(CONSOL4)
  1573  B7            		or	a
  1574  2823          		jr	z,SKPCLK
                      	
  1576  C5            		push	bc
                      	
  1577  3E08          		ld	a,08h		;register8=ch.A volume
  1579  1E0F          		ld	e,0fh
  157B  CDBE1B        		call	SETPSG2
  157E  D5            		push	de		;
                      	
  157F  3E01          		ld	a,01h		;register1=ch.A coarse tune
  1581  1E00          		ld	e,00h
  1583  43            		ld	b,e
  1584  4F            		ld	c,a
  1585  CDBE1B        		call	SETPSG2		;;
                      	
  1588  CDE825        		call	WAITLP		;bc=0001h
  158B  3E01          		ld	a,01h		;register1=ch.A coarse tune
  158D  5A            		ld	e,d		;;
  158E  CDC51B        		call	SETPSG
                      	
  1591  3E08          		ld	a,08h		;register8=ch.A volume
  1593  D1            		pop	de		;
  1594  5A            		ld	e,d
  1595  CDC51B        		call	SETPSG
                      	
  1598  C1            		pop	bc
                      	
  1599                	SKPCLK:
  1599  F1            		pop	af
  159A  C9            		ret
                      	
                      	
                      	;set attribute data
                      	;input: a=color code
                      	;output: a,(ATTDAT)=attribute
                      	;destroy: f
  159B                	_SETATT:ds	SETATT-_SETATT
  15C0                		org	SETATT
                      	
  15C0  CDC715        		call	CNVATT
  15C3  32ACFE        		ld	(ATTDAT),a
  15C6  C9            		ret
                      	
                      	
                      	;convert to attribute data (screen mode 1)
                      	; or color code (screen moode 2)
                      	; or bit pattern (screen mode 3,4)
                      	;input: a=color code
                      	;output: a=attribute
                      	;destroy: f
  15C7                	CNVATT:
  15C7  F5            		push	af		;color
  15C8  3A92FD        		ld	a,(SCREEN1)
  15CB  3D            		dec	a
  15CC  2820          		jr	z,ATT2
  15CE  FAF915        		jp	m,ATT1
  15D1  3D            		dec	a
  15D2  2806          		jr	z,ATT3
                      	
  15D4                	ATT4:
  15D4  F1            		pop	af
  15D5  B7            		or	a
  15D6  C8            		ret	z
  15D7  3EFF          		ld	a,0ffh
  15D9  C9            		ret
                      	
  15DA                	ATT3:
  15DA  F1            		pop	af
  15DB  B7            		or	a
  15DC  C8            		ret	z
                      	
  15DD  C5            		push	bc
  15DE  0604          		ld	b,04h
  15E0  B8            		cp	b
  15E1  3801          		jr	c,ATT3C
  15E3  78            		ld	a,b
  15E4                	ATT3C:
  15E4  3D            		dec	a
  15E5  4F            		ld	c,a
  15E6  05            		dec	b		;=3
  15E7                	ATT3LP:
  15E7  0F            		rrca
  15E8  0F            		rrca
  15E9  B1            		or	c
  15EA  10FB          		djnz	ATT3LP
  15EC  C1            		pop	bc
  15ED  C9            		ret
                      	
  15EE                	ATT2:
  15EE  F1            		pop	af
  15EF  FE09          		cp	09h
  15F1  D8            		ret	c
  15F2  3E08          		ld	a,08h
  15F4  C9            		ret
                      	
                      	
                      	;get attribue data for screen mode 1
                      	;output: a=attirbute value
                      	;destroy: f
  15F5                	CNVATT1:
  15F5  3A93FD        		ld	a,(COLOR1)
  15F8  F5            		push	af
  15F9                	ATT1:
  15F9  F1            		pop	af
  15FA  B7            		or	a
  15FB  2807          		jr	z,ATT1OK
  15FD  3D            		dec	a
  15FE  FE04          		cp	04h
  1600  3802          		jr	c,ATT1OK
  1602  3E03          		ld	a,03h
  1604                	ATT1OK:
  1604  C620          		add	a,20h
  1606  E5            		push	hl
  1607  2195FD        		ld	hl,COLOR3
  160A  AE            		xor	(hl)
  160B  E1            		pop	hl
  160C  C9            		ret
                      	
                      	
                      	;get space character data for scroll
                      	;output: a
                      	;destroy: f
  160D                	GETSP:
  160D  3A92FD        		ld	a,(SCREEN1)
  1610  3D            		dec	a
  1611  3E20          		ld	a,' '
  1613  F8            		ret	m		;screen mode 1
  1614  3A94FD        		ld	a,(COLOR2)
  1617  20AE          		jr	nz,CNVATT	;screen mode 3,4
                      	
                      	;screen mode 2
  1619                	GETSP2:
  1619  B7            		or	a
  161A  2806          		jr	z,GETSP2Z	;color,0
  161C  3D            		dec	a
  161D  0F            		rrca
  161E  0F            		rrca
  161F  F63F          		or	3fh
  1621  C9            		ret
                      	
  1622                	GETSP2Z:
  1622  3A93FD        		ld	a,(COLOR1)
  1625  B7            		or	a
  1626  C8            		ret	z
  1627  3D            		dec	a
  1628  FE04          		cp	04h
  162A  3802          		jr	c,GETSP2ZC
  162C  D604          		sub	04h
  162E                	GETSP2ZC:
  162E  0F            		rrca
  162F  0F            		rrca
  1630  C9            		ret
                      	
                      	
                      	;clear screen using console parameter
                      	;destroy: af,bc,de,hl
  1631                	CLSMAIN2:
  1631  2AA2FD        		ld	hl,(CONSOL1)	;l=(CONSOL1)
  1634  3AA5FD        		ld	a,(LASTLIN)
  1637  95            		sub	l
  1638  3C            		inc	a
                      	
                      	;clear from first line to last line
                      	;input: l=first line+1, a=last line-first line+1
                      	;destroy: af,bc,de,hl
  1639                	CLSMAIN:
  1639  4D            		ld	c,l		;
  163A  2600          		ld	h,00h
  163C  11B6FD        		ld	de,LINEST-1
  163F  19            		add	hl,de
  1640  47            		ld	b,a
  1641  04            		inc	b
  1642                	CLSLP1:
  1642  70            		ld	(hl),b		;b>0
  1643  23            		inc	hl
  1644  10FC          		djnz	CLSLP1
                      	
  1646  69            		ld	l,c		;
  1647  47            		ld	b,a
  1648  CDB811        		call	Y2AD
  164B                	CLSLP2:
  164B  CDDA11        		call	DELLIN
  164E  212000        		ld	hl,COLUMNS
  1651  19            		add	hl,de
  1652  EB            		ex	de,hl
  1653  10F6          		djnz	CLSLP2
                      	
  1655  CD4911        		call	CTLHOM		;h<-1
  1658  25            		dec	h
  1659  6C            		ld	l,h		;hl=0000h
  165A  22AEFD        		ld	(GRPX1),hl
  165D  22B0FD        		ld	(GRPY1),hl
                      	
  1660  C3AF12        		jp	PRTFKEY
                      	
                      	
                      	;cold start (initialize hardware)
  1663                	COLD:
                      	
                      	;8255 (port 93h)
  1663  019306        		ld	bc,0693h
  1666  21DC00        		ld	hl,IOTBL93
  1669  EDB3          		otir
                      	
                      	;8251 (port 81h)
  166B  018105        		ld	bc,0581h
                      	;	ld	hl,IOTBL81
  166E  EDB3          		otir
                      	
                      	;for mkII
                      	;port f0h-f8h and port c1h
  1670  01EF09        		ld	bc,09efh
                      	;	ld	hl,IOTBLF0
  1673                	F0LP:
  1673  0C            		inc	c
  1674  EDA3          		outi
  1676  20FB          		jr	nz,F0LP
  1678  3E06          		ld	a,06h
  167A  D3C1          		out	(0c1h),a	;32x16 text mode
                      	
                      	;interrupt
                      	;	ld	hl,INTTBL
  167C  1102FA        		ld	de,0fa02h
  167F  0E16          		ld	c,11*2		;b=0
  1681  EDB0          		ldir
  1683  7A            		ld	a,d
  1684  ED47          		ld	i,a
  1686  ED5E          		im	2
                      	
                      	;clear work area
  1688  AF            		xor	a
  1689  62            		ld	h,d
  168A  6B            		ld	l,e
  168B  77            		ld	(hl),a
  168C  13            		inc	de
  168D  01E705        		ld	bc,0ffffh-0fa18h
  1690  EDB0          		ldir
                      	
                      	;stack pointer
  1692  21CDF9        		ld	hl,0f9ffh-50
  1695  F9            		ld	sp,hl		;temporary
                      	
                      	;relay,VRAM,timer
                      	;	xor	a
  1696  D3B0          		out	(0b0h),a	;bit3=CMT relay, bit21=VRAM address=c000, bit0=timer on
                      	
  1698  3D            		dec	a
  1699  321FFA        		ld	(BAUD),a	;a=ffh:1200 baud
                      	
  169C  3E01          		ld	a,01h
  169E  322DFA        		ld	(CONSOL4),a
                      	
                      	;hook
  16A1  218AFF        		ld	hl,HOOK
  16A4  061E          		ld	b,1eh
  16A6  1E03          		ld	e,03h		;d=0
  16A8                	COLDLP1:
  16A8  36C9          		ld	(hl),0c9h
  16AA  19            		add	hl,de
  16AB  10FB          		djnz	COLDLP1
                      	
                      	
                      	;buffer
  16AD  21B9FB        		ld	hl,KEYBUF
  16B0  1E40          		ld	e,40h		;d=0
  16B2  2293FB        		ld	(KYBFAD),hl
  16B5  19            		add	hl,de		;hl=RSBUF
  16B6  2299FB        		ld	(RSBFAD),hl
  16B9  19            		add	hl,de		;hl=BUFA
  16BA  22A5FB        		ld	(BUFAAD),hl
  16BD  19            		add	hl,de		;hl=BUFB
  16BE  22ABFB        		ld	(BUFAAD+6),hl
  16C1  19            		add	hl,de		;hl=BUFC
  16C2  22B1FB        		ld	(BUFAAD+12),hl
                      	
  16C5  2192FB        		ld	hl,KYBFSZ
  16C8  1E06          		ld	e,06h		;d=0
  16CA  43            		ld	b,e		;e=6
  16CB                	COLDLP2:
  16CB  363F          		ld	(hl),3fh	;KYBFSZ,RSBFSZ,,BUFASZ,BUFBSZ,BUFCSZ
  16CD  19            		add	hl,de
  16CE  10FB          		djnz	COLDLP2
                      	
  16D0  3E20          		ld	a,' '
  16D2  321EFA        		ld	(ASTSTAT),a
                      	
                      	;command jump table
  16D5  219801        		ld	hl,CMDLST
  16D8  1161FA        		ld	de,CMDTBL
  16DB  0E56          		ld	c,2*(CMDLAST-80h+1)	;b=0
  16DD  EDB0          		ldir
                      	
                      	;function jump table
                      	;	ld	hl,FNCLST
  16DF  11E5FA        		ld	de,FNCTBL
  16E2  0E3C          		ld	c,2*(FNCLAST-FNC1ST+1)	;b=0
  16E4  EDB0          		ldir
                      	
                      	;screen
  16E6  3E10          		ld	a,ROWS
  16E8  3220FA        		ld	(HEIGHT),a
                      	
  16EB  1191FD        		ld	de,VRAM		;top of active page data
  16EE  3E05          		ld	a,05h
  16F0                	COLDLP3:
  16F0  214A01        		ld	hl,PAGEDATA
  16F3  0E37          		ld	c,PAGE1-VRAM	;b=0
  16F5  EDB0          		ldir
  16F7  3D            		dec	a
  16F8  20F6          		jr	nz,COLDLP3
                      	
                      	;	ld	de,PAGE4+(PAGE4-PAGE3)
  16FA  EB            		ex	de,hl
  16FB  11C9FF        		ld	de,0-(PAGE4-PAGE3)
  16FE  3EA0          		ld	a,0a0h
  1700                	COLDLP5:
  1700  19            		add	hl,de
  1701  77            		ld	(hl),a		;(PAGE4)=a0h, (PAGE3)=c0h, (PAGE2)=e0h
  1702  C620          		add	a,20h
  1704  20FA          		jr	nz,COLDLP5
                      	
                      	;RND()
                      	;	call	SETZERO
  1706  CDB53B        		call	RNDMNS
                      	
                      	;16KB or 32KB?
  1709  210080        		ld	hl,8000h
                      	
                      	;for iP6/iP6win: 16KB if patched
  170C  3AEA10        		ld	a,(PATCH1)
  170F  B7            		or	a
  1710  2008          		jr	nz,RAM16K
                      	
  1712  7E            		ld	a,(hl)
  1713  2F            		cpl
  1714  77            		ld	(hl),a
  1715  BE            		cp	(hl)
  1716  3E04          		ld	a,04h
  1718  2804          		jr	z,RAM32K
  171A                	RAM16K:
  171A  26C0          		ld	h,0c0h
  171C  3E02          		ld	a,02h
  171E                	RAM32K:
  171E  328CFD        		ld	(PAGES),a
  1721  47            		ld	b,a		;
                      	
  1722  7C            		ld	a,h
  1723  3291FD        		ld	(VRAM),a
  1726  CBD4          		set	2,h		;h=h+40h
  1728  2229FF        		ld	(BASICAD),hl	;l=0
  172B  2C            		inc	l		;inc hl
  172C  225FFA        		ld	(STARTAD),hl
                      	
  172F                	COLDLP4:
  172F  78            		ld	a,b		;
  1730  3D            		dec	a
  1731  CD0C14        		call	CHGACT
  1734  CDFB1D        		call	CLS
  1737  10F6          		djnz	COLDLP4
                      	
  1739  AF            		xor	a
  173A  CDED13        		call	CHGDSP
                      	
                      	;PLAY
  173D  CDB31B        		call	PLSTOP		;ei
                      	
  1740  C33B00        		jp	HOT
                      	
                      	
                      	;analyze an argument
                      	;input: hl=program address
                      	;output: hl=next address, FAC1=numerical value or pointer to string descriptor
                      	;	 (ARGTYP)=0(numeric), 1(string), other(cannot analyze)
                      	;destroy: FAC2,af,bc,de,hl
  1743                	ARG:
  1743  E5            		push	hl
  1744  CD2A0C        		call	SETZERO		;for first +-
  1747  E1            		pop	hl
  1748  AF            		xor	a
  1749  5F            		ld	e,a
  174A  D5            		push	de		;e=dummy operator
  174B  3D            		dec	a
  174C  3225FF        		ld	(ARGTYP),a	;ff=unknown
  174F  3265FF        		ld	(OPRTR),a
  1752                	ARGLP:
  1752  E5            		push	hl
  1753  CDE934        		call	CHKFRE
  1756  E1            		pop	hl
  1757                	ARGLP2:
  1757  7E            		ld	a,(hl)
  1758  47            		ld	b,a		;
  1759  23            		inc	hl
  175A  224EFF        		ld	(PROGAD),hl
                      	;command, function, operator
  175D  B7            		or	a
  175E  FA4318        		jp	m,ARGCMD
                      	;space
  1761  FE20          		cp	' '
  1763  28F2          		jr	z,ARGLP2
                      	
                      	;previous argument without operator?
  1765  3A65FF        		ld	a,(OPRTR)
  1768  B7            		or	a
  1769  281F          		jr	z,ARGTAIL
                      	;[A-Z]
  176B  78            		ld	a,b		;
  176C  D641          		sub	'A'
  176E  FE1A          		cp	'Z'-'A'+1
  1770  384C          		jr	c,ARGVAR
                      	;[0-9&.]
  1772  D6EF          		sub	'0'-'A'
  1774  FE0A          		cp	'9'-'0'+1
  1776  78            		ld	a,b		;
  1777  382F          		jr	c,ARGNUM
  1779  FE26          		cp	'&'
  177B  282B          		jr	z,ARGNUM
  177D  FE2E          		cp	'.'
  177F  2827          		jr	z,ARGNUM
                      	;others
  1781  FE22          		cp	22h		;double quotation mark
  1783  CA1218        		jp	z,ARGSTR
  1786  FE28          		cp	'('
  1788  2808          		jr	z,PARL
  178A                	ARGTAIL:
  178A  2B            		dec	hl
  178B  224EFF        		ld	(PROGAD),hl
  178E  AF            		xor	a
  178F  C3BA18        		jp	OPR
                      	
  1792                	PARL:
  1792  CD5F3F        		call	CHKCLN
  1795  CA1204        		jp	z,MOERR
  1798  CD4317        		call	ARG
  179B  3A25FF        		ld	a,(ARGTYP)
  179E  FE02          		cp	02h
  17A0  D2DC03        		jp	nc,SNERR
  17A3  CDA40B        		call	CHKRPAR
  17A6  18AA          		jr	ARGLP
                      	
  17A8                	ARGNUM:
  17A8  CDB617        		call	ARGNCHK
  17AB  2B            		dec	hl
  17AC  3EFF          		ld	a,0ffh
  17AE  CD9839        		call	ATOF
  17B1  224EFF        		ld	(PROGAD),hl
  17B4  189C          		jr	ARGLP
                      	
                      	;destroy: af,bc
  17B6                	ARGNCHK:
  17B6  AF            		xor	a
  17B7  3265FF        		ld	(OPRTR),a
  17BA  3225FF        		ld	(ARGTYP),a
  17BD  C9            		ret
                      	
  17BE                	ARGVAR:
  17BE  E5            		push	hl		;program address
  17BF  CD8833        		call	GETNAME
  17C2  3A25FF        		ld	a,(ARGTYP)
  17C5  57            		ld	d,a
  17C6  3A65FF        		ld	a,(OPRTR)
  17C9  5F            		ld	e,a
  17CA  D5            		push	de		;;
  17CB  EB            		ex	de,hl
  17CC  CD1F39        		call	PUSHF1
  17CF  EB            		ex	de,hl
                      	
  17D0  C5            		push	bc		;variable name
  17D1  7E            		ld	a,(hl)
  17D2  FE28          		cp	'('
  17D4  280D          		jr	z,ARGVARR
  17D6  224EFF        		ld	(PROGAD),hl
  17D9  CDAC33        		call	SRCHVAR
  17DC  3008          		jr	nc,ARGVNC
  17DE  11AB0E        		ld	de,ZERO
  17E1  1803          		jr	ARGVNC
  17E3                	ARGVARR:
  17E3  CDAA32        		call	GETARR
  17E6                	ARGVNC:
  17E6  C1            		pop	bc		;variable name
  17E7  CD3D39        		call	POPF1
  17EA  E1            		pop	hl		;;
  17EB  7C            		ld	a,h
  17EC  3225FF        		ld	(ARGTYP),a
  17EF  7D            		ld	a,l
  17F0  3265FF        		ld	(OPRTR),a
  17F3  E1            		pop	hl		;program address
  17F4  CB79          		bit	7,c
  17F6  2011          		jr	nz,ARGVSTR
  17F8                	ARGVNUM:
  17F8  CDB617        		call	ARGNCHK
  17FB  EB            		ex	de,hl
  17FC  1166FF        		ld	de,FAC1
  17FF  EDA0          		ldi
  1801                	ARGVLDIR:
  1801  010400        		ld	bc,0004h
  1804  EDB0          		ldir
  1806  C30819        		jp	ARGNEXT2
                      	
                      	
                      	;string variable
  1809                	ARGVSTR:
  1809  CD3318        		call	ARGSCHK
  180C  EB            		ex	de,hl
  180D  112DFF        		ld	de,STRDSC1
  1810  18EF          		jr	ARGVLDIR
                      	
  1812                	ARGSTR:
  1812  CD3318        		call	ARGSCHK
  1815  222FFF        		ld	(STRDSC1+2),hl
  1818  1E00          		ld	e,00h
  181A                	ARGSLP:
  181A  7E            		ld	a,(hl)
  181B  B7            		or	a
  181C  280B          		jr	z,ARGSOK
  181E  23            		inc	hl
  181F  FE22          		cp	22h		;double quotation mark
  1821  2806          		jr	z,ARGSOK
  1823  1C            		inc	e
  1824  CA0304        		jp	z,LSERR
  1827  18F1          		jr	ARGSLP
  1829                	ARGSOK:
  1829  ED532DFF      		ld	(STRDSC1),de
  182D  224EFF        		ld	(PROGAD),hl
  1830  C35217        		jp	ARGLP
                      	
                      	;destroy: af,bc
  1833                	ARGSCHK:
  1833  AF            		xor	a
  1834  3265FF        		ld	(OPRTR),a	;a=0
  1837  3C            		inc	a
  1838  3225FF        		ld	(ARGTYP),a	;a=1
  183B  012DFF        		ld	bc,STRDSC1
  183E  ED4367FF      		ld	(FAC1+1),bc
  1842  C9            		ret
                      	
  1843                	ARGCMD:
  1843  FEF2          		cp	FNCLAST+1	;0f2h
  1845  D2DC03        		jp	nc,SNERR
  1848  D6CA          		sub	PLUS_		;0cah
  184A  FE0A          		cp	FNC1ST-PLUS_
  184C  78            		ld	a,b		;
  184D  386B          		jr	c,OPR		;0cah-0d3h
                      	
  184F  3A65FF        		ld	a,(OPRTR)
  1852  B7            		or	a
  1853  CA8A17        		jp	z,ARGTAIL
                      	
  1856  78            		ld	a,b		;
  1857  FEC8          		cp	NOT_		;0c8h
  1859  285F          		jr	z,OPR
  185B  FE9F          		cp	SCREEN_		;9fh
  185D  280D          		jr	z,FNC
  185F  FEC4          		cp	FN_		;0c4h
  1861  2809          		jr	z,FNC
  1863  FEC6          		cp	INKEY_		;0c6h
  1865  2805          		jr	z,FNC
  1867  FECA          		cp	PLUS_		;0cah
  1869  DA8A17        		jp	c,ARGTAIL
                      	
                      	;	jr	FNC
                      	
                      	;function
  186C                	FNC:
  186C  AF            		xor	a
  186D  3225FF        		ld	(ARGTYP),a
  1870  78            		ld	a,b		;
  1871  FEC6          		cp	INKEY_
  1873  CA9E27        		jp	z,F_INKY
  1876  FEC4          		cp	FN_
  1878  CAC73E        		jp	z,F_FN
  187B  FE9F          		cp	SCREEN_
  187D  CAA63E        		jp	z,F_SCRN
                      	
  1880  CD9618        		call	CALLFNC
  1883                	FNCRTN:
  1883  2A4EFF        		ld	hl,(PROGAD)
  1886                	CLRSTRD:
  1886  3A25FF        		ld	a,(ARGTYP)
  1889  B7            		or	a
  188A  2003          		jr	nz,CLRSTRDNZ
  188C  322DFF        		ld	(STRDSC1),a	;=0
  188F                	CLRSTRDNZ:
  188F  AF            		xor	a
  1890  3265FF        		ld	(OPRTR),a
  1893  C35217        		jp	ARGLP
                      	
  1896                	CALLFNC:
  1896  FEEA          		cp	LEFT_
  1898  300B          		jr	nc,SKIPARG
                      	
                      	;SGN()...CHR$()
  189A  F5            		push	af
  189B  CD950B        		call	CHKLPAR
  189E  CD4317        		call	ARG
  18A1  CDA40B        		call	CHKRPAR
  18A4  F1            		pop	af
                      	
  18A5                	SKIPARG:
  18A5  113DFA        		ld	de,FNCTBL-(FNC1ST-80h)*2
  18A8  FED7          		cp	USR_
  18AA  C2493F        		jp	nz,JPTBL
                      	
                      	;USR() function
  18AD  E5            		push	hl		;program address
  18AE  CD493F        		call	JPTBL
  18B1  CD7B0B        		call	CHKNUM
  18B4  AF            		xor	a
  18B5  322DFF        		ld	(STRDSC1),a
  18B8  F1            		pop	af		;cancel program address
  18B9  C9            		ret
                      	
                      	
                      	;operator
  18BA                	OPR:
  18BA  FED3          		cp	LT_		;0d3h
  18BC  2001          		jr	nz,OPRNZ
  18BE  3C            		inc	a		;0d3h -> 0d4h
  18BF                	OPRNZ:
  18BF  57            		ld	d,a		;operator
  18C0  3A65FF        		ld	a,(OPRTR)
  18C3  3C            		inc	a
  18C4  FE02          		cp	02h
  18C6  D26219        		jp	nc,OPROPR	;not 00 nor ff
  18C9  3A25FF        		ld	a,(ARGTYP)
  18CC  3C            		inc	a
  18CD  7A            		ld	a,d		;operator
  18CE  2010          		jr	nz,NOTHEAD
                      	;first +, for numeric and string
  18D0  FECA          		cp	PLUS_
  18D2  2834          		jr	z,ARGNEXT2
  18D4  FECB          		cp	MINUS_
  18D6  2808          		jr	z,NOTHEAD
  18D8  FEC8          		cp	NOT_
  18DA  2804          		jr	z,NOTHEAD
  18DC  B7            		or	a
  18DD  C2DC03        		jp	nz,SNERR
  18E0                	NOTHEAD:
  18E0  CD9A19        		call	GETPRIO
  18E3  5F            		ld	e,a
  18E4  C1            		pop	bc		;c=previous operator
  18E5  C5            		push	bc
  18E6  79            		ld	a,c
  18E7  B7            		or	a
  18E8  2870          		jr	z,ARGHEAD
                      	
  18EA  CD9A19        		call	GETPRIO
  18ED  BB            		cp	e
  18EE  301E          		jr	nc,CALLOP
  18F0  7A            		ld	a,d		;operator
  18F1                	ARGNEXT:
  18F1  3265FF        		ld	(OPRTR),a
  18F4  3A25FF        		ld	a,(ARGTYP)
  18F7  3D            		dec	a
  18F8  2805          		jr	z,ARGNEXTS
  18FA  CD1F39        		call	PUSHF1
  18FD  1809          		jr	ARGNEXT2
  18FF                	ARGNEXTS:
  18FF  CDB619        		call	COPYSTR
  1902  2A65FF        		ld	hl,(FAC1-1)	;h=length, l=operator
  1905  CBB5          		res	6,l
  1907  E5            		push	hl
  1908                	ARGNEXT2:
  1908  2A4EFF        		ld	hl,(PROGAD)
  190B  C35217        		jp	ARGLP
                      	
                      	
  190E                	CALLOP:
  190E  D5            		push	de		;;d=following operator
  190F  C5            		push	bc		;c=previous operator
  1910  3A25FF        		ld	a,(ARGTYP)
  1913  3D            		dec	a
  1914  2829          		jr	z,CALLOPS
                      	
                      	;numeric
  1916  CD5B39        		call	CPYFAC
  1919  C1            		pop	bc		;
  191A  D1            		pop	de		;;
  191B  CD3D39        		call	POPF1
                      	
  191E                	CALLOPEND:
  191E  CB71          		bit	6,c
  1920  2007          		jr	nz,TYPNUM
  1922  CBF1          		set	6,c
  1924  2125FF        		ld	hl,ARGTYP
  1927  CBCE          		set	1,(hl)
  1929                	TYPNUM:
  1929  D5            		push	de		;;
  192A  79            		ld	a,c
  192B  FED2          		cp	GT_+1
  192D  3802          		jr	c,CALLOPOK
                      	;>,=,<
  192F  3ED1          		ld	a,GT_
                      	
  1931                	CALLOPOK:
  1931  11F400        		ld	de,OPRTBL-(0c8h-80h)*2
  1934  CD493F        		call	JPTBL
  1937  AF            		xor	a
  1938  3265FF        		ld	(OPRTR),a
  193B  F1            		pop	af		;;
  193C  C3BA18        		jp	OPR
                      	
                      	;string
  193F                	CALLOPS:
  193F  ED4B2DFF      		ld	bc,(STRDSC1)
  1943  ED5B2FFF      		ld	de,(STRDSC1+2)
  1947  CDCA19        		call	BACKSTR
  194A  ED4339FF      		ld	(STRDSC4),bc
  194E  ED533BFF      		ld	(STRDSC4+2),de
  1952  C1            		pop	bc
  1953  D1            		pop	de
  1954  E1            		pop	hl
  1955  2265FF        		ld	(FAC1-1),hl
  1958  18C4          		jr	CALLOPEND
                      	
  195A                	ARGHEAD:
  195A  82            		add	a,d		;d=0?
  195B  2094          		jr	nz,ARGNEXT
  195D  E1            		pop	hl		;cancel dummy operator
  195E  2A4EFF        		ld	hl,(PROGAD)
  1961  C9            		ret
                      	
                      	;operator and operator
  1962                	OPROPR:
  1962  7A            		ld	a,d		;operator
  1963  FEC8          		cp	NOT_		;0c8h
  1965  288A          		jr	z,ARGNEXT
  1967  FECA          		cp	PLUS_		;0cah
  1969  2824          		jr	z,OPRPLS
  196B  21A60E        		ld	hl,MNSONE
  196E  FECB          		cp	MINUS_		;0cbh
  1970  2820          		jr	z,OPRMNS
  1972  FED1          		cp	GT_		;0d1h
  1974  DADC03        		jp	c,SNERR
                      	
                      	;>,=,<
                      	;bit0: >
                      	;bit1: =
                      	;bit2: <
  1977  2165FF        		ld	hl,OPRTR
  197A  7E            		ld	a,(hl)
  197B  FED1          		cp	GT_
  197D  DADC03        		jp	c,SNERR
  1980  B2            		or	d
  1981  BE            		cp	(hl)
  1982  CADC03        		jp	z,SNERR
  1985  77            		ld	(hl),a
                      	
                      	;copy numeric/string bit
  1986  E607          		and	07h		;<=> bits
  1988  C1            		pop	bc		;c=previous operator
  1989  B1            		or	c
  198A  4F            		ld	c,a
  198B  C5            		push	bc
  198C  C30819        		jp	ARGNEXT2
                      	
                      	;+
  198F                	OPRPLS:
  198F  21A10E        		ld	hl,PLSONE
                      	;-
  1992                	OPRMNS:
  1992  CD2D0C        		call	SETF1
  1995  3ECC          		ld	a,ASTRSK_	;0cch
  1997  C3F118        		jp	ARGNEXT
                      	
                      	
                      	;input: a=operator
                      	;output: a=priority
                      	;destroy: f,hl
  199A                	GETPRIO:
  199A  B7            		or	a
  199B  C8            		ret	z
  199C  21B519        		ld	hl,PRIO+GT_-NOT_
  199F  F640          		or	40h		;numeric/string bit
  19A1  D6D1          		sub	GT_		;0d1h
  19A3  3005          		jr	nc,PRIONC	;>,=,<
  19A5  85            		add	a,l
  19A6  6F            		ld	l,a
  19A7  3801          		jr	c,PRIONC
  19A9  25            		dec	h
  19AA                	PRIONC:
  19AA  7E            		ld	a,(hl)
  19AB  C9            		ret
                      	
  19AC                	PRIO:
                      	;operator priority table
                      	;		not   +  -  *  /  ^ and or >=<
  19AC  03000505060607		db	3, 0, 5, 5, 6, 6, 7, 2, 1, 4
                      	
                      	
                      	;check string descriptor and copy
                      	;destroy: af,bc,de,hl
  19B6                	COPYSTR:
  19B6  2139FF        		ld	hl,STRDSC4
  19B9  7E            		ld	a,(hl)
  19BA  B7            		or	a
  19BB  C20604        		jp	nz,STERR
  19BE  2B            		dec	hl		;STRDSC3+3
  19BF  113CFF        		ld	de,STRDSC4+3
  19C2  010C00        		ld	bc,000ch
  19C5  EDB8          		lddr			;STRDSC3->STRDSC4, STRDSC2->STRDSC3, STRDSC1->STRDSC2
                      	;	xor	a
  19C7  23            		inc	hl
  19C8  77            		ld	(hl),a		;(STRDSC1)=0
  19C9  C9            		ret
                      	
                      	
                      	;copy back string descriptor
                      	;destroy: af,hl
  19CA                	BACKSTR:
  19CA  D5            		push	de
  19CB  C5            		push	bc
  19CC  2131FF        		ld	hl,STRDSC2
  19CF  112DFF        		ld	de,STRDSC1
  19D2  010C00        		ld	bc,000ch
  19D5  EDB0          		ldir			;STRDSC2->STRDSC1, STRDSC3->STRDSC2, STRDSC4->STRDSC3
  19D7  AF            		xor	a
  19D8  12            		ld	(de),a		;(STRDSC4)=0
  19D9  C1            		pop	bc
  19DA  D1            		pop	de
  19DB  C9            		ret
                      	
                      	
                      	;send graphic data to PC-6021
                      	;input: c
                      	;destroy: af,b
  19DC                	SENDGRP:
  19DC  0608          		ld	b,08h
  19DE                	SENDGLP:
  19DE  CB01          		rlc	c
  19E0  1F            		rra
  19E1  10FB          		djnz	SENDGLP
  19E3  1852          		jr	PRINTER
                      	
                      	
                      	;put a character to printer
                      	;input: a
                      	;destroy: none
  19E5                	_PUTPRT:ds	PUTPRT-_PUTPRT
  1A1C                		org	PUTPRT
                      	
  1A1C  F5            		push	af
  1A1D  3A31FA        		ld	a,(GRPFLG)
  1A20  B7            		or	a
  1A21  2806          		jr	z,NOTGRP
  1A23  AF            		xor	a
  1A24                	SETGRP:
  1A24  3231FA        		ld	(GRPFLG),a
  1A27  F1            		pop	af
  1A28  C9            		ret
                      	
  1A29                	NOTGRP:
  1A29  F1            		pop	af
  1A2A  F5            		push	af
  1A2B  FE14          		cp	14h
  1A2D  28F5          		jr	z,SETGRP
  1A2F  CDC43F        		call	CNVKANA2
  1A32  CD371A        		call	PRINTER
  1A35  F1            		pop	af
  1A36  C9            		ret
                      	
                      	
                      	;input: a
                      	;destroy: af
  1A37                	PRINTER:
  1A37  F5            		push	af
  1A38                	PRINTERLP:
  1A38  DBC0          		in	a,(0c0h)
  1A3A  E602          		and	02h
  1A3C  2005          		jr	nz,PRINTERZ	;ready
  1A3E  CD4D27        		call	STOPESC
  1A41  18F5          		jr	PRINTERLP
                      	
  1A43                	PRINTERZ:
  1A43  F1            		pop	af
  1A44  2F            		cpl
  1A45  D391          		out	(91h),a
  1A47  3E01          		ld	a,01h
  1A49  D393          		out	(93h),a		;strobe (>1us)
  1A4B  AF            		xor	a
  1A4C  D393          		out	(93h),a
  1A4E  C9            		ret
                      	
                      	
                      	;convert hiragana -> katakana
                      	;input: a
                      	;output: a
                      	;destroy: f
  1A4F                	_CNVKANA:ds	CNVKANA-_CNVKANA
  1A4F                		org	CNVKANA
                      	
  1A4F  FE86          		cp	86h
  1A51  D8            		ret	c
  1A52  FEA0          		cp	0a0h
  1A54  3803          		jr	c,HIRAGANA
  1A56  FEE0          		cp	0e0h
  1A58  D8            		ret	c
  1A59                	HIRAGANA:
  1A59  EE20          		xor	20h
  1A5B  C9            		ret
                      	
                      	
                      	;CMT open for verify
                      	;destroy: a
  1A5C                	VRFOPN:
  1A5C  3EFF          		ld	a,0ffh
  1A5E  32D8FE        		ld	(VERIFY),a
                      	;	jr	ROPEN
                      	
                      	
                      	;open for tape read
                      	;destroy: none
  1A61                	_ROPEN:ds	ROPEN-_ROPEN
  1A61                		org	ROPEN
                      	
  1A61  F5            		push	af
  1A62  C5            		push	bc
  1A63  061E          		ld	b,1eh
  1A65  CD121B        		call	OPENCMT
  1A68  CD4B1B        		call	RLON
  1A6B  C1            		pop	bc
  1A6C  F1            		pop	af
  1A6D  C9            		ret
                      	
                      	
                      	;get 1 character from CMT
                      	;output: a=data, z(0=error, 1=no error)
                      	;destroy: f
  1A6E                	_GETCMT:ds	GETCMT-_GETCMT
  1A70                		org	GETCMT
                      	
  1A70  3A18FA        		ld	a,(STOPFLG)
  1A73  FE03          		cp	03h
  1A75  2812          		jr	z,CMTSTP
  1A77  3A19FA        		ld	a,(CMTSTAT)
  1A7A  CB67          		bit	4,a
  1A7C  C0            		ret	nz		;read error, z-flag=0
  1A7D  E602          		and	02h
  1A7F  28EF          		jr	z,GETCMT
  1A81  AF            		xor	a		;set z-flag
  1A82  3219FA        		ld	(CMTSTAT),a
  1A85  3A1DFA        		ld	a,(CMTBUF)
  1A88  C9            		ret
                      	
  1A89                	CMTSTP:
  1A89  CD991A        		call	CHKVRF
  1A8C  C35127        		jp	STOPSP
                      	
                      	
                      	;call GETCMT, if z=0 then ?TR Error
  1A8F                	GETCMTTR:
  1A8F  CD701A        		call	GETCMT
  1A92  C8            		ret	z
  1A93  CD991A        		call	CHKVRF
  1A96  C30F04        		jp	TRERR
                      	
                      	
                      	;check verify or not
  1A99                	CHKVRF:
  1A99  CDAA1A        		call	RCLOSE
  1A9C                	CHKVRF2:
  1A9C  3AD8FE        		ld	a,(VERIFY)
  1A9F  B7            		or	a
  1AA0  CCDB34        		call	z,NEW
  1AA3  C9            		ret
                      	
                      	
                      	;close for tape read
                      	;destroy: none
  1AA4                	_RCLOSE:ds	RCLOSE-_RCLOSE
  1AAA                		org	RCLOSE
                      	
  1AAA  F5            		push	af
  1AAB  C5            		push	bc
  1AAC  3E1A          		ld	a,1ah
  1AAE                	CLOSEEND:
  1AAE  CD8F0E        		call	OUT90H
  1AB1  CD491B        		call	RLOFF
  1AB4  C1            		pop	bc
  1AB5  F1            		pop	af
  1AB6  C9            		ret
                      	
                      	
                      	;open for tape write
                      	;destroy: none
  1AB7                	_WOPEN:	ds	WOPEN-_WOPEN
  1AB8                		org	WOPEN
                      	
  1AB8  F5            		push	af
  1AB9  C5            		push	bc
  1ABA  CD4B1B        		call	RLON
  1ABD  CDD91A        		call	BLANK
  1AC0  063E          		ld	b,3eh
  1AC2  CD121B        		call	OPENCMT
  1AC5  CDD91A        		call	BLANK
  1AC8  C1            		pop	bc
  1AC9  F1            		pop	af
  1ACA  C9            		ret
                      	
                      	
                      	;put 1 character to CMT
                      	;input: a=data
                      	;destroy: none
  1ACB                	_PUTCMT:ds	PUTCMT-_PUTCMT
  1ACC                		org	PUTCMT
                      	
  1ACC  F5            		push	af
  1ACD                	PUTCMT2:
  1ACD  CDDC1A        		call	CHKWSTP
  1AD0  3E38          		ld	a,38h
  1AD2  CD8F0E        		call	OUT90H
  1AD5  F1            		pop	af
  1AD6  C38F0E        		jp	OUT90H
                      	
                      	
                      	;wait and check stop for tape write
                      	;destroy: af,bc
  1AD9                	BLANK:
  1AD9  CDE525        		call	WAIT
                      	;	jr	CHKWSTP
                      	
                      	;check stop for tape write
                      	;destroy: af (when not stop)
  1ADC                	CHKWSTP:
  1ADC  3A18FA        		ld	a,(STOPFLG)
  1ADF  FE03          		cp	03h
  1AE1  C0            		ret	nz
  1AE2  CD491B        		call	RLOFF
  1AE5  C35127        		jp	STOPSP
                      	
                      	
                      	;check 8255 status (ready for output to port 90h)
                      	;output: z(0=ready)
                      	;destroy: af,(b)
  1AE8                	_CHK90H:ds	CHK90H-_CHK90H
  1AED                		org	CHK90H
                      	
  1AED  3E08          		ld	a,08h
  1AEF  D393          		out	(93h),a		;disable 8255 INT for reading
  1AF1  DB92          		in	a,(92h)
  1AF3  E688          		and	88h		;nobf=1,intr=1?
  1AF5  EAF91A        		jp	pe,CHK90PE
  1AF8  AF            		xor	a		;set z-flag
  1AF9                	CHK90PE:
  1AF9  3E09          		ld	a,09h
  1AFB  D393          		out	(93h),a		;enable 8255 INT for reading
  1AFD  C9            		ret
                      	
                      	
                      	;close for tape write
                      	;destroy: none
  1AFE                	_WCLOSE:ds	WCLOSE-_WCLOSE
  1B06                		org	WCLOSE
                      	
  1B06  F5            		push	af
  1B07  C5            		push	bc
  1B08  01B003        		ld	bc,03b0h
  1B0B  CDE825        		call	WAITLP
  1B0E  3E3A          		ld	a,3ah
  1B10  189C          		jr	CLOSEEND
                      	
                      	
                      	;open CMT subroutine
                      	;input: b (1eh=read, 3eh=write)
                      	;destroy: af,b
  1B12                	OPENCMT:
  1B12  3A1FFA        		ld	a,(BAUD)	;00=600baud, others=1200baud
  1B15  FE01          		cp	01h
  1B17  9F            		sbc	a,a		;ff=600baud, 00=1200baud
  1B18  80            		add	a,b
  1B19  CD8F0E        		call	OUT90H		;read=1dh(600baud), 1eh(1200baud)
                      					;write=3dh(600baud), 3eh(1200baud)
  1B1C  E6F8          		and	0f8h
  1B1E  3C            		inc	a		;read=19h, write=39h
  1B1F  F3            		di
  1B20  CD8F0E        		call	OUT90H
  1B23  FB            		ei
  1B24  AF            		xor	a
  1B25  3219FA        		ld	(CMTSTAT),a
                      	;	ld	(STOPFLG),a
  1B28  C9            		ret
                      	
                      	
                      	;blink asterisk
                      	;destroy: af
  1B29                	_BLNKAST:ds	BLNKAST-_BLNKAST
  1B2A                		org	BLNKAST
                      	
  1B2A  E5            		push	hl
  1B2B  211EFA        		ld	hl,ASTSTAT
  1B2E  7E            		ld	a,(hl)
  1B2F  EE0A          		xor	'*'-' '
  1B31  77            		ld	(hl),a
                      	
  1B32  2AABFD        		ld	hl,(WIDTH-1)	;h=(WIDTH)
  1B35  25            		dec	h
  1B36  2E01          		ld	l,01h
  1B38  CDCD11        		call	XY2AD
  1B3B  77            		ld	(hl),a
                      	
  1B3C  E1            		pop	hl
  1B3D  C9            		ret
                      	
                      	
                      	;CMT relay on/off
                      	;destroy: af,b
  1B3E                	_RLOFF:	ds	RLOFF-_RLOFF
  1B49                		org	RLOFF
                      	
  1B49  1801          		jr	RLOFF2
                      	
  1B4B                	_RLON:	ds	RLON-_RLON
  1B4B                		org	RLON
                      	
  1B4B  3E            		db	3eh		;ld a,
  1B4C                	RLOFF2:
  1B4C  AF            		xor	a		;afh
                      	
  1B4D  0608          		ld	b,00001000b	;bit3=CMT relay
  1B4F  C3541B        		jp	OUTB0H
                      	
                      	
                      	;output to port b0h
                      	;input: a=new data, b=change bit
                      	;destroy: af
  1B52                	_OUTB0H:ds	OUTB0H-_OUTB0H
  1B54                		org	OUTB0H
                      	
  1B54  E5            		push	hl
  1B55  2127FA        		ld	hl,PORTB0H
  1B58  AE            		xor	(hl)
  1B59  A0            		and	b
  1B5A  AE            		xor	(hl)
  1B5B  D3B0          		out	(0b0h),a
  1B5D  77            		ld	(hl),a
  1B5E  E1            		pop	hl
  1B5F  C9            		ret
                      	
                      	
                      	;PLAY stop sub
                      	;destroy: none
  1B60                	_PLSTPS:ds	PLSTPS-_PLSTPS
  1B60                		org	PLSTPS
                      	
  1B60  F5            		push	af
  1B61  E5            		push	hl
  1B62  D5            		push	de
  1B63  C5            		push	bc
                      	
  1B64  3E07          		ld	a,07h		;ch.ABC=tone,portAB=in
  1B66  1E38          		ld	e,38h
  1B68  CDC51B        		call	SETPSG
  1B6B  3C            		inc	a		;register8=ch.A volume
  1B6C  1E00          		ld	e,00h
  1B6E  CDC51B        		call	SETPSG
  1B71  3C            		inc	a		;register9=ch.B volume
  1B72  CDC51B        		call	SETPSG
  1B75  3C            		inc	a		;register10=ch.C volume
  1B76  CDC51B        		call	SETPSG
                      	
  1B79  AF            		xor	a
  1B7A  321BFD        		ld	(PLAYST),a
  1B7D  3214FD        		ld	(CHANNEL),a
                      	
  1B80  67            		ld	h,a
  1B81  6F            		ld	l,a
  1B82  22A1FB        		ld	(BUFAIN),hl	;clear BFIN and BFOUT for ch.A
  1B85  22A7FB        		ld	(BUFBIN),hl	;clear BFIN and BFOUT for ch.B
  1B88  22ADFB        		ld	(BUFCIN),hl	;clear BFIN and BFOUT for ch.C
                      	
  1B8B  211DFD        		ld	hl,PLWKA
  1B8E  77            		ld	(hl),a
  1B8F  54            		ld	d,h
  1B90  5D            		ld	e,l
  1B91  13            		inc	de
  1B92  012400        		ld	bc,PLWKB-PLWKA-1
  1B95  EDB0          		ldir
                      	
  1B97  21B91B        		ld	hl,PLWKTBL
  1B9A  112CFD        		ld	de,PLWKA+OCTAVE
  1B9D  0E05          		ld	c,05h		;b=0
  1B9F  EDB0          		ldir
                      	
  1BA1  211DFD        		ld	hl,PLWKA
  1BA4  1142FD        		ld	de,PLWKB
  1BA7  0E4A          		ld	c,PLWKC-PLWKA	;b=0
  1BA9  EDB0          		ldir
                      	
  1BAB  C1            		pop	bc
  1BAC  D1            		pop	de
  1BAD  E1            		pop	hl
  1BAE  F1            		pop	af
  1BAF  C9            		ret
                      	
                      	
                      	;stop and initialize for PLAY command
                      	;destroy: none
  1BB0                	_PLSTOP:ds	PLSTOP-_PLSTOP
  1BB3                		org	PLSTOP
  1BB3  F3            		di
  1BB4  CD601B        		call	PLSTPS
  1BB7  FB            		ei
  1BB8  C9            		ret
                      	
                      	
  1BB9                	PLWKTBL:
  1BB9  04            		db	4		;O-value
  1BBA  04            		db	4		;L-value
  1BBB  78            		db	120		;T-value
  1BBC  08            		db	8		;V-value
  1BBD  FF            		db	255		;M-value(low)
                      	
                      	
                      	;get PSG resister value and set new value
                      	;input: a=register, e=value
                      	;output: d=old value
                      	;destroy: none
  1BBE                	_SETPSG2:ds	SETPSG2-_SETPSG2
  1BBE                		org	SETPSG2
                      	
  1BBE  F5            		push	af
  1BBF  D3A0          		out	(0a0h),a
  1BC1  DBA2          		in	a,(0a2h)
  1BC3  57            		ld	d,a
  1BC4  F1            		pop	af
                      	;	jp	SETPSG
                      	
                      	
                      	;set PSG register
                      	;input: a=register, e=value
                      	;destroy: none
  1BC5                	_SETPSG:ds	SETPSG-_SETPSG
  1BC5                		org	SETPSG
                      	
  1BC5  F5            		push	af
  1BC6  D3A0          		out	(0a0h),a
  1BC8  7B            		ld	a,e
  1BC9  D3A1          		out	(0a1h),a
  1BCB  F1            		pop	af
  1BCC  C9            		ret
                      	
                      	
                      	;bell
                      	;destroy: af,bc,e
  1BCD                	_BELL:	ds	BELL-_BELL
  1BCD                		org	BELL
                      	
  1BCD  CDB31B        		call	PLSTOP
                      	;tune
  1BD0  AF            		xor	a		;register0=ch.A 8bit fine tune
  1BD1  1E55          		ld	e,55h
  1BD3  CDC51B        		call	SETPSG
  1BD6  5F            		ld	e,a		;a=0
  1BD7  3C            		inc	a		;register1=ch.A 4bit coarse tune
  1BD8  CDC51B        		call	SETPSG
                      	;volume
  1BDB  1E07          		ld	e,07h
                      	;	ld	a,08h		;register8=ch.A volume
  1BDD  83            		add	a,e		;register8=ch.A volume
  1BDE  CDC51B        		call	SETPSG
  1BE1  010004        		ld	bc,0400h
  1BE4  59            		ld	e,c		;c=0
  1BE5  CDE825        		call	WAITLP
                      	;volume
  1BE8  3E08          		ld	a,08h		;register8=ch.A volume
  1BEA  18D9          		jr	SETPSG
                      	
                      	
                      	;play subroutine called by time interrupt
                      	;destroy: af,bc,de,hl
  1BEC                	PLAYINT:
  1BEC  3A14FD        		ld	a,(CHANNEL)
  1BEF  3C            		inc	a
  1BF0  3A1BFD        		ld	a,(PLAYST)
  1BF3  2802          		jr	z,PLIZ
  1BF5  B7            		or	a
  1BF6  C8            		ret	z
  1BF7                	PLIZ:
  1BF7  0F            		rrca
  1BF8  0F            		rrca
  1BF9  0F            		rrca
  1BFA  4F            		ld	c,a
  1BFB  2167FD        		ld	hl,PLWKC
  1BFE  0603          		ld	b,03h
  1C00                	PLILP:
  1C00  E5            		push	hl
  1C01  CB01          		rlc	c
  1C03  300D          		jr	nc,PLINC
                      	
  1C05  56            		ld	d,(hl)
  1C06  2C            		inc	l		;inc	hl
  1C07  5E            		ld	e,(hl)
  1C08  1B            		dec	de
  1C09  73            		ld	(hl),e
  1C0A  2D            		dec	l		;dec	hl
  1C0B  72            		ld	(hl),d
  1C0C  7A            		ld	a,d
  1C0D  B3            		or	e
  1C0E  2061          		jr	nz,PLINEXT
  1C10  180A          		jr	PLIGET
                      	
  1C12                	PLINC:
  1C12  211BFD        		ld	hl,PLAYST
  1C15  3A14FD        		ld	a,(CHANNEL)
  1C18  3C            		inc	a
  1C19  B6            		or	(hl)
  1C1A  2055          		jr	nz,PLINEXT	;if (PLAYST)<>0 or (CHANNEL)<>ff
                      	
  1C1C                	PLIGET:
                      	;length
  1C1C  CD4810        		call	GETPLBF
  1C1F  2850          		jr	z,PLINEXT
  1C21  FEFF          		cp	0ffh
  1C23  2863          		jr	z,PLIRES
  1C25  D1            		pop	de
  1C26  D5            		push	de
  1C27  12            		ld	(de),a
  1C28  CD4810        		call	GETPLBF		;no change in de
  1C2B  1C            		inc	e		;inc de
  1C2C  12            		ld	(de),a
                      	
                      	;tune
  1C2D  CD4810        		call	GETPLBF
  1C30  5F            		ld	e,a
  1C31  78            		ld	a,b
  1C32  87            		add	a,a
  1C33  3D            		dec	a		;register1,3,5=4bit coarse tune
  1C34  CDC51B        		call	SETPSG
  1C37  CD4810        		call	GETPLBF
  1C3A  5F            		ld	e,a
  1C3B  78            		ld	a,b
  1C3C  3D            		dec	a
  1C3D  87            		add	a,a		;register0,2,4=8bit fine tune
  1C3E  CDC51B        		call	SETPSG
                      	
                      	;volume or envelope
  1C41  CD4810        		call	GETPLBF
  1C44  57            		ld	d,a		;
  1C45  FE10          		cp	10h
  1C47  3802          		jr	c,PLIVOL
  1C49  3E10          		ld	a,10h		;envelope
  1C4B                	PLIVOL:
  1C4B  5F            		ld	e,a
  1C4C  78            		ld	a,b
  1C4D  C607          		add	a,07h		;register8,9,10=volume
  1C4F  CDC51B        		call	SETPSG
  1C52  7A            		ld	a,d		;
  1C53  D610          		sub	10h
  1C55  3818          		jr	c,PLISET
                      	
                      	;period
  1C57  57            		ld	d,a		;
  1C58  CD4810        		call	GETPLBF
  1C5B  5F            		ld	e,a
  1C5C  3E0B          		ld	a,0bh
  1C5E  CDC51B        		call	SETPSG
  1C61  CD4810        		call	GETPLBF
  1C64  5F            		ld	e,a
  1C65  3E0C          		ld	a,0ch
  1C67  CDC51B        		call	SETPSG
                      	
                      	;envelope pattern
  1C6A  5A            		ld	e,d		;
  1C6B  3C            		inc	a		;register13=envelope pattern
  1C6C  CDC51B        		call	SETPSG
                      	
                      	;status
  1C6F                	PLISET:
  1C6F  CBC1          		set	0,c
                      	
  1C71                	PLINEXT:
  1C71  E1            		pop	hl
  1C72  7D            		ld	a,l
  1C73  C6DB          		add	a,PLWKA-PLWKB	;no carry
  1C75  6F            		ld	l,a
  1C76  1088          		djnz	PLILP
                      	
  1C78  211BFD        		ld	hl,PLAYST
  1C7B  7E            		ld	a,(hl)
  1C7C  71            		ld	(hl),c
  1C7D  0C            		inc	c
  1C7E  0D            		dec	c
  1C7F  C0            		ret	nz		;new (PLAYST)<>0
  1C80  B7            		or	a		;old (PLAYST)
  1C81  C2EC1B        		jp	nz,PLAYINT
  1C84  3214FD        		ld	(CHANNEL),a	;=0
  1C87  C9            		ret
                      	
                      	;reset
  1C88                	PLIRES:
  1C88  CB81          		res	0,c
  1C8A  78            		ld	a,b
  1C8B  C607          		add	a,07h		;register8,9,10=volume
  1C8D  1E00          		ld	e,00h
  1C8F  CDC51B        		call	SETPSG
  1C92  18DD          		jr	PLINEXT
                      	
                      	
                      	;get play buffer address
                      	;output: hl
                      	;destroy: f
  1C94                	GETPLAD:
  1C94  67            		ld	h,a
  1C95  3A14FD        		ld	a,(CHANNEL)
  1C98  6F            		ld	l,a
  1C99  87            		add	a,a		;a*2
  1C9A  85            		add	a,l		;a*3
  1C9B  87            		add	a,a		;a*6
  1C9C  C6A1          		add	a,BUFAIN-BUFAIN/256*256		;no carry
  1C9E  6F            		ld	l,a
  1C9F  7C            		ld	a,h
  1CA0  26FB          		ld	h,BUFAIN/256	;higher byte
  1CA2  C9            		ret
                      	
                      	
                      	;joystick
                      	;input: a=1or2
                      	;output: a (0-0-trigger2-trigger1-right-left-down-up)
                      	;destroy: f
  1CA3                	_JOYSTK:ds	JOYSTK-_JOYSTK
  1CA6                		org	JOYSTK
                      	
  1CA6  D5            		push	de
  1CA7  0F            		rrca
  1CA8  37            		scf
  1CA9  1F            		rra
  1CAA  5F            		ld	e,a		;c0h or 80h
                      	
  1CAB  3E07          		ld	a,07h		;register7=portABin-out,noise,tone
  1CAD  F3            		di
  1CAE  D3A0          		out	(0a0h),a
  1CB0  DBA2          		in	a,(0a2h)
  1CB2  1802          		jr	SKPPATCH2
                      	
                      	
                      	;1cb4h-1cb5h: to be patched by PC6001V/VX/VW
  1CB4                	_PATCH2:ds	PATCH2-_PATCH2
  1CB4                		org	PATCH2
  1CB4  0000          		db	00h, 00h
                      	
                      	
  1CB6                	SKPPATCH2:
  1CB6  57            		ld	d,a		;
                      	
  1CB7  E6BF          		and	10111111b	;parallel port A=in
  1CB9  F680          		or	10000000b	;parallel port B=out
  1CBB  D3A1          		out	(0a1h),a
                      	
  1CBD  3E0F          		ld	a,0fh		;register15=parallel port B
  1CBF  CDC51B        		call	SETPSG		;c0h or 80h
                      	
  1CC2  3D            		dec	a		;register14=parallel port A
  1CC3  D3A0          		out	(0a0h),a
  1CC5  DBA2          		in	a,(0a2h)
  1CC7  2F            		cpl
  1CC8  5A            		ld	e,d		;
  1CC9  57            		ld	d,a		;;
                      	
  1CCA  3E07          		ld	a,07h
  1CCC  CD931E        		call	SETPSGEI
                      	
  1CCF  7A            		ld	a,d		;;
  1CD0  D1            		pop	de
  1CD1  C9            		ret
                      	
                      	
                      	;LOCATE command
  1CD2                	_C_LOCA:ds	C_LOCA-_C_LOCA
  1CD2                		org	C_LOCA
                      	
  1CD2  CDE40D        		call	INT1ARG
  1CD5  ED5BACFD      		ld	de,(WIDTH)
  1CD9  BB            		cp	e
  1CDA  D2E503        		jp	nc,FCERR
  1CDD  3C            		inc	a
  1CDE  F5            		push	af		;
  1CDF  7E            		ld	a,(hl)
  1CE0  FE2C          		cp	','
  1CE2  C2DC03        		jp	nz,SNERR
  1CE5  CDE30D        		call	INT1INC
                      	
  1CE8  2A20FA        		ld	hl,(HEIGHT)	;l=(HEIGHT)
  1CEB  BD            		cp	l
  1CEC  D2E503        		jp	nc,FCERR
  1CEF  3C            		inc	a
                      	
  1CF0  E1            		pop	hl		;
  1CF1  6F            		ld	l,a
  1CF2  C36D11        		jp	SETCSR
                      	
                      	
                      	;CONSOLE command
  1CF5                	_C_CNSL:ds	C_CNSL-_C_CNSL
  1CF6                		org	C_CNSL
                      	
  1CF6  ED4BA2FD      		ld	bc,(CONSOL1)	;c=(CONSOL1),b=(CONSOL2)
  1CFA  C5            		push	bc
                      	
  1CFB  CD6A3F        		call	CHKCMM
  1CFE  2814          		jr	z,CNSLPAR2
                      	
  1D00  CDE40D        		call	INT1ARG
  1D03  3A20FA        		ld	a,(HEIGHT)
  1D06  3D            		dec	a
  1D07  BB            		cp	e
  1D08  3801          		jr	c,CNSLC
  1D0A  7B            		ld	a,e
  1D0B                	CNSLC:
  1D0B  3C            		inc	a
  1D0C  C1            		pop	bc
  1D0D  4F            		ld	c,a
  1D0E  C5            		push	bc
                      	
  1D0F  CD7A3F        		call	CHKCLCM
  1D12  2838          		jr	z,CNSLEND
                      	
  1D14                	CNSLPAR2:
  1D14  CD6A3F        		call	CHKCMM
  1D17  2810          		jr	z,CNSLPAR3
                      	
  1D19  CDE40D        		call	INT1ARG
                      	
  1D1C  C1            		pop	bc
  1D1D  81            		add	a,c
  1D1E  DAE503        		jp	c,FCERR
  1D21  3D            		dec	a
  1D22  47            		ld	b,a
  1D23  C5            		push	bc
                      	
  1D24  CD7A3F        		call	CHKCLCM
  1D27  2823          		jr	z,CNSLEND
                      	
  1D29                	CNSLPAR3:
  1D29  CD6A3F        		call	CHKCMM
  1D2C  2813          		jr	z,CNSLPAR4
                      	
  1D2E  CDE40D        		call	INT1ARG
  1D31  FE02          		cp	02h
  1D33  D2E503        		jp	nc,FCERR
  1D36  32A6FD        		ld	(CONSOL3),a
                      	
  1D39  CDB312        		call	PRTFKEY2
  1D3C  CD7A3F        		call	CHKCLCM
  1D3F  280B          		jr	z,CNSLEND
                      	
  1D41                	CNSLPAR4:
  1D41  CDE40D        		call	INT1ARG
  1D44  FE02          		cp	02h
  1D46  D2E503        		jp	nc,FCERR
  1D49  322DFA        		ld	(CONSOL4),a
                      	
  1D4C                	CNSLEND:
  1D4C  D1            		pop	de
  1D4D  7A            		ld	a,d
  1D4E  BB            		cp	e
  1D4F  DAE503        		jp	c,FCERR
                      	
  1D52  CD731D        		call	SETCNSL
                      	
  1D55  3AA2FD        		ld	a,(CONSOL1)
  1D58  CD6515        		call	CUTLINE2
                      	
  1D5B  3AA8FD        		ld	a,(CSRY)
  1D5E  3D            		dec	a
  1D5F  21A5FD        		ld	hl,LASTLIN
  1D62  BE            		cp	(hl)
  1D63  D8            		ret	c
  1D64  6E            		ld	l,(hl)
  1D65  C34C11        		jp	CTLCR
                      	
                      	
                      	;set console parameter
                      	;input: d=last line+1, e=first line+1
                      	;destroy: af,bc,hl
  1D68                	_SETCNSL:ds	SETCNSL-_SETCNSL
  1D73                		org	SETCNSL
                      	
  1D73  ED53A2FD      		ld	(CONSOL1),de
                      	
  1D77                	SETCNSL2:
  1D77  3A20FA        		ld	a,(HEIGHT)
  1D7A  21A3FD        		ld	hl,CONSOL2
  1D7D  BE            		cp	(hl)
  1D7E  3001          		jr	nc,CNSLNC
  1D80  77            		ld	(hl),a
  1D81                	CNSLNC:
  1D81  4E            		ld	c,(hl)		;c=(CONSOL2)
                      	
  1D82  21A6FD        		ld	hl,CONSOL3
  1D85  96            		sub	(hl)
  1D86  47            		ld	b,a		;b=(HEIGHT)-(CONSOL3)
                      	
  1D87  2B            		dec	hl		;LASTLIN
  1D88  B9            		cp	c
  1D89  3F            		ccf
  1D8A  3A92FD        		ld	a,(SCREEN1)
  1D8D  1F            		rra
  1D8E  B7            		or	a
  1D8F  2001          		jr	nz,CNSLNZ
  1D91  48            		ld	c,b		;if screen mode 1,2 and (CONSOL2)>(HEIGHT)-(CONSOL3)
  1D92                	CNSLNZ:
  1D92  71            		ld	(hl),c
                      	
  1D93  21A2FD        		ld	hl,CONSOL1
  1D96  7E            		ld	a,(hl)
  1D97  B8            		cp	b
  1D98  D8            		ret	c
  1D99  70            		ld	(hl),b
  1D9A  C9            		ret
                      	
                      	
                      	;COLOR command
  1D9B                	_C_COLR:ds	C_COLR-_C_COLR
  1D9B                		org	C_COLR
                      	
  1D9B  1193FD        		ld	de,COLOR1
  1D9E  0602          		ld	b,02h
  1DA0                	COLRLP:
  1DA0  CD6A3F        		call	CHKCMM
  1DA3  2810          		jr	z,COLRZ
  1DA5  C5            		push	bc
  1DA6  D5            		push	de
  1DA7  CDE40D        		call	INT1ARG
  1DAA  D1            		pop	de
  1DAB  C1            		pop	bc
  1DAC  12            		ld	(de),a
  1DAD  CD5F3F        		call	CHKCLN		;a=(hl)
  1DB0  C8            		ret	z
  1DB1  FE2C          		cp	','
  1DB3  C0            		ret	nz
  1DB4  23            		inc	hl
  1DB5                	COLRZ:
  1DB5  13            		inc	de
  1DB6  10E8          		djnz	COLRLP
                      	
  1DB8                	COLRPAR3:
  1DB8  CDE40D        		call	INT1ARG
                      	;	jp	SETC3
                      	
                      	
                      	;set color 3rd parameter
                      	;input: a=3rd color parameter (1 or 2)
                      	;destroy: af,bc,de
  1DBB                	_SETC3:	ds	SETC3-_SETC3
  1DBB                		org	SETC3
                      	
  1DBB  EB            		ex	de,hl
  1DBC  3D            		dec	a
  1DBD  FE02          		cp	02h
  1DBF  D2E503        		jp	nc,FCERR
  1DC2  87            		add	a,a		;0 or 2
                      	
  1DC3  2195FD        		ld	hl,COLOR3
  1DC6  BE            		cp	(hl)
  1DC7  C8            		ret	z
  1DC8  77            		ld	(hl),a
                      	
  1DC9  2A90FD        		ld	hl,(VRAM-1)
  1DCC  010200        		ld	bc,0002h	;32*16=256*2
  1DCF  68            		ld	l,b		;b=0
  1DD0                	SETC3LP:
  1DD0  7E            		ld	a,(hl)
  1DD1  EE02          		xor	02h
  1DD3  77            		ld	(hl),a
  1DD4  23            		inc	hl
  1DD5  10F9          		djnz	SETC3LP
  1DD7  0D            		dec	c
  1DD8  20F6          		jr	nz,SETC3LP
  1DDA  EB            		ex	de,hl
  1DDB  C9            		ret
                      	
                      	
                      	;CLS command
                      	;destroy: af,de
  1DDC                	_CLS:	ds	CLS-_CLS
  1DFB                	C_CLS:
  1DFB                		org	CLS
                      	
  1DFB  E5            		push	hl
  1DFC  C5            		push	bc
  1DFD  CD3116        		call	CLSMAIN2
  1E00  C1            		pop	bc
  1E01  E1            		pop	hl
  1E02  C9            		ret
                      	
                      	
                      	;SCREEN command
  1E03                	_C_SCRN:ds	C_SCRN-_C_SCRN
  1E04                		org	C_SCRN
                      	
  1E04  3A92FD        		ld	a,(SCREEN1)
  1E07  5F            		ld	e,a
  1E08  1601          		ld	d,01h		;d>0: no 1st parameter
  1E0A  D5            		push	de
  1E0B  ED5B8FFD      		ld	de,(SCREEN2)	;e=(SCREEN2),d=(SCREEN3)
  1E0F  D5            		push	de
                      	
  1E10  CD6A3F        		call	CHKCMM
  1E13  280E          		jr	z,SCRPAR2
  1E15  CDE40D        		call	INT1ARG
  1E18  1D            		dec	e
  1E19  C1            		pop	bc		;c=2nd,b=3rd parameter
  1E1A  EB            		ex	de,hl
  1E1B  E3            		ex	(sp),hl		;cancel 1st parameter, l=1st parameter, h=0
  1E1C  EB            		ex	de,hl
  1E1D  C5            		push	bc		;c=2nd,b=3rd parameter
                      	
  1E1E  CD7A3F        		call	CHKCLCM
  1E21  2818          		jr	z,SCRMAIN
                      	
  1E23                	SCRPAR2:
  1E23  CD6A3F        		call	CHKCMM
  1E26  280C          		jr	z,SCRPAR3
  1E28  CDE40D        		call	INT1ARG
  1E2B  3D            		dec	a
                      	
  1E2C  D1            		pop	de		;e=2nd,d=3rd parameter
  1E2D  5F            		ld	e,a
  1E2E  D5            		push	de
                      	
  1E2F  CD7A3F        		call	CHKCLCM
  1E32  2807          		jr	z,SCRMAIN
                      	
  1E34                	SCRPAR3:
  1E34  CDE40D        		call	INT1ARG
  1E37  3D            		dec	a
                      	
  1E38  D1            		pop	de		;e=2nd,d=3rd parameter
  1E39  57            		ld	d,a
  1E3A  D5            		push	de
                      	
  1E3B                	SCRMAIN:
  1E3B  D1            		pop	de		;e=2nd,d=3rd parameter
  1E3C  C1            		pop	bc		;c=1st parameter
                      	
  1E3D  3A8CFD        		ld	a,(PAGES)
  1E40  3D            		dec	a
  1E41  BA            		cp	d
  1E42  DAE503        		jp	c,FCERR
  1E45  BB            		cp	e
  1E46  DAE503        		jp	c,FCERR
                      	
  1E49  79            		ld	a,c
  1E4A  FE02          		cp	02h
  1E4C  380D          		jr	c,SCROK
  1E4E  FE04          		cp	04h
  1E50  D2E503        		jp	nc,FCERR
  1E53  7B            		ld	a,e
  1E54  B7            		or	a
  1E55  2004          		jr	nz,SCROK
  1E57  B0            		or	b		;b>0: no 1st parameter
  1E58  CAE503        		jp	z,FCERR		;if 1st>=2 and 2nd=0
  1E5B                	SCROK:
  1E5B  D5            		push	de		;e=2nd,d=3rd parameter
  1E5C  7B            		ld	a,e
  1E5D  CD0C14        		call	CHGACT
                      	
  1E60  F1            		pop	af		;a=3rd parameter
  1E61  2190FD        		ld	hl,SCREEN3
  1E64  BE            		cp	(hl)
  1E65  C4ED13        		call	nz,CHGDSP
                      	
  1E68  78            		ld	a,b
  1E69  B7            		or	a
  1E6A  79            		ld	a,c
  1E6B  CA9013        		jp	z,CHGMOD	;b>0: no 1st parameter
  1E6E  C9            		ret
                      	
                      	
                      	;TIME function
  1E6F                	F_TIME:
  1E6F  2A28FA        		ld	hl,(TMCNT)
  1E72  ED5B2AFA      		ld	de,(TMCNT+2)
  1E76  C3A60C        		jp	I4TOF
                      	
                      	
                      	;SOUND command
  1E79                	C_SOUN:
  1E79  CD5F3F        		call	CHKCLN
  1E7C  CADC03        		jp	z,SNERR
  1E7F  CDE40D        		call	INT1ARG
  1E82  FE10          		cp	10h
  1E84  D2E503        		jp	nc,FCERR
  1E87  F5            		push	af
  1E88  7E            		ld	a,(hl)
  1E89  FE2C          		cp	','
  1E8B  C2DC03        		jp	nz,SNERR
  1E8E  CDE30D        		call	INT1INC
  1E91  F1            		pop	af
  1E92  F3            		di
  1E93                	SETPSGEI:
  1E93  CDC51B        		call	SETPSG
  1E96  FB            		ei
  1E97  C9            		ret
                      	
                      	
                      	;PLAY command
  1E98                	C_PLAY:
  1E98  CDB31E        		call	PLAY
  1E9B  224EFF        		ld	(PROGAD),hl
  1E9E  C9            		ret
                      	
                      	
                      	;play routine
                      	;input: hl=data address, (z=0)
                      	;output: hl=next address
                      	;destroy: af,bc,de
  1E9F                	_PLAY:	ds	PLAY-_PLAY
  1EB3                		org	PLAY
                      	
  1EB3  DDE5          		push	ix
  1EB5  ED5B4EFF      		ld	de,(PROGAD)
  1EB9  D5            		push	de
                      	
  1EBA  AF            		xor	a
  1EBB  3244FD        		ld	(PLWKB+PLLEN),a
  1EBE  3269FD        		ld	(PLWKC+PLLEN),a
                      	
  1EC1  DD211DFD      		ld	ix,PLWKA
  1EC5  0603          		ld	b,03h
  1EC7                	PLAYLP1:
  1EC7  C5            		push	bc
  1EC8  CD8A26        		call	STRARG
  1ECB  DD7702        		ld	(ix+PLLEN),a
  1ECE  DD7303        		ld	(ix+PLADR),e
  1ED1  DD7204        		ld	(ix+PLADR+1),d
  1ED4  012500        		ld	bc,PLWKB-PLWKA
  1ED7  DD09          		add	ix,bc
  1ED9  C1            		pop	bc
  1EDA  CD7A3F        		call	CHKCLCM
  1EDD  2802          		jr	z,PLSTR0
  1EDF  10E6          		djnz	PLAYLP1
                      	
                      	;check string length=0?
  1EE1                	PLSTR0:
  1EE1  2169FD        		ld	hl,PLWKC+PLLEN
  1EE4  11DBFF        		ld	de,PLWKB-PLWKC
  1EE7  0603          		ld	b,03h
  1EE9                	PLAYLP2:
  1EE9  7E            		ld	a,(hl)
  1EEA  B7            		or	a
  1EEB  200C          		jr	nz,PLAYNZ
  1EED  78            		ld	a,b
  1EEE  3D            		dec	a
  1EEF  3214FD        		ld	(CHANNEL),a
  1EF2  3EFF          		ld	a,0ffh
  1EF4  E5            		push	hl
  1EF5  CD310F        		call	PUTPLBF
  1EF8  E1            		pop	hl
  1EF9                	PLAYNZ:
  1EF9  19            		add	hl,de
  1EFA  10ED          		djnz	PLAYLP2
                      	
                      	
  1EFC                	PLAYLP3:
  1EFC  3A18FA        		ld	a,(STOPFLG)
  1EFF  FE03          		cp	03h
  1F01  CCB31B        		call	z,PLSTOP
  1F04  2856          		jr	z,PLAYEND
                      	
  1F06  DD2167FD      		ld	ix,PLWKC
  1F0A  0603          		ld	b,03h
  1F0C                	PLAYLP4:
  1F0C  78            		ld	a,b
  1F0D  3D            		dec	a
  1F0E  3214FD        		ld	(CHANNEL),a
  1F11  DD7E02        		ld	a,(ix+PLLEN)
  1F14  B7            		or	a
  1F15  2827          		jr	z,PLAYNEXT
                      	
  1F17  DD6E03        		ld	l,(ix+PLADR)
  1F1A  DD6604        		ld	h,(ix+PLADR+1)
                      	
  1F1D  3217FD        		ld	(PLAYLEN),a
  1F20  2218FD        		ld	(PLAYSTR),hl
  1F23  C5            		push	bc
  1F24  CD671F        		call	CNVPLAY
  1F27  C1            		pop	bc
  1F28  3A17FD        		ld	a,(PLAYLEN)
  1F2B  DD7702        		ld	(ix+PLLEN),a
  1F2E  2A18FD        		ld	hl,(PLAYSTR)
  1F31  DD7503        		ld	(ix+PLADR),l
  1F34  DD7404        		ld	(ix+PLADR+1),h
                      	
  1F37  B7            		or	a
  1F38  2004          		jr	nz,PLAYNEXT
  1F3A  3D            		dec	a
  1F3B  CD310F        		call	PUTPLBF		;a=ffh
                      	
  1F3E                	PLAYNEXT:
  1F3E  11DBFF        		ld	de,PLWKB-PLWKC
  1F41  DD19          		add	ix,de
  1F43  10C7          		djnz	PLAYLP4
                      	
  1F45  3EFF          		ld	a,0ffh
  1F47  3214FD        		ld	(CHANNEL),a
                      	
  1F4A  3A1FFD        		ld	a,(PLWKA+PLLEN)
  1F4D  2144FD        		ld	hl,PLWKB+PLLEN
  1F50  B6            		or	(hl)
  1F51  2169FD        		ld	hl,PLWKC+PLLEN
  1F54  B6            		or	(hl)
  1F55  20A5          		jr	nz,PLAYLP3
                      	
  1F57  F3            		di
  1F58  CDEC1B        		call	PLAYINT
  1F5B  FB            		ei
                      	
  1F5C                	PLAYEND:
  1F5C  D1            		pop	de
  1F5D  2A4EFF        		ld	hl,(PROGAD)
  1F60  ED534EFF      		ld	(PROGAD),de
  1F64  DDE1          		pop	ix
  1F66  C9            		ret
                      	
                      	
                      	;convert play data
                      	;input: a=string length, hl=string address, ix=play work
                      	;destroy: af,bc,de,hl
  1F67                	CNVPLAY:
  1F67  CD941C        		call	GETPLAD
  1F6A  56            		ld	d,(hl)
  1F6B  14            		inc	d		;in+1
  1F6C  23            		inc	hl
  1F6D  7E            		ld	a,(hl)		;out
  1F6E  92            		sub	d
  1F6F  3004          		jr	nc,CNVPLNC
                      	
  1F71  23            		inc	hl
  1F72  23            		inc	hl		;size
  1F73  3C            		inc	a
  1F74  86            		add	a,(hl)
  1F75                	CNVPLNC:
  1F75  FE08          		cp	08h
  1F77  D8            		ret	c
                      	
  1F78  CD1B20        		call	PLAYGETC
  1F7B  C8            		ret	z
  1F7C  CD6A20        		call	PLAYINC
  1F7F  CDEF0B        		call	TOUPPER
  1F82  CD871F        		call	PLAYCMD
  1F85  18E0          		jr	CNVPLAY
                      	
                      	
  1F87                	PLAYCMD:
  1F87  D641          		sub	'A'
  1F89  FE07          		cp	'G'-'A'+1
  1F8B  DAC720        		jp	c,TONE
  1F8E  219F1F        		ld	hl,PLAYTBL
  1F91  0608          		ld	b,08h
  1F93                	PLCMDLP:
  1F93  BE            		cp	(hl)
  1F94  23            		inc	hl
  1F95  CA503F        		jp	z,JPTBLNC
  1F98  23            		inc	hl
  1F99  23            		inc	hl
  1F9A  10F7          		djnz	PLCMDLP
  1F9C  C3E503        		jp	FCERR
                      	
                      	
  1F9F                	PLAYTBL:
  1F9F  15            		db	'V'-'A'
  1FA0  B71F          		dw	PLAYV
  1FA2  0C            		db	'M'-'A'
  1FA3  C71F          		dw	PLAYM
  1FA5  12            		db	'S'-'A'
  1FA6  DB1F          		dw	PLAYS
  1FA8  0B            		db	'L'-'A'
  1FA9  E71F          		dw	PLAYL
  1FAB  13            		db	'T'-'A'
  1FAC  F91F          		dw	PLAYT
  1FAE  0E            		db	'O'-'A'
  1FAF  0920          		dw	PLAYO
  1FB1  11            		db	'R'-'A'
  1FB2  9520          		dw	PLAYR
  1FB4  0D            		db	'N'-'A'
  1FB5  7620          		dw	PLAYN
                      	
                      	
  1FB7                	PLAYV:
  1FB7  CD5621        		call	GETNUM1
  1FBA  3802          		jr	c,PLAYVNUM
  1FBC  3E08          		ld	a,08h
  1FBE                	PLAYVNUM:
  1FBE  FE10          		cp	10h
  1FC0  D2E503        		jp	nc,FCERR
  1FC3                	PUTV:
  1FC3  DD7712        		ld	(ix+VOLUME),a
  1FC6  C9            		ret
                      	
  1FC7                	PLAYM:
  1FC7  CD6121        		call	GETNUM2
  1FCA  3803          		jr	c,PLAYMNUM
  1FCC  11FF00        		ld	de,00ffh
  1FCF                	PLAYMNUM:
  1FCF  7A            		ld	a,d
  1FD0  B3            		or	e
  1FD1  CAE503        		jp	z,FCERR
  1FD4  DD7313        		ld	(ix+PERIOD),e
  1FD7  DD7214        		ld	(ix+PERIOD+1),d
  1FDA  C9            		ret
                      	
  1FDB                	PLAYS:
  1FDB  CD5621        		call	GETNUM1
  1FDE  FE10          		cp	10h
  1FE0  D2E503        		jp	nc,FCERR
  1FE3  C610          		add	a,10h
  1FE5  18DC          		jr	PUTV
                      	
  1FE7                	PLAYL:
  1FE7  CD5621        		call	GETNUM1
  1FEA  3802          		jr	c,PLAYLNUM
  1FEC  3E04          		ld	a,04h
  1FEE                	PLAYLNUM:
  1FEE  3D            		dec	a
  1FEF  FE40          		cp	40h
  1FF1  D2E503        		jp	nc,FCERR
  1FF4  3C            		inc	a
  1FF5  DD7710        		ld	(ix+LENGTH),a
  1FF8  C9            		ret
                      	
  1FF9                	PLAYT:
  1FF9  CD5621        		call	GETNUM1
  1FFC  3802          		jr	c,PLAYTNUM
  1FFE  3E78          		ld	a,120
  2000                	PLAYTNUM:
  2000  FE20          		cp	32
  2002  DAE503        		jp	c,FCERR
  2005  DD7711        		ld	(ix+TEMPO),a
  2008  C9            		ret
                      	
  2009                	PLAYO:
  2009  CD5621        		call	GETNUM1
  200C  3802          		jr	c,PLAYONUM
  200E  3E04          		ld	a,04h
  2010                	PLAYONUM:
  2010  3D            		dec	a
  2011  FE08          		cp	08h
  2013  D2E503        		jp	nc,FCERR
  2016  3C            		inc	a
  2017  DD770F        		ld	(ix+OCTAVE),a
  201A  C9            		ret
                      	
                      	
                      	;get a character from PLAY string
                      	;output: a,z-flag(1=no character)
                      	;destroy: f,hl
  201B                	PLAYGETC:
  201B  3A17FD        		ld	a,(PLAYLEN)
  201E  B7            		or	a
  201F  C8            		ret	z		;a=0 if no character
  2020  2A18FD        		ld	hl,(PLAYSTR)
  2023  7E            		ld	a,(hl)
  2024  FE20          		cp	' '
  2026  C0            		ret	nz
  2027  CD6A20        		call	PLAYINC
  202A  18EF          		jr	PLAYGETC
                      	
                      	
                      	;scale data
                      	;address probably equals to 6001mkII/6601 ROM (used by BELUGA)
  202C                	_SCALE2:ds	SCALE2-_SCALE2
  2030                		org	SCALE2
                      	
                      	;		c/b+	c+/d-	d	d+/e-	e/f-	f/e+
  2030  E80E120E480D89		dw	0ee8h,	0e12h,	0d48h,	0c89h,	0bd5h,	0b2bh
                      	;		f+/g-	g	g+/a-	a	a+/b-	b/c-
  203C  8A0AF3096409DD		dw	0a8ah,	09f3h,	0964h,	08ddh,	085eh,	07e6h
                      	
                      	
                      	;scale data
  2048                	_SCALE:	ds	SCALE-_SCALE
  204B                		org	SCALE
                      	
                      	;		c/b+	c+/d-	d	d+/e-	e/f-	f/e+
  204B  E80E120E480D89		dw	0ee8h,	0e12h,	0d48h,	0c89h,	0bd5h,	0b2bh
                      	;		f+/g-	g	g+/a-	a	a+/b-	b/c-
  2057  8A0AF3096409DD		dw	0a8ah,	09f3h,	0964h,	08ddh,	085eh,	07e6h
                      	
                      	
  2063                	TONETBL:
                      	;		a   b   c  d  e  f   g
  2063  12160004080A0E		db	18, 22, 0, 4, 8, 10, 14
                      	
                      	
                      	;increment (PLAYSTR) and decrement (PLAYLEN)
                      	;destroy: f,hl
  206A                	PLAYINC:
  206A  2117FD        		ld	hl,PLAYLEN
  206D  35            		dec	(hl)
  206E  2118FD        		ld	hl,PLAYSTR
  2071  34            		inc	(hl)
  2072  C0            		ret	nz
  2073  2C            		inc	l
  2074  34            		inc	(hl)
  2075  C9            		ret
                      	
                      	
  2076                	PLAYN:
  2076  CD5621        		call	GETNUM1
  2079  D2E503        		jp	nc,FCERR
  207C  FE61          		cp	97
  207E  D2E503        		jp	nc,FCERR
  2081  B7            		or	a
  2082  283C          		jr	z,PLAYN0
                      	
  2084  0600          		ld	b,00h
  2086                	PLAYNLP:
  2086  04            		inc	b
  2087  D60C          		sub	0ch
  2089  30FB          		jr	nc,PLAYNLP
  208B  87            		add	a,a
  208C  16FF          		ld	d,0ffh
  208E  5F            		ld	e,a
  208F  216320        		ld	hl,SCALE+24
  2092  19            		add	hl,de
  2093  1859          		jr	GETFREQ
                      	
  2095                	PLAYR:
  2095  AF            		xor	a
  2096  F5            		push	af		;volume
  2097  D5            		push	de		;(tune)
  2098  CD5621        		call	GETNUM1
  209B  3006          		jr	nc,PLAYRNC
  209D  B7            		or	a
  209E  2068          		jr	nz,CHKLEN
  20A0  C3E503        		jp	FCERR
                      	
  20A3                	PLAYRNC:
  20A3  3E04          		ld	a,04h
  20A5  1861          		jr	CHKLEN
                      	
                      	;accidental mark
  20A7                	PLAYPLS:
  20A7  CD6A20        		call	PLAYINC
  20AA  1C            		inc	e
  20AB  1C            		inc	e
  20AC  7B            		ld	a,e
  20AD  D618          		sub	24
  20AF  3834          		jr	c,NOACC
  20B1  5F            		ld	e,a		;=0
  20B2  1831          		jr	NOACC
                      	
  20B4                	PLAYMNS:
  20B4  CD6A20        		call	PLAYINC
  20B7  1D            		dec	e
  20B8  1D            		dec	e
  20B9  F2E520        		jp	P,NOACC
  20BC  1E16          		ld	e,22
  20BE  1825          		jr	NOACC
                      	
  20C0                	PLAYN0:
  20C0  AF            		xor	a
  20C1  F5            		push	af		;volume
  20C2  D5            		push	de		;(tune)
                      	
                      	;for compatibility with PC-6001
  20C3  3E50          		ld	a,80
  20C5  1846          		jr	CALCLEN
                      	
                      	
  20C7                	TONE:
  20C7  216320        		ld	hl,TONETBL
  20CA  1600          		ld	d,00h
  20CC  5F            		ld	e,a
  20CD  19            		add	hl,de
  20CE  5E            		ld	e,(hl)
  20CF  CD1B20        		call	PLAYGETC
  20D2  2811          		jr	z,NOACC
  20D4  FE2B          		cp	'+'
  20D6  28CF          		jr	z,PLAYPLS
  20D8  FE23          		cp	'#'
  20DA  28CB          		jr	z,PLAYPLS
  20DC  FE2D          		cp	'-'
  20DE  28D4          		jr	z,PLAYMNS
  20E0  FE3D          		cp	'='
  20E2  CAE503        		jp	z,FCERR
                      	
  20E5                	NOACC:
  20E5  DD460F        		ld	b,(ix+OCTAVE)
  20E8  214B20        		ld	hl,SCALE
  20EB  1600          		ld	d,00h
  20ED  19            		add	hl,de
  20EE                	GETFREQ:
  20EE  7E            		ld	a,(hl)
  20EF  23            		inc	hl
  20F0  56            		ld	d,(hl)
  20F1  05            		dec	b
  20F2  2805          		jr	z,OCTAVE1
  20F4                	OCTVLP:
  20F4  CB3A          		srl	d
  20F6  1F            		rra
  20F7  10FB          		djnz	OCTVLP
  20F9                	OCTAVE1:
  20F9  5F            		ld	e,a
  20FA  DD7E12        		ld	a,(ix+VOLUME)
                      	
  20FD                	PUSHVOL:
  20FD  F5            		push	af		;volume
  20FE  D5            		push	de		;tune
  20FF  CD5621        		call	GETNUM1
  2102  B7            		or	a
  2103  2003          		jr	nz,CHKLEN
  2105  DD7E10        		ld	a,(ix+LENGTH)
  2108                	CHKLEN:
  2108  FE41          		cp	65
  210A  D2E503        		jp	nc,FCERR
                      	
  210D                	CALCLEN:
                      	;60[s/min]/T[/min]/(L/4)/(8192/3993600[Hz])=117000/T/L
  210D  DD4611        		ld	b,(ix+TEMPO)	;T-value
  2110  CD5C24        		call	MULINT1		;hl=T*L (<=3fc0h)
  2113  1184E4        		ld	de,0e484h	;117000/2
  2116  CD8724        		call	DIVINT2		;bc=(117000/2)/(T*L)*2
  2119  50            		ld	d,b
  211A  59            		ld	e,c
                      	
                      	;dotted note
  211B                	DOTLP:
  211B  CD1B20        		call	PLAYGETC
  211E  FE2E          		cp	'.'
  2120  200C          		jr	nz,NOTDOT
  2122  CD6A20        		call	PLAYINC
  2125  CB38          		srl	b
  2127  CB19          		rr	c
  2129  EB            		ex	de,hl
  212A  09            		add	hl,bc
  212B  EB            		ex	de,hl
  212C  18ED          		jr	DOTLP
  212E                	NOTDOT:
                      	
  212E  F3            		di
  212F  7A            		ld	a,d		;length
  2130  CD310F        		call	PUTPLBF
  2133  7B            		ld	a,e
  2134  CD310F        		call	PUTPLBF
  2137  D1            		pop	de		;tune
  2138  7A            		ld	a,d
  2139  CD310F        		call	PUTPLBF
  213C  7B            		ld	a,e
  213D  CD310F        		call	PUTPLBF
  2140  F1            		pop	af		;volume
  2141  CD310F        		call	PUTPLBF
  2144  FE10          		cp	10h
  2146  380C          		jr	c,PLCMDEND
  2148  DD7E13        		ld	a,(ix+PERIOD)	;period
  214B  CD310F        		call	PUTPLBF
  214E  DD7E14        		ld	a,(ix+PERIOD+1)
  2151  CD310F        		call	PUTPLBF
  2154                	PLCMDEND:
  2154  FB            		ei
  2155  C9            		ret
                      	
                      	
  2156                	GETNUM1:
                      	;output: a,c-flag(1=number)
                      	;destroy: f,bc,de,hl
  2156  CD6121        		call	GETNUM2
  2159  7B            		ld	a,e
  215A  D0            		ret	nc
  215B  14            		inc	d
  215C  15            		dec	d
  215D  C8            		ret	z		;c-flag=1
  215E  C3E503        		jp	FCERR
                      	
                      	
  2161                	GETNUM2:
                      	;output: de,c-flag(1=number)
                      	;destroy: af,bc,de
  2161  CD1B20        		call	PLAYGETC
  2164  110000        		ld	de,0000h
  2167  C8            		ret	z		;c-flag=0
  2168  FE3D          		cp	'='
  216A  2835          		jr	z,GETNUMEQ
  216C  FE3B          		cp	';'
  216E  282A          		jr	z,GETNUMSEMI
  2170  D630          		sub	'0'
  2172  FE0A          		cp	'9'-'0'+1
  2174  D0            		ret	nc		;c-flag=0
  2175  2A18FD        		ld	hl,(PLAYSTR)
  2178  E5            		push	hl		;
  2179  3A17FD        		ld	a,(PLAYLEN)
  217C  47            		ld	b,a
  217D  CDD627        		call	ATOI2LEN
  2180  2218FD        		ld	(PLAYSTR),hl
  2183  C1            		pop	bc		;
  2184  B7            		or	a
  2185  ED42          		sbc	hl,bc
  2187  3A17FD        		ld	a,(PLAYLEN)
  218A  95            		sub	l
  218B  3217FD        		ld	(PLAYLEN),a
  218E  CD1B20        		call	PLAYGETC
  2191  2805          		jr	z,GETNUMZ
  2193  FE3B          		cp	';'
  2195  CC6A20        		call	z,PLAYINC
  2198                	GETNUMZ:
  2198  37            		scf			;set c-flag
  2199  C9            		ret
                      	
  219A                	GETNUMSEMI:
  219A  CD6A20        		call	PLAYINC
  219D  AF            		xor	a		;reset c-flag
  219E  57            		ld	d,a
  219F  5F            		ld	e,a
  21A0  C9            		ret
                      	
  21A1                	GETNUMEQ:
  21A1  CD6A20        		call	PLAYINC
  21A4  11DAFE        		ld	de,INPBUF
  21A7                	GETNUMEQLP:
  21A7  CD1B20        		call	PLAYGETC
  21AA  CAE503        		jp	z,FCERR
  21AD  CD6A20        		call	PLAYINC
  21B0  CDF20B        		call	TOUPPER2
  21B3  12            		ld	(de),a
  21B4  13            		inc	de
  21B5  FE3B          		cp	';'
  21B7  20EE          		jr	nz,GETNUMEQLP
                      	
  21B9  21DAFE        		ld	hl,INPBUF
  21BC  46            		ld	b,(hl)
  21BD  78            		ld	a,b
  21BE  D641          		sub	'A'
  21C0  FE1A          		cp	'Z'-'A'+1
  21C2  D2E503        		jp	nc,FCERR
  21C5  23            		inc	hl
  21C6  CD8833        		call	GETNAME
                      	
  21C9  FE24          		cp	'$'
  21CB  CAFD03        		jp	z,TMERR
                      	
  21CE  FE28          		cp	'('
  21D0  2811          		jr	z,GETNUMARR
                      	
  21D2  CDAC33        		call	SRCHVAR
  21D5  EB            		ex	de,hl
  21D6  3003          		jr	nc,GETNUMEQNC
  21D8  21AB0E        		ld	hl,ZERO
  21DB                	GETNUMEQNC:
  21DB  CD2D0C        		call	SETF1
  21DE  CDBD0C        		call	FTOI
  21E1  37            		scf
  21E2  C9            		ret
                      	
                      	
  21E3                	GETNUMARR:
  21E3  ED5B4EFF      		ld	de,(PROGAD)
  21E7  D5            		push	de
  21E8  CDAA32        		call	GETARR
  21EB  E1            		pop	hl
  21EC  224EFF        		ld	(PROGAD),hl
  21EF  EB            		ex	de,hl
  21F0  18E9          		jr	GETNUMEQNC
                      	
                      	
                      	;STICK() function
  21F2                	F_STCK:
  21F2  CD4C07        		call	FNCI1		;a=0
  21F5  83            		add	a,e
  21F6  2813          		jr	z,STCK0		;e=0?
  21F8  FE03          		cp	03h
  21FA  D2E503        		jp	nc,FCERR
  21FD  CDA61C        		call	JOYSTK
                      	;exchange bit2 and bit3
  2200  47            		ld	b,a
  2201  E60C          		and	0ch
  2203  78            		ld	a,b
  2204  EA1022        		jp	pe,CNVSTCK
  2207  EE0C          		xor	0ch
  2209  1805          		jr	CNVSTCK
                      	
  220B                	STCK0:
  220B  CD6110        		call	STICK
  220E  0F            		rrca
  220F  0F            		rrca
  2210                	CNVSTCK:
  2210  E60F          		and	0fh
  2212  211E22        		ld	hl,STCKTBL
  2215  85            		add	a,l
  2216  6F            		ld	l,a
  2217  3001          		jr	nc,STCKNC
  2219  24            		inc	h
  221A                	STCKNC:
  221A  6E            		ld	l,(hl)
  221B  C3660C        		jp	I1TOF
                      	
  221E                	STCKTBL:
                      	;		    u   d   ud  r   ur  dr  udr
  221E  00010500030204		db	0,  1,  5,  0,  3,  2,  4,  3
                      	;		l   ul  dl  udl rl url drl udrl
  2226  07080607000105		db	7,  8,  6,  7,  0,  1,  5,  0
                      	
                      	
                      	;STRIG() function
  222E                	F_STRG:
  222E  CD4C07        		call	FNCI1		;a=0
  2231  83            		add	a,e
  2232  2813          		jr	z,STRG0		;e=0?
  2234  FE03          		cp	03h
  2236  D2E503        		jp	nc,FCERR
  2239  CDA61C        		call	JOYSTK
  223C  E610          		and	10h
  223E                	STRGEND:
  223E  CA2A0C        		jp	z,SETZERO
                      	;	jp	SETPLS1
                      	
  2241                	SETPLS1:
  2241  21A10E        		ld	hl,PLSONE
  2244  C32D0C        		jp	SETF1
                      	
  2247                	STRG0:
  2247  CD6110        		call	STICK
  224A  E680          		and	80h
  224C  18F0          		jr	STRGEND
                      	
                      	
                      	;LCOPY command
  224E                	C_LCPY:
  224E  218422        		ld	hl,LCPYHD
  2251  0605          		ld	b,05h
  2253                	LCPYLP1:
  2253  7E            		ld	a,(hl)
  2254  CD371A        		call	PRINTER
  2257  23            		inc	hl
  2258  10F9          		djnz	LCPYLP1
                      	
  225A  2A90FD        		ld	hl,(VRAM-1)	;h=(VRAM)
  225D  24            		inc	h
  225E  24            		inc	h
  225F  68            		ld	l,b		;b=0
  2260  3A92FD        		ld	a,(SCREEN1)
  2263  FE02          		cp	02h
  2265  3822          		jr	c,LCPYTXT
                      	
  2267                	LCPYGRP:
  2267  110018        		ld	de,1800h
  226A                	LCPYLP2:
  226A  4E            		ld	c,(hl)
  226B  CDDC19        		call	SENDGRP
  226E  23            		inc	hl
  226F  1B            		dec	de
  2270  7A            		ld	a,d
  2271  B3            		or	e
  2272  20F6          		jr	nz,LCPYLP2
                      	
  2274                	LCPYEND:
  2274  010020        		ld	bc,2000h
  2277  CD7D22        		call	LCPYLP3
                      	
  227A  010A06        		ld	bc,060ah
  227D                	LCPYLP3:
  227D  79            		ld	a,c
  227E  CD371A        		call	PRINTER
  2281  10FA          		djnz	LCPYLP3
  2283  C9            		ret
                      	
                      	;header for lcopy
  2284                	LCPYHD:
  2284  20200A1DC1    		db	20h, 20h, 0ah, 1dh, 0c1h
                      	
                      	;lcopy text screen
  2289                	LCPYTXT:
  2289  3E04          		ld	a,04h
  228B  D393          		out	(93h),a		;CGROM ON
  228D  3A20FA        		ld	a,(HEIGHT)
  2290  47            		ld	b,a
  2291                	LCPYTLP1:
  2291  C5            		push	bc
  2292  0E0C          		ld	c,0ch
  2294                	LCPYTLP2:
  2294  0620          		ld	b,COLUMNS
  2296                	LCPYTLP3:
  2296  C5            		push	bc
  2297  7E            		ld	a,(hl)
  2298  CDA014        		call	CGROM
  229B  7B            		ld	a,e
  229C  C60C          		add	a,0ch
  229E  91            		sub	c
  229F  5F            		ld	e,a
  22A0  1A            		ld	a,(de)
                      	
  22A1  57            		ld	d,a		;
  22A2  25            		dec	h
  22A3  25            		dec	h
  22A4  7E            		ld	a,(hl)		;attribute
  22A5  24            		inc	h
  22A6  24            		inc	h
  22A7  CB77          		bit	6,a
  22A9  201F          		jr	nz,LCPYSEMI
  22AB  0F            		rrca
  22AC  7A            		ld	a,d		;
  22AD  3001          		jr	nc,NOTREV
  22AF  2F            		cpl
  22B0                	NOTREV:
  22B0  4F            		ld	c,a
  22B1  CDDC19        		call	SENDGRP
  22B4  23            		inc	hl
  22B5  C1            		pop	bc
  22B6  10DE          		djnz	LCPYTLP3
                      	
  22B8  0D            		dec	c
  22B9  2806          		jr	z,LCPYTZ
  22BB  11E0FF        		ld	de,0-COLUMNS
  22BE  19            		add	hl,de
  22BF  18D3          		jr	LCPYTLP2
                      	
  22C1                	LCPYTZ:
  22C1  C1            		pop	bc
  22C2  10CD          		djnz	LCPYTLP1
                      	
  22C4  3E05          		ld	a,05h
  22C6  D393          		out	(93h),a		;CGROM OFF
  22C8  18AA          		jr	LCPYEND
                      	
  22CA                	LCPYSEMI:
                      	;c=	9-12	xx**....
                      	;	5-8	xx..**..
                      	;	1-4	xx....**
                      	
  22CA  7E            		ld	a,(hl)
  22CB  0D            		dec	c
  22CC  CB51          		bit	2,c
  22CE  2006          		jr	nz,SEMI58	;c=5-8
  22D0  CB59          		bit	3,c
  22D2  2804          		jr	z,SEMI14	;c=1-4
                      	
  22D4  0F            		rrca
  22D5  0F            		rrca
  22D6                	SEMI58:
  22D6  0F            		rrca
  22D7  0F            		rrca
  22D8                	SEMI14:
  22D8  E603          		and	03h
                      	
                      	;00h->00h, 01h->0fh, 02h->f0h, 03h->ffh
  22DA  1F            		rra			;c-flag=0
  22DB  3002          		jr	nc,SEMINC
  22DD  C61E          		add	a,0fh*2
  22DF                	SEMINC:
  22DF  1F            		rra			;c-flag=0
  22E0  30CE          		jr	nc,NOTREV
  22E2  C6F0          		add	a,0f0h
  22E4  18CA          		jr	NOTREV
                      	
                      	
                      	;KEY command
  22E6                	C_KEY:
  22E6  CDE40D        		call	INT1ARG
  22E9  3D            		dec	a
  22EA  FE0A          		cp	0ah
  22EC  D2E503        		jp	nc,FCERR
  22EF  113DFB        		ld	de,FKEYTBL
  22F2  87            		add	a,a		;*2
  22F3  87            		add	a,a		;*4
  22F4  87            		add	a,a		;*8
  22F5  83            		add	a,e		;no carry
  22F6  5F            		ld	e,a
  22F7  7E            		ld	a,(hl)
  22F8  FE2C          		cp	','
  22FA  C2DC03        		jp	nz,SNERR
  22FD  D5            		push	de		;
  22FE  23            		inc	hl
  22FF  CD8A26        		call	STRARG
  2302  EB            		ex	de,hl
  2303  D1            		pop	de		;
                      	
  2304  01FF08        		ld	bc,08ffh
  2307  3C            		inc	a
  2308                	KEYLP1:
  2308  3D            		dec	a
  2309  2806          		jr	z,KEYLP2
  230B  EDA0          		ldi
  230D  10F9          		djnz	KEYLP1
  230F  1804          		jr	KEYEND
                      	
  2311                	KEYLP2:
  2311  12            		ld	(de),a		;a=0
  2312  13            		inc	de
  2313  10FC          		djnz	KEYLP2
  2315                	KEYEND:
  2315  C3AF12        		jp	PRTFKEY
                      	
                      	
                      	;CSAVE command
  2318                	C_CSV:
  2318  CD8A26        		call	STRARG
  231B  B7            		or	a
  231C  CAE503        		jp	z,FCERR
  231F  4F            		ld	c,a
                      	
                      	;write header (d3h*10)
  2320  CDB81A        		call	WOPEN
  2323  3ED3          		ld	a,0d3h
  2325  060A          		ld	b,0ah
  2327                	CSVLP1:
  2327  CDCC1A        		call	PUTCMT
  232A  10FB          		djnz	CSVLP1
                      	
                      	;file name
  232C  0606          		ld	b,06h
                      	
  232E  0C            		inc	c
  232F                	CSVLP2:
  232F  1A            		ld	a,(de)
  2330  0D            		dec	c
  2331  2002          		jr	nz,CSVNZ
  2333  AF            		xor	a
  2334  0C            		inc	c
  2335                	CSVNZ:
  2335  CDCC1A        		call	PUTCMT
  2338  13            		inc	de
  2339  10F4          		djnz	CSVLP2
                      	
                      	;data
  233B                	CSVDATA:
  233B  01B024        		ld	bc,24b0h
  233E  CDE825        		call	WAITLP
                      	
  2341  2A5FFA        		ld	hl,(STARTAD)
  2344  ED5B56FF      		ld	de,(VARAD)
  2348                	CSVLP3:
  2348  7E            		ld	a,(hl)
  2349  CDCC1A        		call	PUTCMT
  234C  23            		inc	hl
  234D  E7            		rst	CPHLDE
  234E  20F8          		jr	nz,CSVLP3
                      	
                      	;footer (00h*9)
  2350  0609          		ld	b,09h
  2352                	CSVLP4:
  2352  CDCC1A        		call	PUTCMT		;a=0
  2355  10FB          		djnz	CSVLP4
                      	
  2357  C3061B        		jp	WCLOSE
                      	
                      	
                      	;CLOAD command
  235A                	C_CLD:
  235A  F1            		pop	af		;cancel return address
  235B  CD583F        		call	SKIPSP
  235E  FE95          		cp	PRINT_
  2360  0E00          		ld	c,00h		;file name length
  2362  79            		ld	a,c
  2363  2002          		jr	nz,CLDNZ1
  2365  23            		inc	hl
  2366  2F            		cpl
  2367                	CLDNZ1:
  2367  32D8FE        		ld	(VERIFY),a
  236A  F5            		push	af		;verify?
  236B  CD5F3F        		call	CHKCLN
  236E  2808          		jr	z,NOTARGET
  2370  CD8A26        		call	STRARG
  2373  B7            		or	a
  2374  CAE503        		jp	z,FCERR
  2377  4F            		ld	c,a
  2378                	NOTARGET:
  2378  CD611A        		call	ROPEN
                      	
  237B                	CLDLP1:
  237B  EB            		ex	de,hl
  237C  CD3925        		call	GETFN
  237F  EB            		ex	de,hl
                      	
                      	;compare file name
  2380  79            		ld	a,c
  2381  B7            		or	a
  2382  2815          		jr	z,CLDFND
                      	
  2384  D5            		push	de		;target
                      	
  2385  21D1FE        		ld	hl,FNAME
  2388  0C            		inc	c
  2389  0606          		ld	b,06h
  238B                	CLDLP2:
  238B  1A            		ld	a,(de)
  238C  0D            		dec	c
  238D  2002          		jr	nz,CLDNZ2
  238F  AF            		xor	a
  2390  0C            		inc	c
  2391                	CLDNZ2:
  2391  BE            		cp	(hl)
  2392  2045          		jr	nz,CLDSKIP
  2394  23            		inc	hl
  2395  13            		inc	de
  2396  10F3          		djnz	CLDLP2
  2398  D1            		pop	de		;target
                      	
  2399                	CLDFND:
  2399  217625        		ld	hl,FOUND
  239C  CD8325        		call	PUTFN
  239F  CD9C1A        		call	CHKVRF2
                      	
  23A2  F1            		pop	af		;verify?
  23A3  4F            		ld	c,a		;c=0: not verify
                      	
  23A4  2A5FFA        		ld	hl,(STARTAD)
  23A7                	CLDLP3:
  23A7  060A          		ld	b,0ah
  23A9                	CLDLP4:
  23A9  EB            		ex	de,hl
  23AA  21FEFF        		ld	hl,0-0002h
  23AD  39            		add	hl,sp
  23AE  E7            		rst	CPHLDE
  23AF  2837          		jr	z,CLDOM		;over memory error
  23B1  EB            		ex	de,hl
  23B2  CD8F1A        		call	GETCMTTR
  23B5  0C            		inc	c
  23B6  0D            		dec	c
  23B7  2803          		jr	z,CLDZ2		;c=0: not verify
  23B9  BE            		cp	(hl)
  23BA  2035          		jr	nz,CLDBAD
  23BC                	CLDZ2:
  23BC  77            		ld	(hl),a
  23BD  23            		inc	hl
  23BE  B7            		or	a
  23BF  20E6          		jr	nz,CLDLP3
  23C1  CD2A1B        		call	BLNKAST
  23C4  10E3          		djnz	CLDLP4
  23C6  CDAA1A        		call	RCLOSE
                      	
  23C9  2256FF        		ld	(VARAD),hl
                      	
  23CC  AF            		xor	a
  23CD  3258FA        		ld	(DEVICE),a
  23D0  219003        		ld	hl,OK
  23D3  CDCF30        		call	PUTS
  23D6  C3B704        		jp	CHGLKP
                      	
  23D9                	CLDSKIP:
  23D9  217D25        		ld	hl,SKIP
  23DC  CD8325        		call	PUTFN
                      	
                      	;check footer (00h*10)
  23DF  11000A        		ld	de,0a00h
  23E2  CD4E25        		call	CMTSKP
  23E5  D1            		pop	de		;target
  23E6  1893          		jr	CLDLP1
                      	
  23E8                	CLDOM:
  23E8  CDAA1A        		call	RCLOSE
  23EB  CDDB34        		call	NEW
  23EE  C3EB03        		jp	OMERR
                      	
  23F1                	CLDBAD:
  23F1  CDAA1A        		call	RCLOSE
  23F4  21FA23        		ld	hl,BAD
  23F7  C3D600        		jp	PUTSEDIT
                      	
  23FA                	BAD:
  23FA  42616400      		db	"Bad", 00h
                      	
                      	
                      	;input by external device
                      	;input: a=(INPDEV), hl=INPBUF-1
                      	;destroy: af,bc,de,hl
  23FE                	INPTEXT:
  23FE  FE02          		cp	02h
  2400  F5            		push	af
  2401  2807          		jr	z,INPT232
  2403  07            		rlca
  2404  D2E503        		jp	nc,FCERR
  2407  CD9A25        		call	INPOPN
  240A                	INPT232:
  240A  21DAFE        		ld	hl,INPBUF
  240D  0647          		ld	b,71
  240F                	INPTEXTLP:
  240F  F1            		pop	af
  2410  F5            		push	af
  2411  2020          		jr	nz,INPTCMT
                      	
  2413  E5            		push	hl
  2414                	INPT232LP:
  2414  3A18FA        		ld	a,(STOPFLG)
  2417  FE03          		cp	03h
  2419  CA5127        		jp	z,STOPSP
  241C  CD4310        		call	GETRSBF
  241F  28F3          		jr	z,INPT232LP
  2421  E1            		pop	hl
  2422                	INPTCHKRET:
  2422  FE0D          		cp	0dh
  2424  2804          		jr	z,INPTEXTEND
  2426  77            		ld	(hl),a
  2427  23            		inc	hl
  2428  10E5          		djnz	INPTEXTLP
                      	
  242A                	INPTEXTEND:
  242A  F1            		pop	af
  242B  C4AA1A        		call	nz,RCLOSE
  242E  3600          		ld	(hl),00h
  2430  C3DB09        		jp	INPTANA
                      	
  2433                	INPTCMT:
  2433  CD8F1A        		call	GETCMTTR
  2436  18EA          		jr	INPTCHKRET
                      	
  2438                	EXTRA:
  2438  3F457874726120		db	"?Extra ignored", 0dh, 0ah, 00h
                      	
  2449                	REDO:
  2449  3F5265646F2066		db	"?Redo from start", 0dh, 0ah, 00h
                      	
                      	
                      	;hl=a*b
                      	;input: a,b
                      	;output: hl
                      	;destroy: af,c
  245C                	MULINT1:
  245C  210000        		ld	hl,0000h
  245F  4D            		ld	c,l		;=0
  2460  1801          		jr	MULI1NZ
  2462                	MULI1C:
  2462  09            		add	hl,bc
  2463                	MULI1NZ:
  2463  CB38          		srl	b
  2465  CB19          		rr	c
  2467  87            		add	a,a
  2468  38F8          		jr	c,MULI1C
  246A  20F7          		jr	nz,MULI1NZ
  246C  C9            		ret
                      	
                      	
                      	;dehl=de*hl
                      	;input: de,hl
                      	;output: dehl
                      	;destroy: af,bc
  246D                	MULINT2:
  246D  7C            		ld	a,h
  246E  4D            		ld	c,l
  246F  210000        		ld	hl,0000h
  2472  0610          		ld	b,10h
  2474                	MULI2LP:
  2474  1F            		rra
  2475  CB19          		rr	c
  2477  3001          		jr	nc,MULI2NC
  2479  19            		add	hl,de
  247A                	MULI2NC:
  247A  CB1C          		rr	h
  247C  CB1D          		rr	l
  247E  10F4          		djnz	MULI2LP
  2480  1F            		rra
  2481  CB19          		rr	c
  2483  59            		ld	e,c
  2484  57            		ld	d,a
  2485  EB            		ex	de,hl
  2486  C9            		ret
                      	
                      	
                      	;bc=de/hl*2
                      	;input: de,hl(<8000h)
                      	;output: bc
                      	;destroy: af,de,hl
  2487                	DIVINT2:
  2487  AF            		xor	a
  2488  010002        		ld	bc,0200h	;*2^(b-1)
  248B                	DIVILP1:
  248B  04            		inc	b
  248C  ED6A          		adc	hl,hl		;c-flag=0
  248E  F28B24        		jp	p,DIVILP1
  2491  EB            		ex	de,hl
  2492                	DIVILP2:
  2492  B7            		or	a
  2493  ED52          		sbc	hl,de
  2495  3001          		jr	nc,DIVNC
  2497  19            		add	hl,de
  2498                	DIVNC:
  2498  3F            		ccf
  2499  CB11          		rl	c
  249B  17            		rla
  249C  CB3A          		srl	d
  249E  CB1B          		rr	e
  24A0  10F0          		djnz	DIVILP2
                      	
  24A2  47            		ld	b,a
  24A3  C9            		ret
                      	
                      	
                      	;add 4-byte integer and 4byte integer
                      	;input: FAC1(integer),FAC2(integer)
                      	;output: FAC1(integer),f
                      	;destroy: de,hl
  24A4                	ADDINT4:
  24A4  2A66FF        		ld	hl,(FAC1)
  24A7  ED5B6DFF      		ld	de,(FAC2)
  24AB  19            		add	hl,de
  24AC  2266FF        		ld	(FAC1),hl
  24AF  2A68FF        		ld	hl,(FAC1+2)
  24B2  ED5B6FFF      		ld	de,(FAC2+2)
  24B6  ED5A          		adc	hl,de
  24B8  2268FF        		ld	(FAC1+2),hl
  24BB  C9            		ret
                      	
                      	
                      	;subtract 4-byte integer from 4byte integer
                      	;input: FAC1(integer),FAC2(integer)
                      	;output: FAC1(integer),f
                      	;destroy: de,hl
  24BC                	SUBINT4:
  24BC  2A66FF        		ld	hl,(FAC1)
  24BF  ED5B6DFF      		ld	de,(FAC2)
  24C3  B7            		or	a
  24C4  ED52          		sbc	hl,de
  24C6  2266FF        		ld	(FAC1),hl
  24C9  2A68FF        		ld	hl,(FAC1+2)
  24CC  ED5B6FFF      		ld	de,(FAC2+2)
  24D0  ED52          		sbc	hl,de
  24D2  2268FF        		ld	(FAC1+2),hl
  24D5  C9            		ret
                      	
                      	
                      	;a=FAC1(int4)/(hl)<=9, FAC1%=(hl), hl+=4
                      	;input: FAC1(integer), hl=division factor (4byte int) address
                      	;output: a=FAC1/(hl4), FAC1=FAC1%(hl4), hl=hl+4, a=[0,9]
                      	;destroy: f,FAC2
  24D6                	DIVINT4:
  24D6  C5            		push	bc
  24D7  D5            		push	de
  24D8  CD360C        		call	SETF2
  24DB  2B            		dec	hl
  24DC  E5            		push	hl
  24DD  AF            		xor	a
  24DE                	DIVINT4LP:
  24DE  3C            		inc	a
  24DF  CDBC24        		call	SUBINT4
  24E2  30FA          		jr	nc,DIVINT4LP
  24E4  3D            		dec	a
  24E5  CDA424        		call	ADDINT4
  24E8  E1            		pop	hl
  24E9  D1            		pop	de
  24EA  C1            		pop	bc
  24EB  C9            		ret
                      	
                      	
                      	;negate 4-byte integer
                      	;input: FAC1(integer)
                      	;output: FAC1(integer), c-flag, z-flag
                      	;destroy: af,hl
  24EC                	NEGINT4:
  24EC  2166FF        		ld	hl,FAC1
  24EF  AF            		xor	a
  24F0  96            		sub	(hl)
  24F1  77            		ld	(hl),a
  24F2  23            		inc	hl
  24F3  3E00          		ld	a,00h
  24F5  9E            		sbc	a,(hl)
  24F6  77            		ld	(hl),a
  24F7  23            		inc	hl
  24F8  3E00          		ld	a,00h
  24FA  9E            		sbc	a,(hl)
  24FB  77            		ld	(hl),a
  24FC  23            		inc	hl
  24FD  3E00          		ld	a,00h
  24FF  9E            		sbc	a,(hl)
  2500  77            		ld	(hl),a
  2501  C9            		ret
                      	
                      	
                      	;compare 4-byte integer and 4-byte integer
                      	;input: FAC1(integer), FAC2(integer)
                      	;output: c-flag,z-flag
                      	;destroy: f,de,hl
  2502                	CMPINT4:
  2502  2A68FF        		ld	hl,(FAC1+2)
  2505  ED5B6FFF      		ld	de,(FAC2+2)
  2509  B7            		or	a
  250A  ED52          		sbc	hl,de
  250C  C0            		ret	nz
  250D  2A66FF        		ld	hl,(FAC1)
  2510  ED5B6DFF      		ld	de,(FAC2)
  2514  ED52          		sbc	hl,de
  2516  C9            		ret
                      	
                      	
                      	;decrement 4-byte integer
                      	;input: FAC1
                      	;output: FAC1
                      	;destroy: af,hl
  2517                	DECINT4:
  2517  2A66FF        		ld	hl,(FAC1)
  251A  7C            		ld	a,h
  251B  B5            		or	l		;
  251C  2B            		dec	hl
  251D  2266FF        		ld	(FAC1),hl
  2520  C0            		ret	nz		;
  2521  2A68FF        		ld	hl,(FAC1+2)
  2524  2B            		dec	hl
  2525  2268FF        		ld	(FAC1+2),hl
  2528  C9            		ret
                      	
                      	
                      	;4-byte integer=0?
                      	;output: a(4 bytes OR),z
                      	;destroy: af
  2529                	CHKINT4:
  2529  E5            		push	hl
  252A  2A66FF        		ld	hl,(FAC1)
  252D  7C            		ld	a,h
  252E  B5            		or	l
  252F  2A68FF        		ld	hl,(FAC1+2)
  2532  B4            		or	h
  2533  B5            		or	l
  2534  E1            		pop	hl
  2535  C9            		ret
                      	
                      	
                      	;check cload header and get file name
                      	;destroy: af,b,de (if no error)
  2536                	_GETFN:	ds	GETFN-_GETFN
  2539                		org	GETFN
                      	
                      	;check header (d3h*10)
  2539  11D30A        		ld	de,0ad3h
  253C  CD4E25        		call	CMTSKP
                      	;file name
  253F  11D1FE        		ld	de,FNAME
                      	
  2542  0606          		ld	b,06h
  2544                	GETFNLP:
  2544  CD8F1A        		call	GETCMTTR
  2547  12            		ld	(de),a
  2548  13            		inc	de
  2549  10F9          		djnz	GETFNLP
  254B  C9            		ret
                      	
                      	
                      	;skip CMT data
                      	;input: d=count, e=data
                      	;destroy: af,b
  254C                	_CMTSKP:ds	CMTSKP-_CMTSKP
  254E                		org	CMTSKP
                      	
  254E  42            		ld	b,d
  254F                	CMTSKPLP:
  254F  CD8F1A        		call	GETCMTTR
  2552  BB            		cp	e
  2553  20F9          		jr	nz,CMTSKP
  2555  10F8          		djnz	CMTSKPLP
  2557  C9            		ret
                      	
                      	
  2558                	_FOUND:	ds	FOUND-_FOUND
  2576                		org	FOUND
                      	
  2576  466F756E643A00		db	"Found:", 00h
                      	
  257D                	SKIP:
  257D  536B69703A00  		db	"Skip:", 00h
                      	
                      	
                      	;put message and file name in device
                      	;input: hl=message address
                      	;destroy: af,hl,(bc,de,FAC1)
  2583                	_PUTFN:	ds	PUTFN-_PUTFN
  2583                		org	PUTFN
                      	
  2583  CDCF30        		call	PUTS
  2586  21D1FE        		ld	hl,FNAME
  2589  CDCF30        		call	PUTS
  258C  C33927        		jp	PUTNL
                      	
                      	
                      	;CMT open for INPUT #-1
                      	;destroy: af,b
  258F                	_INPOPN:ds	INPOPN-_INPOPN
  259A                		org	INPOPN
                      	
                      	;	ld	a,0ffh
                      	;	ld	(VERIFY),a
                      	;	call	ROPEN
  259A  CD5C1A        		call	VRFOPN
  259D  D5            		push	de
  259E  119C06        		ld	de,069ch
  25A1  CD4E25        		call	CMTSKP
  25A4  D1            		pop	de
  25A5  C9            		ret
                      	
                      	
                      	;CMT open for PRINT #-1
                      	;destroy: a,b
  25A6                	_PRTOPN:ds	PRTOPN-_PRTOPN
  25A8                		org	PRTOPN
                      	
  25A8  CDB81A        		call	WOPEN
  25AB  3E9C          		ld	a,9ch
  25AD  0606          		ld	b,06h
  25AF                	PRTOPNLP:
  25AF  CDCC1A        		call	PUTCMT
  25B2  10FB          		djnz	PRTOPNLP
  25B4  C9            		ret
                      	
                      	
                      	;convert intermediate code to command/function ascii characters
                      	;input: a=code
                      	;output: a=first character, hl=address
                      	;destroy: f,b,hl
  25B5                	CNVASC:
  25B5  212902        		ld	hl,CMDNAME-1
  25B8  FEC2          		cp	TAB_
  25BA  3805          		jr	c,CNVASCC
                      	;function
  25BC  21E102        		ld	hl,FNCNAME-1
  25BF  D642          		sub	TAB_-80h
                      	
  25C1                	CNVASCC:
  25C1  D67F          		sub	7fh
  25C3  47            		ld	b,a
  25C4                	CNVASCLP:
  25C4  23            		inc	hl
  25C5  7E            		ld	a,(hl)
  25C6  D680          		sub	80h
  25C8  38FA          		jr	c,CNVASCLP
  25CA  10F8          		djnz	CNVASCLP
  25CC  C9            		ret
                      	
                      	
                      	;wait for about 3.5s
                      	;destroy: af,bc
  25CD                	_WAIT:	ds	WAIT-_WAIT
  25E5                		org	WAIT
                      	
  25E5  010000        		ld	bc,0000h
                      	;25e8
  25E8                	WAITLP:
  25E8  3A18FA        		ld	a,(STOPFLG)
  25EB  FE03          		cp	03h
  25ED  C8            		ret	z
                      	
  25EE  E3            		ex	(sp),hl		;waste time
  25EF  E3            		ex	(sp),hl		;waste time
  25F0  00            		nop			;waste time
                      	
  25F1  0B            		dec	bc
  25F2  78            		ld	a,b
  25F3  B1            		or	c
  25F4  20F2          		jr	nz,WAITLP
  25F6  C9            		ret
                      	
                      	
                      	;EXEC command
  25F7                	C_EXEC:
  25F7  CD720B        		call	NUMARGMO
  25FA  CDBD0C        		call	FTOI
                      	
                      	;for compatibility with N60-BASIC
  25FD  2A4EFF        		ld	hl,(PROGAD)
  2600  EB            		ex	de,hl
  2601  7C            		ld	a,h
  2602  B5            		or	l
  2603  ED44          		neg			;set c-flag if address<>0
  2605  D5            		push	de
  2606  CDA507        		call	JPHL
  2609  D1            		pop	de
  260A  C9            		ret
                      	
                      	
                      	;get a character from buffer
                      	;input: hl=buffer map address
                      	;output: a,z-flag(1=no input)
                      	;destroy: f,hl
  260B                	GETBF:
  260B  7E            		ld	a,(hl)		;in
  260C  2C            		inc	l		;inc hl
  260D  BE            		cp	(hl)		;out
  260E  C8            		ret	z
  260F  7E            		ld	a,(hl)
  2610  3C            		inc	a
  2611  2C            		inc	l		;inc hl
  2612  2C            		inc	l		;inc hl
  2613  A6            		and	(hl)		;size
  2614  2D            		dec	l		;dec hl
  2615  2D            		dec	l		;dec hl
  2616  77            		ld	(hl),a		;out
  2617  2C            		inc	l		;inc hl
  2618  2C            		inc	l		;inc hl
  2619  2C            		inc	l		;inc hl
  261A  86            		add	a,(hl)		;hl=(buffer address)+a
  261B  2C            		inc	l		;inc hl, l>0, reset z-flag
  261C  66            		ld	h,(hl)
  261D  6F            		ld	l,a
  261E  3001          		jr	nc,GETBFNC
  2620  24            		inc	h		;h>0, reset z-flag
  2621                	GETBFNC:
  2621  7E            		ld	a,(hl)
  2622  C9            		ret
                      	
                      	
                      	;input: a=length, hl=string address
                      	;destroy: af,bc,de,hl
  2623                	MAKESTR:
  2623  322DFF        		ld	(STRDSC1),a
  2626  222FFF        		ld	(STRDSC1+2),hl
  2629  AF            		xor	a
  262A  1803          		jr	ADDSTR2
                      	
                      	;input: STRDSC1, STRDSC4
                      	;destroy: af,bc,de,hl
  262C                	ADDSTR:
  262C  3A39FF        		ld	a,(STRDSC4)
  262F                	ADDSTR2:
  262F  47            		ld	b,a		;;
  2630  3A2DFF        		ld	a,(STRDSC1)
  2633  4F            		ld	c,a		;
  2634  80            		add	a,b		;c+b
  2635  DA0304        		jp	c,LSERR		;over 255 characters
                      	
  2638  CD6626        		call	GCCHECK
  263B  223DFF        		ld	(STRAD),hl
  263E  23            		inc	hl
  263F  EB            		ex	de,hl
  2640  2A2FFF        		ld	hl,(STRDSC1+2)
  2643  ED532FFF      		ld	(STRDSC1+2),de
                      	
  2647  78            		ld	a,b		;;
  2648  81            		add	a,c		;
  2649  322DFF        		ld	(STRDSC1),a
  264C  90            		sub	b		;c=0?
                      	
  264D  78            		ld	a,b
  264E  0600          		ld	b,00h
  2650  2802          		jr	z,ADDSZ1
  2652  EDB0          		ldir
  2654                	ADDSZ1:
  2654  B7            		or	a
  2655  2806          		jr	z,ADDSZ2
  2657  4F            		ld	c,a		;b=0
  2658  2A3BFF        		ld	hl,(STRDSC4+2)
  265B  EDB0          		ldir
  265D                	ADDSZ2:
  265D  AF            		xor	a
  265E  3239FF        		ld	(STRDSC4),a
  2661  3C            		inc	a
  2662  3225FF        		ld	(ARGTYP),a	;=1
  2665  C9            		ret
                      	
                      	
                      	;check string area and call GC if necessary
                      	;input: a=new string length
                      	;output: hl=new (STRAD)+1
                      	;destroy: af,de
  2666                	GCCHECK:
  2666  C5            		push	bc
  2667  1600          		ld	d,00h
  2669  5F            		ld	e,a
  266A  D5            		push	de
  266B  CD7E26        		call	CHKSAREA
  266E  D1            		pop	de
  266F  300B          		jr	nc,GCCHECKOK
  2671  D5            		push	de
  2672  CD0F31        		call	GC
  2675  D1            		pop	de
  2676  CD7E26        		call	CHKSAREA
  2679  DA0004        		jp	c,OSERR
  267C                	GCCHECKOK:
  267C  C1            		pop	bc
  267D  C9            		ret
                      	
                      	;check string area
  267E                	CHKSAREA:
  267E  2A3DFF        		ld	hl,(STRAD)
  2681  B7            		or	a
  2682  ED52          		sbc	hl,de
  2684  ED5B5BFA      		ld	de,(STACK)
  2688  E7            		rst	CPHLDE
  2689  C9            		ret
                      	
                      	
                      	;get a string argument
                      	;input: hl=program address
                      	;output: a=length, de=string address, hl=next address, z-flag(1=ok)
                      	;destroy: f,bc,FAC1,FAC2
  268A                	STRARG:
  268A  CD5F3F        		call	CHKCLN
  268D  CA1204        		jp	z,MOERR
  2690  CD4317        		call	ARG
  2693                	STRARG2:
  2693  EB            		ex	de,hl
  2694  CD870B        		call	CHKSTR
                      	;get string pointer
  2697  23            		inc	hl
  2698  23            		inc	hl
  2699  46            		ld	b,(hl)
  269A  23            		inc	hl
  269B  66            		ld	h,(hl)
  269C  68            		ld	l,b
  269D  EB            		ex	de,hl
  269E  C9            		ret
                      	
                      	
                      	;get string,integer argument
                      	;input: hl=program address
                      	;output: STRDSC1, a=length, hl=string address, FAC1,de=integer
                      	;destroy: FAC1, FAC2, f,bc
  269F                	ARGSI1:
  269F  CD950B        		call	CHKLPAR
  26A2  CD8A26        		call	STRARG
  26A5  F5            		push	af
  26A6  322DFF        		ld	(STRDSC1),a	;length
  26A9  ED532FFF      		ld	(STRDSC1+2),de	;string address
  26AD  7E            		ld	a,(hl)
  26AE  FE2C          		cp	','
  26B0  C2DC03        		jp	nz,SNERR
  26B3  CDBB26        		call	ARGI1
  26B6  2A2FFF        		ld	hl,(STRDSC1+2)	;string address
  26B9  F1            		pop	af		;length
  26BA  C9            		ret
                      	
                      	
                      	;protect string descriptor and get integer argument
                      	;input: hl=program address
                      	;output: FAC1,de=integer
                      	;destroy: FAC1, FAC2, af, bc, de
  26BB                	ARGI1:
  26BB  E5            		push	hl		;program address
  26BC  CDB619        		call	COPYSTR
  26BF  E1            		pop	hl
  26C0  CDE30D        		call	INT1INC
  26C3  C3CA19        		jp	BACKSTR
                      	
                      	
                      	;put a character in device
                      	;input: a=character code, (DEVICE)=0:CRT, 1:printer, 2:RS-232C, 80h-ffh:CMT
                      	; (1,4,5,8,9,c,d...:printer, 2,3,6,7,a,b,e,f...:RS-232C)
                      	;destroy: af
  26C6                	_PUTDEV:ds	PUTDEV-_PUTDEV
  26C7                		org	PUTDEV
                      	
  26C7  F5            		push	af
  26C8  3A58FA        		ld	a,(DEVICE)
  26CB  B7            		or	a
  26CC  2859          		jr	z,PUTCRT
  26CE  FACD1A        		jp	m,PUTCMT2
  26D1  E602          		and	02h
  26D3  283D          		jr	z,PUTPRT2
                      	;	jr	nz,PUT232
                      	
                      	;input: a
                      	;output: none
                      	;destroy: af
  26D5                	PUT232:
  26D5  3E17          		ld	a,17h		;RTS=0
  26D7  D381          		out	(81h),a
                      	
  26D9                	PUT232LP:
  26D9  DB81          		in	a,(81h)
  26DB  2F            		cpl
  26DC  E681          		and	81h
  26DE  280C          		jr	z,PUT232Z	;if DSR=1, TxRDY=1
                      	
  26E0  3A18FA        		ld	a,(STOPFLG)
  26E3  FE03          		cp	03h
  26E5  20F2          		jr	nz,PUT232LP
                      	;stop
  26E7  CDEF26        		call	PUT232END
  26EA  1865          		jr	STOPSP
                      	
  26EC                	PUT232Z:
  26EC  F1            		pop	af
  26ED  D380          		out	(80h),a
  26EF                	PUT232END:
  26EF  3E37          		ld	a,37h		;RTS=1
  26F1  D381          		out	(81h),a
  26F3  C9            		ret
                      	
                      	
  26F4                	PUTPRTTAB:
  26F4  3E20          		ld	a,' '
  26F6  CD1727        		call	PUTPRT3		;a<-(PRTPOS)
  26F9  E607          		and	07h
  26FB  20F7          		jr	nz,PUTPRTTAB
  26FD  C9            		ret
                      	
                      	
                      	;put new line to printer
                      	;destroy: af
  26FE                	_PUTPRNL:ds	PUTPRNL-_PUTPRNL
  2701                		org	PUTPRNL
  2701  3E01          		ld	a,01h
  2703  3258FA        		ld	(DEVICE),a
  2706  3A57FA        		ld	a,(PRTPOS)
  2709  B7            		or	a
  270A  C43927        		call	nz,PUTNL
  270D  AF            		xor	a
  270E  3258FA        		ld	(DEVICE),a
  2711  C9            		ret
                      	
                      	
  2712                	PUTPRT2:
  2712  F1            		pop	af
  2713  FE09          		cp	09h
  2715  28DD          		jr	z,PUTPRTTAB
  2717                	PUTPRT3:
  2717  CD1C1A        		call	PUTPRT
  271A  D60D          		sub	0dh
  271C  2805          		jr	z,PUTPRTZ	;a=0 if CR
  271E  D8            		ret	c
  271F  3A57FA        		ld	a,(PRTPOS)
  2722  3C            		inc	a
  2723                	PUTPRTZ:
  2723  3257FA        		ld	(PRTPOS),a
  2726  C9            		ret
                      	
                      	
  2727                	PUTCRT:
  2727  F1            		pop	af
  2728  C37510        		jp	PRTC
                      	
                      	
                      	;put new line if necessary
                      	;destroy: af
  272B                	_PUTNLX:ds	PUTNLX-_PUTNLX
  272D                		org	PUTNLX
  272D  3A58FA        		ld	a,(DEVICE)
  2730  B7            		or	a
  2731  CA5A09        		jp	z,PRTNLX
  2734  3D            		dec	a
  2735  28CA          		jr	z,PUTPRNL
  2737  1800          		jr	PUTNL
                      	
                      	
                      	;put new line
                      	;destroy: af
  2739                	_PUTNL:	ds	PUTNL-_PUTNL
  2739                		org	PUTNL
                      	
  2739  3E0D          		ld	a,0dh
  273B  CDC726        		call	PUTDEV
  273E  3E0A          		ld	a,0ah
  2740  C3C726        		jp	PUTDEV
                      	
                      	
                      	;STOP and ESC
                      	;destroy: af
  2743                	_STOPESC:ds	STOPESC-_STOPESC
  274D                		org	STOPESC
                      	
  274D  CD5B27        		call	CHKSTOP
  2750  C0            		ret	nz
                      	;	jp	STOPSP
                      	
                      	;reset sp and stop
  2751                	STOPSP:
  2751  ED7B5BFA      		ld	sp,(STACK)
  2755  21FFFF        		ld	hl,0ffffh
  2758  C33B35        		jp	STOP2
                      	
                      	
                      	;check STOP/ESC
                      	;output: z-flag (1=STOP)
                      	;destroy: af
  275B                	CHKSTOP:
  275B  3A18FA        		ld	a,(STOPFLG)
  275E  FE03          		cp	03h
  2760  C8            		ret	z
  2761  FE1B          		cp	1bh
  2763  C0            		ret	nz
  2764  CDBC0F        		call	GETCH		;cancel ESC
  2767                	CHKSTLP:
  2767  3A18FA        		ld	a,(STOPFLG)
  276A  FE1B          		cp	1bh
  276C  28F9          		jr	z,CHKSTLP
  276E  CDBC0F        		call	GETCH		;ESC end
  2771  18E8          		jr	CHKSTOP
                      	
                      	
                      	;get device number
                      	;in	out
                      	;#0	0	CRT
                      	;#-1	80h	CMT
                      	;#-2	2	RS-232C
                      	;#-3	1	printer
                      	;input: hl=program address
                      	;output: a, hl,(PROGAD)=next address
                      	;destroy: f,bc,de
  2773                	DEVNUM:
  2773  CD583F        		call	SKIPSP
  2776  FE23          		cp	'#'
  2778  3E00          		ld	a,00h
  277A  C0            		ret	nz
                      	
  277B  23            		inc	hl
  277C  CD1A0E        		call	INT2ARG
  277F  E5            		push	hl
  2780  1B            		dec	de
  2781  210400        		ld	hl,0004h
  2784  19            		add	hl,de
  2785  D2E503        		jp	nc,FCERR
  2788  7D            		ld	a,l
  2789  3C            		inc	a
  278A  FE03          		cp	03h
  278C  3803          		jr	c,DEVNUMC	;#-2,#-3
  278E  E601          		and	01h
  2790  0F            		rrca
  2791                	DEVNUMC:
  2791  E1            		pop	hl
  2792  F5            		push	af
  2793  CD6A3F        		call	CHKCMM
  2796  C2DC03        		jp	nz,SNERR
  2799  224EFF        		ld	(PROGAD),hl
  279C  F1            		pop	af
  279D  C9            		ret
                      	
                      	
                      	;INKEY$
  279E                	F_INKY:
  279E  218318        		ld	hl,FNCRTN
  27A1  E5            		push	hl
                      	
  27A2  CDBC0F        		call	GETCH
  27A5  282A          		jr	z,INKYZ
                      	
  27A7  FE03          		cp	03h
  27A9  2826          		jr	z,INKYZ
  27AB  FE1B          		cp	1bh
  27AD  2822          		jr	z,INKYZ
                      	
  27AF  47            		ld	b,a		;
  27B0                	INKYNZ:
  27B0  3E01          		ld	a,01h		;new string length=1
  27B2  3230FF        		ld	(STRDSC1+3),a	;old string descriptor address < (STRAD)
  27B5  CD6626        		call	GCCHECK
  27B8  223DFF        		ld	(STRAD),hl
  27BB  23            		inc	hl
  27BC  70            		ld	(hl),b		;
  27BD  222FFF        		ld	(STRDSC1+2),hl	;new string descriptor address
  27C0  3E01          		ld	a,01h
  27C2                	INKYLEN:
  27C2  322DFF        		ld	(STRDSC1),a
                      	
  27C5                	INKYEND:
  27C5  212DFF        		ld	hl,STRDSC1
  27C8  2267FF        		ld	(FAC1+1),hl
  27CB  3E01          		ld	a,01h
  27CD  3225FF        		ld	(ARGTYP),a
  27D0  C9            		ret
                      	
  27D1                	INKYZ:
  27D1  AF            		xor	a
  27D2  18EE          		jr	INKYLEN
                      	
                      	
                      	;convert string to 2-byte integer (unsigned)
                      	;input: hl=program address
                      	;output: de=integer, hl=next address
                      	;destroy: af,bc
  27D4                	ATOI2:
  27D4  0600          		ld	b,00h
                      	
                      	;input: hl=program address, b=string length
  27D6                	ATOI2LEN:
  27D6  110000        		ld	de,0000h
  27D9  2B            		dec	hl
  27DA                	ATOI2LP:
  27DA  23            		inc	hl
  27DB  7E            		ld	a,(hl)
  27DC  FE20          		cp	' '
  27DE  28FA          		jr	z,ATOI2LP
  27E0  D630          		sub	'0'
  27E2  FE0A          		cp	'9'-'0'+1
  27E4  D0            		ret	nc
                      	
  27E5  E5            		push	hl
                      	
  27E6  2166E6        		ld	hl,0-6554
  27E9  19            		add	hl,de
  27EA  3813          		jr	c,ATOI2END
                      	
  27EC  62            		ld	h,d
  27ED  6B            		ld	l,e
  27EE  29            		add	hl,hl
  27EF  29            		add	hl,hl
  27F0  19            		add	hl,de
  27F1  29            		add	hl,hl
                      	
  27F2  85            		add	a,l
  27F3  6F            		ld	l,a
  27F4  3003          		jr	nc,ATOI2NC
  27F6  24            		inc	h
  27F7  2806          		jr	z,ATOI2END
                      	
  27F9                	ATOI2NC:
  27F9  EB            		ex	de,hl
  27FA  E1            		pop	hl
  27FB  10DD          		djnz	ATOI2LP
  27FD  23            		inc	hl
  27FE  C9            		ret
                      	
  27FF                	ATOI2END:
  27FF  E1            		pop	hl
  2800  C9            		ret
                      	
                      	
                      	;convert string to float (integer part)
                      	;input: b=length, hl=program address
                      	;output: FAC1, hl=next address, b=remained length, d=sign(0=plus, 1=minus)
                      	;destroy: FAC2, af,de
  2801                	ATOIF:
  2801  78            		ld	a,b		;
  2802  E5            		push	hl
  2803  CD2A0C        		call	SETZERO
  2806  E1            		pop	hl
  2807  47            		ld	b,a		;
  2808  1600          		ld	d,00h		;sign
                      	
  280A                	ATOIFLP1:
  280A  CD893A        		call	SKIPSPB
  280D  C8            		ret	z		;all space
                      	
  280E  05            		dec	b
  280F  FE2B          		cp	'+'
  2811  2810          		jr	z,ATOIFLP2
  2813  FECA          		cp	PLUS_
  2815  280C          		jr	z,ATOIFLP2
  2817  14            		inc	d
  2818  FE2D          		cp	'-'
  281A  2807          		jr	z,ATOIFLP2
  281C  FECB          		cp	MINUS_
  281E  2803          		jr	z,ATOIFLP2
  2820  04            		inc	b
  2821  15            		dec	d
  2822  2B            		dec	hl
  2823                	ATOIFLP2:
  2823  CD2A28        		call	CTOF
  2826  D0            		ret	nc
  2827  10FA          		djnz	ATOIFLP2
  2829  C9            		ret
                      	
                      	
                      	;FAC1 = FAC1 * 10 + [0-9]
                      	;input: FAC1, hl=program address, b=length(>0), d=sign(0=plus, 1=minus)
                      	;output: FAC1, c-flag(1=OK), b=remained length(for space), hl=next address
                      	;destroy: FAC2,af
  282A                	CTOF:
  282A  CD893A        		call	SKIPSPB
  282D  2B            		dec	hl
  282E  C8            		ret	z
                      	
  282F  D630          		sub	'0'
  2831  FE0A          		cp	'9'-'0'+1
  2833  D0            		ret	nc
  2834  23            		inc	hl
  2835  E5            		push	hl		;program address
                      	
  2836  D5            		push	de
  2837  C5            		push	bc
  2838  F5            		push	af
  2839  CD1939        		call	ABS
  283C  CD9037        		call	MULF10
  283F  CD5B39        		call	CPYFAC
  2842  F1            		pop	af
  2843  CD650C        		call	I1TOFA
  2846  CD8F36        		call	ADDF
  2849  C1            		pop	bc
  284A  D1            		pop	de
                      	
  284B  7A            		ld	a,d
  284C  0F            		rrca
  284D  DC280D        		call	c,NEGABS
                      	
  2850  E1            		pop	hl		;program address
  2851  37            		scf
  2852  C9            		ret
                      	
                      	
                      	;insert a character
                      	;input: a=data (for checking BELL)
                      	;output: hl=VRAM address
                      	;destroy: f,bc,de
  2853                	INSERT:
  2853  FE07          		cp	07h
  2855  2005          		jr	nz,INSNZ
  2857  CD7510        		call	PRTC
  285A  3E20          		ld	a,' '
  285C                	INSNZ:
  285C  F5            		push	af
                      	
  285D  2AAAFD        		ld	hl,(CSRAD)
                      	
  2860  CDD328        		call	GETSCRC
  2863  47            		ld	b,a		;
                      	
  2864  23            		inc	hl
  2865  CD4B15        		call	CHKLINE
  2868  201B          		jr	nz,INSSCRL
                      	
  286A  CDF515        		call	CNVATT1
  286D  4F            		ld	c,a
                      	
  286E                	INSLP:
  286E  CDD328        		call	GETSCRC
  2871  25            		dec	h
  2872  25            		dec	h
  2873  71            		ld	(hl),c
  2874  24            		inc	h
  2875  24            		inc	h
                      	
  2876  70            		ld	(hl),b
  2877  47            		ld	b,a
  2878  23            		inc	hl
  2879  CD4B15        		call	CHKLINE
  287C  28F0          		jr	z,INSLP
                      	
  287E  78            		ld	a,b
  287F  FE20          		cp	' '
  2881  2002          		jr	nz,INSSCRL
  2883  F1            		pop	af
  2884  C9            		ret
                      	
                      	
  2885                	INSSCRL:
  2885  E5            		push	hl
                      	
  2886  CD3313        		call	AD2XY
                      	
  2889  3AA5FD        		ld	a,(LASTLIN)
  288C  BD            		cp	l
  288D  381F          		jr	c,INSSCRLU
                      	
  288F  67            		ld	h,a		;(LASTLIN)
  2890  2835          		jr	z,NOSCRLD
  2892  CDA912        		call	SCRLDW
                      	
  2895                	INSSCRLEND:
  2895  4D            		ld	c,l		;;
  2896  2D            		dec	l
  2897  AF            		xor	a
                      	
  2898                	INSSCRLEND2:
  2898  CD6715        		call	SETLINE
  289B  69            		ld	l,c		;;
  289C  CDB811        		call	Y2AD
  289F  CDDA11        		call	DELLIN
  28A2  E1            		pop	hl
                      	
  28A3  25            		dec	h
  28A4  25            		dec	h
  28A5  CDF515        		call	CNVATT1
  28A8  77            		ld	(hl),a
  28A9  24            		inc	h
  28AA  24            		inc	h
                      	
  28AB  70            		ld	(hl),b		;
  28AC  F1            		pop	af
  28AD  C9            		ret
                      	
  28AE                	INSSCRLU:
  28AE  E1            		pop	hl
  28AF  11E0FF        		ld	de,0-COLUMNS
  28B2  19            		add	hl,de
  28B3  E5            		push	hl
                      	
  28B4  6F            		ld	l,a		;(LASTLIN)
  28B5  3E1E          		ld	a,1eh		;up
  28B7  CD7510        		call	PRTC
                      	
  28BA  3AA2FD        		ld	a,(CONSOL1)
  28BD  67            		ld	h,a
  28BE  95            		sub	l
  28BF  4D            		ld	c,l
  28C0  280B          		jr	z,NOSCRLU	;a=0
                      	
  28C2  CD6012        		call	SCRLUP
  28C5  18CE          		jr	INSSCRLEND
                      	
  28C7                	NOSCRLD:
  28C7  AF            		xor	a
  28C8  32A4FE        		ld	(INPTXY),a	;scroll out
  28CB  18C8          		jr	INSSCRLEND
                      	
  28CD                	NOSCRLU:
                      	;	xor	a
  28CD  32A4FE        		ld	(INPTXY),a	;scroll out
  28D0  3C            		inc	a
  28D1  18C5          		jr	INSSCRLEND2
                      	
                      	
                      	;get character from screen
                      	;input: hl
                      	;output: a
                      	;destroy: f
  28D3                	GETSCRC:
  28D3  7E            		ld	a,(hl)
  28D4  25            		dec	h
  28D5  25            		dec	h
  28D6  CB76          		bit	6,(hl)
  28D8  2802          		jr	z,GETSCRCZ
  28DA  3E20          		ld	a,' '
  28DC                	GETSCRCZ:
  28DC  24            		inc	h
  28DD  24            		inc	h
  28DE  C9            		ret
                      	
                      	
                      	;one line input
                      	;output: hl=INPBUF-1, c-flag(1=stop)
                      	;destroy: af,bc,de
  28DF                	_INPT1:	ds	INPT1-_INPT1
  28F9                		org	INPT1
                      	
  28F9  2AA8FD        		ld	hl,(CSRY)
  28FC  22A5FE        		ld	(INPTX-1),hl	;h=end position for INPUT command
  28FF  22A4FE        		ld	(INPTXY),hl	;initial position for INPUT command
  2902  3AABFD        		ld	a,(CSRAD+1)
  2905  32A7FE        		ld	(INPTPAG),a	;page for INPUT command
                      	
  2908                	INPT1LP:
  2908  CDC40F        		call	GETC
  290B  CD1029        		call	INPT1CMD
  290E  18F8          		jr	INPT1LP
                      	
                      	
                      	;one line input main routine
  2910                	INPT1CMD:
  2910  FE20          		cp	20h
  2912  3832          		jr	c,INPT1CTL
                      	
  2914  2AA8FE        		ld	hl,(INSMODE)
  2917  2C            		inc	l
  2918  2D            		dec	l
  2919  C45328        		call	nz,INSERT
                      	
  291C                	INPT1PRTC:
  291C  CD7510        		call	PRTC
                      	
  291F                	CHKIPOS2:
  291F  2AAAFD        		ld	hl,(CSRAD)
                      	
                      	;check cursor position for input command
                      	;input: hl=cursor address
                      	;output: hl=cursor XY, de=cursor address
                      	;destroy: af
  2922                	CHKIPOS:
  2922  54            		ld	d,h
  2923  5D            		ld	e,l
  2924  CD3313        		call	AD2XY
  2927  3AA4FE        		ld	a,(INPTXY)	;y+1
  292A  BD            		cp	l
  292B  C0            		ret	nz
                      	
  292C  3AA6FE        		ld	a,(INPTX)
  292F  BC            		cp	h
  2930  3004          		jr	nc,IPOSNC
  2932  7C            		ld	a,h
  2933  32A6FE        		ld	(INPTX),a
  2936                	IPOSNC:
  2936  3AA5FE        		ld	a,(INPTXY+1)	;x+1
  2939  BC            		cp	h
  293A  D8            		ret	c
                      	
  293B  3AA7FE        		ld	a,(INPTPAG)
  293E  AA            		xor	d
  293F  E6F0          		and	0f0h
  2941  C0            		ret	nz
                      	
  2942  22A4FE        		ld	(INPTXY),hl
  2945  C9            		ret
                      	
                      	
                      	;control code
  2946                	INPT1CTL:
  2946  21182B        		ld	hl,NOINSTBL
  2949  010C00        		ld	bc,000ch
  294C  EDB1          		cpir
  294E  2004          		jr	nz,INPT1CMDNZ
  2950  21A8FE        		ld	hl,INSMODE
  2953  70            		ld	(hl),b		;b=0
  2954                	INPT1CMDNZ:
  2954  2AAAFD        		ld	hl,(CSRAD)
                      	
  2957  FE02          		cp	02h
  2959  CADC29        		jp	z,INPT1CTLB
  295C  FE03          		cp	03h
  295E  CA6E2A        		jp	z,INPT1STOP
  2961  FE05          		cp	05h
  2963  2866          		jr	z,INPT1CTLE
  2965  FE06          		cp	06h
  2967  CAFE29        		jp	z,INPT1CTLF
  296A  FE08          		cp	08h
  296C  2824          		jr	z,INPT1DEL
  296E  FE09          		cp	09h
  2970  CA502A        		jp	z,INPT1TAB
  2973  FE0A          		cp	0ah
  2975  CA292A        		jp	z,INPT1CTLJ
  2978  FE0D          		cp	0dh
  297A  CA902A        		jp	z,INPT1RET
  297D  FE12          		cp	12h
  297F  280A          		jr	z,INPT1INS
  2981  FE14          		cp	14h
  2983  2897          		jr	z,INPT1PRTC
  2985  FE15          		cp	15h
  2987  283D          		jr	z,INPT1CTLU
  2989  1891          		jr	INPT1PRTC	;1ch,1dh,1eh,1fh
                      	
                      	
  298B                	INPT1INS:
  298B  21A8FE        		ld	hl,INSMODE
  298E  7E            		ld	a,(hl)
  298F  2F            		cpl
  2990  77            		ld	(hl),a
  2991  C9            		ret
                      	
                      	
  2992                	INPT1DEL:
  2992  CD4B15        		call	CHKLINE
  2995  E5            		push	hl		;
  2996  21C129        		ld	hl,DELSTR
  2999  2801          		jr	z,INPT1DELZ
  299B  23            		inc	hl
  299C                	INPT1DELZ:
  299C  CDCF30        		call	PUTS
  299F  CD1F29        		call	CHKIPOS2
  29A2  E1            		pop	hl		;
  29A3  54            		ld	d,h
  29A4  5D            		ld	e,l
  29A5  1B            		dec	de
  29A6  CDF515        		call	CNVATT1
  29A9  4F            		ld	c,a
  29AA                	DELLP:
  29AA  CDD328        		call	GETSCRC
  29AD  12            		ld	(de),a
                      	
  29AE  25            		dec	h
  29AF  25            		dec	h
  29B0  71            		ld	(hl),c
  29B1  24            		inc	h
  29B2  24            		inc	h
                      	
  29B3  13            		inc	de
  29B4  23            		inc	hl
                      	
  29B5  CD4B15        		call	CHKLINE
  29B8  28F0          		jr	z,DELLP
                      	
  29BA  2B            		dec	hl
  29BB  3620          		ld	(hl),' '
  29BD  25            		dec	h
  29BE  25            		dec	h
  29BF  71            		ld	(hl),c
                      	
  29C0  C9            		ret
                      	
  29C1                	DELSTR:
  29C1  1D201D00      		db	1dh, ' ', 1dh, 00h
                      	
                      	
  29C5                	CTLULP:
  29C5  2B            		dec	hl
  29C6                	INPT1CTLU:
  29C6  CD4B15        		call	CHKLINE
  29C9  28FA          		jr	z,CTLULP
                      	
  29CB                	INPT1CTLE:
  29CB  25            		dec	h
  29CC  25            		dec	h
  29CD  CDF515        		call	CNVATT1
  29D0  77            		ld	(hl),a
  29D1  24            		inc	h
  29D2  24            		inc	h
  29D3  3620          		ld	(hl),' '
  29D5  23            		inc	hl
  29D6  CD4B15        		call	CHKLINE
  29D9  28F0          		jr	z,INPT1CTLE
  29DB  C9            		ret
                      	
                      	
  29DC                	INPT1CTLB:
  29DC  CDED29        		call	CHKCTLB
  29DF  30FB          		jr	nc,INPT1CTLB
  29E1                	CTLBLP:
  29E1  CDED29        		call	CHKCTLB
  29E4  38FB          		jr	c,CTLBLP
  29E6  23            		inc	hl
  29E7  CD2229        		call	CHKIPOS
  29EA  C36D11        		jp	SETCSR
                      	
  29ED                	CHKCTLB:
  29ED  2B            		dec	hl
  29EE  54            		ld	d,h
  29EF  5D            		ld	e,l
  29F0  CD3313        		call	AD2XY
  29F3  3AA2FD        		ld	a,(CONSOL1)
  29F6  2C            		inc	l
  29F7  BD            		cp	l
  29F8  3821          		jr	c,CTLBC
  29FA                	CTLBNC:
  29FA  F1            		pop	af		;cancel return address
  29FB  C34C11        		jp	CTLCR
                      	
                      	
  29FE                	INPT1CTLF:
  29FE  CD0F2A        		call	CHKCTLF
  2A01  38FB          		jr	c,INPT1CTLF
  2A03                	CTLFLP:
  2A03  CD0F2A        		call	CHKCTLF
  2A06  30FB          		jr	nc,CTLFLP
  2A08  EB            		ex	de,hl
  2A09  CD6D11        		call	SETCSR
  2A0C  C31F29        		jp	CHKIPOS2
                      	
  2A0F                	CHKCTLF:
  2A0F  23            		inc	hl
  2A10  54            		ld	d,h
  2A11  5D            		ld	e,l
  2A12  CD3313        		call	AD2XY
  2A15  3AA5FD        		ld	a,(LASTLIN)
  2A18  BD            		cp	l
  2A19  3807          		jr	c,CTLFC
  2A1B                	CTLBC:
  2A1B  EB            		ex	de,hl
  2A1C  CDD328        		call	GETSCRC
  2A1F  C3AF0B        		jp	ALPNUM2
                      	
  2A22                	CTLFC:
  2A22  6F            		ld	l,a
  2A23  2620          		ld	h,COLUMNS
  2A25  F1            		pop	af		;cancel return address
  2A26  C36D11        		jp	SETCSR
                      	
                      	
  2A29                	INPT1CTLJ:
  2A29  3AA8FE        		ld	a,(INSMODE)
  2A2C  B7            		or	a
  2A2D  3E0A          		ld	a,0ah
  2A2F  CA1C29        		jp	z,INPT1PRTC
                      	
  2A32  2AA8FD        		ld	hl,(CSRY)	;l=y+1, h=x+1
  2A35  3E21          		ld	a,COLUMNS+1
  2A37  94            		sub	h
  2A38  47            		ld	b,a
  2A39                	CTLJLP:
  2A39  C5            		push	bc
  2A3A  3E20          		ld	a,' '
  2A3C  CD5328        		call	INSERT
  2A3F  2AAAFD        		ld	hl,(CSRAD)
  2A42  77            		ld	(hl),a
  2A43  C1            		pop	bc
  2A44  10F3          		djnz	CTLJLP
                      	
  2A46  CD3313        		call	AD2XY
  2A49  CD6D11        		call	SETCSR
  2A4C  7C            		ld	a,h		;>0
  2A4D  C36715        		jp	SETLINE
                      	
                      	
  2A50                	INPT1TAB:
  2A50  3AA9FD        		ld	a,(CSRX)
  2A53  D619          		sub	COLUMNS-07h
  2A55  DE07          		sbc	a,07h
  2A57  C8            		ret	z
  2A58  2F            		cpl
  2A59  E607          		and	07h
  2A5B  3C            		inc	a
  2A5C  47            		ld	b,a
  2A5D  3AA8FE        		ld	a,(INSMODE)
  2A60  E604          		and	04h
  2A62  C61C          		add	a,1ch		;1ch(right) or 20h(space)
  2A64  4F            		ld	c,a
  2A65                	INPT1TABLP:
  2A65  C5            		push	bc
  2A66  79            		ld	a,c
  2A67  CD1029        		call	INPT1CMD
  2A6A  C1            		pop	bc
  2A6B  10F8          		djnz	INPT1TABLP
  2A6D  C9            		ret
                      	
                      	
  2A6E                	INPT1STOP:
  2A6E  E1            		pop	hl		;cancel return address
  2A6F  2AA8FD        		ld	hl,(CSRY)
  2A72  AF            		xor	a
  2A73  67            		ld	h,a
  2A74  11B5FD        		ld	de,LINEST-2
  2A77  19            		add	hl,de
  2A78                	STOPLP:
  2A78  23            		inc	hl
  2A79  B6            		or	(hl)
  2A7A  28FC          		jr	z,STOPLP
                      	
                      	;	or	a
  2A7C  ED52          		sbc	hl,de
  2A7E  2D            		dec	l
  2A7F  CD4C11        		call	CTLCR
  2A82  CD3927        		call	PUTNL
                      	
  2A85  21DAFE        		ld	hl,INPBUF
  2A88  AF            		xor	a
  2A89  3218FA        		ld	(STOPFLG),a
  2A8C  77            		ld	(hl),a
  2A8D  2B            		dec	hl
  2A8E  37            		scf
  2A8F  C9            		ret
                      	
                      	
  2A90                	INPT1RET:
  2A90  F1            		pop	af		;cancel return address
                      	
  2A91  3AABFD        		ld	a,(CSRAD+1)
  2A94  57            		ld	d,a
  2A95  3AA7FE        		ld	a,(INPTPAG)
  2A98  AA            		xor	d		;
  2A99  E6F0          		and	0f0h
  2A9B  201A          		jr	nz,IRETNZ1
                      	
  2A9D  3AA8FD        		ld	a,(CSRY)
  2AA0  ED5BA4FE      		ld	de,(INPTXY)
  2AA4  BB            		cp	e
  2AA5  2010          		jr	nz,IRETNZ1
                      	
  2AA7  01B6FD        		ld	bc,LINEST-1
  2AAA  79            		ld	a,c
  2AAB  83            		add	a,e		;no carry
  2AAC  4F            		ld	c,a
  2AAD  0A            		ld	a,(bc)
  2AAE  B7            		or	a
  2AAF  2806          		jr	z,IRETNZ1
                      	
  2AB1  3AA6FE        		ld	a,(INPTX)
  2AB4  92            		sub	d
  2AB5  1802          		jr	SETINPTX
  2AB7                	IRETNZ1:
  2AB7  3E47          		ld	a,71
  2AB9                	SETINPTX:
  2AB9  32A6FE        		ld	(INPTX),a
                      	
  2ABC                	IRETLP1:
  2ABC  CD4B15        		call	CHKLINE
  2ABF  2B            		dec	hl
  2AC0  28FA          		jr	z,IRETLP1
  2AC2  23            		inc	hl
  2AC3  54            		ld	d,h		;
  2AC4  CD3313        		call	AD2XY
  2AC7  3AA7FE        		ld	a,(INPTPAG)
  2ACA  AA            		xor	d		;
  2ACB  E6F0          		and	0f0h
  2ACD  2009          		jr	nz,IRETNZ2
                      	
  2ACF  ED5BA4FE      		ld	de,(INPTXY)	;initial position
  2AD3  7D            		ld	a,l
  2AD4  BB            		cp	e
  2AD5  2001          		jr	nz,IRETNZ2
  2AD7  EB            		ex	de,hl
                      	
  2AD8                	IRETNZ2:
  2AD8  CDCD11        		call	XY2AD
                      	
  2ADB                	IRETLP2:
  2ADB  CDD328        		call	GETSCRC
  2ADE  11DAFE        		ld	de,INPBUF
  2AE1  0647          		ld	b,71
  2AE3                	IRETLP3:
  2AE3  CDD328        		call	GETSCRC
  2AE6  FE20          		cp	20h
  2AE8  300D          		jr	nc,IRETNC
  2AEA  EB            		ex	de,hl
  2AEB  3614          		ld	(hl),14h
  2AED  EB            		ex	de,hl
                      	;	dec	b
                      	;	jr	nz,IRETGRP
  2AEE  1004          		djnz	IRETGRP
  2AF0  AF            		xor	a
  2AF1  12            		ld	(de),a
  2AF2  180D          		jr	IRETNZ3
  2AF4                	IRETGRP:
  2AF4  13            		inc	de
  2AF5  C630          		add	a,30h
                      	
  2AF7                	IRETNC:
  2AF7  12            		ld	(de),a
  2AF8  13            		inc	de
  2AF9  23            		inc	hl
  2AFA  CD4B15        		call	CHKLINE
  2AFD  2002          		jr	nz,IRETNZ3
  2AFF  10E2          		djnz	IRETLP3
                      	
  2B01                	IRETNZ3:
  2B01  2B            		dec	hl
  2B02  CD3313        		call	AD2XY
  2B05  CD6D11        		call	SETCSR
                      	
  2B08                	IRETLP4:
  2B08  1B            		dec	de
  2B09  1A            		ld	a,(de)
  2B0A  FE20          		cp	' '
  2B0C  28FA          		jr	z,IRETLP4
  2B0E  13            		inc	de
  2B0F  AF            		xor	a
  2B10  12            		ld	(de),a
                      	
  2B11  CD3927        		call	PUTNL
                      	
  2B14  21D9FE        		ld	hl,INPBUF-1
  2B17  C9            		ret
                      	
  2B18                	NOINSTBL:
  2B18  020305060B0C0D		db	02h, 03h, 05h, 06h, 0bh, 0ch, 0dh, 15h, 1ch, 1dh, 1eh, 1fh
                      	
                      	
                      	;x=x+-(8 or 4 or 2 or 1)
                      	;input: b(bit7=0:increment, bit7=1:decrement), (GRPX3)
                      	;output: hl,(GRPX3)=X
                      	;destroy: af,de
  2B24                	INCGX:
  2B24  3A92FD        		ld	a,(SCREEN1)
  2B27  3C            		inc	a
  2B28  57            		ld	d,a
  2B29  3E10          		ld	a,10h
  2B2B                	INCGXLP:
  2B2B  0F            		rrca
  2B2C  15            		dec	d
  2B2D  20FC          		jr	nz,INCGXLP
                      	
  2B2F  2AB1FE        		ld	hl,(GRPX3)
  2B32  5F            		ld	e,a
  2B33  CB78          		bit	7,b
  2B35  2003          		jr	nz,INCGXNZ
                      	
  2B37  19            		add	hl,de		;de=8,4,2,1
  2B38  1802          		jr	INCGXEND
                      	
  2B3A                	INCGXNZ:
                      	;	or	a
  2B3A  ED52          		sbc	hl,de		;de=8,4,2,1
                      	
  2B3C                	INCGXEND:
  2B3C  22B1FE        		ld	(GRPX3),hl
  2B3F  C9            		ret
                      	
                      	
                      	;y=y+(12 or 4 or 1 or 1)
                      	;input: (GRPY3)
                      	;output: de=(GRPY3)
                      	;destroy: af
  2B40                	INCGY:
  2B40  3A92FD        		ld	a,(SCREEN1)
  2B43  3D            		dec	a
  2B44  3E04          		ld	a,04h		;screen mode 2
  2B46  2807          		jr	z,INCGYZ
  2B48  3E01          		ld	a,01h		;screen mode 3,4
  2B4A  F24F2B        		jp	p,INCGYZ
  2B4D  3E0C          		ld	a,0ch		;screen mode 1
  2B4F                	INCGYZ:
  2B4F  ED5BB3FE      		ld	de,(GRPY3)
  2B53  83            		add	a,e
  2B54  5F            		ld	e,a
  2B55  32B3FE        		ld	(GRPY3),a	;<256
  2B58  C9            		ret
                      	
                      	
                      	;error-=|dx|*2
                      	;input: bc=dx, hl=error
                      	;output: hl=error
                      	;destroy: af
  2B59                	SUB2DX:
  2B59  78            		ld	a,b
  2B5A  07            		rlca
  2B5B  3805          		jr	c,SUB2DXNZ
                      	;dx>0
                      	;	or	a
  2B5D  ED42          		sbc	hl,bc
  2B5F  ED42          		sbc	hl,bc
  2B61  C9            		ret
                      	
                      	;dx<0
  2B62                	SUB2DXNZ:
  2B62  09            		add	hl,bc
  2B63  09            		add	hl,bc
  2B64  C9            		ret
                      	
                      	
                      	;x1 <-> x2, y1 <-> y2
                      	;input: bc=x1, de=y1, (GRPX2)=x2, (GRPY2)=y2
                      	;output: (GRPX2)=larger x,(GRPY2)=larger y,(GRPX3)=smaller x,(GRPY3)=smaller y
                      	;destroy: bc,de,hl
  2B65                	SORTXY:
  2B65  F5            		push	af
  2B66  CDB52B        		call	CHKGXY23
  2B69  3807          		jr	c,SORTXOK
  2B6B  22AFFE        		ld	(GRPY2),hl
  2B6E  ED53B3FE      		ld	(GRPY3),de
  2B72                	SORTXOK:
  2B72  2AADFE        		ld	hl,(GRPX2)
  2B75  ED5BB1FE      		ld	de,(GRPX3)
  2B79  E7            		rst	CPHLDE
  2B7A  3816          		jr	c,SORTX
  2B7C  F1            		pop	af
  2B7D  C9            		ret
                      	
                      	
                      	;(x1,y1) <-> (x2,y2)
                      	;input: bc=x1, de=y1, (GRPX2)=x2, (GRPY2)=y2
                      	;output: (GRPX2),(GRPY2)=larger y with x, (GRPX3),(GRPY3)=small y with x
                      	;destroy: bc,de,hl
  2B7E                	SORTY:
  2B7E  F5            		push	af
  2B7F  CDB52B        		call	CHKGXY23
  2B82  3815          		jr	c,NOSORT
  2B84  22AFFE        		ld	(GRPY2),hl
  2B87  ED53B3FE      		ld	(GRPY3),de
  2B8B  2AADFE        		ld	hl,(GRPX2)
  2B8E  ED5BB1FE      		ld	de,(GRPX3)
  2B92                	SORTX:
  2B92  ED53ADFE      		ld	(GRPX2),de
  2B96  22B1FE        		ld	(GRPX3),hl
  2B99                	NOSORT:
  2B99  F1            		pop	af
  2B9A  C9            		ret
                      	
                      	
                      	;sort XY and put bc,de,hl
                      	;output: bc=smaller x, de=smaller y, hl=larger y
                      	;destroy: none
  2B9B                	SORTXY2:
  2B9B  CD652B        		call	SORTXY
  2B9E  ED4BB1FE      		ld	bc,(GRPX3)	;smaller x, b=0
  2BA2  ED5BB3FE      		ld	de,(GRPY3)	;smaller y
  2BA6  2AAFFE        		ld	hl,(GRPY2)	;larger y
  2BA9  C9            		ret
                      	
                      	
                      	;push (GRPX2),(GRPY2)
                      	;destroy: af,hl
  2BAA                	PUSHGXY:
  2BAA  F1            		pop	af		;return address
  2BAB  2AADFE        		ld	hl,(GRPX2)
  2BAE  E5            		push	hl
  2BAF  2AAFFE        		ld	hl,(GRPY2)
  2BB2  E5            		push	hl
  2BB3  F5            		push	af		;return address
  2BB4  C9            		ret
                      	
                      	
                      	;input: bc=x1, de=y1, (GRPX2)=x2, (GRPY2)=y2
                      	;output: bc=(GRPX2)=x2, de=(GRPY2)=y2, hl=(GRPY3)=y1, (GRPX3)=x1, z,c(y1>y2?)
                      	;destroy: af
  2BB5                	CHKGXY23:
  2BB5  CDD82B        		call	CHKGXY
  2BB8  ED43B1FE      		ld	(GRPX3),bc
  2BBC  ED53B3FE      		ld	(GRPY3),de
  2BC0  ED4BADFE      		ld	bc,(GRPX2)
  2BC4  ED5BAFFE      		ld	de,(GRPY2)
  2BC8  CDD82B        		call	CHKGXY
  2BCB  ED43ADFE      		ld	(GRPX2),bc
  2BCF  ED53AFFE      		ld	(GRPY2),de
                      	
  2BD3  2AB3FE        		ld	hl,(GRPY3)
  2BD6  E7            		rst	CPHLDE
  2BD7  C9            		ret
                      	
                      	
                      	;check and round graphic coordinates (x,y)
                      	;input: bc=x, de=y
                      	;output: bc=x, de=y
                      	;destroy: af,hl
  2BD8                	CHKGXY:
  2BD8  78            		ld	a,b
  2BD9  B7            		or	a
  2BDA  2802          		jr	z,CHKMNS
  2BDC  0EFF          		ld	c,0ffh
  2BDE                	CHKMNS:
  2BDE  B2            		or	d
  2BDF  FAE503        		jp	m,FCERR
                      	
  2BE2  3A92FD        		ld	a,(SCREEN1)
  2BE5  47            		ld	b,a		;
  2BE6  0F            		rrca
  2BE7  2F            		cpl
  2BE8  21A6FD        		ld	hl,CONSOL3
  2BEB  A6            		and	(hl)
  2BEC  0F            		rrca
  2BED  21BF00        		ld	hl,191
  2BF0  3003          		jr	nc,NOFKEY
  2BF2  21B300        		ld	hl,191-12
  2BF5                	NOFKEY:
  2BF5  E7            		rst	CPHLDE
  2BF6  3001          		jr	nc,CHKYOK
  2BF8  EB            		ex	de,hl
  2BF9                	CHKYOK:
  2BF9  78            		ld	a,b		;
  2BFA  3D            		dec	a
  2BFB  280D          		jr	z,ROUNDY2
  2BFD  F20E2C        		jp	p,ROUNDX	;screen mode 3,4
                      	
                      	;round off y for screen mode 1
  2C00                	ROUNDY1:
  2C00  CDB23F        		call	DIV12
  2C03  7B            		ld	a,e
  2C04  87            		add	a,a		;*2
  2C05  83            		add	a,e		;*3
  2C06  87            		add	a,a		;*6
  2C07  87            		add	a,a		;*12
  2C08  1803          		jr	ROUNDY2END
                      	
                      	;round off y for screen mode 2
  2C0A                	ROUNDY2:
  2C0A  7B            		ld	a,e
  2C0B  E6FC          		and	0fch
  2C0D                	ROUNDY2END:
  2C0D  5F            		ld	e,a
                      	
  2C0E                	ROUNDX:
  2C0E  04            		inc	b		;
  2C0F  3EF0          		ld	a,0f0h
  2C11                	ROUNDXLP:
  2C11  37            		scf
  2C12  1F            		rra			;f8,fc,fe,ff
  2C13  10FC          		djnz	ROUNDXLP
                      	
  2C15  A1            		and	c
  2C16  4F            		ld	c,a		;b=0
  2C17  C9            		ret
                      	
                      	
                      	;screen mode 2
  2C18                	PSET2:
  2C18  C5            		push	bc		;X
  2C19  CD863F        		call	GXY2AD
                      	
  2C1C  3AACFE        		ld	a,(ATTDAT)
  2C1F  5E            		ld	e,(hl)		;
  2C20  B7            		or	a
  2C21  2004          		jr	nz,PS2NZ1
  2C23  CB73          		bit	6,e
  2C25  2004          		jr	nz,PS2NZ2
  2C27                	PS2NZ1:
  2C27  CD3F15        		call	GETSPA2NZ
  2C2A  77            		ld	(hl),a
  2C2B                	PS2NZ2:
  2C2B  24            		inc	h
  2C2C  24            		inc	h
  2C2D  CB73          		bit	6,e		;
  2C2F  2004          		jr	nz,PS2SEMI	;semi-graphic mode?
  2C31  CD0D16        		call	GETSP
  2C34  77            		ld	(hl),a
                      	
  2C35                	PS2SEMI:
  2C35  C1            		pop	bc		;X
  2C36  CD542C        		call	MASK2
  2C39  47            		ld	b,a
                      	
  2C3A  7E            		ld	a,(hl)
  2C3B  E63F          		and	3fh
  2C3D  B0            		or	b
  2C3E  4F            		ld	c,a
                      	
  2C3F  3AACFE        		ld	a,(ATTDAT)
  2C42  B7            		or	a
  2C43  2809          		jr	z,PSET2C0
  2C45  3D            		dec	a
  2C46  E603          		and	03h
  2C48  0F            		rrca
  2C49  0F            		rrca
  2C4A  81            		add	a,c
  2C4B  77            		ld	(hl),a
  2C4C  E1            		pop	hl
  2C4D  C9            		ret
                      	
  2C4E                	PSET2C0:
  2C4E  78            		ld	a,b
  2C4F  2F            		cpl
  2C50  A6            		and	(hl)
  2C51  77            		ld	(hl),a
  2C52  E1            		pop	hl
  2C53  C9            		ret
                      	
                      	
                      	;screen mode 2 dot bit mask
                      	;input: c=X, d=Y mod 12
                      	;output: a
                      	; bit5 bit4
                      	; bit3 bit2
                      	; bit1 bit0
                      	;destroy: f,bc
  2C54                	MASK2:
  2C54  79            		ld	a,c
  2C55  2F            		cpl
  2C56  E604          		and	04h
  2C58  C604          		add	a,04h
  2C5A  CB52          		bit	2,d
  2C5C  C0            		ret	nz	;4<=d<=7
  2C5D  CB5A          		bit	3,d
  2C5F  2003          		jr	nz,MASK2NZ
  2C61  07            		rlca
  2C62  07            		rlca
  2C63  C9            		ret		;0<=d<=3
  2C64                	MASK2NZ:
  2C64  0F            		rrca
  2C65  0F            		rrca
  2C66  C9            		ret		;8<=d<=11
                      	
                      	
                      	;continued from BFG
  2C67                	BFG2:
  2C67  3AB1FE        		ld	a,(GRPX3)	;smaller x
  2C6A  E6F8          		and	0f8h
  2C6C  47            		ld	b,a
  2C6D  3AADFE        		ld	a,(GRPX2)	;larger x
  2C70  E6F8          		and	0f8h
  2C72  90            		sub	b		;
  2C73  0F            		rrca
  2C74  0F            		rrca
  2C75  0F            		rrca			;int(larger/8)-int(smaller/8)
  2C76  47            		ld	b,a		;bytes
  2C77  2003          		jr	nz,BFGLP3	;a=0?
  2C79  7A            		ld	a,d
  2C7A  A3            		and	e
  2C7B  57            		ld	d,a
                      	
  2C7C                	BFGLP3:
                      	;left part
  2C7C  C5            		push	bc
  2C7D  3AACFE        		ld	a,(ATTDAT)
  2C80  AE            		xor	(hl)
  2C81  A2            		and	d		;d=left part mask
  2C82  AE            		xor	(hl)
  2C83  77            		ld	(hl),a
                      	
  2C84  05            		dec	b		;
  2C85  FA982C        		jp	m,BFGNEXT
                      	
                      	;middle part
  2C88  E5            		push	hl		;VRAM address
  2C89  3AACFE        		ld	a,(ATTDAT)
  2C8C  2804          		jr	z,BFGRGT	;
  2C8E                	BFGLP4:
  2C8E  23            		inc	hl
  2C8F  77            		ld	(hl),a
  2C90  10FC          		djnz	BFGLP4
                      	
                      	;right part
  2C92                	BFGRGT:
  2C92  23            		inc	hl
  2C93  AE            		xor	(hl)
  2C94  A3            		and	e		;e=right part mask
  2C95  AE            		xor	(hl)
  2C96  77            		ld	(hl),a
  2C97  E1            		pop	hl		;VRAM address
                      	
  2C98                	BFGNEXT:
  2C98  012000        		ld	bc,0020h
  2C9B  09            		add	hl,bc
  2C9C  C1            		pop	bc
  2C9D  0D            		dec	c
  2C9E  20DC          		jr	nz,BFGLP3
  2CA0  C3C42E        		jp	LINEEND2
                      	
                      	
                      	;|dy/dx|<=1
  2CA3                	SLOPEX:
  2CA3  AF            		xor	a
  2CA4  95            		sub	l
  2CA5  6F            		ld	l,a
  2CA6  9C            		sbc	a,h
  2CA7  95            		sub	l
  2CA8  67            		ld	h,a
  2CA9  E5            		push	hl		;error=-|dx|
                      	
  2CAA                	LINELP2:
  2CAA  ED4BB1FE      		ld	bc,(GRPX3)
  2CAE  ED5BB3FE      		ld	de,(GRPY3)
  2CB2  CD472D        		call	PSET
                      	
  2CB5  E1            		pop	hl		;error
  2CB6  C1            		pop	bc		;dx
  2CB7  D1            		pop	de		;dy
  2CB8  D5            		push	de		;dy
  2CB9  C5            		push	bc		;dx
                      	
  2CBA  19            		add	hl,de		;error+=dy
  2CBB  19            		add	hl,de		;error+=dy
  2CBC  CB7C          		bit	7,h
  2CBE  2006          		jr	nz,ERRMNS
                      	
                      	;if error>=0
  2CC0  CD592B        		call	SUB2DX		;error-=|dx|*2
  2CC3  CD402B        		call	INCGY		;increment Y
                      	
  2CC6                	ERRMNS:
  2CC6  E5            		push	hl		;error
  2CC7  CD242B        		call	INCGX		;increment or decrement X
  2CCA  DABF2E        		jp	c,LINEEND
                      	
  2CCD  EB            		ex	de,hl		;de=X
  2CCE  2AADFE        		ld	hl,(GRPX2)
  2CD1  CB78          		bit	7,b
  2CD3  2801          		jr	z,LINEZ
  2CD5  EB            		ex	de,hl		;if dx<0
  2CD6                	LINEZ:
  2CD6  E7            		rst	CPHLDE
  2CD7  30D1          		jr	nc,LINELP2
  2CD9  C3BF2E        		jp	LINEEND
                      	
                      	
                      	;get (step)(x,y) cordinate
                      	;input: hl=program address
                      	;output: bc=graphic X, de=graphic Y,
                      	;	 (GRPX1)=(GRPX2),(GRPY1)=(GRPY2)=0-32767
                      	;	 a=next data, hl=next data address, z(, or not)
                      	;destroy: f
  2CDC                	GETGXY:
  2CDC  CD583F        		call	SKIPSP
  2CDF  FEC9          		cp	STEP_
  2CE1  F5            		push	af		;STEP?
  2CE2  2001          		jr	nz,GETGXYNZ
  2CE4  23            		inc	hl
  2CE5                	GETGXYNZ:
  2CE5  CD950B        		call	CHKLPAR
  2CE8  CD230E        		call	INTARG2		;bc,de
  2CEB  CDA40B        		call	CHKRPAR
  2CEE  F1            		pop	af		;STEP?
  2CEF  CD132D        		call	SETGXY
  2CF2  CD583F        		call	SKIPSP
  2CF5  FE2C          		cp	','
  2CF7  C9            		ret
                      	
                      	
                      	;input: bc=x, de=y, z(0=absolute, 1=relative)
                      	;output: bc=x, de=y, (GRPX1)=(GRPX2)=x, (GRPY1)=(GRPY2)=y
                      	;destroy: f
  2CF8                	_SETGXY:ds	SETGXY-_SETGXY
  2D13                		org	SETGXY
                      	
  2D13  E5            		push	hl
  2D14  200B          		jr	nz,SETGXYNZ
  2D16  2AAEFD        		ld	hl,(GRPX1)
  2D19  09            		add	hl,bc
  2D1A  44            		ld	b,h
  2D1B  4D            		ld	c,l
  2D1C  2AB0FD        		ld	hl,(GRPY1)
  2D1F  19            		add	hl,de
  2D20  EB            		ex	de,hl
  2D21                	SETGXYNZ:
  2D21  ED43AEFD      		ld	(GRPX1),bc
  2D25  ED43ADFE      		ld	(GRPX2),bc
  2D29  ED53B0FD      		ld	(GRPY1),de
  2D2D  ED53AFFE      		ld	(GRPY2),de
  2D31  E1            		pop	hl
  2D32  C9            		ret
                      	
                      	
                      	;PRESET command
  2D33                	C_PRST:
  2D33  3A94FD        		ld	a,(COLOR2)
  2D36  1803          		jr	PSETRST
                      	
                      	;PSET command
  2D38                	C_PSET:
  2D38  3A93FD        		ld	a,(COLOR1)
                      	
                      	;PSET and PRESET
  2D3B                	PSETRST:
  2D3B  CDC015        		call	SETATT
  2D3E  CDDC2C        		call	GETGXY
  2D41  CC662D        		call	z,PSCOLR
                      	;	jp	PSET
                      	
                      	
                      	;input: bc=x1, de=y1, (ATTDAT)=attribute
                      	;destroy: af,bc,de
  2D44                	_PSET:	ds	PSET-_PSET
  2D47                		org	PSET
                      	
  2D47  E5            		push	hl
                      	;	call	CHKGXY
  2D48  3A92FD        		ld	a,(SCREEN1)
  2D4B  3D            		dec	a
  2D4C  CA182C        		jp	z,PSET2
  2D4F  F25A2D        		jp	p,PSETG
                      	
                      	
                      	;screen mode 1
  2D52                	PSET1:
  2D52  CD863F        		call	GXY2AD
  2D55  3AACFE        		ld	a,(ATTDAT)
  2D58  1809          		jr	PSETEND
                      	
                      	
                      	;screen mode 3,4
  2D5A                	PSETG:
  2D5A  CD3F14        		call	GXY2GAD
  2D5D  3AACFE        		ld	a,(ATTDAT)
  2D60  AE            		xor	(hl)
  2D61  A2            		and	d
  2D62  AE            		xor	(hl)
  2D63                	PSETEND:
  2D63  77            		ld	(hl),a
  2D64  E1            		pop	hl
  2D65  C9            		ret
                      	
                      	
  2D66                	PSCOLR:
  2D66  C5            		push	bc		;x
  2D67  D5            		push	de		;y
  2D68  CDE30D        		call	INT1INC
  2D6B  D1            		pop	de		;y
  2D6C  C1            		pop	bc		;x
  2D6D  C3C015        		jp	SETATT
                      	
                      	
                      	;POINT() function
  2D70                	F_POIN:
  2D70  CDDC2C        		call	GETGXY
  2D73  CD6E3E        		call	POINT
  2D76  C3650C        		jp	I1TOFA
                      	
                      	
                      	;LINE command
  2D79                	C_LINE:
  2D79  CD583F        		call	SKIPSP
  2D7C  FECB          		cp	MINUS_
  2D7E  ED4BAEFD      		ld	bc,(GRPX1)
  2D82  ED5BB0FD      		ld	de,(GRPY1)
  2D86  2808          		jr	z,LINE2P
                      	
  2D88                	LINE1P:
  2D88  CDDC2C        		call	GETGXY
  2D8B  FECB          		cp	MINUS_
  2D8D  C2DC03        		jp	nz,SNERR
                      	
  2D90                	LINE2P:
  2D90  C5            		push	bc		;x1
  2D91  D5            		push	de		;y1
                      	
  2D92  23            		inc	hl
  2D93  CDDC2C        		call	GETGXY
  2D96  ED43ADFE      		ld	(GRPX2),bc	;x2
  2D9A  ED53AFFE      		ld	(GRPY2),de	;y2
  2D9E  3A93FD        		ld	a,(COLOR1)
  2DA1  2011          		jr	nz,NOTBOX
  2DA3  5F            		ld	e,a
  2DA4  CD573F        		call	SKIPSPINC
  2DA7  FE2C          		cp	','
  2DA9  2810          		jr	z,CHKBF
                      	
                      	;color
  2DAB  CDE40D        		call	INT1ARG
  2DAE  7E            		ld	a,(hl)
  2DAF  FE2C          		cp	','
  2DB1  2808          		jr	z,CHKBF
  2DB3  7B            		ld	a,e
                      	
  2DB4                	NOTBOX:
  2DB4  CDC015        		call	SETATT
  2DB7  D1            		pop	de		;y1
  2DB8  C1            		pop	bc		;x1
  2DB9  1864          		jr	LINE
                      	
                      	;check 'B' and 'F'
  2DBB                	CHKBF:
  2DBB  CD573F        		call	SKIPSPINC
  2DBE  FE42          		cp	'B'
  2DC0  C2DC03        		jp	nz,SNERR
                      	
  2DC3  7B            		ld	a,e
  2DC4  CDC015        		call	SETATT
                      	
  2DC7  CD573F        		call	SKIPSPINC
  2DCA  FE46          		cp	'F'
  2DCC  2001          		jr	nz,BOX
  2DCE  23            		inc	hl		;for box fill
  2DCF                	BOX:
  2DCF  224EFF        		ld	(PROGAD),hl
  2DD2  D1            		pop	de		;y1
  2DD3  C1            		pop	bc		;x1
  2DD4  205F          		jr	nz,LINEB
  2DD6  180C          		jr	LINEBF
                      	
                      	
                      	;input: bc=x1, de=y1, (GRPX2)=x2, (GRPY2)=y2, (ATTDAT)=attribute
                      	;destroy: af,bc,de
  2DD8                	_LINEBF:ds	LINEBF-_LINEBF
  2DE4                		org	LINEBF
                      	
  2DE4  E5            		push	hl
  2DE5  CDAA2B        		call	PUSHGXY
  2DE8  CD9B2B        		call	SORTXY2
                      	
  2DEB  3A92FD        		ld	a,(SCREEN1)
  2DEE  FE02          		cp	02h
  2DF0  3868          		jr	c,BF12
                      	
                      	;line bf (graphic mode)
  2DF2                	BFG:
  2DF2  3AADFE        		ld	a,(GRPX2)	;larger x
  2DF5  2001          		jr	nz,BF4
  2DF7  3C            		inc	a		;screen mode 3
  2DF8                	BF4:
  2DF8  E607          		and	07h
  2DFA  3C            		inc	a
  2DFB  47            		ld	b,a
  2DFC  AF            		xor	a
  2DFD                	BFGLP1:
  2DFD  37            		scf
  2DFE  1F            		rra			;right part mask
  2DFF  10FC          		djnz	BFGLP1
                      	
                      	;	or	a
  2E01  ED52          		sbc	hl,de
  2E03  2C            		inc	l
  2E04  E5            		push	hl		;l=lines
                      	
  2E05  6F            		ld	l,a		;right part mask
                      	
  2E06  79            		ld	a,c		;smaller x
  2E07  E607          		and	07h
  2E09  47            		ld	b,a
  2E0A  3EFF          		ld	a,0ffh
  2E0C  2804          		jr	z,BFGZ
  2E0E                	BFGLP2:
  2E0E  CB3F          		srl	a
  2E10  10FC          		djnz	BFGLP2
  2E12                	BFGZ:
  2E12  67            		ld	h,a		;left part mask
                      	
  2E13  E5            		push	hl
  2E14  CD3F14        		call	GXY2GAD		;b=0
  2E17  D1            		pop	de		;d=left part mask, e=right part mask
  2E18  C1            		pop	bc		;c=lines
                      	
  2E19  C3672C        		jp	BFG2
                      	
                      	
                      	;by Bresenham's line algorithm
                      	;input: bc=x1, de=y1, (GRPX2)=x2, (GRPY2)=y2, (ATTDAT)=attribute
                      	;destroy: af
  2E1C                	_LINE:	ds	LINE-_LINE
  2E1F                		org	LINE
                      	
  2E1F  E5            		push	hl
  2E20  CDAA2B        		call	PUSHGXY
  2E23  D5            		push	de
  2E24  C5            		push	bc
                      	
  2E25  CD7E2B        		call	SORTY
  2E28  ED4BB3FE      		ld	bc,(GRPY3)	;start (smaller y)
  2E2C  2AAFFE        		ld	hl,(GRPY2)	;end (larger y)
  2E2F  B7            		or	a
  2E30  ED42          		sbc	hl,bc		;dy
  2E32  1839          		jr	LINE2
                      	
                      	
                      	;input: bc=x1, de=y1, (GRPX2)=x2, (GRPY2)=y2, (ATTDAT)=attribute
                      	;destroy: af,bc,de
  2E34                	_LINEB:	ds	LINEB-_LINEB
  2E35                		org	LINEB
                      	
  2E35  E5            		push	hl
  2E36  CDAA2B        		call	PUSHGXY
  2E39  CD9B2B        		call	SORTXY2
  2E3C  EB            		ex	de,hl
                      	
                      	;down-up-right-left
  2E3D  C5            		push	bc
  2E3E  CD1F2E        		call	LINE
  2E41  22AFFE        		ld	(GRPY2),hl	;smaller y
  2E44  EB            		ex	de,hl
  2E45  CD1F2E        		call	LINE
  2E48  ED4BADFE      		ld	bc,(GRPX2)	;larger x
  2E4C  EB            		ex	de,hl		;larger y
  2E4D  CD1F2E        		call	LINE
  2E50  C1            		pop	bc		;smaller x
  2E51  ED43ADFE      		ld	(GRPX2),bc
  2E55  CD1F2E        		call	LINE
  2E58  186A          		jr	LINEEND2
                      	
                      	
                      	;line bf (screen mode 1,2)
  2E5A                	BF12:
  2E5A  ED53AFFE      		ld	(GRPY2),de
  2E5E  CD1F2E        		call	LINE
  2E61  E7            		rst	CPHLDE
  2E62  2860          		jr	z,LINEEND2
  2E64  ED53B3FE      		ld	(GRPY3),de
  2E68  CD402B        		call	INCGY
  2E6B  18ED          		jr	BF12
                      	
                      	
  2E6D                	LINE2:
  2E6D  3A92FD        		ld	a,(SCREEN1)
  2E70  0F            		rrca
  2E71  3801          		jr	c,SKIPY
                      	;screen mode 1 or 3: dy=dy*2
  2E73  29            		add	hl,hl
                      	
  2E74                	SKIPY:
  2E74  EB            		ex	de,hl
  2E75  ED4BB1FE      		ld	bc,(GRPX3)	;start (x)
  2E79  2AADFE        		ld	hl,(GRPX2)	;end (x)
  2E7C  B7            		or	a
  2E7D  ED42          		sbc	hl,bc		;dx
  2E7F  B7            		or	a
  2E80  2004          		jr	nz,SKIPX
                      	;screen mode 1: dx=dx*3
  2E82  44            		ld	b,h
  2E83  4D            		ld	c,l
  2E84  29            		add	hl,hl
  2E85  09            		add	hl,bc
                      	
  2E86                	SKIPX:
  2E86  D5            		push	de		;dy
  2E87  E5            		push	hl		;dx
                      	
  2E88  CB7C          		bit	7,h
  2E8A  2806          		jr	z,DXPLUS	;dx>0
                      	;if dx<0, hl=-hl
  2E8C  AF            		xor	a
  2E8D  95            		sub	l
  2E8E  6F            		ld	l,a
  2E8F  9C            		sbc	a,h
  2E90  95            		sub	l
  2E91  67            		ld	h,a
  2E92                	DXPLUS:
  2E92  E7            		rst	CPHLDE
  2E93  D2A32C        		jp	nc,SLOPEX	;if |dy/dx|<=1
                      	
                      	;|dy/dx|>1
  2E96  D5            		push	de		;error=dy
                      	
  2E97  ED5BB3FE      		ld	de,(GRPY3)
  2E9B                	LINELP1:
  2E9B  ED4BB1FE      		ld	bc,(GRPX3)
  2E9F  CD472D        		call	PSET
                      	
  2EA2  E1            		pop	hl		;error
  2EA3  C1            		pop	bc		;dx
  2EA4  D1            		pop	de		;dy
  2EA5  D5            		push	de		;dy
  2EA6  C5            		push	bc		;dx
                      	
  2EA7  CD592B        		call	SUB2DX		;error-=|dx|*2
  2EAA  E5            		push	hl
  2EAB  CB7C          		bit	7,h
  2EAD  2807          		jr	z,ERRPLS
                      	
                      	;if error<0
  2EAF  E1            		pop	hl
  2EB0  19            		add	hl,de		;error+=dy
  2EB1  19            		add	hl,de		;error+=dy
  2EB2  E5            		push	hl		;error
  2EB3  CD242B        		call	INCGX		;increment or decrement X
                      	;	jr	c,LINEEND
                      	
  2EB6                	ERRPLS:
  2EB6  CD402B        		call	INCGY		;increment Y
  2EB9  2AAFFE        		ld	hl,(GRPY2)
  2EBC  E7            		rst	CPHLDE
  2EBD  30DC          		jr	nc,LINELP1
                      	
  2EBF                	LINEEND:
  2EBF  F1            		pop	af		;cancel error
  2EC0  F1            		pop	af		;cancel dx
  2EC1  F1            		pop	af		;cancel dy
                      	
  2EC2  C1            		pop	bc
  2EC3  D1            		pop	de
                      	
  2EC4                	LINEEND2:
  2EC4  E1            		pop	hl
  2EC5  22AFFE        		ld	(GRPY2),hl
  2EC8  E1            		pop	hl
  2EC9  22ADFE        		ld	(GRPX2),hl
  2ECC  E1            		pop	hl
  2ECD  C9            		ret
                      	
                      	
                      	;PAINT command
  2ECE                	C_PAIN:
  2ECE  CDDC2C        		call	GETGXY
  2ED1  C5            		push	bc		;X
  2ED2  D5            		push	de		;Y
  2ED3  3A93FD        		ld	a,(COLOR1)
  2ED6  CCE30D        		call	z,INT1INC
  2ED9  CDC015        		call	SETATT
  2EDC  7E            		ld	a,(hl)
  2EDD  FE2C          		cp	','
  2EDF  3AC5FE        		ld	a,(BORDERA)
  2EE2  2006          		jr	nz,PANZ
  2EE4  CDE30D        		call	INT1INC
  2EE7  32C6FE        		ld	(BORDERC),a
  2EEA                	PANZ:
  2EEA  5F            		ld	e,a
  2EEB  C3F12E        		jp	PAINT2
                      	
                      	
                      	;paint called by OKHOTSK
                      	;input: e=color code, (sp)=Y, (sp+2)=X
  2EEE                	_PAINT2:ds	PAINT2-_PAINT2
  2EF1                		org	PAINT2
                      	
  2EF1  7B            		ld	a,e
  2EF2  CDC715        		call	CNVATT
  2EF5  32C5FE        		ld	(BORDERA),a
  2EF8  D1            		pop	de		;Y
  2EF9  C1            		pop	bc		;X
                      	
                      	
                      	;input: bc=x, de=y, (ATTDAT)=attribute, (BORDERA)=attribute, (BORDERC)=color
                      	;destroy: af,bc,de
  2EFA                	_PAINT:	ds	PAINT-_PAINT
  2EFA                		org	PAINT
                      	
  2EFA  3A92FD        		ld	a,(SCREEN1)
  2EFD  3D            		dec	a
  2EFE  C8            		ret	z		;screen mode 2
  2EFF  E5            		push	hl
  2F00  FA0B2F        		jp	m,PAINT1	;screen mode 1
  2F03  CD3F14        		call	GXY2GAD
  2F06                	PAMAIN:
  2F06  CD122F        		call	PADWN
  2F09  E1            		pop	hl
  2F0A  C9            		ret
                      	
  2F0B                	PAINT1:
  2F0B  CD863F        		call	GXY2AD
  2F0E  16FF          		ld	d,0ffh
  2F10  18F4          		jr	PAMAIN
                      	
                      	
  2F12                	PADWN:
  2F12  CDC42F        		call	PALINE
  2F15  C8            		ret	z
  2F16  E5            		push	hl		;address
  2F17  D5            		push	de		;bit
  2F18  ED4BB5FE      		ld	bc,(PAWRK)
  2F1C  C5            		push	bc
  2F1D  CD812F        		call	PAUP2
  2F20  E1            		pop	hl
  2F21  22B5FE        		ld	(PAWRK),hl
  2F24  D1            		pop	de		;bit
  2F25  E1            		pop	hl		;address
                      	
  2F26                	PADWN2:
  2F26  3A92FD        		ld	a,(SCREEN1)
  2F29  B7            		or	a
  2F2A  1E19          		ld	e,19h
  2F2C  2002          		jr	nz,PADWNG
  2F2E  1E01          		ld	e,01h
  2F30                	PADWNG:
  2F30  7C            		ld	a,h
  2F31  E61F          		and	1fh
  2F33  BB            		cp	e
  2F34  3804          		jr	c,PADWNOK
  2F36  7D            		ld	a,l
  2F37  FEE0          		cp	0e0h
  2F39  D0            		ret	nc
  2F3A                	PADWNOK:
  2F3A  012000        		ld	bc,COLUMNS
  2F3D  09            		add	hl,bc
                      	
  2F3E  AF            		xor	a
  2F3F  32B7FE        		ld	(PACNT),a
  2F42                	PADWNLP1:
  2F42  CD1E30        		call	SRCHOK
  2F45  3812          		jr	c,CALLDWN
  2F47  E5            		push	hl		;address
  2F48  21B7FE        		ld	hl,PACNT
  2F4B  34            		inc	(hl)
  2F4C  D5            		push	de		;bit
  2F4D  CDE934        		call	CHKFRE
  2F50  D1            		pop	de
  2F51  E1            		pop	hl
  2F52  E5            		push	hl
  2F53  D5            		push	de
                      	
                      	;	ld	a,l
                      	;	inc	a
                      	;	and	1fh
                      	;	jr	nz,PADWNNZ
                      	;	bit	0,d
                      	;	jr	nz,CALLDWN
                      	;PADWNNZ:
                      	;	call	INCGXPA
                      	
  2F54  CD4B30        		call	SRCHNG
  2F57  30E9          		jr	nc,PADWNLP1
                      	
  2F59                	CALLDWN:
  2F59  3AB7FE        		ld	a,(PACNT)
  2F5C  B7            		or	a
  2F5D  C8            		ret	z
  2F5E                	PADWNLP2:
  2F5E  D1            		pop	de		;bit
  2F5F  E1            		pop	hl		;address
  2F60  3D            		dec	a
  2F61  32B7FE        		ld	(PACNT),a
  2F64  28AC          		jr	z,PADWN
  2F66  F5            		push	af
  2F67  CD122F        		call	PADWN
  2F6A  F1            		pop	af
  2F6B  18F1          		jr	PADWNLP2
                      	
                      	
  2F6D                	PAUP:
  2F6D  CDC42F        		call	PALINE
  2F70  C8            		ret	z
  2F71  E5            		push	hl		;address
  2F72  D5            		push	de		;bit
  2F73  ED4BB5FE      		ld	bc,(PAWRK)
  2F77  C5            		push	bc
  2F78  CD262F        		call	PADWN2
  2F7B  E1            		pop	hl
  2F7C  22B5FE        		ld	(PAWRK),hl
  2F7F  D1            		pop	de		;bit
  2F80  E1            		pop	hl		;address
                      	
  2F81                	PAUP2:
                      	;if (hl - start address) < 0020h
  2F81  3A92FD        		ld	a,(SCREEN1)
  2F84  E602          		and	02h		;0(screen mode 1) or 2(screen mode 3,4)
  2F86  5F            		ld	e,a
  2F87  7C            		ld	a,h
  2F88  E61F          		and	1fh
  2F8A  BB            		cp	e
  2F8B  2004          		jr	nz,PAUPNZ1
  2F8D  7D            		ld	a,l
  2F8E  FE20          		cp	20h
  2F90  D8            		ret	c
  2F91                	PAUPNZ1:
  2F91  01E0FF        		ld	bc,0-COLUMNS
  2F94  09            		add	hl,bc
                      	
  2F95  AF            		xor	a
  2F96  32B7FE        		ld	(PACNT),a
  2F99                	PAUPLP1:
  2F99  CD1E30        		call	SRCHOK
  2F9C  3812          		jr	c,CALLUP
  2F9E  E5            		push	hl		;address
  2F9F  21B7FE        		ld	hl,PACNT
  2FA2  34            		inc	(hl)
  2FA3  D5            		push	de		;bit
  2FA4  CDE934        		call	CHKFRE
  2FA7  D1            		pop	de
  2FA8  E1            		pop	hl
  2FA9  E5            		push	hl
  2FAA  D5            		push	de
                      	
                      	;	ld	a,l
                      	;	inc	a
                      	;	and	1fh
                      	;	jr	nz,PAUPNZ2
                      	;	bit	0,d
                      	;	jr	nz,CALLUP
                      	;PAUPNZ2:
                      	;	call	INCGXPA
                      	
  2FAB  CD4B30        		call	SRCHNG
  2FAE  30E9          		jr	nc,PAUPLP1
                      	
  2FB0                	CALLUP:
  2FB0  3AB7FE        		ld	a,(PACNT)
  2FB3  B7            		or	a
  2FB4  C8            		ret	z
  2FB5                	PAUPLP2:
  2FB5  D1            		pop	de		;bit
  2FB6  E1            		pop	hl		;address
  2FB7  3D            		dec	a
  2FB8  32B7FE        		ld	(PACNT),a
  2FBB  28B0          		jr	z,PAUP
  2FBD  F5            		push	af
  2FBE  CD6D2F        		call	PAUP
  2FC1  F1            		pop	af
  2FC2  18F1          		jr	PAUPLP2
                      	
                      	
                      	;paint 1 line
                      	;input: hl=address, d=bit
                      	;output: hl, d, z-flag(1=not painted)
                      	;destroy: af,bc
  2FC4                	PALINE:
  2FC4  CD4D27        		call	STOPESC
  2FC7  CD0F30        		call	CHKPA
  2FCA  C8            		ret	z
  2FCB  E5            		push	hl		;address
  2FCC  D5            		push	de		;bit
  2FCD  CDF42F        		call	PAINTR
  2FD0  7D            		ld	a,l
  2FD1  E61F          		and	1fh
  2FD3  6F            		ld	l,a
  2FD4  62            		ld	h,d
  2FD5  22B5FE        		ld	(PAWRK),hl	;X_right
  2FD8  D1            		pop	de		;bit
  2FD9  E1            		pop	hl		;address
                      	;	jr	PAINTL		;z-flag=0
                      	
                      	
                      	;paint to left direction
                      	;input: hl=address, d=bit
                      	;output: hl, d, z-flag=0
                      	;destroy: af,bc
  2FDA                	PAINTL:
  2FDA  7D            		ld	a,l
  2FDB  E61F          		and	1fh
  2FDD  2003          		jr	nz,PALOK
  2FDF  CB7A          		bit	7,d
  2FE1  C0            		ret	nz
                      	
  2FE2                	PALOK:
  2FE2  CD0530        		call	DECGXPA
  2FE5  CD0F30        		call	CHKPA
  2FE8  20F0          		jr	nz,PAINTL
                      	;	jp	INCGXPA
                      	
                      	
                      	;increment x for paint
                      	;input: hl=address, d=bit
                      	;output: hl, d, z-flag=0
                      	;destroy: af
  2FEA                	INCGXPA:
  2FEA  CB0A          		rrc	d
  2FEC  E2F12F        		jp	po,INCGXPA4	;screen mode 4
  2FEF  CB0A          		rrc	d		;screen mode 1 or 3
  2FF1                	INCGXPA4:
  2FF1  D0            		ret	nc
  2FF2  23            		inc	hl
  2FF3  C9            		ret
                      	
                      	
                      	;paint to right direction
                      	;input: hl=address, d=bit
                      	;output: hl,d
                      	;destroy: af,bc
  2FF4                	PAINTR:
  2FF4  7D            		ld	a,l
  2FF5  3C            		inc	a
  2FF6  E61F          		and	1fh
  2FF8  2003          		jr	nz,PAROK
  2FFA  CB42          		bit	0,d
  2FFC  C0            		ret	nz
                      	
  2FFD                	PAROK:
  2FFD  CDEA2F        		call	INCGXPA
  3000  CD0F30        		call	CHKPA
  3003  20EF          		jr	nz,PAINTR
                      	;	jp	DECGXPA
                      	
                      	
                      	;decrement x for paint
                      	;input: hl=address, d=bit
                      	;output: hl, d
                      	;destroy: af
  3005                	DECGXPA:
  3005  CB02          		rlc	d
  3007  E20C30        		jp	po,DECGXPA4	;screen mode 4
  300A  CB02          		rlc	d		;screen mode 1 or 3
  300C                	DECGXPA4:
  300C  D0            		ret	nc
  300D  2B            		dec	hl
  300E  C9            		ret
                      	
                      	
                      	;check and paint
                      	;input: hl=address, d=bit
                      	;output: z(1=not painted)
                      	;destroy: af
  300F                	CHKPA:
  300F  3AC5FE        		ld	a,(BORDERA)
  3012  AE            		xor	(hl)
  3013  A2            		and	d
  3014  C8            		ret	z
                      	
  3015  3AACFE        		ld	a,(ATTDAT)
  3018  AE            		xor	(hl)
  3019  A2            		and	d
  301A  AE            		xor	(hl)
  301B  77            		ld	(hl),a
  301C  B2            		or	d		;d>0, reset z-flag
  301D  C9            		ret
                      	
                      	
                      	;search for paintable area
                      	;input: hl=address, d=bit
                      	;output: hl, d, c-flag(1=over,0=OK)
                      	;destroy: af,e
  301E                	SRCHOK:
  301E  7D            		ld	a,l
  301F  E61F          		and	1fh
  3021  5F            		ld	e,a
  3022  3AB5FE        		ld	a,(PAWRK)	;address low (5 bits)
  3025  BB            		cp	e
  3026  D8            		ret	c
                      	
  3027  2007          		jr	nz,SRCHOKNZ
  3029  3AB6FE        		ld	a,(PAWRK+1)	;bit
  302C  3D            		dec	a
  302D  BA            		cp	d
  302E  3F            		ccf
  302F  D8            		ret	c
                      	
  3030                	SRCHOKNZ:
  3030  5E            		ld	e,(hl)
  3031  3AC5FE        		ld	a,(BORDERA)
  3034  AB            		xor	e
  3035  A2            		and	d
  3036  2806          		jr	z,SRCHOKZ
                      	
  3038  3AACFE        		ld	a,(ATTDAT)
  303B  AB            		xor	e
  303C  A2            		and	d
  303D  C0            		ret	nz		;c-flag=0
                      	
  303E                	SRCHOKZ:
  303E  CDEA2F        		call	INCGXPA
  3041  7D            		ld	a,l
  3042  E61F          		and	1fh
  3044  20D8          		jr	nz,SRCHOK
  3046  7A            		ld	a,d
  3047  07            		rlca
  3048  30D4          		jr	nc,SRCHOK
  304A  C9            		ret			;c-flag=1
                      	
                      	
                      	;search for unpaintable area
                      	;get unpaintable area
                      	;input: hl=address, d=bit
                      	;output: hl, d, c-flag(1=over,0=NG)
                      	;destroy: af,e
  304B                	SRCHNG:
  304B  7D            		ld	a,l
  304C  E61F          		and	1fh
  304E  5F            		ld	e,a
  304F  3AB5FE        		ld	a,(PAWRK)	;address low (5 bits)
  3052  BB            		cp	e
  3053  D8            		ret	c
                      	
  3054  2007          		jr	nz,SRCHNGNZ
  3056  3AB6FE        		ld	a,(PAWRK+1)	;bit
  3059  3D            		dec	a
  305A  BA            		cp	d
  305B  3F            		ccf
  305C  D8            		ret	c
                      	
  305D                	SRCHNGNZ:
  305D  5E            		ld	e,(hl)
  305E  3AC5FE        		ld	a,(BORDERA)
  3061  AB            		xor	e
  3062  A2            		and	d
  3063  C8            		ret	z		;c-flag=0
                      	
  3064  CDEA2F        		call	INCGXPA
  3067  7D            		ld	a,l
  3068  E61F          		and	1fh
  306A  20DF          		jr	nz,SRCHNG
  306C  7A            		ld	a,d
  306D  07            		rlca
  306E  30DB          		jr	nc,SRCHNG
  3070  C9            		ret			;c-flag=1
                      	
                      	
                      	;STR$() function
  3071                	F_STR:
  3071  CD7B0B        		call	CHKNUM
  3074  CDAC3A        		call	FTOA
  3077  54            		ld	d,h
  3078  5D            		ld	e,l
                      	
  3079  06FF          		ld	b,0-01h
  307B                	STRLP:
  307B  04            		inc	b
  307C  1A            		ld	a,(de)
  307D  13            		inc	de
  307E  B7            		or	a
  307F  20FA          		jr	nz,STRLP
                      	
  3081  78            		ld	a,b
  3082  CD2326        		call	MAKESTR
  3085  C3C527        		jp	INKYEND
                      	
                      	
                      	;put characters (skip first character)
  3088                	_PUTSINC:ds	PUTSINC-_PUTSINC
  30CE                		org	PUTSINC
                      	
  30CE  23            		inc	hl
                      	
                      	;put characters
                      	;input: hl,(DEVICE)=0:CRT, 1:printer, 2:RS-232C, 80h-ffh:CMT
                      	;destroy: af,hl,(bc,de,FAC1)
  30CF                	_PUTS:	ds	PUTS-_PUTS
  30CF                		org	PUTS
                      	
  30CF  7E            		ld	a,(hl)
  30D0  B7            		or	a
  30D1  C8            		ret	z
  30D2  FE22          		cp	22h		;double quotation mark
  30D4  C8            		ret	z
  30D5  CDC726        		call	PUTDEV
  30D8  18F4          		jr	PUTSINC
                      	
                      	
                      	;input: bc=candidate of string descriptor address
                      	;       hl=string descriptor address to be checked
                      	;	(GCWRK)=candidate of string data address to be comacted
                      	;	stack=string descriptor address pointing to same data (endmark=0000)
                      	;output: bc=string descriptor address to be compacted
                      	;	 (GCWRK)=string data address to be comacted
                      	;	 hl=next string descriptor address
                      	;	 stack=string descriptor address pointing to same data (endmark=0000)
                      	;destroy: af,de
  30DA                	GETSTR:
  30DA  E5            		push	hl
  30DB  7E            		ld	a,(hl)
  30DC  23            		inc	hl
  30DD  23            		inc	hl
  30DE  5E            		ld	e,(hl)
  30DF  23            		inc	hl
  30E0  56            		ld	d,(hl)
  30E1  23            		inc	hl
                      	
  30E2  B7            		or	a
  30E3  2852          		jr	z,GETNOSTR2	;length=0
                      	
  30E5  E5            		push	hl
  30E6  2A56FF        		ld	hl,(VARAD)
  30E9  E7            		rst	CPHLDE
  30EA  304A          		jr	nc,GETNOSTR	;in program area
  30EC  2A3DFF        		ld	hl,(STRAD)
  30EF  E7            		rst	CPHLDE
  30F0  3844          		jr	c,GETNOSTR	;in work area or have been compacted
  30F2  2A41FF        		ld	hl,(GCWRK)
  30F5  E7            		rst	CPHLDE
  30F6  E1            		pop	hl
  30F7  2811          		jr	z,GETSTRZ
  30F9  303C          		jr	nc,GETNOSTR2	;de < candidate
                      	
                      	;new candidate found
  30FB  ED5341FF      		ld	(GCWRK),de
  30FF  C1            		pop	bc
  3100  D1            		pop	de		;return address
                      	
  3101                	GETSTRLP:
  3101  F1            		pop	af		;pushed address? (a=higher byte)
  3102  F5            		push	af
  3103  D5            		push	de		;return address
  3104  B7            		or	a
  3105  C8            		ret	z		;no data
  3106  D1            		pop	de		;return address
  3107  F1            		pop	af		;cancel
  3108  18F7          		jr	GETSTRLP
                      	
  310A                	GETSTRZ:
  310A  F1            		pop	af
  310B  D1            		pop	de		;return address
  310C  F5            		push	af
  310D  D5            		push	de		;return address
  310E  C9            		ret
                      	
                      	
                      	;garbage collection
                      	;destroy: af,bc,de,hl
  310F                	_GC:	ds	GC-_GC
  310F                		org	GC
                      	
  310F  2A27FF        		ld	hl,(STREND)
  3112  223DFF        		ld	(STRAD),hl
                      	
                      	;	ld	hl,0000h
  3115  2600          		ld	h,00h		;higher byte
  3117                	GCLP1:
  3117  E5            		push	hl
  3118  2241FF        		ld	(GCWRK),hl
  311B  2A56FF        		ld	hl,(VARAD)
                      	
                      	;normal variable
  311E                	GCVAR:
  311E  ED5B58FF      		ld	de,(ARRAD)
  3122  E7            		rst	CPHLDE
  3123  3014          		jr	nc,GCARR
  3125  23            		inc	hl
  3126  7E            		ld	a,(hl)
  3127  07            		rlca
  3128  3806          		jr	c,GCVARS
  312A  110600        		ld	de,0006h
  312D  19            		add	hl,de
  312E  18EE          		jr	GCVAR
  3130                	GCVARS:
  3130  23            		inc	hl
  3131  CDDA30        		call	GETSTR
  3134  18E8          		jr	GCVAR
                      	
                      	
  3136                	GETNOSTR:
  3136  E1            		pop	hl
  3137                	GETNOSTR2:
  3137  F1            		pop	af	;cancel
  3138  C9            		ret
                      	
                      	
                      	;array variable
  3139                	GCARR:
  3139  ED5B5AFF      		ld	de,(FREEAD)
  313D  E7            		rst	CPHLDE
  313E  3029          		jr	nc,GCSDSC
  3140  23            		inc	hl
  3141  7E            		ld	a,(hl)
  3142  23            		inc	hl
  3143  5E            		ld	e,(hl)
  3144  23            		inc	hl
  3145  56            		ld	d,(hl)
  3146  23            		inc	hl
  3147  07            		rlca
  3148  3803          		jr	c,GCARRS
  314A  19            		add	hl,de
  314B  18EC          		jr	GCARR
                      	
  314D                	GCARRS:
  314D  EB            		ex	de,hl
  314E  19            		add	hl,de
  314F  EB            		ex	de,hl
  3150  D5            		push	de
  3151  5E            		ld	e,(hl)		;dimensions
  3152  1600          		ld	d,00h
  3154  23            		inc	hl
  3155  19            		add	hl,de
  3156  19            		add	hl,de
  3157                	GCARRLP:
  3157  D1            		pop	de
  3158  E7            		rst	CPHLDE
  3159  30DE          		jr	nc,GCARR
  315B  ED5350FF      		ld	(TMP),de
  315F  CDDA30        		call	GETSTR
  3162  ED5B50FF      		ld	de,(TMP)
  3166  D5            		push	de
  3167  18EE          		jr	GCARRLP
                      	
                      	;temporary string descriptor
  3169                	GCSDSC:
  3169  212DFF        		ld	hl,STRDSC1
  316C  CDDA30        		call	GETSTR
                      	;	ld	hl,STRDSC2
  316F  CDDA30        		call	GETSTR
                      	;	ld	hl,STRDSC3
  3172  CDDA30        		call	GETSTR
                      	;	ld	hl,STRDSC4
  3175  CDDA30        		call	GETSTR
                      	
  3178  2A41FF        		ld	hl,(GCWRK)
  317B  7C            		ld	a,h
                      	;	or	l
  317C  B7            		or	a		;higher byte
  317D  D1            		pop	de		;cancel
  317E  C8            		ret	z
  317F  D5            		push	de
                      	
                      	;compaction
                      	;input:	bc=string descriptor address
                      	;	hl=string data address to be comacted
                      	
  3180  0A            		ld	a,(bc)
  3181  C5            		push	bc		;
  3182  0600          		ld	b,00h
  3184  4F            		ld	c,a
  3185  09            		add	hl,bc
  3186  2B            		dec	hl
  3187  ED5B3DFF      		ld	de,(STRAD)
  318B  EDB8          		lddr
  318D  ED533DFF      		ld	(STRAD),de
  3191  13            		inc	de
                      	
  3192  E1            		pop	hl		;
  3193                	GCLP2:
  3193  23            		inc	hl
  3194  23            		inc	hl
  3195  73            		ld	(hl),e
  3196  23            		inc	hl
  3197  72            		ld	(hl),d
  3198  E1            		pop	hl
  3199  7C            		ld	a,h
                      	;	or	l
  319A  B7            		or	a		;higher byte
  319B  CA1731        		jp	z,GCLP1
  319E  18F3          		jr	GCLP2
                      	
                      	
                      	;LEN() function
  31A0                	F_LEN:
  31A0  CD870B        		call	CHKSTR
  31A3                	SETI1A:
  31A3  6F            		ld	l,a
  31A4                	SETI1:
  31A4  CD660C        		call	I1TOF
  31A7                	SETTYP0:
  31A7  AF            		xor	a
  31A8  3225FF        		ld	(ARGTYP),a	;0=numeric
  31AB  C9            		ret
                      	
                      	
                      	;ASC() function
  31AC                	F_ASC:
  31AC  CD9326        		call	STRARG2
  31AF  B7            		or	a
  31B0  CAE503        		jp	z,FCERR
  31B3  1A            		ld	a,(de)
  31B4  18ED          		jr	SETI1A
                      	
                      	
                      	;CHR$() function
  31B6                	F_CHR:
  31B6  CD7B0B        		call	CHKNUM
  31B9  CD4F07        		call	FTOI1
  31BC  43            		ld	b,e
  31BD  C3B027        		jp	INKYNZ
                      	
                      	
                      	;LEFT$() function
  31C0                	F_LEFT:
  31C0  CD9F26        		call	ARGSI1
  31C3  0E00          		ld	c,00h
  31C5  1824          		jr	MID
                      	
                      	
                      	;RIGHT$() function
  31C7                	F_RGT:
  31C7  CD9F26        		call	ARGSI1
  31CA  57            		ld	d,a		;
  31CB  93            		sub	e
  31CC  3001          		jr	nc,RGTNC
  31CE  AF            		xor	a
  31CF                	RGTNC:
  31CF  4F            		ld	c,a
  31D0  5A            		ld	e,d		;
  31D1  7A            		ld	a,d
  31D2  1817          		jr	MID
                      	
                      	
                      	;MID$() function
  31D4                	F_MID:
  31D4  CD9F26        		call	ARGSI1
  31D7  57            		ld	d,a		;length
  31D8  1D            		dec	e
  31D9  D5            		push	de		;d=length, e=2nd parameter-1
  31DA  1C            		inc	e
  31DB  CAE503        		jp	z,FCERR		;if 2nd parameter=0
                      	
  31DE  2A4EFF        		ld	hl,(PROGAD)
  31E1  1EFF          		ld	e,0ffh
  31E3  7E            		ld	a,(hl)
  31E4  FE2C          		cp	','
  31E6  CCBB26        		call	z,ARGI1
  31E9  C1            		pop	bc		;b=length, c=2nd parameter-1
  31EA  78            		ld	a,b
                      	
                      	;input: STRDSC1=string, a=string length, c=2nd parameter-1, e=3rd parameter
  31EB                	MID:
  31EB  91            		sub	c
  31EC  3001          		jr	nc,MIDNC
  31EE  AF            		xor	a
  31EF                	MIDNC:
  31EF  BB            		cp	e
  31F0  3801          		jr	c,MIDC
  31F2  7B            		ld	a,e
  31F3                	MIDC:
  31F3  47            		ld	b,a		;output length
  31F4  CD6626        		call	GCCHECK
  31F7  78            		ld	a,b		;output length
  31F8  2A2FFF        		ld	hl,(STRDSC1+2)
  31FB  0600          		ld	b,00h
  31FD  09            		add	hl,bc
  31FE  CD2326        		call	MAKESTR
  3201  2A4EFF        		ld	hl,(PROGAD)
  3204  CDA40B        		call	CHKRPAR
  3207  C3C527        		jp	INKYEND
                      	
                      	
                      	;VAL() function
  320A                	F_VAL:
  320A  CD9326        		call	STRARG2
  320D  EB            		ex	de,hl
  320E  CD9839        		call	ATOF
  3211  1894          		jr	SETTYP0
                      	
                      	
                      	;FRE() function
  3213                	F_FRE:
  3213  2125FF        		ld	hl,ARGTYP
  3216  7E            		ld	a,(hl)
  3217  B7            		or	a
  3218  2814          		jr	z,FRENUM
  321A  3D            		dec	a
  321B  C2DC03        		jp	nz,SNERR
                      	;string
  321E  77            		ld	(hl),a		;=0
  321F  322DFF        		ld	(STRDSC1),a	;=0
  3222  CD0F31        		call	GC
  3225  2A3DFF        		ld	hl,(STRAD)
  3228  ED5B5BFA      		ld	de,(STACK)
  322C  1808          		jr	CALCFRE
                      	;numeric
  322E                	FRENUM:
  322E  21FAFF        		ld	hl,0-0006h	;for compatibility with N60-BASIC
  3231  39            		add	hl,sp
  3232  ED5B5AFF      		ld	de,(FREEAD)
  3236                	CALCFRE:
  3236  B7            		or	a
  3237  ED52          		sbc	hl,de
  3239  C3680C        		jp	I2TOF
                      	
                      	
                      	;DIM command
  323C                	C_DIM:
  323C  CD7A33        		call	CHKVAR
                      	
  323F  7E            		ld	a,(hl)
  3240  FE28          		cp	'('
  3242  2045          		jr	nz,NOTARR
  3244  23            		inc	hl
                      	
  3245  E5            		push	hl		;program address
  3246  CD2834        		call	SRCHARR
  3249  D2F403        		jp	nc,DDERR
  324C  CD7033        		call	SETARNM
  324F  E1            		pop	hl		;program address
  3250  D5            		push	de		;array address
  3251  CD2034        		call	VARSIZE
  3254  D5            		push	de		;array address
  3255  C5            		push	bc		;element bytes
                      	
  3256  48            		ld	c,b		;b=0
  3257                	DIMLP:
  3257  C5            		push	bc		;dimensions
  3258  CD1A0E        		call	INT2ARG
  325B  C1            		pop	bc		;dimensions
  325C  CB7A          		bit	7,d
  325E  C2E503        		jp	nz,FCERR
  3261  0C            		inc	c
  3262  CAEB03        		jp	z,OMERR		;over 255 dimensions
  3265  13            		inc	de		;+1
  3266  E1            		pop	hl		;element bytes
                      	
  3267  EB            		ex	de,hl
  3268  E3            		ex	(sp),hl		;push size, pop array address
  3269  EB            		ex	de,hl
                      	
  326A  D5            		push	de		;array address
  326B  E5            		push	hl		;element bytes
  326C  2A4EFF        		ld	hl,(PROGAD)
  326F  7E            		ld	a,(hl)
  3270  23            		inc	hl
  3271  FE2C          		cp	','
  3273  28E2          		jr	z,DIMLP
  3275  FE29          		cp	')'
  3277  C2DC03        		jp	nz,SNERR
  327A  CD4434        		call	MAKEARR
                      	
  327D                	DIMNEXT:
  327D  2A4EFF        		ld	hl,(PROGAD)
  3280  2B            		dec	hl
  3281  D7            		rst	ANADAT
  3282  C8            		ret	z
  3283  23            		inc	hl
  3284  FE2C          		cp	','
  3286  C0            		ret	nz
  3287  18B3          		jr	C_DIM
                      	
  3289                	NOTARR:
  3289  224EFF        		ld	(PROGAD),hl
  328C  CDAC33        		call	SRCHVAR
  328F  DCD633        		call	c,MAKEVAR
  3292  18E9          		jr	DIMNEXT
                      	
                      	
                      	;get variable address
                      	;input: bc=variable name, hl=program address
                      	;output: de=variable address, (PROGAD)=next program address
                      	;destroy: FAC1,FAC2,af,de
  3294                	GETVAR:
  3294  C5            		push	bc
  3295  7E            		ld	a,(hl)
  3296  FE28          		cp	'('
  3298  280B          		jr	z,GETVARR
  329A  224EFF        		ld	(PROGAD),hl
  329D  CDAC33        		call	SRCHVAR
  32A0  DCD633        		call	c,MAKEVAR
  32A3  C1            		pop	bc
  32A4  C9            		ret
  32A5                	GETVARR:
  32A5  CDAA32        		call	GETARR
  32A8  C1            		pop	bc
  32A9  C9            		ret
                      	
                      	
                      	;get array address
                      	;input: bc=variable name, hl=program address of '('
                      	;output: de=array address
                      	;destroy: af,bc,hl
  32AA                	GETARR:
  32AA  23            		inc	hl
  32AB  224EFF        		ld	(PROGAD),hl
  32AE  C5            		push	bc		;variable name
  32AF  CD2834        		call	SRCHARR
  32B2  300F          		jr	nc,ARROK
                      	
  32B4  D5            		push	de		;array address
  32B5  2A4EFF        		ld	hl,(PROGAD)
  32B8  E5            		push	hl		;program address
  32B9  CD3E33        		call	DIMAUTO
  32BC  E1            		pop	hl		;program address
  32BD  224EFF        		ld	(PROGAD),hl
  32C0  D1            		pop	de		;array address
  32C1  13            		inc	de
  32C2  13            		inc	de
  32C3                	ARROK:
  32C3  EB            		ex	de,hl
  32C4  23            		inc	hl
  32C5  23            		inc	hl
  32C6  0600          		ld	b,00h
  32C8  4E            		ld	c,(hl)		;dimensions
  32C9  09            		add	hl,bc
  32CA  09            		add	hl,bc
  32CB  E5            		push	hl		;array address
  32CC  79            		ld	a,c
  32CD  08            		ex	af,af'		;dimensions
  32CE  F5            		push	af
  32CF  48            		ld	c,b		;=0
  32D0  C5            		push	bc		;pointer
  32D1  E5            		push	hl		;array address
  32D2  210100        		ld	hl,0001h
  32D5  2250FF        		ld	(TMP),hl	;multiplied sizes
                      	
  32D8                	GETALP:
  32D8  2A50FF        		ld	hl,(TMP)
  32DB  E5            		push	hl		;multiplied sizes
  32DC  2A4EFF        		ld	hl,(PROGAD)
  32DF  CD1A0E        		call	INT2ARG		;get suffix
  32E2  E1            		pop	hl		;multiplied sizes
  32E3  2250FF        		ld	(TMP),hl
  32E6  E1            		pop	hl		;array address
  32E7  D5            		push	de		;suffix
  32E8  56            		ld	d,(hl)		;de=size of each dimension
  32E9  2B            		dec	hl
  32EA  5E            		ld	e,(hl)
  32EB  2B            		dec	hl
  32EC  E3            		ex	(sp),hl		;push array address, pop suffix
  32ED  E7            		rst	CPHLDE
  32EE  D2F103        		jp	nc,BSERR
                      	
  32F1  D5            		push	de		;size
  32F2  ED5B50FF      		ld	de,(TMP)	;multiplied sizes
  32F6  CD6D24        		call	MULINT2		;hl = multiplied sizes * suffix
  32F9  C1            		pop	bc		;size
  32FA  EB            		ex	de,hl
  32FB  E1            		pop	hl		;array address
  32FC  E3            		ex	(sp),hl		;push array address, pop pointer
  32FD  19            		add	hl,de		;pointer += multiplied size * suffix
  32FE  E5            		push	hl		;pointer
  32FF  2A50FF        		ld	hl,(TMP)	;multiplied sizes
  3302  50            		ld	d,b		;size
  3303  59            		ld	e,c
  3304  CD6D24        		call	MULINT2		;hl = multiplied size * size
  3307  2250FF        		ld	(TMP),hl
  330A  E1            		pop	hl		;pointer
  330B  E3            		ex	(sp),hl		;push poniter, pop array address
  330C  E5            		push	hl		;array address
                      	
  330D  08            		ex	af,af'		;dimensions
  330E  3D            		dec	a
  330F  47            		ld	b,a
  3310  08            		ex	af,af'
  3311  2A4EFF        		ld	hl,(PROGAD)
  3314  7E            		ld	a,(hl)
  3315  FE2C          		cp	','
  3317  200A          		jr	nz,NOTCMM
  3319  23            		inc	hl
  331A  224EFF        		ld	(PROGAD),hl
  331D  04            		inc	b
                      	;	dec	b
                      	;	jr	nz,GETALP
  331E  10B8          		djnz	GETALP
  3320  C3DC03        		jp	SNERR
                      	
  3323                	NOTCMM:
  3323  04            		inc	b
  3324  05            		dec	b
  3325  C2F103        		jp	nz,BSERR
  3328  CDA40B        		call	CHKRPAR
                      	
  332B  D1            		pop	de		;array address
  332C  E1            		pop	hl		;pointer
  332D  F1            		pop	af
  332E  08            		ex	af,af'
  332F  D1            		pop	de		;array address
  3330  C1            		pop	bc		;variable name
  3331  79            		ld	a,c
  3332  44            		ld	b,h
  3333  4D            		ld	c,l
  3334  29            		add	hl,hl		;*2
  3335  29            		add	hl,hl		;*4
  3336  07            		rlca
  3337  3801          		jr	c,NCMMSTR
  3339  09            		add	hl,bc		;*5
  333A                	NCMMSTR:
  333A  19            		add	hl,de
  333B  23            		inc	hl
  333C  EB            		ex	de,hl
  333D  C9            		ret
                      	
                      	
  333E                	DIMAUTO:
  333E  CD7033        		call	SETARNM
  3341  D5            		push	de		;array address
  3342  CD2034        		call	VARSIZE
  3345  D5            		push	de		;array address
  3346  C5            		push	bc		;element bytes
                      	
  3347  2A4EFF        		ld	hl,(PROGAD)
  334A  48            		ld	c,b		;b=0
  334B                	AUTOLP1:
  334B  C5            		push	bc		;dimensions
  334C  CD1A0E        		call	INT2ARG		;de=size
                      	;	ld	a,d
                      	;	or	a
                      	;	jp	nz,BSERR
                      	;	ld	a,e
                      	;	ld	e,0bh		;de=10+1
                      	;	cp	e
                      	;	jp	nc,BSERR
                      	
  334F  110B00        		ld	de,000bh
                      	
  3352  C1            		pop	bc		;dimensions
  3353  0C            		inc	c
  3354  CB51          		bit	2,c
  3356  D2F103        		jp	nc,BSERR	;over 3 dimensions
                      	
  3359  EB            		ex	de,hl
  335A  D1            		pop	de		;element bytes
  335B  E3            		ex	(sp),hl		;push size, pop array address
  335C  E5            		push	hl		;array address
  335D  D5            		push	de		;element bytes
                      	
                      	
  335E  2A4EFF        		ld	hl,(PROGAD)
  3361  7E            		ld	a,(hl)
  3362  23            		inc	hl
  3363  FE2C          		cp	','
  3365  28E4          		jr	z,AUTOLP1
  3367  FE29          		cp	')'
  3369  C2DC03        		jp	nz,SNERR
  336C  CD4434        		call	MAKEARR
  336F  C9            		ret
                      	
                      	
                      	;set array name
                      	;input: bc=name, de=array address
                      	;output: de=array address (next of name)
                      	;destroy: af,hl
  3370                	SETARNM:
  3370  EB            		ex	de,hl
  3371  CDEC34        		call	CHKSTK
  3374  70            		ld	(hl),b
  3375  23            		inc	hl
  3376  71            		ld	(hl),c
  3377  23            		inc	hl
  3378  EB            		ex	de,hl
  3379  C9            		ret
                      	
                      	
                      	;check variable name
                      	;input: hl=program address
                      	;output: bc=name, hl=next address
                      	;destroy: af
  337A                	CHKVAR:
  337A  7E            		ld	a,(hl)
  337B  23            		inc	hl
  337C  FE20          		cp	' '
  337E  28FA          		jr	z,CHKVAR
  3380  47            		ld	b,a
  3381  D641          		sub	'A'
  3383  FE1A          		cp	'Z'-'A'+1
  3385  D2DC03        		jp	nc,SNERR
                      	;	call	GETNAME
                      	;	ret
                      	
                      	
                      	;get variable name
                      	;input: hl=2nd character address, b=1st character
                      	;output: bc=name, hl=next address
                      	;destroy: af
  3388                	GETNAME:
  3388  0E00          		ld	c,00h
  338A  2B            		dec	hl
  338B                	GETNMLP1:
  338B  23            		inc	hl
  338C  7E            		ld	a,(hl)
  338D  FE20          		cp	' '
  338F  28FA          		jr	z,GETNMLP1
  3391  FE24          		cp	'$'
  3393  2813          		jr	z,NAMESTR
  3395  CDB90B        		call	ALPNUM
  3398  D0            		ret	nc
  3399  4F            		ld	c,a
  339A                	GETNMLP2:
  339A  23            		inc	hl
  339B  7E            		ld	a,(hl)
  339C  FE20          		cp	' '
  339E  28FA          		jr	z,GETNMLP2
  33A0  CDB90B        		call	ALPNUM
  33A3  38F5          		jr	c,GETNMLP2
  33A5  FE24          		cp	'$'
  33A7  C0            		ret	nz
  33A8                	NAMESTR:
  33A8  23            		inc	hl
  33A9  CBF9          		set	7,c
  33AB  C9            		ret
                      	
                      	
                      	;search for variable
                      	;input: bc=variable name
                      	;output: de=variable address, c-flag(1=not found)
                      	;destroy: af,hl
  33AC                	SRCHVAR:
  33AC  2A5EFF        		ld	hl,(FNPAR)
  33AF  7D            		ld	a,l
  33B0  B8            		cp	b
  33B1  2006          		jr	nz,SRCHVMAIN
  33B3  7C            		ld	a,h
  33B4  B9            		cp	c
  33B5  1160FF        		ld	de,FNARG
  33B8  C8            		ret	z
                      	
  33B9                	SRCHVMAIN:
  33B9  ED5B56FF      		ld	de,(VARAD)
  33BD                	SRCHVLP:
  33BD  2A58FF        		ld	hl,(ARRAD)
  33C0  2B            		dec	hl
  33C1  E7            		rst	CPHLDE
  33C2  D8            		ret	c		;not found
  33C3  1A            		ld	a,(de)
  33C4  13            		inc	de
  33C5  B8            		cp	b		;
  33C6  1A            		ld	a,(de)
  33C7  13            		inc	de
  33C8  2002          		jr	nz,VARNEXT	;
  33CA  B9            		cp	c
  33CB  C8            		ret	z		;found
  33CC                	VARNEXT:
  33CC  07            		rlca			;c-flag=1 if string
  33CD  3F            		ccf
  33CE  210400        		ld	hl,6-2
  33D1  ED5A          		adc	hl,de
  33D3  EB            		ex	de,hl
  33D4  18E7          		jr	SRCHVLP
                      	
                      	
                      	;make variable
                      	;input: bc=variable name, de=variable address
                      	;output: de=de+2
                      	;destroy: af,bc,hl
  33D6                	MAKEVAR:
  33D6  C5            		push	bc		;variable name
  33D7  D5            		push	de		;variable address
                      	
  33D8  110600        		ld	de,0006h
  33DB  CB79          		bit	7,c
  33DD  2001          		jr	nz,MAKEVSTR	;numeric=7, string=6
  33DF  1C            		inc	e
  33E0                	MAKEVSTR:
  33E0  2A5AFF        		ld	hl,(FREEAD)
  33E3  ED4B58FF      		ld	bc,(ARRAD)
  33E7  B7            		or	a
  33E8  ED42          		sbc	hl,bc
  33EA  44            		ld	b,h
  33EB  4D            		ld	c,l
                      	
  33EC  2A5AFF        		ld	hl,(FREEAD)
                      	
  33EF  19            		add	hl,de
  33F0  D5            		push	de		;numeric=7, string=6
  33F1  CDEC34        		call	CHKSTK
  33F4  D1            		pop	de		;numeric=7, string=6
  33F5  225AFF        		ld	(FREEAD),hl
  33F8  2A58FF        		ld	hl,(ARRAD)
  33FB  19            		add	hl,de
  33FC  2258FF        		ld	(ARRAD),hl
                      	
  33FF  78            		ld	a,b
  3400  B1            		or	c
  3401  280B          		jr	z,NOLDDR
  3403  2A5AFF        		ld	hl,(FREEAD)
  3406  E5            		push	hl		;
                      	;	or	a
  3407  ED52          		sbc	hl,de
  3409  2B            		dec	hl
  340A  D1            		pop	de		;
  340B  1B            		dec	de
  340C  EDB8          		lddr
  340E                	NOLDDR:
  340E  E1            		pop	hl		;variable address
  340F  C1            		pop	bc		;variable name
  3410  70            		ld	(hl),b
  3411  23            		inc	hl
  3412  71            		ld	(hl),c
  3413  23            		inc	hl
  3414  E5            		push	hl		;variable address
                      	
  3415  EB            		ex	de,hl
  3416  21AB0E        		ld	hl,ZERO
  3419  CD2034        		call	VARSIZE
  341C  EDB0          		ldir
  341E  D1            		pop	de		;variable address
  341F  C9            		ret
                      	
                      	
                      	;get variable element size
                      	;input: bc=variable name
                      	;output: bc=5(numeric) or 4(string)
                      	;destroy: f
  3420                	VARSIZE:
  3420  CB79          		bit	7,c
  3422  010500        		ld	bc,0005h
  3425  C8            		ret	z
  3426  0D            		dec	c
  3427  C9            		ret
                      	
                      	
                      	;search for array variable
                      	;input: bc=variable name
                      	;output: de=array address, c-flag(1=not found)
                      	;destroy: af,hl
  3428                	SRCHARR:
  3428  ED5B58FF      		ld	de,(ARRAD)
  342C                	SRCHALP:
  342C  2A5AFF        		ld	hl,(FREEAD)
  342F  2B            		dec	hl
  3430  E7            		rst	CPHLDE
  3431  D8            		ret	c		;not found
  3432  1A            		ld	a,(de)
  3433  13            		inc	de
  3434  B8            		cp	b		;
  3435  1A            		ld	a,(de)
  3436  13            		inc	de
  3437  2002          		jr	nz,ARRNEXT	;
  3439  B9            		cp	c
  343A  C8            		ret	z		;found
  343B                	ARRNEXT:
  343B  EB            		ex	de,hl
  343C  5E            		ld	e,(hl)
  343D  23            		inc	hl
  343E  56            		ld	d,(hl)
  343F  23            		inc	hl
  3440  19            		add	hl,de
  3441  EB            		ex	de,hl
  3442  18E8          		jr	SRCHALP
                      	
                      	
                      	;make array
                      	;input: c=dimensions, hl=program address
                      	;pushed: size*(1~255), array address, element bytes
                      	;destroy:
  3444                	MAKEARR:
  3444  224EFF        		ld	(PROGAD),hl
  3447  E1            		pop	hl		;return address
  3448  2250FF        		ld	(TMP),hl
  344B  D1            		pop	de		;element bytes
  344C  E1            		pop	hl		;array address
  344D  23            		inc	hl
  344E  23            		inc	hl
  344F  71            		ld	(hl),c		;dimensions
  3450  23            		inc	hl
  3451  EB            		ex	de,hl
                      	
  3452  41            		ld	b,c
  3453                	MKALP:
  3453  E3            		ex	(sp),hl		;push element bytes, pop size
  3454  EB            		ex	de,hl
  3455  73            		ld	(hl),e
  3456  23            		inc	hl
  3457  72            		ld	(hl),d
  3458  23            		inc	hl
  3459  E3            		ex	(sp),hl		;push array address, pop element bytes
  345A  C5            		push	bc		;dimensions
  345B  CD6D24        		call	MULINT2		;element bytes *= each dimension size
  345E  C1            		pop	bc		;dimensions
  345F  7A            		ld	a,d
  3460  B3            		or	e
  3461  C2F103        		jp	nz,BSERR	;for compatibility with N60-BASIC
  3464  D1            		pop	de		;array address
  3465  10EC          		djnz	MKALP
                      	
  3467  E5            		push	hl		;element bytes
  3468  EB            		ex	de,hl
  3469  60            		ld	h,b
  346A  69            		ld	l,c
  346B  29            		add	hl,hl
  346C  23            		inc	hl
  346D  19            		add	hl,de
  346E  DAEB03        		jp	c,OMERR
                      	
  3471  E5            		push	hl		;dimensions*2 + element bytes + 1
  3472  ED5B5AFF      		ld	de,(FREEAD)
  3476  13            		inc	de		;+2(total bytes)
  3477  13            		inc	de
  3478  13            		inc	de		;+2(name)
  3479  13            		inc	de
  347A  19            		add	hl,de
  347B  DAEB03        		jp	c,OMERR
  347E  CDEC34        		call	CHKSTK
  3481  225AFF        		ld	(FREEAD),hl
                      	
  3484  79            		ld	a,c		;dimensions
  3485  D1            		pop	de		;dimensions*2 + element bytes + 1
  3486  E1            		pop	hl		;element bytes
  3487  E3            		ex	(sp),hl		;push element bytes, pop array address
                      	
  3488  73            		ld	(hl),e
  3489  23            		inc	hl
  348A  72            		ld	(hl),d
  348B  23            		inc	hl
  348C  23            		inc	hl
  348D  09            		add	hl,bc
  348E  09            		add	hl,bc
                      	
  348F  70            		ld	(hl),b		;b=0
  3490  54            		ld	d,h
  3491  5D            		ld	e,l
  3492  13            		inc	de
  3493  C1            		pop	bc		;element bytes
  3494  0B            		dec	bc
  3495  EDB0          		ldir
  3497  2A50FF        		ld	hl,(TMP)	;return address
  349A  E9            		jp	(hl)
                      	
                      	
                      	;put into variable
                      	;input: bc=variable name, hl=program address, de=variable address
                      	;output: hl=next program address
                      	;destroy: FAC1,FAC2,af,bc,de
  349B                	VARIN:
  349B  CD583F        		call	SKIPSP
  349E  FED2          		cp	EQ_		;0d2h
  34A0  C2DC03        		jp	nz,SNERR
                      	
  34A3  D5            		push	de		;variable address
  34A4  23            		inc	hl
  34A5  CB79          		bit	7,c
  34A7  2814          		jr	z,VARNUM
                      	
                      	;string
  34A9  CD8A26        		call	STRARG
  34AC  47            		ld	b,a
  34AD  7A            		ld	a,d
  34AE  FEFA          		cp	0fah		;fa00h=work area start address
  34B0  78            		ld	a,b
  34B1  EB            		ex	de,hl
  34B2  D42326        		call	nc,MAKESTR
  34B5  212DFF        		ld	hl,STRDSC1
  34B8  010400        		ld	bc,0004h
  34BB  1809          		jr	VARINOK
                      	
  34BD                	VARNUM:
  34BD  CD720B        		call	NUMARGMO
  34C0  2166FF        		ld	hl,FAC1
  34C3  010500        		ld	bc,0005h
  34C6                	VARINOK:
  34C6  D1            		pop	de		;variable address
  34C7  EDB0          		ldir
  34C9  2A4EFF        		ld	hl,(PROGAD)
  34CC  C9            		ret
                      	
                      	
                      	;NEW command
  34CD                	C_NEW:
  34CD  CD5F3F        		call	CHKCLN
  34D0  C2DC03        		jp	nz,SNERR
  34D3  214204        		ld	hl,EDIT
  34D6  E5            		push	hl
                      	
  34D7                	NEWRESSTK:
  34D7  21F934        		ld	hl,RESSTK
  34DA  E5            		push	hl
                      	;	jp	NEW
                      	
                      	;destroy: af,hl
  34DB                	NEW:
  34DB  2A29FF        		ld	hl,(BASICAD)
  34DE  AF            		xor	a
  34DF  77            		ld	(hl),a
  34E0  23            		inc	hl
  34E1  77            		ld	(hl),a
  34E2  23            		inc	hl
  34E3  77            		ld	(hl),a
  34E4  23            		inc	hl
  34E5  2256FF        		ld	(VARAD),hl
  34E8  C9            		ret
                      	
                      	
                      	;check free size
                      	;output: de=free size
                      	;destroy: f,hl
  34E9                	CHKFRE:
  34E9  2A5AFF        		ld	hl,(FREEAD)
                      	
                      	;check stack size
                      	;hl <= stack - 003bh?
                      	;input: hl=address
                      	;output: de=free size
                      	;destroy: f
  34EC                	CHKSTK:
  34EC  EB            		ex	de,hl
  34ED  21C6FF        		ld	hl,0-003bh+1
  34F0  39            		add	hl,sp
  34F1  ED52          		sbc	hl,de		;c-flag=1
  34F3  DAEB03        		jp	c,OMERR
  34F6  EB            		ex	de,hl
  34F7  C9            		ret
                      	
                      	
                      	;reset stack and variables
                      	;destroy: a,bc,de
  34F8                	_RESSTK:ds	RESSTK-_RESSTK
  34F9                		org	RESSTK
                      	
  34F9  EB            		ex	de,hl
  34FA  C1            		pop	bc		;return address
  34FB  ED7B5BFA      		ld	sp,(STACK)
  34FF  C5            		push	bc		;return address
  3500  3EFF          		ld	a,0ffh
  3502  3255FF        		ld	(STOPAD+1),a
                      	
  3505  2A56FF        		ld	hl,(VARAD)
  3508  2258FF        		ld	(ARRAD),hl
  350B  225AFF        		ld	(FREEAD),hl
  350E                	RSTRZ:
  350E  2A29FF        		ld	hl,(BASICAD)
  3511  225CFF        		ld	(DATAAD),hl
  3514  EB            		ex	de,hl
  3515  C9            		ret
                      	
                      	
                      	;RESTORE command
  3516                	C_RSTR:
  3516  CD5F3F        		call	CHKCLN
  3519  224EFF        		ld	(PROGAD),hl
  351C  28F0          		jr	z,RSTRZ
  351E  CD6B07        		call	GETLN
  3521  224EFF        		ld	(PROGAD),hl
  3524  CDF004        		call	SRCHLN
  3527  D2EE03        		jp	nc,ULERR
  352A  ED435CFF      		ld	(DATAAD),bc
  352E  C9            		ret
                      	
                      	
                      	;STOP command
  352F                	C_STOP:
  352F  CD5F3F        		call	CHKCLN
  3532  C2DC03        		jp	nz,SNERR
  3535  F1            		pop	af		;cancel return address
                      	
  3536                	STOP1:
  3536  7C            		ld	a,h
  3537  FEFA          		cp	0fah
  3539  300F          		jr	nc,STOPNC
  353B                	STOP2:
  353B  2254FF        		ld	(STOPAD),hl
  353E                	STOP3:
  353E  2A8FFD        		ld	hl,(SCREEN2)	;l=(SCREEN2),h=(SCREEN3)
  3541  22AAFE        		ld	(STOPSC2),hl	;(STOPSC2)=l,(STOPSC3)=h
  3544  2A5DFA        		ld	hl,(LINENUM)
  3547  2252FF        		ld	(STOPLN),hl
  354A                	STOPNC:
  354A  FB            		ei
  354B  AF            		xor	a
  354C  3218FA        		ld	(STOPFLG),a
                      	
  354F  CD5A09        		call	PRTNLX
  3552  CD7B13        		call	CHGTXT
  3555  215E35        		ld	hl,BREAK
  3558  CDCF30        		call	PUTS
  355B  C32F04        		jp	PRTLNUM
                      	
  355E                	BREAK:
  355E  07427265616B00		db	07h, "Break", 00h
                      	
                      	
                      	;END command
  3565                	C_END:
  3565  F1            		pop	af		;cancel stack
  3566  C34204        		jp	EDIT
                      	
                      	
                      	;CONT command
  3569                	C_CONT:
  3569  F1            		pop	af		;cancel return address
                      	
  356A  7C            		ld	a,h
  356B  FEFA          		cp	0fah
  356D  DA0904        		jp	c,CNERR
                      	
  3570  2A54FF        		ld	hl,(STOPAD)
  3573  7C            		ld	a,h
  3574  FEFA          		cp	0fah
  3576  D24204        		jp	nc,EDIT		;if direct command mode
                      	
  3579  9F            		sbc	a,a		;c-flag=1
  357A  3255FF        		ld	(STOPAD+1),a	;a=ffh
                      	
  357D  EB            		ex	de,hl
  357E  2A52FF        		ld	hl,(STOPLN)
  3581  225DFA        		ld	(LINENUM),hl
  3584  2AAAFE        		ld	hl,(STOPSC2)	;l=(STOPSC2),h=(STOPSC3)
  3587  7D            		ld	a,l
  3588  CD0C14        		call	CHGACT
  358B  7C            		ld	a,h
  358C  CDED13        		call	CHGDSP
  358F  EB            		ex	de,hl
                      	
  3590  C30A07        		jp	INTPRT
                      	
                      	
                      	;CLEAR command
  3593                	C_CLR:
  3593  CD5F3F        		call	CHKCLN
  3596  CAF934        		jp	z,RESSTK
  3599  CD1A0E        		call	INT2ARG
  359C  CB7A          		bit	7,d
  359E  C2E503        		jp	nz,FCERR
  35A1  D5            		push	de		;1st parameter
  35A2  ED5B27FF      		ld	de,(STREND)
  35A6  7E            		ld	a,(hl)
  35A7  FE2C          		cp	','
  35A9  2008          		jr	nz,CLRNZ
  35AB  23            		inc	hl
  35AC  CD720B        		call	NUMARGMO
  35AF  CDBD0C        		call	FTOI
  35B2  1B            		dec	de
                      	
  35B3                	CLRNZ:
  35B3  2A8DFD        		ld	hl,(USREND)
  35B6  E7            		rst	CPHLDE
  35B7  DAE503        		jp	c,FCERR
  35BA  EB            		ex	de,hl
  35BB  C1            		pop	bc		;1st parameter
                      	
                      	;input: bc=1st parameter, hl=2nd parameter-1
  35BC                	CLRMAIN:
  35BC  E5            		push	hl		;2nd parameter-1
  35BD  B7            		or	a
  35BE  ED42          		sbc	hl,bc
                      	;	jp	c,OMERR
  35C0  380A          		jr	c,CLRC
  35C2  EB            		ex	de,hl
  35C3  2A56FF        		ld	hl,(VARAD)
  35C6  013800        		ld	bc,0038h
  35C9  09            		add	hl,bc
  35CA  EB            		ex	de,hl
  35CB  E7            		rst	CPHLDE
  35CC                	CLRC:
  35CC  DAEB03        		jp	c,OMERR
  35CF  225BFA        		ld	(STACK),hl
  35D2  E1            		pop	hl		;2nd parameter-1
  35D3  2227FF        		ld	(STREND),hl
  35D6  223DFF        		ld	(STRAD),hl
  35D9  C3F934        		jp	RESSTK
                      	
                      	
                      	;NEXT command
  35DC                	C_NEXT:
  35DC  F1            		pop	af		;cancel return address
  35DD  0600          		ld	b,00h
  35DF                	NEXTLP:
  35DF  CD5F3F        		call	CHKCLN
  35E2  C47A33        		call	nz,CHKVAR
  35E5  224EFF        		ld	(PROGAD),hl
                      	
  35E8                	NEXTVLP:
  35E8  CDD107        		call	SRCHSTK
  35EB  CAD903        		jp	z,NFERR		;not found or gosub identifier found
  35EE  33            		inc	sp		;identifier
                      	
  35EF  D1            		pop	de		;variable address
                      	
  35F0  78            		ld	a,b
  35F1  B7            		or	a
  35F2  2813          		jr	z,NEXTOK
                      	
  35F4  1B            		dec	de
  35F5  1A            		ld	a,(de)
  35F6  B9            		cp	c
  35F7  2007          		jr	nz,NEXTNZ
  35F9  1B            		dec	de
  35FA  1A            		ld	a,(de)
  35FB  13            		inc	de
  35FC  13            		inc	de
  35FD  B8            		cp	b
  35FE  2807          		jr	z,NEXTOK
  3600                	NEXTNZ:
  3600  211000        		ld	hl,0010h	;STEP(6)+TO(6)+line number(2)+program address(2)
  3603  39            		add	hl,sp
  3604  F9            		ld	sp,hl
  3605  18E1          		jr	NEXTVLP
                      	
  3607                	NEXTOK:
  3607  CD3D39        		call	POPF1		;STEP value
  360A  CD1F39        		call	PUSHF1		;STEP value
                      	
  360D  D5            		push	de		;variable address
  360E  EB            		ex	de,hl
  360F  CD8036        		call	SETADDF
  3612  D1            		pop	de		;variable address
  3613  D5            		push	de		;variable address
  3614  2166FF        		ld	hl,FAC1
  3617  CD300C        		call	SETF
                      	
  361A  D1            		pop	de		;variable address
                      	
  361B  CD4C39        		call	POPF2		;STEP value
  361E  CD3D39        		call	POPF1		;TO value
  3621  CD1F39        		call	PUSHF1		;TO value
  3624  CD2E39        		call	PUSHF2		;STEP value
                      	
  3627  D5            		push	de		;variable address
                      	
  3628  2A70FF        		ld	hl,(FAC2+3)
  362B  7C            		ld	a,h
  362C  B7            		or	a		;z-flag
  362D  7D            		ld	a,l
  362E  07            		rlca			;c-flag
  362F  EB            		ex	de,hl		;variable address
  3630  CD360C        		call	SETF2		;no change in z,c-flag
  3633  2822          		jr	z,STEP0		;if STEP value = 0
  3635  DC6E39        		call	c,EXFAC		;if STEP value < 0
  3638  CD1B3F        		call	CMPF
  363B  381F          		jr	c,NEXTEND	;for-next loop end
                      	
  363D                	NEXTCONT:
  363D  210E00        		ld	hl,000eh	;variable address(2)+STEP(6)+TO(6)
  3640  39            		add	hl,sp
  3641  5E            		ld	e,(hl)
  3642  23            		inc	hl
  3643  56            		ld	d,(hl)
  3644  ED535DFA      		ld	(LINENUM),de
  3648  23            		inc	hl
  3649  5E            		ld	e,(hl)
  364A  23            		inc	hl
  364B  56            		ld	d,(hl)
  364C  ED534EFF      		ld	(PROGAD),de
  3650  3E81          		ld	a,FOR_
  3652  F5            		push	af		;identifier
  3653  33            		inc	sp
  3654  C3E706        		jp	INTPEND
                      	
  3657                	STEP0:
  3657  CD1B3F        		call	CMPF
  365A  20E1          		jr	nz,NEXTCONT
                      	
  365C                	NEXTEND:
  365C  211200        		ld	hl,0012h	;variable address(2)+STEP(6)+TO(6)+line number(2)+program address(2)
  365F  39            		add	hl,sp
  3660  F9            		ld	sp,hl
                      	
  3661  2A4EFF        		ld	hl,(PROGAD)
  3664  7E            		ld	a,(hl)
  3665  23            		inc	hl
  3666  FE2C          		cp	','
  3668  C2E706        		jp	nz,INTPEND
  366B  47            		ld	b,a		;not variable name
  366C  C3DF35        		jp	NEXTLP
                      	
                      	
                      	;+ operator
  366F                	O_PLS:
  366F  3A25FF        		ld	a,(ARGTYP)
  3672  B7            		or	a
  3673  281A          		jr	z,ADDF
                      	
  3675                	PLSSTR:
  3675  D603          		sub	03h		;string and string
  3677  CA2C26        		jp	z,ADDSTR
  367A  C3FD03        		jp	TMERR
                      	
                      	
                      	;FAC1=FAC1+1
                      	;destroy: FAC2,af,bc,de,hl
  367D                	INCF1:
  367D  21A10E        		ld	hl,PLSONE
                      	
                      	;set FAC2 and add
                      	;input: FAC1, hl=float address
  3680                	SETADDF:
  3680  CD360C        		call	SETF2
  3683  180A          		jr	ADDF
                      	
                      	
                      	;- operator
  3685                	O_MNS:
  3685  CD7B0B        		call	CHKNUM
                      	;	call	SUBF
                      	;	ret
                      	
                      	
                      	;FAC1 = FAC1 - FAC2
                      	;input: FAC1,FAC2
                      	;output: FAC1
                      	;destroy: FAC2,af,b,de,hl
  3688                	SUBF:
  3688  2170FF        		ld	hl,FAC2+3
  368B                	CHGSGNADD:
  368B  7E            		ld	a,(hl)
  368C  EE80          		xor	80h
  368E  77            		ld	(hl),a
                      	;	call	ADDF
                      	;	ret
                      	
                      	
                      	;FAC1 = FAC1 + FAC2
                      	;input: FAC1,FAC2
                      	;output: FAC1
                      	;destroy: FAC2,af,bc,de,hl
  368F                	ADDF:
  368F  2A70FF        		ld	hl,(FAC2+3)	;l=(FAC2+3),h=(FAC2+4)
  3692  7C            		ld	a,h
  3693  B7            		or	a
  3694  C8            		ret	z		;FAC2=0
                      	
  3695  E5            		push	hl
  3696  CBFD          		set	7,l
  3698  2270FF        		ld	(FAC2+3),hl
                      	
  369B  2A69FF        		ld	hl,(FAC1+3)	;l=(FAC1+3),h=(FAC1+4)
  369E  E5            		push	hl
  369F  CBFD          		set	7,l
  36A1  2269FF        		ld	(FAC1+3),hl
                      	
  36A4  94            		sub	h
  36A5  3814          		jr	c,ADD1GT2	;|FAC1| > |FAC2|
  36A7  2005          		jr	nz,ADD1LT2	;|FAC1| < |FAC2|
                      	
  36A9  CD1B3F        		call	CMPF
  36AC  380D          		jr	c,ADD1GT2	;|FAC1| > |FAC2|
                      	
  36AE                	ADD1LT2:
  36AE  CD6E39        		call	EXFAC
                      	
  36B1  D1            		pop	de		;e=(FAC1+3),d=(FAC1+4)
  36B2  E1            		pop	hl		;l=(FAC2+3),h=(FAC2+4)
                      	
  36B3  7A            		ld	a,d
  36B4  B7            		or	a
  36B5  2006          		jr	nz,ADDCHKSGN
  36B7  2269FF        		ld	(FAC1+3),hl
  36BA  C9            		ret
                      	
  36BB                	ADD1GT2:
  36BB  E1            		pop	hl		;l=(FAC1+3),h=(FAC1+4)
  36BC  D1            		pop	de		;e=(FAC2+3),d=(FAC2+4)
                      	
  36BD                	ADDCHKSGN:
  36BD  7C            		ld	a,h
  36BE  92            		sub	d
  36BF  47            		ld	b,a		;
                      	
  36C0  7D            		ld	a,l
  36C1  F5            		push	af		;sign
                      	
  36C2  AB            		xor	e
  36C3  07            		rlca
  36C4  F5            		push	af		;c-flag=0:same sign
  36C5  3009          		jr	nc,SAMESGN
                      	
  36C7  CD6E39        		call	EXFAC
  36CA  CDEC24        		call	NEGINT4
  36CD  CD6E39        		call	EXFAC
                      	
  36D0                	SAMESGN:
  36D0  0E00          		ld	c,00h		;guard/round/sticky bit
  36D2  2A6DFF        		ld	hl,(FAC2)
  36D5  ED5B6FFF      		ld	de,(FAC2+2)
                      	
  36D9                	ADDFLP1:
  36D9  78            		ld	a,b
  36DA  D608          		sub	08h
  36DC  3818          		jr	c,EXPLT8
  36DE  47            		ld	b,a
                      	
  36DF  7D            		ld	a,l
  36E0  E63F          		and	3fh
  36E2  2004          		jr	nz,SETSBIT
  36E4  0C            		inc	c
  36E5  0D            		dec	c
  36E6  2802          		jr	z,NOSBIT
  36E8                	SETSBIT:
  36E8  0E20          		ld	c,20h		;set sticky bit
  36EA                	NOSBIT:
  36EA  AD            		xor	l
  36EB  B1            		or	c
  36EC  4F            		ld	c,a
  36ED  6C            		ld	l,h
  36EE  63            		ld	h,e
  36EF  5A            		ld	e,d
  36F0  F1            		pop	af		;same sign?
  36F1  F5            		push	af
  36F2  9F            		sbc	a,a		;00h or ffh
  36F3  57            		ld	d,a
  36F4  18E3          		jr	ADDFLP1
                      	
  36F6                	EXPLT8:
  36F6  C608          		add	a,08h
  36F8  2813          		jr	z,SAMEEXP
                      	
  36FA                	ADDFLP2:
  36FA  F1            		pop	af		;same sign?
  36FB  F5            		push	af
  36FC  CB1A          		rr	d
  36FE  CB1B          		rr	e
  3700  CB1C          		rr	h
  3702  CB1D          		rr	l
                      	
  3704  79            		ld	a,c
  3705  CB19          		rr	c
  3707  E620          		and	20h		;sticky bit
  3709  B1            		or	c
                      	;	and	0e0h
  370A  4F            		ld	c,a
  370B  10ED          		djnz	ADDFLP2
                      	
  370D                	SAMEEXP:
  370D  226DFF        		ld	(FAC2),hl
  3710  ED536FFF      		ld	(FAC2+2),de
                      	
  3714  EB            		ex	de,hl
                      	
  3715  2A66FF        		ld	hl,(FAC1)
                      	;	ld	de,(FAC2)
  3718  19            		add	hl,de
  3719  2266FF        		ld	(FAC1),hl
  371C  2A68FF        		ld	hl,(FAC1+2)
  371F  ED5B6FFF      		ld	de,(FAC2+2)
  3723  ED5A          		adc	hl,de
  3725  2268FF        		ld	(FAC1+2),hl
                      	
  3728  17            		rla
  3729  E1            		pop	hl		;l-bit0=same sign?
  372A  AD            		xor	l
  372B  0F            		rrca
  372C  301A          		jr	nc,SEARCH1
                      	
  372E  216AFF        		ld	hl,FAC1+4
  3731  34            		inc	(hl)
  3732  CAE803        		jp	z,OVERR
                      	
                      	;	scf			;c-flag=1
  3735  2D            		dec	l		;dec hl,FAC1+3
  3736  CB1E          		rr	(hl)
  3738  2D            		dec	l		;dec hl,FAC1+2
  3739  CB1E          		rr	(hl)
  373B  2D            		dec	l		;dec hl,FAC1+1
  373C  CB1E          		rr	(hl)
  373E  2D            		dec	l		;dec hl,FAC1
  373F  CB1E          		rr	(hl)
                      	
  3741  79            		ld	a,c
  3742  CB19          		rr	c
  3744  E620          		and	20h		;sticky bit
  3746  B1            		or	c
                      	;	and	0e0h
  3747  4F            		ld	c,a
                      	
  3748                	SEARCH1:
  3748  2A66FF        		ld	hl,(FAC1)
  374B  ED5B68FF      		ld	de,(FAC1+2)
                      	;	ld	a,h
                      	;	or	l
                      	;	or	d
                      	;	or	e
                      	;	or	c
                      	;	jp	z,MULFZERO
                      	
  374F                	ADDFLP3:
  374F  CB7A          		bit	7,d
  3751  2014          		jr	nz,CHKROUND
  3753  CB21          		sla	c
  3755  ED6A          		adc	hl,hl
  3757  CB13          		rl	e
  3759  CB12          		rl	d
  375B  3A6AFF        		ld	a,(FAC1+4)
  375E  3D            		dec	a
  375F  326AFF        		ld	(FAC1+4),a
  3762  20EB          		jr	nz,ADDFLP3
  3764  C33338        		jp	MULFZERO	;pop af/jp SETZERO
                      	
                      	;round to the nearest even
  3767                	CHKROUND:
  3767  7D            		ld	a,l
  3768  0F            		rrca
  3769  79            		ld	a,c
  376A  8F            		adc	a,a
  376B  3015          		jr	nc,ADDSGN	;guard bit=0
  376D  2813          		jr	z,ADDSGN	;round bit=0 and sticky bit=0 and l=even
                      	
                      	;round up
  376F  23            		inc	hl
  3770  7C            		ld	a,h
  3771  B5            		or	l
  3772  200E          		jr	nz,ADDSGN
  3774  13            		inc	de
  3775  7A            		ld	a,d
  3776  B3            		or	e
  3777  2009          		jr	nz,ADDSGN
                      	
  3779  216AFF        		ld	hl,FAC1+4
  377C  34            		inc	(hl)
  377D  CAE803        		jp	z,OVERR
                      	;	ld	d,80h
  3780  63            		ld	h,e		;e=0
  3781  6B            		ld	l,e		;e=0
                      	
  3782                	ADDSGN:
  3782  F1            		pop	af		;sign
  3783  AA            		xor	d
  3784  E680          		and	80h
  3786  AA            		xor	d
  3787  57            		ld	d,a
                      	
                      	;set 4-byte integer in FAC1
                      	;input: de-hl
  3788                	SETI4:
  3788  2266FF        		ld	(FAC1),hl
  378B  ED5368FF      		ld	(FAC1+2),de
  378F  C9            		ret
                      	
                      	
                      	;FAC1 = FAC1 * 10
                      	;input: FAC1
                      	;output: FAC1
                      	;destroy: FAC2,af,bc,de,hl
  3790                	MULF10:
  3790  219837        		ld	hl,TEN
  3793  CD360C        		call	SETF2
  3796  1808          		jr	MULF
                      	
  3798                	TEN:
  3798  0000002084    		db	00h, 00h, 00h, 20h, 84h
                      	
                      	
                      	;* operator
  379D                	O_MUL:
  379D  CD7B0B        		call	CHKNUM
                      	;	call	MULF
                      	;	ret
                      	
                      	
                      	;FAC1 = FAC1 * FAC2
                      	;input: FAC1,FAC2
                      	;output: FAC1
                      	;destroy: FAC2,af,bc,de,hl
  37A0                	MULF:
                      	
                      	;result=bc-de-de'-hl'
                      	
  37A0  3A6AFF        		ld	a,(FAC1+4)
  37A3  B7            		or	a
  37A4  C8            		ret	z
                      	
  37A5  2171FF        		ld	hl,FAC2+4
  37A8  7E            		ld	a,(hl)
  37A9  B7            		or	a
  37AA  CA2A0C        		jp	z,SETZERO
                      	
  37AD  2D            		dec	l		;dec hl,FAC2+3
  37AE  7E            		ld	a,(hl)
  37AF  CBFE          		set	7,(hl)
  37B1  2E69          		ld	l,FAC1+3-(FAC1+3)/256*256	;hl=FAC1+3
  37B3  AE            		xor	(hl)
  37B4  F5            		push	af		;sign
  37B5  CBFE          		set	7,(hl)
                      	
  37B7  010000        		ld	bc,0000h
  37BA  50            		ld	d,b
  37BB  59            		ld	e,c
                      	
  37BC  D9            		exx
  37BD  E5            		push	hl
  37BE  D5            		push	de
  37BF  C5            		push	bc
                      	
  37C0  2A66FF        		ld	hl,(FAC1)
  37C3  ED5B68FF      		ld	de,(FAC1+2)
                      	;	or	a		;c-flag=0
                      	
  37C7  0620          		ld	b,20h
  37C9                	MULFLP:
                      	;shift result and FAC1
  37C9  D9            		exx
  37CA  CB18          		rr	b
  37CC  CB19          		rr	c
  37CE  CB1A          		rr	d
  37D0  CB1B          		rr	e
  37D2  D9            		exx
  37D3  CB1A          		rr	d
  37D5  CB1B          		rr	e
  37D7  CB1C          		rr	h
  37D9  CB1D          		rr	l
  37DB  300E          		jr	nc,MULFNC1
                      	
                      	;add
  37DD  D9            		exx
  37DE  2A6DFF        		ld	hl,(FAC2)
  37E1  19            		add	hl,de
  37E2  EB            		ex	de,hl
  37E3  2A6FFF        		ld	hl,(FAC2+2)
  37E6  ED4A          		adc	hl,bc
  37E8  44            		ld	b,h
  37E9  4D            		ld	c,l
  37EA  D9            		exx
  37EB                	MULFNC1:
  37EB  10DC          		djnz	MULFLP
                      	
  37ED  D9            		exx
  37EE  216AFF        		ld	hl,FAC1+4
  37F1  300D          		jr	nc,MULFNC2
                      	
                      	;shift result
  37F3  CB18          		rr	b
  37F5  CB19          		rr	c
  37F7  CB1A          		rr	d
  37F9  CB1B          		rr	e
  37FB  D9            		exx
  37FC  CB1A          		rr	d
                      	;	rr	e
                      	;	rr	h
                      	;	rr	l
  37FE  D9            		exx
  37FF  34            		inc	(hl)
                      	
  3800                	MULFNC2:
  3800  35            		dec	(hl)		;;
                      	
                      	;round to the nearest even
  3801  D9            		exx
  3802  7A            		ld	a,d
  3803  8F            		adc	a,a		;check d-bit7
  3804  3041          		jr	nc,NOROUNDUP
  3806  B3            		or	e
  3807  B4            		or	h
  3808  B5            		or	l
  3809  C1            		pop	bc
  380A  D1            		pop	de
  380B  E1            		pop	hl
  380C  D9            		exx
  380D  2004          		jr	nz,ROUNDUP
  380F  7B            		ld	a,e
  3810  0F            		rrca
  3811  300E          		jr	nc,MULFEND
                      	
  3813                	ROUNDUP:
  3813  13            		inc	de
  3814  7A            		ld	a,d
  3815  B3            		or	e
  3816  2009          		jr	nz,MULFEND
  3818  03            		inc	bc
  3819  78            		ld	a,b
  381A  B1            		or	c
  381B  2004          		jr	nz,MULFEND
                      	;	ld	b,80h
  381D  34            		inc	(hl)
  381E  CAE803        		jp	z,OVERR
                      	
  3821                	MULFEND:
  3821  ED5366FF      		ld	(FAC1),de
  3825  ED4368FF      		ld	(FAC1+2),bc
                      	
                      	;exponent
  3829  3A71FF        		ld	a,(FAC2+4)
  382C  86            		add	a,(hl)
  382D  3808          		jr	c,MULFC
  382F  D680          		sub	80h		;;not 81h
  3831  3009          		jr	nc,MULEXP
                      	
  3833                	MULFZERO:
  3833  F1            		pop	af		;cancel
  3834  C32A0C        		jp	SETZERO
                      	
  3837                	MULFC:
  3837  D680          		sub	80h		;;not 81h
  3839                	MULFCHKOV:
  3839  D2E803        		jp	nc,OVERR
  383C                	MULEXP:
  383C  77            		ld	(hl),a		;exponent
  383D  28F4          		jr	z,MULFZERO
  383F  2D            		dec	l		;dec hl,FAC1+3
  3840  F1            		pop	af		;sign
  3841  07            		rlca
  3842  7E            		ld	a,(hl)
  3843  17            		rla
  3844  0F            		rrca
  3845  77            		ld	(hl),a
  3846  C9            		ret
                      	
  3847                	NOROUNDUP:
  3847  C1            		pop	bc
  3848  D1            		pop	de
  3849  E1            		pop	hl
  384A  D9            		exx
  384B  18D4          		jr	MULFEND
                      	
                      	
                      	;FAC1 = FAC1 * 0.1
                      	;input: FAC1
                      	;output: FAC1
                      	;destroy: FAC2,af,bc,de,hl
  384D                	DIVF10:
  384D  215638        		ld	hl,EM1
  3850  CD360C        		call	SETF2
  3853  C3A037        		jp	MULF
                      	
                      	;0.1
  3856                	EM1:
  3856  CDCCCC4C7D    		db	0cdh, 0cch, 0cch, 4ch, 7dh
                      	
                      	
                      	;/ operator
  385B                	O_DIV:
  385B  CD7B0B        		call	CHKNUM
                      	;	call	DIVF
                      	;	ret
                      	
                      	
                      	;FAC1 = FAC1 / FAC2
                      	;input: FAC1,FAC2
                      	;output: FAC1
                      	;destroy: FAC2,af,bc,de,hl
  385E                	DIVF:
  385E  3A71FF        		ld	a,(FAC2+4)
  3861  B7            		or	a
  3862  CAF703        		jp	z,DIV0ERR
                      	
  3865  216AFF        		ld	hl,FAC1+4
  3868  7E            		ld	a,(hl)
  3869  B7            		or	a
  386A  C8            		ret	z
                      	
  386B  2D            		dec	l		;dec hl, FAC1+3
  386C  7E            		ld	a,(hl)
  386D  CBFE          		set	7,(hl)
  386F  2E70          		ld	l,FAC2+3-(FAC2+3)/256*256	;hl<-FAC2+3
  3871  AE            		xor	(hl)
  3872  CBFE          		set	7,(hl)
  3874  F5            		push	af		;sign
                      	
  3875  210000        		ld	hl,0000h
  3878  54            		ld	d,h
  3879  5D            		ld	e,l
                      	
  387A  D9            		exx			;
  387B  E5            		push	hl
  387C  D5            		push	de
                      	
  387D  CD0225        		call	CMPINT4
  3880  3007          		jr	nc,DIVFNC
  3882  216AFF        		ld	hl,FAC1+4
  3885  35            		dec	(hl)
  3886  CD3A3F        		call	SLAF1
                      	
  3889                	DIVFNC:
  3889  0620          		ld	b,20h
  388B                	DIVFLP:
  388B  3805          		jr	c,DIVFC1
  388D  CD0225        		call	CMPINT4
  3890  381A          		jr	c,DIVFC2
  3892                	DIVFC1:
  3892  2A66FF        		ld	hl,(FAC1)
  3895  ED5B6DFF      		ld	de,(FAC2)
  3899  B7            		or	a
  389A  ED52          		sbc	hl,de
  389C  2266FF        		ld	(FAC1),hl
  389F  2A68FF        		ld	hl,(FAC1+2)
  38A2  ED5B6FFF      		ld	de,(FAC2+2)
  38A6  ED52          		sbc	hl,de
  38A8  2268FF        		ld	(FAC1+2),hl
  38AB  B7            		or	a		;reset c-flag
  38AC                	DIVFC2:
  38AC  3F            		ccf
  38AD  D9            		exx			;
  38AE  ED6A          		adc	hl,hl
  38B0  CB13          		rl	e
  38B2  CB12          		rl	d
  38B4  D9            		exx			;
                      	
  38B5  CD3A3F        		call	SLAF1
  38B8  10D1          		djnz	DIVFLP
                      	
  38BA  17            		rla			;store c-flag
  38BB  CD0225        		call	CMPINT4		;no change in a
  38BE  D1            		pop	de
  38BF  E1            		pop	hl
  38C0  D9            		exx
                      	
  38C1  1F            		rra
  38C2  3809          		jr	c,DIVFUP
  38C4  07            		rlca
  38C5  381B          		jr	c,DIVFNOUP
                      	
                      	;round to the nearest even
  38C7  2004          		jr	nz,DIVFUP
  38C9  7D            		ld	a,l
  38CA  0F            		rrca
  38CB  3015          		jr	nc,DIVFNOUP
                      	
                      	;round up
  38CD                	DIVFUP:
  38CD  23            		inc	hl
  38CE  7C            		ld	a,h
  38CF  B5            		or	l
  38D0  2010          		jr	nz,DIVFNOUP
  38D2  13            		inc	de
  38D3  7A            		ld	a,d
  38D4  B3            		or	e
  38D5  200B          		jr	nz,DIVFNOUP
  38D7  216AFF        		ld	hl,FAC1+4
  38DA  34            		inc	(hl)
  38DB  CAE803        		jp	z,OVERR
  38DE  EB            		ex	de,hl		;hl<-0
  38DF  110080        		ld	de,8000h
                      	
  38E2                	DIVFNOUP:
  38E2  2266FF        		ld	(FAC1),hl
  38E5  ED5368FF      		ld	(FAC1+2),de
                      	
  38E9  3A71FF        		ld	a,(FAC2+4)
  38EC  47            		ld	b,a
  38ED  216AFF        		ld	hl,FAC1+4
  38F0  7E            		ld	a,(hl)
  38F1  90            		sub	b
  38F2  3806          		jr	c,DIVF1LT2
                      	
                      	;FAC1>=FAC2
  38F4  C681          		add	a,81h
  38F6  3F            		ccf
  38F7  C33938        		jp	MULFCHKOV
                      	
                      	
                      	;FAC1<FAC2
  38FA                	DIVF1LT2:
  38FA  F22A0C        		jp	p,SETZERO
  38FD  C681          		add	a,81h
  38FF  C33C38        		jp	MULEXP
                      	
                      	
                      	;continued from CHKSGN
  3902                	CHKSGN2:
  3902  3A69FF        		ld	a,(FAC1+3)
  3905  07            		rlca
  3906  9F            		sbc	a,a		;00h or ffh
  3907  D8            		ret	c		;ffh
  3908  3C            		inc	a
  3909  C9            		ret			;01h
                      	
                      	
                      	;SGN() function
  390A                	F_SGN:
  390A  CD7B0B        		call	CHKNUM
  390D  EF            		rst	CHKSGN
  390E  C8            		ret	z
  390F  3C            		inc	a
  3910  CA3B0C        		jp	z,SETMNS1
  3913  C34122        		jp	SETPLS1
                      	
                      	
                      	;ABS() function
  3916                	F_ABS:
  3916  CD7B0B        		call	CHKNUM
  3919                	ABS:
  3919  2169FF        		ld	hl,FAC1+3
  391C  CBBE          		res	7,(hl)
  391E  C9            		ret
                      	
                      	
                      	;push FAC1
                      	;input: FAC1
                      	;output: sp=sp-6
                      	;destroy: af,hl
  391F                	PUSHF1:
  391F  F1            		pop	af		;return address
  3920  2A69FF        		ld	hl,(FAC1+3)
  3923  E5            		push	hl
  3924  2A67FF        		ld	hl,(FAC1+1)
  3927  E5            		push	hl
  3928  2A65FF        		ld	hl,(FAC1-1)
  392B  E5            		push	hl
  392C  F5            		push	af		;
  392D  C9            		ret
                      	
                      	
                      	;push FAC2
                      	;input: FAC2
                      	;output: sp=sp-6
                      	;destroy: af,hl
  392E                	PUSHF2:
  392E  F1            		pop	af		;return address
  392F  2A70FF        		ld	hl,(FAC2+3)
  3932  E5            		push	hl
  3933  2A6EFF        		ld	hl,(FAC2+1)
  3936  E5            		push	hl
  3937  2A6CFF        		ld	hl,(FAC2-1)
  393A  E5            		push	hl
  393B  F5            		push	af		;
  393C  C9            		ret
                      	
                      	
                      	;pop FAC1
                      	;input: none
                      	;output: FAC1,sp=sp+6
                      	;destroy: af,hl
  393D                	POPF1:
  393D  F1            		pop	af		;return address
  393E  E1            		pop	hl
  393F  2265FF        		ld	(FAC1-1),hl
  3942  E1            		pop	hl
  3943  2267FF        		ld	(FAC1+1),hl
  3946  E1            		pop	hl
  3947  2269FF        		ld	(FAC1+3),hl
  394A  F5            		push	af		;
  394B  C9            		ret
                      	
                      	
                      	;pop FAC2
                      	;input: none
                      	;output: FAC2,sp=sp+6
                      	;destroy: af,hl
  394C                	POPF2:
  394C  F1            		pop	af		;return address
  394D  E1            		pop	hl
  394E  226CFF        		ld	(FAC2-1),hl
  3951  E1            		pop	hl
  3952  226EFF        		ld	(FAC2+1),hl
  3955  E1            		pop	hl
  3956  2270FF        		ld	(FAC2+3),hl
  3959  F5            		push	af		;
  395A  C9            		ret
                      	
                      	
                      	;copy FAC1 to FAC2
                      	;input: FAC1
                      	;output: FAC2
                      	;destroy: hl
  395B                	CPYFAC:
  395B  2A65FF        		ld	hl,(FAC1-1)
  395E  226CFF        		ld	(FAC2-1),hl
  3961  2A67FF        		ld	hl,(FAC1+1)
  3964  226EFF        		ld	(FAC2+1),hl
  3967  2A69FF        		ld	hl,(FAC1+3)
  396A  2270FF        		ld	(FAC2+3),hl
  396D  C9            		ret
                      	
                      	
                      	;exchange FAC1 and FAC2
                      	;input: FAC1,FAC2
                      	;output: FAC1,FAC2
                      	;destroy: hl
  396E                	EXFAC:
  396E  F5            		push	af
  396F  CD2E39        		call	PUSHF2
  3972  CD5B39        		call	CPYFAC
  3975  CD3D39        		call	POPF1
  3978  F1            		pop	af
  3979  C9            		ret
                      	
                      	
                      	;print 1-byte integer in device (unsigned)
                      	;input: a
                      	;destroy: af,bc,de,hl
  397A                	_PUTI1:	ds	PUTI1-_PUTI1
  397A                		org	PUTI1
                      	
  397A  2600          		ld	h,00h
  397C  6F            		ld	l,a
  397D  C3A13A        		jp	PUTI2
                      	
                      	
                      	;INT() function
                      	;(round toward minus infinity)
  3980                	F_INT:
  3980  CD7B0B        		call	CHKNUM
  3983                	INT:
  3983  CD5B39        		call	CPYFAC
  3986  CDEE0C        		call	GETINT
  3989  3A70FF        		ld	a,(FAC2+3)
  398C  07            		rlca
  398D  D0            		ret	nc
  398E  CD1B3F        		call	CMPF
  3991  C8            		ret	z
  3992  21A60E        		ld	hl,MNSONE
  3995  C38036        		jp	SETADDF
                      	
                      	
                      	;convert string to float
                      	;input: a=max length, hl=program address
                      	;output: FAC1, hl=next address (except for VAL())
                      	;destroy: FAC2, af,bc,de
  3998                	ATOF:
  3998  47            		ld	b,a
  3999  CD893A        		call	SKIPSPB
  399C  CA2A0C        		jp	z,SETZERO
  399F  FE26          		cp	'&'
  39A1  CA383A        		jp	z,ATOFHEX
  39A4  2B            		dec	hl
                      	
  39A5  CD0128        		call	ATOIF
                      	
  39A8  CD893A        		call	SKIPSPB
  39AB  C8            		ret	z
  39AC  0E00          		ld	c,00h		;after-dot counter
  39AE  FE2E          		cp	'.'
  39B0  280F          		jr	z,ATOFDOT
  39B2  05            		dec	b
  39B3  F620          		or	20h
  39B5  FE65          		cp	'e'
  39B7  281B          		jr	z,ATOFEXP
  39B9  2B            		dec	hl
  39BA  C9            		ret
                      	
  39BB                	ATOFLP1:
  39BB  CD2A28        		call	CTOF
  39BE  3005          		jr	nc,CHKEXP
  39C0  0C            		inc	c
  39C1                	ATOFDOT:
  39C1  10F8          		djnz	ATOFLP1
  39C3  180F          		jr	ATOFEXP
                      	
  39C5                	CHKEXP:
  39C5  CD893A        		call	SKIPSPB
  39C8  280A          		jr	z,ATOFEXP
  39CA  05            		dec	b
  39CB  F620          		or	20h
  39CD  FE65          		cp	'e'
  39CF  2803          		jr	z,ATOFEXP
                      	
  39D1  2B            		dec	hl
  39D2  0600          		ld	b,00h
  39D4                	ATOFEXP:
  39D4  EB            		ex	de,hl
  39D5  CD1F39        		call	PUSHF1
  39D8  EB            		ex	de,hl
                      	
  39D9  C5            		push	bc		;c=after-dot counter
  39DA  78            		ld	a,b
  39DB  B7            		or	a
  39DC  2805          		jr	z,ATOFE0
  39DE  CD0128        		call	ATOIF
  39E1  1805          		jr	CALCEXP
                      	
  39E3                	ATOFE0:
  39E3  E5            		push	hl		;program address
  39E4  CD2A0C        		call	SETZERO
  39E7  E1            		pop	hl		;program address
                      	
  39E8                	CALCEXP:
  39E8  C1            		pop	bc		;c=after-dot counter
  39E9  E5            		push	hl		;program address
  39EA  CD1F39        		call	PUSHF1
  39ED  69            		ld	l,c
  39EE  CD660C        		call	I1TOF
  39F1  CD4C39        		call	POPF2
  39F4  CD0F3E        		call	SUBF21
                      	
  39F7  D1            		pop	de		;program address
  39F8  CD5B39        		call	CPYFAC
  39FB  CD3D39        		call	POPF1
  39FE  D5            		push	de		;program address
                      	
  39FF  2170FF        		ld	hl,FAC2+3
  3A02  7E            		ld	a,(hl)
  3A03  F5            		push	af		;E sign
  3A04  CBBE          		res	7,(hl)
                      	
  3A06                	ATOFLP2:
  3A06  3A71FF        		ld	a,(FAC2+4)
  3A09  B7            		or	a
  3A0A  2829          		jr	z,ATOFEND	;E0
                      	
  3A0C  3A6AFF        		ld	a,(FAC1+4)
  3A0F  B7            		or	a
  3A10  2823          		jr	z,ATOFEND	;=0
                      	
  3A12  CD1F39        		call	PUSHF1
  3A15  CD3B0C        		call	SETMNS1
  3A18  CD8F36        		call	ADDF
  3A1B  CD5B39        		call	CPYFAC
  3A1E  CD3D39        		call	POPF1
                      	
  3A21  C1            		pop	bc		;b=E sign
  3A22  C5            		push	bc
  3A23  CD2E39        		call	PUSHF2
  3A26  78            		ld	a,b
  3A27  07            		rlca
  3A28  F5            		push	af
  3A29  D49037        		call	nc,MULF10
  3A2C  F1            		pop	af
  3A2D  DC4D38        		call	c,DIVF10
  3A30  CD4C39        		call	POPF2
                      	
  3A33  18D1          		jr	ATOFLP2
                      	
  3A35                	ATOFEND:
  3A35  F1            		pop	af
  3A36  E1            		pop	hl		;program address
  3A37  C9            		ret
                      	
  3A38                	ATOFHEX:
  3A38  2B            		dec	hl
  3A39  05            		dec	b
  3A3A  CD433A        		call	HEX
  3A3D  EB            		ex	de,hl
  3A3E  CD180D        		call	S2TOF
  3A41  EB            		ex	de,hl
  3A42  C9            		ret
                      	
                      	
                      	;convert hexadecimal string to integer
                      	;input: hl="&" address, b=max length-1("&")
                      	;output: de=decimal, hl=next address
                      	;destroy: af,bc
  3A43                	HEX:
  3A43  110000        		ld	de,0000h
  3A46                	HEXLP1:
  3A46  23            		inc	hl
  3A47  7E            		ld	a,(hl)
  3A48  FE20          		cp	' '
  3A4A  2003          		jr	nz,CHKH
  3A4C  10F8          		djnz	HEXLP1
  3A4E  C9            		ret
                      	
  3A4F                	CHKH:
  3A4F  F620          		or	20h
  3A51  FE68          		cp	'h'
  3A53  C2DC03        		jp	nz,SNERR
  3A56  05            		dec	b
  3A57  0E05          		ld	c,05h
                      	
  3A59                	HEXLP2:
  3A59  23            		inc	hl
  3A5A  7E            		ld	a,(hl)
  3A5B  FE20          		cp	' '
  3A5D  281F          		jr	z,HEXEND
  3A5F  FE93          		cp	DEF_		;93h
  3A61  281F          		jr	z,HEXDEF
  3A63  D630          		sub	'0'
  3A65  FE0A          		cp	'9'-'0'+1
  3A67  3809          		jr	c,HEX0F
  3A69  F620          		or	20h
  3A6B  D631          		sub	'a'-'0'
  3A6D  FE06          		cp	'f'-'a'+1
  3A6F  D0            		ret	nc		;not hex
  3A70  C60A          		add	a,0ah
  3A72                	HEX0F:
  3A72  0D            		dec	c
  3A73  CAE803        		jp	z,OVERR
  3A76  EB            		ex	de,hl
  3A77  29            		add	hl,hl		;*2
  3A78  29            		add	hl,hl		;*4
  3A79  29            		add	hl,hl		;*8
  3A7A  29            		add	hl,hl		;*16
  3A7B  85            		add	a,l		;no carry
  3A7C  6F            		ld	l,a
  3A7D  EB            		ex	de,hl
  3A7E                	HEXEND:
  3A7E  10D9          		djnz	HEXLP2
  3A80  23            		inc	hl
  3A81  C9            		ret
                      	
  3A82                	HEXDEF:
  3A82  53            		ld	d,e
  3A83  1EDE          		ld	e,0deh
  3A85  3E0F          		ld	a,0fh
  3A87  18E9          		jr	HEX0F
                      	
                      	
                      	;skip space and decrement b
                      	;input: hl=program address, b=length
                      	;output: a=data, hl=next address, b=remained length, z-flag(1:b=0)
                      	;destroy: f
  3A89                	SKIPSPB:
  3A89  04            		inc	b
  3A8A                	SKIPSPBLP:
  3A8A  05            		dec	b
  3A8B  C8            		ret	z
  3A8C  7E            		ld	a,(hl)
  3A8D  23            		inc	hl
  3A8E  FE20          		cp	' '
  3A90  C0            		ret	nz
  3A91  18F7          		jr	SKIPSPBLP
                      	
                      	
                      	;print "in *****"
                      	;input: hl
                      	;destory: af,bc,de,hl
  3A93                	_INLNUM:
  3A93                		ds	INLNUM-_INLNUM
  3A99                		org	INLNUM
                      	
  3A99  E5            		push	hl		;
  3A9A  218B03        		ld	hl,ERRIN
  3A9D  CDCF30        		call	PUTS
  3AA0  E1            		pop	hl		;
                      	;	jp	PUTI2
                      	
                      	
                      	;print 2-byte integer in device (unsigned)
                      	;input: hl
                      	;destory: af,bc,de,hl
  3AA1                	_PUTI2:	ds	PUTI2-_PUTI2
  3AA1                		org	PUTI2
                      	
  3AA1  11CE30        		ld	de,PUTSINC
  3AA4  D5            		push	de
                      	;	jp	I2TOA
                      	
                      	
                      	;convert 2-byte integer (unsigned) to string
                      	;input: hl
                      	;output: FAC3, hl=FAC3
                      	;destroy: af,bc,de
  3AA5                	_I2TOA:	ds	I2TOA-_I2TOA
  3AA5                		org	I2TOA
                      	
  3AA5  CD680C        		call	I2TOF
  3AA8  C3AC3A        		jp	FTOA
                      	
                      	
                      	;convert float to string
                      	;input: FAC1
                      	;output: FAC3, hl=FAC3
                      	;destroy: FAC1,FAC2,af,bc,de
  3AAB                	_FTOA:	ds	FTOA-_FTOA
  3AAC                		org	FTOA
                      	
  3AAC  2172FF        		ld	hl,FAC3
  3AAF  3620          		ld	(hl),' '
  3AB1  EF            		rst	CHKSGN
  3AB2  CA633B        		jp	z,FTOAZERO	;a=0
  3AB5  3D            		dec	a
  3AB6  2802          		jr	z,FTOAPLS	;if FAC1>0
  3AB8  362D          		ld	(hl),'-'
  3ABA                	FTOAPLS:
  3ABA  CD1939        		call	ABS
                      	
  3ABD                	FTOA2:
  3ABD  216C3B        		ld	hl,E8
  3AC0  CD183F        		call	SETCMPF
  3AC3  3070          		jr	nc,LARGE	;FAC1 >= 1e8
  3AC5  21803B        		ld	hl,EM2
  3AC8  CD183F        		call	SETCMPF
  3ACB  3856          		jr	c,SMALL		;FAC1 < 1e-2
                      	
  3ACD  0609          		ld	b,09h
  3ACF                	FTOALP1:
  3ACF  C5            		push	bc
  3AD0  21713B        		ld	hl,E7
  3AD3  CD183F        		call	SETCMPF
  3AD6  3007          		jr	nc,FTOAOK
  3AD8  CD9037        		call	MULF10
  3ADB  C1            		pop	bc
  3ADC  10F1          		djnz	FTOALP1
  3ADE  C5            		push	bc
                      	
  3ADF                	FTOAOK:
  3ADF  3A6AFF        		ld	a,(FAC1+4)
  3AE2  D697          		sub	97h
  3AE4  47            		ld	b,a
  3AE5  3A66FF        		ld	a,(FAC1)
  3AE8                	FTOALP2:
  3AE8  07            		rlca
  3AE9  10FD          		djnz	FTOALP2
  3AEB  DC7D36        		call	c,INCF1		;round up
                      	
  3AEE  CD870C        		call	FTOI4
                      	
  3AF1  21853B        		ld	hl,INTE7
  3AF4  1173FF        		ld	de,FAC3+1
  3AF7  C1            		pop	bc
  3AF8  48            		ld	c,b		;
  3AF9  05            		dec	b
  3AFA                	FTOALP3:
  3AFA  05            		dec	b
  3AFB  FA073B        		jp	m,FTOADOT
  3AFE  CDD624        		call	DIVINT4
  3B01  C630          		add	a,'0'
  3B03  12            		ld	(de),a
  3B04  13            		inc	de
  3B05  18F3          		jr	FTOALP3
                      	
  3B07                	FTOADOT:
  3B07  CD2925        		call	CHKINT4
  3B0A  2812          		jr	z,FTOAEND
                      	
  3B0C  3E2E          		ld	a,'.'
  3B0E  12            		ld	(de),a
  3B0F  13            		inc	de
                      	
  3B10  79            		ld	a,c		;
  3B11  B7            		or	a
  3B12                	FTOALP4:
  3B12  C4D624        		call	nz,DIVINT4
  3B15  C630          		add	a,'0'
  3B17  12            		ld	(de),a
  3B18  13            		inc	de
  3B19  CD2925        		call	CHKINT4
  3B1C  20F4          		jr	nz,FTOALP4
                      	
  3B1E                	FTOAEND:
  3B1E  12            		ld	(de),a		;a=0
  3B1F  2172FF        		ld	hl,FAC3
  3B22  C9            		ret
                      	
  3B23                	SMALL:
  3B23  0600          		ld	b,00h
  3B25                	SMALLLP:
  3B25  04            		inc	b
  3B26  C5            		push	bc
  3B27  CD9037        		call	MULF10
  3B2A  217B3B        		ld	hl,E0
  3B2D  CD183F        		call	SETCMPF
  3B30  C1            		pop	bc
  3B31  38F2          		jr	c,SMALLLP
  3B33  1810          		jr	LARSMA
                      	
  3B35                	LARGE:
  3B35  0600          		ld	b,00h
  3B37                	LARGELP:
  3B37  04            		inc	b
  3B38  C5            		push	bc
  3B39  CD4D38        		call	DIVF10
  3B3C  21763B        		ld	hl,E2
  3B3F  CD183F        		call	SETCMPF
  3B42  C1            		pop	bc
  3B43  30F2          		jr	nc,LARGELP
                      	
  3B45                	LARSMA:
  3B45  78            		ld	a,b
  3B46  F5            		push	af		;
  3B47  CDBD3A        		call	FTOA2
                      	
  3B4A                	LARLP2:
  3B4A  23            		inc	hl
  3B4B  7E            		ld	a,(hl)
  3B4C  B7            		or	a
  3B4D  20FB          		jr	nz,LARLP2
                      	
  3B4F  3645          		ld	(hl),'E'
  3B51  23            		inc	hl
  3B52  362B          		ld	(hl),'+'
  3B54  F1            		pop	af		;
  3B55  3802          		jr	c,LARPLS
  3B57  362D          		ld	(hl),'-'
                      	
  3B59                	LARPLS:
  3B59  23            		inc	hl
  3B5A  010A2F        		ld	bc,2f0ah	;b='0'-1, c=10
  3B5D                	LARLP3:
  3B5D  04            		inc	b
  3B5E  91            		sub	c
  3B5F  30FC          		jr	nc,LARLP3
  3B61  81            		add	a,c
  3B62  70            		ld	(hl),b
                      	
  3B63                	FTOAZERO:
  3B63  23            		inc	hl
  3B64  C630          		add	a,'0'
  3B66  77            		ld	(hl),a
  3B67  23            		inc	hl
  3B68  EB            		ex	de,hl
  3B69  AF            		xor	a
  3B6A  18B2          		jr	FTOAEND
                      	
                      	
                      	;99999999.5
  3B6C                	E8:
  3B6C  F01FBC3E9B    		db	0f0h, 1fh, 0bch, 3eh, 9bh
                      	
                      	;9999999.95
  3B71                	E7:
  3B71  F37F961898    		db	0f3h, 7fh, 96h, 18h, 98h
                      	
                      	;9.99999995
  3B76                	E2:
  3B76  F3FFFF1F84    		db	0f3h, 0ffh, 0ffh, 1fh, 84h
                      	
                      	;0.999999995
  3B7B                	E0:
  3B7B  EBFFFF7F80    		db	0ebh, 0ffh, 0ffh, 7fh, 80h
                      	
                      	;0.00999999995
  3B80                	EM2:
  3B80  300AD7237A    		db	30h, 0ah, 0d7h, 23h, 7ah
                      	
  3B85                	INTE7:
  3B85  80969800      		db	80h, 96h, 98h, 00h	;10^7
  3B89  40420F00      		db	40h, 42h, 0fh, 00h	;10^6
  3B8D  A0860100      		db	0a0h,86h, 01h, 00h	;10^5
  3B91  10270000      		db	10h, 27h, 00h, 00h	;10^4
  3B95  E8030000      		db	0e8h,03h, 00h, 00h	;10^3
  3B99  64000000      		db	64h, 00h, 00h, 00h	;10^2
  3B9D  0A000000      		db	0ah, 00h, 00h, 00h	;10^1
  3BA1  01000000      		db	01h, 00h, 00h, 00h	;10^0
                      	
                      	
                      	;RND() function
  3BA5                	_F_RND:	ds	RNDPLS-9-_F_RND
  3BA6                		org	RNDPLS-9
  3BA6                	F_RND:
  3BA6  CD7B0B        		call	CHKNUM
  3BA9  EF            		rst	CHKSGN
  3BAA  2815          		jr	z,RND0		;rnd(0)
  3BAC  3C            		inc	a
  3BAD  2806          		jr	z,RNDMNS	;rnd(-x)
                      	
                      	;rnd(+x)
  3BAF                	_RNDPLS:ds	RNDPLS-_RNDPLS
  3BAF                		org	RNDPLS
                      	
  3BAF  2151FA        		ld	hl,RSEED
  3BB2  CD2D0C        		call	SETF1
  3BB5                	RNDMNS:
  3BB5  CD1725        		call	DECINT4
  3BB8  21D53B        		ld	hl,RNDFCT
  3BBB  CD360C        		call	SETF2
  3BBE  CDD93B        		call	MULRND
  3BC1                	RND0:
  3BC1  2A51FA        		ld	hl,(RSEED)
  3BC4  ED5B53FA      		ld	de,(RSEED+2)
  3BC8  CDA60C        		call	I4TOF
  3BCB  216AFF        		ld	hl,FAC1+4
  3BCE  7E            		ld	a,(hl)
  3BCF  B7            		or	a
  3BD0  C8            		ret	z
  3BD1  D620          		sub	20h
  3BD3  77            		ld	(hl),a
  3BD4  C9            		ret
                      	
                      	
  3BD5                	RNDFCT:
  3BD5  65520F00      		db	65h, 52h, 0fh, 00h
                      	
                      	
                      	;RSEED = (FAC1 (integer) * FAC2 (integer)) & ff,ff,ff,ff,ffh
                      	; for RND() function
                      	;input: FAC1,FAC2 (not zero)
                      	;output: RSEED
                      	;destroy: FAC1,af,b,de,hl
  3BD9                	MULRND:
  3BD9  21AB0E        		ld	hl,ZERO
  3BDC  1151FA        		ld	de,RSEED
  3BDF  CD300C        		call	SETF
  3BE2  AF            		xor	a
  3BE3  326AFF        		ld	(FAC1+4),a
                      	
  3BE6  D9            		exx			;
  3BE7  E5            		push	hl
  3BE8  D5            		push	de
  3BE9  C5            		push	bc
  3BEA  D9            		exx			;
                      	
  3BEB  2A6DFF        		ld	hl,(FAC2)
  3BEE  ED5B6FFF      		ld	de,(FAC2+2)
                      	
  3BF2  0620          		ld	b,20h
  3BF4                	MULRLP1:
  3BF4  CB3A          		srl	d
  3BF6  CB1B          		rr	e
  3BF8  CB1C          		rr	h
  3BFA  CB1D          		rr	l
  3BFC  D9            		exx			;
  3BFD  3010          		jr	nc,MULRNC
                      	
  3BFF  1166FF        		ld	de,FAC1
  3C02  2151FA        		ld	hl,RSEED
  3C05  B7            		or	a
  3C06  0605          		ld	b,05h
  3C08                	MULRLP2:
  3C08  1A            		ld	a,(de)
  3C09  8E            		adc	a,(hl)
  3C0A  77            		ld	(hl),a
  3C0B  1C            		inc	e		;inc de
  3C0C  2C            		inc	l		;inc hl
  3C0D  10F9          		djnz	MULRLP2
                      	
  3C0F                	MULRNC:
  3C0F  CD3A3F        		call	SLAF1
  3C12  216AFF        		ld	hl,FAC1+4
  3C15  CB16          		rl	(hl)
  3C17  D9            		exx			;
  3C18  10DA          		djnz	MULRLP1
                      	
  3C1A  D9            		exx			;
  3C1B  C1            		pop	bc
  3C1C  D1            		pop	de
  3C1D  E1            		pop	hl
  3C1E  D9            		exx			;
  3C1F  C9            		ret
                      	
                      	
                      	;EXP() function
                      	;exp(x)=2^n*e^a, n=[x/log2], a=x-n*log2
  3C20                	F_EXP:
  3C20  CD7B0B        		call	CHKNUM
  3C23                	EXP:
  3C23  CD1F39        		call	PUSHF1		;x
  3C26  218E3C        		ld	hl,LOG2
  3C29  CD360C        		call	SETF2
  3C2C  CD5E38        		call	DIVF		;x/log2
                      	
  3C2F  2A69FF        		ld	hl,(FAC1+3)
  3C32  7C            		ld	a,h		;(FAC1+4)
  3C33  FE89          		cp	89h
  3C35  304C          		jr	nc,EXPLARGE
                      	
  3C37  CD8339        		call	INT		;n=[x/log2]
  3C3A  CDBD0C        		call	FTOI
  3C3D  D5            		push	de		;n
  3C3E  218E3C        		ld	hl,LOG2
  3C41  CD360C        		call	SETF2
  3C44  CDA037        		call	MULF		;n*log2
  3C47  D1            		pop	de		;n
  3C48  CD4C39        		call	POPF2
  3C4B  D5            		push	de		;n
  3C4C  CD0F3E        		call	SUBF21		;a=x-n*log2
                      	
                      	;1+a(1+a/2(1+a/3(1+...(1+a/10))))))))))
  3C4F  CD5B39        		call	CPYFAC
  3C52  CD4122        		call	SETPLS1
                      	
  3C55  0E0A          		ld	c,0ah
  3C57                	EXPLP:
  3C57  CD2E39        		call	PUSHF2		;a
  3C5A  C5            		push	bc
  3C5B  CDA037        		call	MULF		;a*y
  3C5E  CD5B39        		call	CPYFAC
  3C61  E1            		pop	hl
  3C62  E5            		push	hl
  3C63  CD660C        		call	I1TOF
  3C66  CD6E39        		call	EXFAC
  3C69  CD5E38        		call	DIVF		;a*y/b
  3C6C  CD7D36        		call	INCF1		;a*y/b+1
  3C6F  C1            		pop	bc
  3C70  CD4C39        		call	POPF2		;a
  3C73  0D            		dec	c
  3C74  20E1          		jr	nz,EXPLP
                      	
  3C76  D1            		pop	de		;n
                      	;	ld	a,(FAC1+4)	;=81h?
  3C77  3E81          		ld	a,81h
  3C79  83            		add	a,e
  3C7A  326AFF        		ld	(FAC1+4),a	;e^a*2^n
  3C7D  79            		ld	a,c		;c=0
  3C7E  8A            		adc	a,d
  3C7F  C8            		ret	z
  3C80  C3E803        		jp	OVERR
                      	
  3C83                	EXPLARGE:
  3C83  CB7D          		bit	7,l		;(FAC1+3)
  3C85  CAE803        		jp	z,OVERR		;x>0
  3C88  CD3D39        		call	POPF1
  3C8B  C32A0C        		jp	SETZERO		;x<0
                      	
                      	;log(2)
  3C8E                	LOG2:
  3C8E  F817723180    		db	0f8h, 17h, 72h, 31h, 80h
                      	
                      	
                      	;LOG() function
                      	;log(x)=log(a*2^n)=log(a)+n*log(2)
                      	;a'=(a-1)/(a+1)
                      	;log(a)=2*(a'+a'^3/3+a'^5/5+...)=2a'(1+1/3*a'^2(1+3/5*a'^2(...(1+19/21*a'^2))))))))))
  3C93                	F_LOG:
  3C93  CD7B0B        		call	CHKNUM
  3C96                	LOG:
  3C96  EF            		rst	CHKSGN
  3C97  3D            		dec	a
  3C98  C2E503        		jp	nz,FCERR	;x<=0
                      	
  3C9B  216AFF        		ld	hl,FAC1+4
  3C9E  7E            		ld	a,(hl)
  3C9F  D681          		sub	81h
  3CA1  F5            		push	af		;n
  3CA2  3681          		ld	(hl),81h	;a
  3CA4  CD1F39        		call	PUSHF1		;a
  3CA7  CD7D36        		call	INCF1
  3CAA  CD4C39        		call	POPF2		;a
  3CAD  CD1F39        		call	PUSHF1		;a+1
  3CB0  CD3B0C        		call	SETMNS1
  3CB3  CD8F36        		call	ADDF		;a-1
  3CB6  CD4C39        		call	POPF2		;a+1
  3CB9  CD5E38        		call	DIVF		;a'=(a-1)/(a+1)
  3CBC  CD1F39        		call	PUSHF1		;a'
  3CBF  CD5B39        		call	CPYFAC
  3CC2  CDA037        		call	MULF		;a'^2
  3CC5  CD5B39        		call	CPYFAC		;a'^2
  3CC8  CD4122        		call	SETPLS1		;y
                      	
  3CCB  0E12          		ld	c,12h
  3CCD                	LOGLP:
  3CCD  CD2E39        		call	PUSHF2		;a'^2
  3CD0  C5            		push	bc
  3CD1  CDA037        		call	MULF		;y*a'^2
  3CD4  CD5B39        		call	CPYFAC
  3CD7  E1            		pop	hl		;l=c
  3CD8  E5            		push	hl
  3CD9  2C            		inc	l
  3CDA  CD660C        		call	I1TOF
  3CDD  CD6E39        		call	EXFAC
  3CE0  CD5E38        		call	DIVF		;y*a'^2/(c+1)
  3CE3  CD5B39        		call	CPYFAC
  3CE6  E1            		pop	hl
  3CE7  2D            		dec	l
  3CE8  E5            		push	hl
  3CE9  CD660C        		call	I1TOF		;c-1
  3CEC  CDA037        		call	MULF		;y*a'^2*(c-1)/(c+1)
  3CEF  CD7D36        		call	INCF1		;y=1+y*a'^2*(c-1)/(c+1)
  3CF2  C1            		pop	bc
  3CF3  CD4C39        		call	POPF2		;a'^2
  3CF6  0D            		dec	c
  3CF7  20D4          		jr	nz,LOGLP
                      	
  3CF9  CD4C39        		call	POPF2		;a'
  3CFC  CDA037        		call	MULF		;a'*y
  3CFF  216AFF        		ld	hl,FAC1+4
  3D02  7E            		ld	a,(hl)
  3D03  B7            		or	a
  3D04  2804          		jr	z,LOGZ
  3D06  34            		inc	(hl)		;2*a'*y
  3D07  CAE803        		jp	z,OVERR
  3D0A                	LOGZ:
  3D0A  C1            		pop	bc		;b=n
  3D0B  CD1F39        		call	PUSHF1		;2*a'*y
  3D0E  CD110D        		call	S1TOF
  3D11  218E3C        		ld	hl,LOG2
  3D14  CD360C        		call	SETF2
  3D17  CDA037        		call	MULF		;n*log(2)
  3D1A  CD4C39        		call	POPF2		;2*a'*y
  3D1D  C38F36        		jp	ADDF		;n*log(2)+2*a'*y
                      	
                      	
                      	;^ operator
  3D20                	O_POW:
  3D20  CD7B0B        		call	CHKNUM
                      	
  3D23  2A70FF        		ld	hl,(FAC2+3)
  3D26  7C            		ld	a,h		;(FAC2+4)
  3D27  B7            		or	a
  3D28  CA4122        		jp	z,SETPLS1	;x^0
                      	
  3D2B  EF            		rst	CHKSGN
  3D2C  2812          		jr	z,POWZERO
  3D2E  3C            		inc	a
  3D2F  2815          		jr	z,POWNEG
                      	
                      	;x^y=exp(ylog(x))
  3D31                	POWPOS:
  3D31  CD2E39        		call	PUSHF2		;y
  3D34  CD963C        		call	LOG		;log(x)
  3D37  CD4C39        		call	POPF2		;y
  3D3A  CDA037        		call	MULF		;ylog(x)
  3D3D  C3233C        		jp	EXP
                      	
                      	;0^y
  3D40                	POWZERO:
  3D40  7D            		ld	a,l		;(FAC2+3)
  3D41  07            		rlca
  3D42  D0            		ret	nc		;0^(positive)
  3D43  C3F703        		jp	DIV0ERR		;0^(negative)
                      	
                      	;(negative)^y
  3D46                	POWNEG:
  3D46  7C            		ld	a,h		;(FAC2+4)
  3D47  FEA1          		cp	0a1h
  3D49  D2E503        		jp	nc,FCERR
                      	
  3D4C  CD1F39        		call	PUSHF1
  3D4F  CD2E39        		call	PUSHF2
  3D52  CD3D39        		call	POPF1
                      	
  3D55  CDEE0C        		call	GETINT
  3D58  CD1B3F        		call	CMPF
  3D5B  C2E503        		jp	nz,FCERR	;y=not integer
  3D5E  CD870C        		call	FTOI4
  3D61  3A66FF        		ld	a,(FAC1)	;even/odd
  3D64  47            		ld	b,a
                      	
  3D65  CD3D39        		call	POPF1
  3D68  C5            		push	bc		;
  3D69  CD1939        		call	ABS
  3D6C  CD313D        		call	POWPOS
  3D6F  F1            		pop	af		;
  3D70  0F            		rrca
  3D71  D0            		ret	nc		;y=even
  3D72  C32D0D        		jp	NEGABSNZ	;y=odd
                      	
                      	
                      	;COS() function
                      	;cos(x)=sin(pi/2-|x|)
  3D75                	F_COS:
  3D75  CD7B0B        		call	CHKNUM
  3D78                	COS:
  3D78  CD280D        		call	NEGABS
  3D7B  21833D        		ld	hl,PIDIV2
  3D7E  CD8036        		call	SETADDF		;pi/2-|x|
  3D81  1808          		jr	SIN
                      	
                      	;pi/2
  3D83                	PIDIV2:
  3D83  A2DA0F4981    		db	0a2h, 0dah, 0fh, 49h, 81h
                      	
                      	
                      	;SIN() function
                      	;x-x^3/3!+x^5/5!-...=x(1-x^2/(2*3)(1-x^2/(4*5)(...(1-x^2/(12*13)))))))))))
  3D88                	F_SIN:
  3D88  CD7B0B        		call	CHKNUM
  3D8B                	SIN:
  3D8B  CD1F39        		call	PUSHF1		;x
  3D8E  21153E        		ld	hl,PI2
  3D91  CD360C        		call	SETF2
  3D94  CD5E38        		call	DIVF		;x/2pi
  3D97  CDEE0C        		call	GETINT		;int(x/2pi)
  3D9A  21153E        		ld	hl,PI2
  3D9D  CD360C        		call	SETF2
  3DA0  CDA037        		call	MULF		;int(x/2pi)*2pi
  3DA3  CD4C39        		call	POPF2		;x
  3DA6  CD0F3E        		call	SUBF21		;x'=x-int(x/2pi)*2pi
                      	
  3DA9  CDFA3D        		call	SINRANGE	;-pi~+pi
  3DAC  CDFA3D        		call	SINRANGE	;-pi/2~+pi/2
                      	
  3DAF  CD1F39        		call	PUSHF1		;x'
  3DB2  CD5B39        		call	CPYFAC
  3DB5  CDA037        		call	MULF		;x'^2
                      	
  3DB8  CD5B39        		call	CPYFAC
  3DBB  CD4122        		call	SETPLS1		;y=1
                      	
  3DBE  060C          		ld	b,0ch
  3DC0                	SINLP:
  3DC0  CD2E39        		call	PUSHF2		;x'^2
  3DC3  C5            		push	bc
  3DC4  CDA037        		call	MULF		;x'^2*y
  3DC7  C1            		pop	bc
  3DC8  C5            		push	bc
  3DC9  CD1F39        		call	PUSHF1		;x'^2*y
                      	
  3DCC  78            		ld	a,b
  3DCD  04            		inc	b
  3DCE  CD5C24        		call	MULINT1		;hl=b(b+1)
  3DD1  CD680C        		call	I2TOF		;hl->FAC1
                      	
  3DD4  CD5B39        		call	CPYFAC
  3DD7  CD3D39        		call	POPF1		;x'^2*y
  3DDA  CD5E38        		call	DIVF		;x'^2/b/(b+1)*y
  3DDD  CD280D        		call	NEGABS		;-x'^2/b/(b+1)*y (y>0)
  3DE0  CD7D36        		call	INCF1		;y=1-x'^2/b/(b+1)*y
                      	
  3DE3  C1            		pop	bc
  3DE4  CD4C39        		call	POPF2		;x'^2
  3DE7  05            		dec	b
  3DE8  10D6          		djnz	SINLP
                      	
  3DEA  CD4C39        		call	POPF2		;x'
  3DED  CDA037        		call	MULF
                      	
                      	;if |sin(x)|>1
  3DF0  3A6AFF        		ld	a,(FAC1+4)
  3DF3  D681          		sub	81h
  3DF5  C0            		ret	nz
  3DF6  3266FF        		ld	(FAC1),a	;=0
  3DF9  C9            		ret
                      	
                      	;-pi/2~+pi/2
  3DFA                	SINRANGE:
  3DFA  21833D        		ld	hl,PIDIV2	;pi/2
  3DFD  CD183F        		call	SETCMPF
  3E00  3009          		jr	nc,RANGENC
  3E02  2170FF        		ld	hl,FAC2+3
  3E05  CBFE          		set	7,(hl)		;-pi/2
  3E07  CD1B3F        		call	CMPF
  3E0A  D0            		ret	nc
  3E0B                	RANGENC:
  3E0B  2171FF        		ld	hl,FAC2+4
  3E0E  34            		inc	(hl)		;+-pi
                      	
                      	;FAC1=FAC2-FAC1
  3E0F                	SUBF21:
  3E0F  2169FF        		ld	hl,FAC1+3
  3E12  C38B36        		jp	CHGSGNADD
                      	
                      	;pi*2
  3E15                	PI2:
  3E15  A2DA0F4983    		db	0a2h, 0dah, 0fh, 49h, 83h
                      	
                      	
                      	;SQR() function
                      	;y_n+1=(y_n + x/y_n)/2
  3E1A                	F_SQR:
  3E1A  CD7B0B        		call	CHKNUM
  3E1D  EF            		rst	CHKSGN
  3E1E  C8            		ret	z		;sqr(0)=0
  3E1F  3C            		inc	a
  3E20  CAE503        		jp	z,FCERR		;sqr(-x)
                      	
  3E23  CD1F39        		call	PUSHF1
  3E26  216AFF        		ld	hl,FAC1+4
  3E29  7E            		ld	a,(hl)
  3E2A  D681          		sub	81h
  3E2C  1F            		rra			;=sra a,c-flag=bit7
  3E2D  C681          		add	a,81h
  3E2F  77            		ld	(hl),a		;exponent of y_0 = (exponent of x)/2
                      	
  3E30  0605          		ld	b,05h
  3E32                	SQRLP:
  3E32  CD5B39        		call	CPYFAC
  3E35  CD3D39        		call	POPF1		;x
  3E38  CD1F39        		call	PUSHF1		;x
  3E3B  C5            		push	bc
  3E3C  CD2E39        		call	PUSHF2		;;y
  3E3F  CD5E38        		call	DIVF		;x/y
  3E42  CD4C39        		call	POPF2		;;y
  3E45  CD8F36        		call	ADDF		;x/y+y
  3E48  216AFF        		ld	hl,FAC1+4
  3E4B  35            		dec	(hl)		;y=(x/y+y)/2
  3E4C  C1            		pop	bc
  3E4D  10E3          		djnz	SQRLP
  3E4F  CD4C39        		call	POPF2
  3E52  C9            		ret
                      	
                      	
                      	;TAN() function
                      	;tan(x)=sin(x)/cos(x)
  3E53                	F_TAN:
  3E53  CD7B0B        		call	CHKNUM
  3E56  CD1F39        		call	PUSHF1		;x
  3E59  CD783D        		call	COS		;cos(x)
  3E5C  CD5B39        		call	CPYFAC
  3E5F  CD3D39        		call	POPF1		;x
  3E62  CD2E39        		call	PUSHF2		;cos(x)
  3E65  CD8B3D        		call	SIN		;sin(x)
  3E68  CD4C39        		call	POPF2		;cos(x)
  3E6B  C35E38        		jp	DIVF		;sin(x)/cos(x)
                      	
                      	
                      	;input: bc=X, de=Y
                      	;output: a=color
                      	;destroy: f,bc,de,hl
  3E6E                	POINT:
  3E6E  3A92FD        		ld	a,(SCREEN1)
  3E71  3D            		dec	a
  3E72  281A          		jr	z,POINT2
  3E74  F27E3E        		jp	p,POINTG
                      	
                      	;screen mode 1
  3E77                	POINT1:
  3E77  CD863F        		call	GXY2AD
  3E7A  7E            		ld	a,(hl)
  3E7B  D61F          		sub	1fh
  3E7D  C9            		ret
                      	
                      	;graphic mode (screen mode 3,4)
  3E7E                	POINTG:
  3E7E  CD3F14        		call	GXY2GAD
  3E81  7E            		ld	a,(hl)
  3E82  A2            		and	d
  3E83  07            		rlca
  3E84                	POINTLP:
  3E84  0F            		rrca
  3E85  CB0A          		rrc	d
  3E87  30FB          		jr	nc,POINTLP
  3E89                	POINTOK:
  3E89  CB0A          		rrc	d
  3E8B  D0            		ret	nc		;screen mode 4
  3E8C  3C            		inc	a		;screen mode 3
  3E8D  C9            		ret
                      	
                      	;screen mode 2
  3E8E                	POINT2:
  3E8E  C5            		push	bc		;x
  3E8F  CD863F        		call	GXY2AD
  3E92  C1            		pop	bc		;x
  3E93  CD542C        		call	MASK2
  3E96  46            		ld	b,(hl)		;
  3E97  24            		inc	h
  3E98  24            		inc	h
  3E99  A6            		and	(hl)
  3E9A  C8            		ret	z
                      	
  3E9B  78            		ld	a,b		;
  3E9C  0F            		rrca
  3E9D  0F            		rrca
  3E9E  7E            		ld	a,(hl)
  3E9F  17            		rla
  3EA0  17            		rla
  3EA1  17            		rla
  3EA2  E607          		and	07h
  3EA4  3C            		inc	a
  3EA5  C9            		ret
                      	
                      	
                      	;SCREEN() function
                      	;not called but jumped
  3EA6                	F_SCRN:
  3EA6  CD950B        		call	CHKLPAR
  3EA9  CDE40D        		call	INT1ARG
  3EAC  F5            		push	af		;
  3EAD  7E            		ld	a,(hl)
  3EAE  FE2C          		cp	','
  3EB0  C2DC03        		jp	nz,SNERR
  3EB3  CDE30D        		call	INT1INC
  3EB6  CDA40B        		call	CHKRPAR
  3EB9  E1            		pop	hl		;
  3EBA  24            		inc	h
  3EBB  6B            		ld	l,e
  3EBC  2C            		inc	l
  3EBD  CDCD11        		call	XY2AD
  3EC0  6E            		ld	l,(hl)
  3EC1  CD660C        		call	I1TOF
  3EC4  C38318        		jp	FNCRTN
                      	
                      	
                      	;FN() function
                      	;not called but jumped
  3EC7                	F_FN:
  3EC7  CD7A33        		call	CHKVAR
  3ECA  E5            		push	hl		;program address
  3ECB  CBF8          		set	7,b
  3ECD  CDAC33        		call	SRCHVAR
  3ED0  DA0C04        		jp	c,UFERR
  3ED3  E1            		pop	hl		;program address
  3ED4  D5            		push	de		;function address
  3ED5  CD670B        		call	FNCNUM
  3ED8  C1            		pop	bc		;function address
  3ED9  E5            		push	hl		;program address
                      	
                      	;push old FN() argument
  3EDA  2A63FF        		ld	hl,(FNARG+3)
  3EDD  E5            		push	hl
  3EDE  2A61FF        		ld	hl,(FNARG+1)
  3EE1  E5            		push	hl
  3EE2  3A60FF        		ld	a,(FNARG)
  3EE5  F5            		push	af
  3EE6  2A5EFF        		ld	hl,(FNPAR)
  3EE9  E5            		push	hl		;old FN() parameter
                      	
  3EEA  C5            		push	bc		;function address
  3EEB  2166FF        		ld	hl,FAC1
  3EEE  1160FF        		ld	de,FNARG
  3EF1  CD300C        		call	SETF
  3EF4  E1            		pop	hl		;function address
                      	
  3EF5  5E            		ld	e,(hl)
  3EF6  23            		inc	hl
  3EF7  56            		ld	d,(hl)
  3EF8  23            		inc	hl
  3EF9  7E            		ld	a,(hl)
  3EFA  23            		inc	hl
  3EFB  66            		ld	h,(hl)
  3EFC  6F            		ld	l,a
  3EFD  225EFF        		ld	(FNPAR),hl
                      	
  3F00  EB            		ex	de,hl
  3F01  CD4317        		call	ARG
                      	
  3F04  E1            		pop	hl		;old FN() parameter
  3F05  225EFF        		ld	(FNPAR),hl
                      	
                      	;pop old FN() argument
  3F08  F1            		pop	af
  3F09  3260FF        		ld	(FNARG),a
  3F0C  E1            		pop	hl
  3F0D  2261FF        		ld	(FNARG+1),hl
  3F10  E1            		pop	hl
  3F11  2263FF        		ld	(FNARG+3),hl
  3F14  E1            		pop	hl		;program address
  3F15  C38618        		jp	CLRSTRD
                      	
                      	
                      	;set FAC2 and compare
                      	;input: FAC1,hl=float address
                      	;output: c-flag, z-flag, FAC2
                      	;destroy: af,bc,de,hl
  3F18                	SETCMPF:
  3F18  CD360C        		call	SETF2
                      	;	jp	CMPF
                      	
                      	
                      	;compare FAC2 and FAC1
                      	;input: FAC1,FAC2
                      	;output: c-flag,z-flag
                      	;destroy: af,b,de,hl
  3F1B                	CMPF:
  3F1B  1169FF        		ld	de,FAC1+3
  3F1E  2170FF        		ld	hl,FAC2+3
  3F21  B4            		or	h		;h>0, reset z-flag
  3F22  7E            		ld	a,(hl)
  3F23  07            		rlca
  3F24  1A            		ld	a,(de)
  3F25  380E          		jr	c,CMPFNEG
  3F27  07            		rlca
  3F28  D8            		ret	c
                      	
  3F29                	CMPFPOS:
  3F29  13            		inc	de
  3F2A  23            		inc	hl
  3F2B  0605          		ld	b,05h
  3F2D                	CMPFLP:
  3F2D  1A            		ld	a,(de)
  3F2E  BE            		cp	(hl)
  3F2F  C0            		ret	nz
  3F30  2B            		dec	hl
  3F31  1B            		dec	de
  3F32  10F9          		djnz	CMPFLP
  3F34  C9            		ret
                      	
  3F35                	CMPFNEG:
  3F35  07            		rlca
  3F36  D0            		ret	nc
                      	
                      	;FAC1<0, FAC2<0
  3F37  EB            		ex	de,hl
  3F38  18EF          		jr	CMPFPOS
                      	
                      	
                      	;shift left arithmetic for FAC1
                      	;input: FAC1
                      	;output: FAC1,c-flag
                      	;destroy: f,hl
  3F3A                	SLAF1:
  3F3A  2166FF        		ld	hl,FAC1
  3F3D  CB26          		sla	(hl)
  3F3F  23            		inc	hl
  3F40  CB16          		rl	(hl)
  3F42  23            		inc	hl
  3F43  CB16          		rl	(hl)
  3F45  23            		inc	hl
  3F46  CB16          		rl	(hl)
  3F48  C9            		ret
                      	
                      	
                      	;jump subroutine in table
                      	;input: de=table address, a=data(00h-7fh or 80h-ffh)
                      	;output: de=jump address
                      	;destroy: af
  3F49                	JPTBL:
  3F49  EB            		ex	de,hl
  3F4A  87            		add	a,a
  3F4B  85            		add	a,l
  3F4C  6F            		ld	l,a
  3F4D  3001          		jr	nc,JPTBLNC
  3F4F  24            		inc	h
  3F50                	JPTBLNC:
  3F50  7E            		ld	a,(hl)
  3F51  23            		inc	hl
  3F52  66            		ld	h,(hl)
  3F53  6F            		ld	l,a
                      	
  3F54  EB            		ex	de,hl
  3F55  D5            		push	de
  3F56  C9            		ret
                      	
                      	
                      	;skip space
                      	;input: hl=program address-1
                      	;output: a=data, hl=next address
                      	;destroy: f
  3F57                	SKIPSPINC:
  3F57  23            		inc	hl
  3F58                	SKIPSP:
  3F58  7E            		ld	a,(hl)
  3F59  FE20          		cp	' '
  3F5B  C0            		ret	nz
  3F5C  18F9          		jr	SKIPSPINC
                      	
                      	
                      	;check colon and line end
                      	;input: hl=program address
                      	;output: a=(hl), z-flag(1= 00h or ":")
                      	;destroy: af
  3F5E                	CHKCLNINC:
  3F5E  23            		inc	hl
  3F5F                	CHKCLN:
  3F5F  7E            		ld	a,(hl)
  3F60  B7            		or	a
  3F61  C8            		ret	z
  3F62  FE3A          		cp	':'
  3F64  C8            		ret	z
  3F65  FE20          		cp	' '
  3F67  C0            		ret	nz
  3F68  18F4          		jr	CHKCLNINC
                      	
                      	
                      	;check comma or argument
                      	;input: hl=program address
                      	;output: a=(hl), f(z=1:comma), hl=next address
  3F6A                	CHKCMM:
  3F6A  CD5F3F        		call	CHKCLN		;a=(hl)
  3F6D  CA1204        		jp	z,MOERR
  3F70  23            		inc	hl
  3F71  FE20          		cp	' '
  3F73  28F5          		jr	z,CHKCMM
  3F75  FE2C          		cp	','
  3F77  C8            		ret	z
  3F78  2B            		dec	hl
  3F79  C9            		ret
                      	
                      	
                      	;check colon and comma
                      	;input: hl=program address
                      	;output: a=(hl), z-flag(1= 00h or ":" ), hl=hl+1(comma)
                      	;destroy: af
  3F7A                	CHKCLCM:
  3F7A  CD5F3F        		call	CHKCLN
  3F7D  C8            		ret	z
  3F7E  FE2C          		cp	','
  3F80  C2DC03        		jp	nz,SNERR
  3F83  23            		inc	hl
  3F84  B7            		or	a		;reset z-flag
  3F85  C9            		ret
                      	
                      	
                      	;get VRAM attirbute address (screen mode 1,2)
                      	;hl=(VRAM)+X/8+Y/12*32
                      	;input: bc=graphic X, de=graphic Y
                      	;output: hl=VRAM address, d=Y mod 12
                      	;destroy: af,bc,e
  3F86                	GXY2AD:
  3F86  CDD82B        		call	CHKGXY
  3F89  CDB23F        		call	DIV12
  3F8C  EB            		ex	de,hl
  3F8D  57            		ld	d,a
  3F8E  29            		add	hl,hl		;*2
  3F8F  29            		add	hl,hl		;*4
  3F90  29            		add	hl,hl		;*8
  3F91  29            		add	hl,hl		;*16
  3F92  29            		add	hl,hl		;*32
  3F93  3A91FD        		ld	a,(VRAM)
  3F96  47            		ld	b,a
  3F97  CB39          		srl	c
  3F99  CB39          		srl	c
  3F9B  CB39          		srl	c
  3F9D  09            		add	hl,bc
  3F9E  C9            		ret
                      	
                      	
                      	;bc=a*32
                      	;input: a(<=16), b=0
                      	;output: bc
                      	;destroy: f
  3F9F                	MUL32:
  3F9F  87            		add	a,a		;*2
  3FA0  87            		add	a,a		;*4
  3FA1  87            		add	a,a		;*8
  3FA2  87            		add	a,a		;*16
  3FA3  CB10          		rl	b
  3FA5  87            		add	a,a		;*32
  3FA6  CB10          		rl	b
  3FA8  4F            		ld	c,a
  3FA9  C9            		ret
                      	
                      	
                      	;ldir or lddr
                      	;input: bc,de,hl,z(0=ldir,1=lddr)
                      	;output: bc=0,de,hl
                      	;destroy: f
  3FAA                	LDIDR:
  3FAA  2803          		jr	z,LDIDRZ
  3FAC  EDB0          		ldir
  3FAE  C9            		ret
  3FAF                	LDIDRZ:
  3FAF  EDB8          		lddr
  3FB1  C9            		ret
                      	
                      	
                      	;de=de/12
                      	;input: de<192
                      	;output: de=int(de/12), a=de mod 12
                      	;destroy: f,b=0,l
  3FB2                	DIV12:
  3FB2  7B            		ld	a,e
  3FB3  5A            		ld	e,d		;d=0
  3FB4  2E60          		ld	l,01100000b
  3FB6  0604          		ld	b,04h
  3FB8                	DIV12LP:
  3FB8  BD            		cp	l
  3FB9  3801          		jr	c,DIV12C
  3FBB  95            		sub	l
  3FBC                	DIV12C:
  3FBC  3F            		ccf
  3FBD  CB13          		rl	e
  3FBF  CB3D          		srl	l
  3FC1  10F5          		djnz	DIV12LP
  3FC3  C9            		ret
                      	
                      	
                      	;convert hiragana -> katakana (PC-6001)
                      	;convert kana -> 00h (PC-6001A)
  3FC4                	CNVKANA2
  3FC4  B7            		or	a
  3FC5  F0            		ret	p
  3FC6  E5            		push	hl
  3FC7  C5            		push	bc
  3FC8  4F            		ld	c,a
  3FC9  CDD43F        		call	CHK6001A
  3FCC  79            		ld	a,c
  3FCD  C1            		pop	bc
  3FCE  E1            		pop	hl
  3FCF  C24F1A        		jp	nz,CNVKANA
  3FD2  AF            		xor	a
  3FD3  C9            		ret
                      	
                      	
                      	;check if PC-6001A
                      	;output: z-flag(1=PC-6001A)
                      	;destroy: af,b,hl
  3FD4                	CHK6001A:
  3FD4  3E04          		ld	a,04h
  3FD6  D393          		out	(93h),a		;CGROM ON
  3FD8  21D06F        		ld	hl,6fd0h	;nn
  3FDB  060C          		ld	b,0ch
  3FDD  AF            		xor	a
  3FDE                	CHK6001ALP:
  3FDE  B6            		or	(hl)
  3FDF  23            		inc	hl
  3FE0  10FC          		djnz	CHK6001ALP
  3FE2  3E05          		ld	a,05h
  3FE4  D393          		out	(93h),a		;CGROM OFF
  3FE6  C9            		ret
                      	
                      	
                      	;ROM end
  3FE7                	_4000H:	ds	4000h-_4000H
                      	
  4000                		end
